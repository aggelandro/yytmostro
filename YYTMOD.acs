#library "YYTMOD"
#include "zcommon.acs"
 
#define PLAYER_TID_START 1000
#define PLAYER_MAX 20
#define DISPLAYABLE_POWERUP_MAX 43
#define TEAR_STATS 7
#define TEMPORAL_POWERUPS 11

//HPBar
#define FULL_HUD_WIDTH 800
#define FULL_HUD_HEIGHT 600
#define FULL_HOLDTIME 0.5
#define FULL_HPBAR_X 400.0
#define FULL_HPBAR_Y 40.0
#define FULL_NAME_Y (FULL_HPBAR_Y + 25.0)
#define FULL_INFO_Y (FULL_NAME_Y + 12.0)

int  SV_PLAYERS_NUMBERS_2_TIDS[PLAYER_MAX];
bool ENERGY_SHIELD_RELOAD = true;
int TEARS_STATS_BY_PLAYER[PLAYER_MAX][TEAR_STATS];
str BRIMSTONE_COLOR = 0; //Para el trail que vive en Marte.
int IPECAC_FLAT_BONUS_DMG = 20;
int SV_SEMAPHORE_TOKEN; //Para scripts con mucha concurrencia
int SUB_SUN_TID;

str  SV_DISPLAYABLE_POWERUPS[DISPLAYABLE_POWERUP_MAX] = {
    "CONTGiverP000A0", "GrowthHormonesGiverP001A0", "JesusJuiceGiverP002A0", "MomsHeelsGiverP003A0", "MomsLipstickGiverP004A0", 
    "MomsUnderwearGiverP005A0", "RoidRageGiverP006A0", "SafetyPinGiverP007A0", "SynthoilGiverP008A0", "SpeedBallGiverP009A0", 
    "Magic8BallGiverP010A0", "ScrewGiverP011A0", "ToothpicksGiverP012A0", "TornPhotoGiverP013A0", "AdrenalineGiverP014A0", 
    "TwentyTwentyGiverP015A0", "ALumpOfCoalGiverP016A0", "BloodOfTheMartyrGiverP017A0", "DarkMatterGiverP018A0", "Infestation2GiverP019A0",
    "LostContactGiverP020A0", "MomsContactsGiverP021A0", "MomsEyeGiverP022A0", "MutantSpiderGiverP023A0", "PiscesGiverP024A0",
    "RubberCementGiverP025A0", "SacredHeartGiverP026A0", "SpoonBenderGiverP027A0", "TheCommonColdGiverP028A0", "TheInnerEyeGiverP029A0",
    "TheParasiteGiverP030A0", "TheWizGiverP031A0", "BackstabberGiverP032A0", "CompoundFractureGiverP033A0", "8InchNailsGiverP034A0",
    "DeathsTouchGiverP035A0", "MyReflectionGiverP036A0", "MonstrosLungGiverP037A0", "BrimstoneGiverP038A0", "FireMindGiverP039A0",
    "IpecacGiverP040A0", "UranusGiverP041A0", "OcularRiftGiverP042A0"
};

str SV_DRAWABLE_CHARGE_BAR[40] = {
    "CHRBAR3", "CHRBAR4", "CHRBAR5", "CHRBAR6", "CHRBAR7", "CHRBAR8", "CHRBAR9", "CHRBAR10", "CHRBAR11", "CHRBAR12", "CHRBAR13", 
    "CHRBAR14", "CHRBAR15", "CHRBAR16", "CHRBAR17", "CHRBAR18", "CHRBAR19", "CHRBAR20", "CHRBAR21", "CHRBAR22", "CHRBAR23", "CHRBAR24", 
    "CHRBAR25", "CHRBAR26", "CHRBAR27", "CHRBAR28", "CHRBAR29", "CHRBAR30", "CHRBAR31", "CHRBAR32", "CHRBAR33", "CHRBAR34", "CHRBAR35", 
    "CHRBAR36", "CHRBAR37", "CHRBAR38", "CHRBAR39", "CHRBAR40", "CHRBAR41", "CHRBAR42"
};

str SV_DRAWABLE_DOGS_ICON_A[5] = {
    "DSHOTA1", "DSHOTA2", "DSHOTA3", "DSHOTA4", "DSHOTA5"
};

str SV_DRAWABLE_DOGS_ICON_B[5] = {
    "DSHOTB1", "DSHOTB2", "DSHOTB3", "DSHOTB4", "DSHOTA5"
};

str SV_TEMPORAL_POWERUPS[TEMPORAL_POWERUPS] = {
    "RubberCementGiver", "SacredHeartGiver", "SpoonBenderGiver", "TheParasiteGiver", "CompoundFractureGiver", "MonstrosLungGiver", "BrimstoneGiver",
    "FireMindGiver", "IpecacGiver", "UranusGiver", "OcularRiftGiver"
};

script "yytmod_mensajesalida" DEATH
{
    int yyt_mensaje = random(1,3);
    switch(yyt_mensaje)
    {
        case 1:
        print(s:"No papu, asi no!");
        break;
 
        case 2:
        print(s:"Sos la cara de la verga.");
        break;
 
        case 3:
        print(s:"Ah listo...");
        break;
    }
}

script 2 (int value)
{
    SetActorProperty(0,APROP_Damage,value);
}

// Player Information Script
script 331 (void) NET CLIENTSIDE {
    Log(s:"Current player Number: ", i:PlayerNumber());
    Log(s:"Current player TID: ", i:ActivatorTID());
    Log(s:"Current player Speed: ", f:GetActorProperty(0, APROP_Speed));
    Log(s:"Player list: ");

    for (int i=0; i<PLAYER_MAX; i++) {
        Log(s:"Player ", i:i, s:": TID -> ", i:SV_PLAYERS_NUMBERS_2_TIDS[i]);
    }
}

// Actor Information Script (solo para debugs)
script "printMyPointers" (void) {
    log(s:"MASTER: ", i:GetActorProperty(0, APROP_MasterTID));
    log(s:"TARGET: ", i:GetActorProperty(0, APROP_TargetTID));
    log(s:"TRACER: ", i:GetActorProperty(0, APROP_TracerTID));
    log(s:"X: ", f:GetActorX(0));
    log(s:"Y: ", f:GetActorY(0));
    log(s:"Z: ", f:GetActorZ(0));
}

script 332 OPEN {
    //Translations

    //Tears
    CreateTranslation (2, 128:225=[0,0,0]:[24,13,2], 3:4=[0,0,0]:[24,13,2], 80:96=[0,0,0]:[24,13,2]); // A Lump of Coal
    CreateTranslation (3, 192:193=[0,0,0]:[238,25,34], 194:197=[0,0,0]:[158,6,12], 3:4=[0,0,0]:[158,6,12], 80:96=[0,0,0]:[158,6,12]); // Blood of the Martyr
    CreateTranslation (4, 192:193=[0,0,0]:[42,42,42], 194:224=[0,0,0]:[23,23,23], 3:4=[0,0,0]:[42,42,42], 80:96=[0,0,0]:[23,23,23]); // Dark Matter
    CreateTranslation (6, 128:225=[0,0,0]:[179,26,60], 3:4=[0,0,0]:[179,26,60], 80:96=[0,0,0]:[179,26,60]); // Moms Contact
    CreateTranslation (8, 192:193=[0,0,0]:[214,215,223], 194:197=[0,0,0]:[199,225,230], 3:4=[0,0,0]:[214,215,223], 80:96=[0,0,0]:[199,225,230]); // Sacred Heart
    CreateTranslation (9, 192:193=[0,0,0]:[122,38,158], 194:197=[0,0,0]:[111,32,155], 198:224=[0,0,0]:[97,19,146], 3:4=[0,0,0]:[122,38,158], 80:96=[0,0,0]:[111,32,155]); // Spoon Bender
    CreateTranslation (10, 192:193=[0,0,0]:[83,115,168], 194:197=[0,0,0]:[81,116,174], 198:224=[0,0,0]:[76,115,178], 3:4=[0,0,0]:[83,115,168], 80:96=[0,0,0]:[81,116,174]); // Lost Contact
    CreateTranslation (11, 128:225=[0,0,0]:[85,154,117], 3:4=[0,0,0]:[85,154,117], 80:96=[0,0,0]:[85,154,117]); // The Common Cold
    CreateTranslation (13, 192:193=[0,0,0]:[187,98,69], 194:197=[0,0,0]:[154,78,40], 198:224=[0,0,0]:[128,71,43], 3:4=[0,0,0]:[187,98,69], 80:96=[0,0,0]:[154,78,40]); // The Parasite
    CreateTranslation (20, 192:193=[0,0,0]:[255,148,73], 194:197=[0,0,0]:[239,104,11], 198:224=[0,0,0]:[218,95,10], 3:4=[0,0,0]:[255,148,73], 80:96=[0,0,0]:[239,104,11]); // Fire Mind
    CreateTranslation (21, 192:193=[0,0,0]:[119,136,89], 194:197=[0,0,0]:[140,176,109], 198:224=[0,0,0]:[202,239,190], 3:4=[0,0,0]:[119,136,89], 80:96=[0,0,0]:[140,176,109]); // Ipecac
    CreateTranslation (22, 128:225=[0,0,0]:[1,1,2], 3:4=[0,0,0]:[1,1,2], 80:96=[0,0,0]:[1,1,2]); // Ocular Rift

    //Red Beams
    CreateTranslation (14, 16:47=[0,0,0]:[64,36,4], 169:191=[0,0,0]:[64,36,4]); // A Lump of Coal
    CreateTranslation (15, 16:47=[0,0,0]:[13,83,233], 169:191=[0,0,0]:[13,83,233]); // Lost Contact
    CreateTranslation (16, 16:47=[0,0,0]:[13,233,83], 169:191=[0,0,0]:[13,233,83]); // The Common Cold
    CreateTranslation (17, 16:47=[0,0,0]:[158,158,158], 169:191=[0,0,0]:[158,158,158]); // Sacred Heart
    CreateTranslation (18, 16:47=[0,0,0]:[205,13,233], 169:191=[0,0,0]:[205,13,233]); // Spoon Bender
    CreateTranslation (19, 16:47=[0,0,0]:[233,131,13], 169:191=[0,0,0]:[233,131,13]); // The Parasite
}

Script 333 (void) NET { GiveInventory("WeaponSpecialAction", 1); }
Script 339 (void) NET { GiveInventory("ClassThroweableAction", 1); }
Script 340 (void) NET { GiveInventory("SlideKickAction", 1); }
 
script "setEnergyShieldReload" (int flag) {
    ENERGY_SHIELD_RELOAD = flag;
}
 
script 335 ENTER {
    ACS_ExecuteAlways(338, 0);

    int pnum = PlayerNumber();
    int classNum = PlayerClass(pnum);
    if (classNum == 0) {
        ACS_NamedExecuteAlways("loadTearStatsData", 0, pnum);

        TakeInventory("OrangeIsaacHands", 1);
        TakeInventory("GreenIsaacHands", 1);
        TakeInventory("BrimstoneHands", 1);
        if (CheckInventory("DarkMatterGiver")) {
            TakeInventory("IsaacHands", 1);
            if (!CheckInventory("BlackIsaacHands")) GiveInventory("BlackIsaacHands", 1);
        }
        else {
            TakeInventory("BlackIsaacHands", 1);
            if (!CheckInventory("IsaacHands"))
                if (CheckInventory("FireMindGiver") || CheckInventory("IpecacGiver") || CheckInventory("BrimstoneGiver"))
                    GiveInventory("IsaacHands", 1);
        }

        for (int i; i<TEMPORAL_POWERUPS; i++) {
            if (CheckInventory(SV_TEMPORAL_POWERUPS[i])) {
                TakeInventory(SV_TEMPORAL_POWERUPS[i], 1);
            }

            // Obligado a hacer esto para actualizar el cliente en el cambio de mapa
            str powerup = StrLeft(SV_TEMPORAL_POWERUPS[i], StrLen(SV_TEMPORAL_POWERUPS[i])-5);
            if (CheckInventory(powerup)) {
                TakeInventory(powerup, 1);
                GiveInventory(powerup, 1);
            }
        }
                
        while (true) {
            if (ENERGY_SHIELD_RELOAD) {
                GiveInventory("EnergyShieldAmmo",1);
                if (!CheckInventory("PowerStrength")) Delay(1);
            }
            Delay(1);
        }
    }

    if (classNum == 1) {
        TakeInventory("CrusherCooldown", 3);
    }
}
 
script 336 DEATH {
    int classNum = PlayerClass(PlayerNumber());
    if (classNum == 0) {
        LocalSetMusic("*");
        ACS_NamedExecuteAlways("resetTearStats", 0);

        if (CheckInventory("DarkMatterGiver")) {
            TakeInventory("BlackIsaacHands", 1);
            GiveInventory("IsaacHands", 1);
        }

        if (CheckInventory("FireMindGiver")) {
            TakeInventory("OrangeIsaacHands", 1);
            if (!CheckInventory("IsaacHands")) GiveInventory("IsaacHands", 1);
        }

        if (CheckInventory("IpecacGiver")) {
            TakeInventory("GreenIsaacHands", 1);
            if (!CheckInventory("IsaacHands")) GiveInventory("IsaacHands", 1);
        }

        if (CheckInventory("BrimstoneGiver")) {
            TakeInventory("BrimstoneHands", 1);
            if (!CheckInventory("IsaacHands")) GiveInventory("IsaacHands", 1);
        }

        for (int i=0; i<DISPLAYABLE_POWERUP_MAX; i++) {
            TakeInventory(StrLeft(SV_DISPLAYABLE_POWERUPS[i], StrLen(SV_DISPLAYABLE_POWERUPS[i])-6), 1);
        }
    }

    if (classNum == 1) {
        TakeInventory("CrusherCooldown", 3);
        TakeInventory("SpreadGunGiver", 2);
        TakeInventory("HomingGunGiver", 2);
        TakeInventory("PrototypeGunGiver", 2);
        TakeInventory("LaserGunGiver", 2);
        TakeInventory("FlamethrowerGunGiver", 2);
        TakeInventory("CrusherGunGiver", 2);
    }

    SetActorProperty(0, APROP_Speed, 1.0);
    SV_PLAYERS_NUMBERS_2_TIDS[PlayerNumber()] = 0;
    Thing_ChangeTID(PLAYER_TID_START+PlayerNumber(), 0);
}
 
script 337 RESPAWN {
    ACS_ExecuteAlways(338, 0);
}
 
script 338 (void) {
    int playerNum = PlayerNumber();
    int newTID = playerNum + PLAYER_TID_START;
    SV_PLAYERS_NUMBERS_2_TIDS[playerNum] = newTID;
    Thing_ChangeTID(0, newTID);
}
 
script "isMasterPressingAltFire" (void) {
    int playerTID = GetActorProperty(0, APROP_MasterTID);
    int playerNum = ACS_NamedExecuteWithResult("getPlayerNumberFromTID", playerTID); 
    int buttons = GetPlayerInput(playerNum, INPUT_BUTTONS);
 
    SetResultValue(buttons & BT_ALTATTACK);
}

script "getPlayerNumberFromTID" (int ptid) {
    for (int i=0; i<PLAYER_MAX; i++) {
 
        if (SV_PLAYERS_NUMBERS_2_TIDS[i] == ptid) {
            SetResultValue(i);
            break;
        }
    }
}

//Hago estas dos funciones porque los jumps de decorate son una cagada
script "checkForNoAmmoInInventory" (int choice) {
    switch(choice) {
        case 1: {
            SetResultValue(CheckInventory("EnergyShieldAmmo") == 0);
        }; break;
    }
}

script "checkForNoAmmoInPointerInventory" (int choice) {
    switch(choice) {
        case 1: {
            int ptid = GetActorProperty(0, APROP_MasterTID);
            SetResultValue(CheckActorInventory(ptid, "EnergyShieldAmmo") == 0);
        }; break;
    }
}

script "loadChargedAttack" (int fullyCharged) {
    SetUserVariable(0, "user_charge_state", 0);
    int progressFactor = 40.0 / fullyCharged;
    int playFeedbackSound = 1;
    int chargedTime, progressC;
    ACS_NamedExecuteWithResult("displayChargeBar", 1);

    while (ACS_NamedExecuteWithResult("isPressingFire")) {
        progressC = (progressFactor * chargedTime) >> 16;
        ACS_NamedExecuteWithResult("showProgressChargeBar", progressC);

        if (chargedTime < fullyCharged) chargedTime++;
        else {
            if (playFeedbackSound) {
                PlaySound(0, "agge/charged", CHAN_AUTO);
                playFeedbackSound = 0;
            }
        }
        Delay(1);
    }

    ACS_NamedExecuteWithResult("displayChargeBar", 0);
    if (chargedTime == fullyCharged) SetUserVariable(0, "user_charge_state", 1);
    else SetUserVariable(0, "user_charge_state", 2);
}

script "stopAndPositionPlayer" (void) {
    ACS_NamedExecuteWithResult("pausePlayer", 1);
    SetActorPitch(0, 0.1);
}

script "displayChargeBar" (int isOn) CLIENTSIDE {
    if (isOn) {
        SetFont("CHRBAR1");
        HudMessage(s:"A"; HUDMSG_PLAIN, 2, 0, 0.503, 0.503, 0);
    }
    else {
        HudMessage(s:""; HUDMSG_PLAIN, 2, 0, 0, 0, 1);
        HudMessage(s:""; HUDMSG_PLAIN, 1, 0, 0, 0, 1);
    }
}

script "showProgressChargeBar" (int entry) CLIENTSIDE {
    SetFont(SV_DRAWABLE_CHARGE_BAR[entry]);
    HudMessage(s:"A"; HUDMSG_PLAIN, 1, 0, 0.502, 0.502, 0);
}

script "pausePlayer" (int activeFlag) {
    if (activeFlag) {
        Thing_Stop(0);
        SetPlayerProperty(0, activeFlag, PROP_TOTALLYFROZEN);
    }
    else SetPlayerProperty(0, activeFlag, PROP_TOTALLYFROZEN);
}

script "getChargeState" (void) {
    int chargeState = GetUserVariable(0, "user_charge_state");
    SetResultValue(chargeState);
}

script "isPressingFire" (void) {
    int buttons = GetPlayerInput(PlayerNumber(), INPUT_BUTTONS);
    SetResultValue(buttons & BT_ATTACK);
}

// codigo escopeta terro
// COMIENZO
script "clipDoubleLeft" (void) {
    SetResultValue(CheckInventory("ClipDoubleLeft"));
}

script "clipDoubleRight" (void) {
    SetResultValue(CheckInventory("ClipDoubleRight"));
}

script 891 (void)
{
    while(checkinventory("SynthFireActive")>0)
    {       
        if(getplayerinput(PlayerNumber(), INPUT_BUTTONS) & BT_ATTACK)
        {
            giveinventory("SynthFireLeft",1);
            while(checkinventory("SynthFireLeft">0) || getplayerinput(PlayerNumber(), INPUT_BUTTONS) & BT_ATTACK) { delay(1); }
            takeinventory("SynthFireLeft",1);
        }
        delay(1);
    }
}

// Right (secondary) weapon.
script 892 (void)
{
    while(CheckInventory("SynthFireActive")>0)
    {
        if(GetPlayerInput(PlayerNumber(), INPUT_BUTTONS) & BT_ALTATTACK)
        {
            GiveInventory("SynthFireRight",1);
            while(GetPlayerInput(PlayerNumber(), INPUT_BUTTONS) & BT_ALTATTACK) { delay(1); }
            TakeInventory("SynthFireRight",1);
        }
        delay(1);
    }
}
// FIN

script "setDamageBoostPerTime" (int boost, int tics, int maxi) {
    // Prerequisito: El actor que llama al script tiene que tener user_deadflag.
    for (int i=0; i<maxi; i++) {
        Delay(tics);
        if (GetUserVariable(0, "user_deadflag")) { terminate; }
        int newDmg = GetActorProperty(0, APROP_Damage) + boost;
        SetActorProperty(0, APROP_Damage, newDmg);
    }
}

script "isEffectChanceSuccessful" (int chance) {
    int res = Random(1, 100);
    SetResultValue(res <= chance);
}

// Obligado a usar switch a causa de que ACS no se banca otras variables que
// no sean int como parametros y que admite una cantidad limitada de ellos
script "applyEffect" (int option, int dur, int arg2) {
    int mtid, ptid, times, i;
    if (ClassifyActor(0) & ACTOR_DEAD) terminate;
    switch(option) {
        case 1: {
            // Fear: El enemigo huye del jugador pero tiene chance de contraatacar en medio
            mtid = UniqueTID();
            int ftid = UniqueTID();

            Thing_ChangeTID(0, mtid);
            Spawn("FearEffect", GetActorX(0), GetActorY(0), GetActorZ(0), ftid);
            SetActivator(ftid);
            SetPointer(AAPTR_TRACER, mtid);
            SetUserVariable(0, "user_se_monster_height", GetActorProperty(mtid, APROP_Height) >> 16);
            SetActivator(mtid);
            Thing_ChangeTID(mtid, 0);
            SetActorProperty(0, APROP_Frightened, 1);
            Delay(dur);
            SetActorProperty(0, APROP_Frightened, 0);
            TakeInventory("FearDebuffGiver", 1);
            SetActorState(ftid, "Death");
            Thing_ChangeTID(ftid, 0);
        }; break;
        case 2: {
            // Araña aliada
            while(ClassifyActor(0) & ACTOR_ALIVE) { Delay(1); }
            Spawn("FriendlyMiniArachnotron", GetActorX(0), GetActorY(0), GetActorZ(0));
        }; break;
        case 3: {
            // Petrifying: no puede atacar ni moverse
            if (CheckFlag(0, "BOSS")) terminate;
            mtid = UniqueTID();
            ptid = UniqueTID();

            Thing_ChangeTID(0, mtid);
            Spawn("PetrifyingEffect", GetActorX(0), GetActorY(0), GetActorZ(0), ptid);
            SetActivator(ptid);
            SetPointer(AAPTR_TRACER, mtid);
            SetUserVariable(0, "user_se_monster_height", GetActorProperty(mtid, APROP_Height) >> 16);
            SetActivator(mtid);
            Thing_ChangeTID(mtid, 0);
            SetActorState(0, "Petrify");
            Delay(dur);
            TakeInventory("PetrifyingDebuffGiver", 1);
            SetActorState(ptid, "Death");
            Thing_ChangeTID(ptid, 0);
            if (ClassifyActor(0) & ACTOR_DEAD) terminate;
            SetActorState(0, "Depetrify");
        }; break;
        case 4: {
            // Knock-back
            SetActivator(GetActorProperty(0, APROP_TracerTID));
            int angle = GetActorAngle(0) >> 8;
            if (angle < 128) angle = angle + 128; else angle = angle - 128;
            ThrustThing(angle, arg2, 0, 0);
        }; break;
        case 5: {
            // Poison hace el daño de la lágrima al monstruo cada 2 segundos una X cantidad de veces
            times = dur / 35;
            int poisonDmg;
            if (CheckFlag(0, "ISMONSTER")) {
                poisonDmg = Random(3, 7); // Se pierde la relación con el damage dealer
            }
            else {
                poisonDmg = GetActorProperty(0, APROP_Damage);
                if (!SetActivator(0, AAPTR_TRACER)) { break; }
            }

            mtid = UniqueTID();
            ptid = UniqueTID();

            Thing_ChangeTID(0, mtid);
            Spawn("PoisonEffect", GetActorX(0), GetActorY(0), GetActorZ(0), ptid);
            SetActivator(ptid);
            SetPointer(AAPTR_TRACER, mtid);
            SetUserVariable(0, "user_se_monster_height", GetActorProperty(mtid, APROP_Height) >> 16);
            SetActivator(mtid);
            Thing_ChangeTID(mtid, 0);
            for (i=0; i<times; i++) {
                Thing_Damage2(0, poisonDmg, "PPoison");
                Delay(70);
            }

            SetActorState(ptid, "Death");
            TakeInventory("PoisonDebuffGiver", 1);
            Thing_ChangeTID(ptid, 0);
        }; break;
        case 6: {
            // Bleeding hace 10% de su salud maxima de daño al monstruo cada 5 segundos y no tiene limite de tiempo.
            // Importante: No debe afectar a monstruos tipo BOSSES.
            if (CheckFlag(0, "BOSS")) terminate;
            mtid = UniqueTID();
            int btid = UniqueTID();

            Thing_ChangeTID(0, mtid);
            Spawn("BleedingEffect", GetActorX(0), GetActorY(0), GetActorZ(0), btid);
            SetActivator(btid);
            SetPointer(AAPTR_TRACER, mtid);
            SetUserVariable(0, "user_se_monster_height", GetActorProperty(mtid, APROP_Height) >> 16);
            SetActivator(mtid);
            Thing_ChangeTID(mtid, 0);

            while (ClassifyActor(0) & ACTOR_ALIVE) {
                Thing_Damage2(0, multiplyValue(GetUserVariable(0, "user_max_health"), 0.1), "Bleeding");
                Delay(175);
            }

            TakeInventory("BleedingDebuffGiver", 1);
            Thing_ChangeTID(btid, 0);
        }; break;
        case 7: {
            // Daño añadido por distancia recorrida (solo usable por laseres)
            // +20% daño cada 300 espacios
            ptid = GetActorProperty(0, APROP_TargetTID);
            int distance = fdistance(0, ptid);
            int factor = distance / 300 >> 16;
            if (factor > 0) {
                int addDmg = (GetActorProperty(0, APROP_Damage) * (20 * factor)) / 100;
                if (SetActivator(0, AAPTR_TRACER)) Thing_Damage2(0, addDmg, "None");
            }
        }; break;
        case 8: {
            // Burn hace el daño de la lágrima cada segundo durante toda su duración
            times = dur / 35;
            int burnDmg = GetActorProperty(0, APROP_Damage);
            if (SetActivator(0, AAPTR_TRACER)) {
                mtid = UniqueTID();
                ptid = UniqueTID();

                Thing_ChangeTID(0, mtid);
                Spawn("BurnEffect", GetActorX(0), GetActorY(0), GetActorZ(0), ptid);
                SetActivator(ptid);
                SetPointer(AAPTR_TRACER, mtid);
                SetUserVariable(0, "user_se_monster_height", GetActorProperty(mtid, APROP_Height) >> 16);
                SetActivator(mtid);
                Thing_ChangeTID(mtid, 0);
                for (i=0; i<times; i++) {
                    if (ClassifyActor(0) & ACTOR_DEAD) break;
                    Thing_Damage2(0, burnDmg, "FireRF");
                    Delay(35);
                }

                TakeInventory("BurnDebuffGiver", 1);
                Thing_ChangeTID(ptid, 0);
            }
        }; break;
        case 9: {
            // Slow mengua la velocidad del monstruo a la mitad
            if (GetActorProperty(0, APROP_TracerTID) >= 1000 && GetActorProperty(0, APROP_TracerTID) <= 1020)
                terminate; // Si es jugador
            if (SetActivator(0, AAPTR_TRACER)) {
                mtid = UniqueTID();
                ptid = UniqueTID();

                Thing_ChangeTID(0, mtid);
                Spawn("SlowEffect", GetActorX(0), GetActorY(0), GetActorZ(0), ptid);
                SetActivator(ptid);
                SetPointer(AAPTR_TRACER, mtid);
                SetUserVariable(0, "user_se_monster_height", GetActorProperty(mtid, APROP_Height) >> 16);
                SetActivator(mtid);
                Thing_ChangeTID(mtid, 0);
                SetActorProperty(0, APROP_Speed, FixedDiv(GetActorProperty(0, APROP_Speed), 2.0));
                Delay(dur);
                SetActorProperty(0, APROP_Speed, FixedMul(GetActorProperty(0, APROP_Speed), 2.0));
                SetActorState(ptid, "Death");
                Thing_ChangeTID(ptid, 0);
            }
        }; break;
        case 10: {
            // Curación instantanea y buff de velocidad
            if (CheckActorClass(0, "Archvile2") || 
                CheckActorClass(0, "Archevil") || 
                CheckActorClass(0, "SpookyVile") || 
                CheckActorClass(0, "Summoner")) {
                terminate;
            }
            int htid = UniqueTID();
            mtid = UniqueTID();
            Spawn("HealEffectArea", GetActorX(0)+5, GetActorY(0), GetActorZ(0), htid);
            Thing_ChangeTID(0, mtid);
            SetActivator(htid);
            SetPointer(AAPTR_TRACER, mtid);
            SetActivator(mtid);
            SetActorProperty(0, APROP_Speed, FixedMul(GetActorProperty(0, APROP_Speed), 1.5));
            Delay(15);
            Thing_Damage2(0, Random(-250, -150), "Healing");
            Delay(510);
            SetActorProperty(0, APROP_Speed, FixedDiv(GetActorProperty(0, APROP_Speed), 1.5));
            Thing_ChangeTID(mtid, 0);
        }; break;
        case 11: {
            // Buff de fuerza
            ptid = UniqueTID();
            mtid = UniqueTID();
            Spawn("DmgUpEffect", GetActorX(0), GetActorY(0), GetActorZ(0), ptid);
            Thing_ChangeTID(0, mtid);
            SetActivator(ptid);
            SetPointer(AAPTR_TRACER, mtid);
            SetUserVariable(0, "user_se_monster_height", GetActorProperty(mtid, APROP_Height) >> 16);
            SetUserVariable(0, "user_se_monster_radius", GetActorProperty(mtid, APROP_Radius) >> 16);
            SetActivator(mtid);
            int currMul = GetActorProperty(0, APROP_DamageMultiplier);
            //TODO: DamageMultiplier no funciona en Zandroshit
            SetActorProperty(0, APROP_DamageMultiplier, currMul + 0.5);
            Delay(dur);
            SetActorProperty(0, APROP_DamageMultiplier, currMul);
            Thing_ChangeTID(mtid, 0);
            TakeInventory("BuffSTRGiver", 1);
            SetActorState(ptid, "Death");
            Thing_ChangeTID(ptid, 0);
        }; break;
        case 12: {
            // Corazones del castlevania
            Delay(1);
            if (ClassifyActor(0) & ACTOR_ALIVE) {
                TakeInventory("CHDCGiver", 1);
                terminate;
            }
            int dropChance = Random(0, 99);
            int actHeight;
            if (dropChance < 75) {
                int heartChance = Random(0, 99);
                actHeight = GetActorProperty(0, APROP_Height);
                if (heartChance < 25)
                    Spawn("BigHeart", GetActorX(0), GetActorY(0), GetActorZ(0)+actHeight);
                else
                    Spawn("Heart", GetActorX(0), GetActorY(0), GetActorZ(0)+actHeight);
            }
            dropChance = Random(0, 99);
            if (dropChance < 15) {
                int subweaponChance = Random(0, 99);
                actHeight = GetActorProperty(0, APROP_Height);
                if (subweaponChance < 10)
                    Spawn("HolyWaterItem", GetActorX(0), GetActorY(0), GetActorZ(0)+actHeight);
                else {
                    if (subweaponChance >= 10 && subweaponChance < 30)
                        Spawn("AxeItem", GetActorX(0), GetActorY(0), GetActorZ(0)+actHeight);
                    else {
                        if (subweaponChance >= 30 && subweaponChance < 50)
                            Spawn("CrossItem", GetActorX(0), GetActorY(0), GetActorZ(0)+actHeight);
                        else
                            Spawn("DaggerItem", GetActorX(0), GetActorY(0), GetActorZ(0)+actHeight);
                    }
                }
            }
            TakeInventory("CHDCGiver", 1);
        }; break;
        case 13: {
            // Lightning, parecido a poison, hace daño por tiempo
            times = dur / 35;
            for (i=0; i<times; i++) {
                Thing_Damage2(0, arg2, "Lightning");
                Delay(105);
            }

            TakeInventory("LightningCounter", 1);
        }; break;
    }
}

script "applyEffectOnPlayer" (int option, int duration, int arg2, int arg3) {
    // Efectos de los monstruos al jugador
    // NOTA: Puede pasar que un monstruo le pegue a otro en el medio, ese caso se tiene en cuenta
    // arg2, etc. son comodines
    int ptid, ntid, i, lapse, j;
    switch(option) {
        case 1: {
            if (SetActivator(0, AAPTR_TRACER)) {
                if (ClassifyActor(0) & ACTOR_MONSTER) {
                    ACS_NamedExecuteAlways("applyEffect", 0, 5, 140);
                    TakeInventory("PoisonCounter", 1);
                    terminate;
                }

                int poisonDmg;
                lapse = duration / arg2;
                ptid = ActivatorTID();
                ntid = UniqueTID();
                Spawn("PoisonEffect", GetActorX(0), GetActorY(0), GetActorZ(0), ntid);
                SetActivator(ntid);
                SetPointer(AAPTR_TRACER, ptid);
                SetUserVariable(0, "user_se_monster_height", GetActorProperty(ptid, APROP_Height) >> 16);
                SetActivator(ptid);

                for (i=0; i<arg2; i++) {
                    poisonDmg = Random(0, 1);
                    Thing_Damage2(0, poisonDmg, "Poison");
                    for (j=0; j<lapse; j++) {
                        Delay(1);
                        if (ClassifyActor(0) & ACTOR_DEAD) {
                            TakeInventory("PoisonCounter", 1);
                            Thing_ChangeTID(ntid, 0);
                            terminate;
                        }
                    }
                }

                SetActorState(ntid, "Death");
                TakeInventory("PoisonCounter", 1);
                Thing_ChangeTID(ntid, 0);
            }
        }; break;
        case 2: {
            // Slow mengua la velocidad gradualmente
            if (SetActivator(0, AAPTR_TRACER)) {
                ptid = ActivatorTID();
                ntid = UniqueTID();

                Spawn("SlowEffect", GetActorX(0), GetActorY(0), GetActorZ(0), ntid);
                SetActivator(ntid);
                SetPointer(AAPTR_TRACER, ptid);
                SetUserVariable(0, "user_se_monster_height", GetActorProperty(ptid, APROP_Height) >> 16);
                SetActivator(ptid);

                SetActorProperty(0, APROP_Speed, FixedDiv(GetActorProperty(0, APROP_Speed), 2.0));
                for (i=0; i<duration; i++) {
                    Delay(1);
                    if (ClassifyActor(0) & ACTOR_DEAD) {
                        SetActorState(ntid, "Death");
                        TakeInventory("SlowCounter", 1);
                        Thing_ChangeTID(ntid, 0);
                        terminate;
                    }
                }
                SetActorProperty(0, APROP_Speed, FixedMul(GetActorProperty(0, APROP_Speed), 2.0));

                SetActorState(ntid, "Death");
                TakeInventory("SlowCounter", 1);
                Thing_ChangeTID(ntid, 0);
            }
        }; break;
        case 3: {
            // Lightning, parecido a poison, hace daño por tiempo
            if (SetActivator(0, AAPTR_TRACER)) {
                if (ClassifyActor(0) & ACTOR_MONSTER) {
                    // Buscar una solución más general
                    if (CheckActorClass(0, "DoomImp2") || CheckActorClass(0, "SpiderImp") ||
                        CheckActorClass(0, "Impherno") || CheckActorClass(0, "Kimp")) {
                        terminate;
                    }
                    ACS_NamedExecuteAlways("applyEffect", 0, 13, 140, 3);
                    terminate;
                }

                lapse = duration / arg2;
                for (i=0; i<arg2; i++) {
                    int damage = Random(0, arg3);
                    Thing_Damage2(0, damage, "Lightning");
                    for (j=0; j<lapse; j++) {
                        Delay(1);
                        if (ClassifyActor(0) & ACTOR_DEAD) {
                            TakeInventory("LightningCounter", 1);
                            terminate;
                        }
                    }
                }

                TakeInventory("LightningCounter", 1);
            }
        }; break;
    }
}

script "showTranslation" (int option) CLIENTSIDE {
    Thing_SetTranslation(0, option);
}

script "deathTimer" (int tics) {
    Delay(tics);
    if (CheckFlag(0, "CORPSE")) terminate;
    SetActorState(0, "Death");
}

script "deathTimerNoMonster" (int tics) {
    Delay(tics);
    SetActorState(0, "Death");
}

script "explodeTimer" (int tics) {
    Delay(tics);
    if (GetUserVariable(0, "user_has_exploded")) terminate;
    SetActorState(0, "Explode");
}

script "getUniqueTID" (void) {
    SetResultValue(UniqueTID());
}

script "aggeSGShellsLeft" (void) {
    SetResultValue(CheckInventory("AggeSGShellsLeft"));
}

script "aggeSSGShellsLeft" (void) {
    SetResultValue(CheckInventory("AggeSSGShellsLeft"));
}

script "terroSGFireCount" (void) {
    SetResultValue(CheckInventory("TerroSGFireCount"));
}

script "fireReleased" (void) {
    SetResultValue(CheckInventory("FireReleased"));
}

script "shells" (void) {
    SetResultValue(CheckInventory("Shell"));
}

script "targetLoopFlags" (void) {
    int ptid = GetActorProperty(0, APROP_TargetTID);
    SetResultValue(CheckActorInventory(ptid, "LoopFlag"));
}

script "readHoldingFireButtonFor" (int tics) {
    // Se queda leyendo los inputs del jugador durante X cantidad de tics hasta que deje
    // de presionar el click izquierdo en cuyo caso activa un flag de inventario
    while(tics > 0) {
        if (ACS_NamedExecuteWithResult("isPressingFire")) {
            Delay(1);
            tics--;
        }
        else {
            GiveInventory("FireReleased", 1);
            terminate;
        }
    }
}

function int fdistance (int tid1, int tid2) {
    int len;
    int y = GetActorY(tid1) - GetActorY(tid2);
    int x = GetActorX(tid1) - GetActorX(tid2);
    int z = GetActorZ(tid1) - GetActorZ(tid2);

    int ang = VectorAngle(x,y);
    if (((ang+0.125)%0.5) > 0.25) len = FixedDiv(y, sin(ang));
    else len = FixedDiv(x, cos(ang));

    ang = VectorAngle(len, z);
    if (((ang+0.125)%0.5) > 0.25) len = FixedDiv(z, sin(ang));
    else len = FixedDiv(len, cos(ang));

    return len;
}

script "setChildrenStats" (int ctid) {
    int masterDmg = GetActorProperty(0, APROP_Damage);
    SetUserVariable(ctid, "user_explosion_damage", masterDmg);
    if (CheckActorInventory(GetActorProperty(0, APROP_TargetTID), "IpecacGiver")) {
        if (CheckActorClass(0, "IsaacEffectPuff") ||
            CheckActorClass(0, "PiercingDamageDealer") ||
            CheckActorClass(0, "BrimstoneBeam")) {
            int ptid = GetActorProperty(0, APROP_TargetTID);
            if (!ptid) {
                int ttid = UniqueTID();
                Thing_ChangeTID(0, ttid);
                SetActivator(0, AAPTR_TARGET);
                ptid = GetActorProperty(0, APROP_MasterTID);
                if (!ptid) {
                    SetActivator(0, AAPTR_MASTER);
                    ptid = GetActorProperty(0, APROP_TargetTID);
                }
                SetActivator(ttid);
                Thing_ChangeTID(ttid, 0);
            }
            int pnum = ACS_NamedExecuteWithResult("getPlayerNumberFromTID", ptid); 
            ACS_NamedExecuteAlways("setExplosionRange", 0, TEARS_STATS_BY_PLAYER[pnum][5]);
        }
        int masterFMFlag = CheckActorInventory(GetActorProperty(0, APROP_TargetTID), "FireMindGiver");
        SetUserVariable(ctid, "user_fire_flag", masterFMFlag);
        int masterExplRange = GetUserVariable(0, "user_explosion_range");
        SetUserVariable(ctid, "user_explosion_range", masterExplRange);
    }
}

script "setPlayerLight" (int isOn) {
    if (GetUserVariable(0, "user_player_light_tid") == 0) {
        SetUserVariable(0, "user_player_light_tid", UniqueTID());
    }
    int utid = GetUserVariable(0, "user_player_light_tid");
    int ptid = ActivatorTID();

    if (isOn) {
        if (ThingCount(T_NONE, utid) > 0) terminate;
        Spawn("LightFollower", GetActorX(0), GetActorY(0), GetActorZ(0), utid);
        SetActivator(utid);
        SetPointer(AAPTR_TRACER, ptid);
        SetUserVariable(0, "user_se_monster_height", GetActorProperty(ptid, APROP_Height) >> 16);
    }
    else {
        SetActorState(utid, "Death");
        Thing_ChangeTID(utid, 0);
    }
}

script "getTargetRadius" (void) {
    if (SetActivator(0, AAPTR_TARGET)) {
        SetResultValue(GetActorProperty(0, APROP_Radius));
    }
}

script "setPlayersDontThrusting" (int tics) {
    SV_SEMAPHORE_TOKEN++;
    for (int i=PLAYER_TID_START; i<PLAYER_TID_START+PLAYER_MAX; i++) {
        if (SetActivator(i)) SetActorProperty(0, APROP_Mass, 0x7fffffff);
    }
    Delay(tics);
    SV_SEMAPHORE_TOKEN--;
    for (i=PLAYER_TID_START; i<PLAYER_TID_START+PLAYER_MAX; i++) {
        if (SetActivator(i)) {
            if (SV_SEMAPHORE_TOKEN == 0) SetActorProperty(0, APROP_Mass, 100);
        }
    }
}

script "isTargetFullyHealed" (void) {
    int ttid = UniqueTID();
    Thing_ChangeTID(0, ttid);
    if (SetActivator(0, AAPTR_TARGET)) {
        if (!CheckActorClass(0, "Archvile2") && GetActorProperty(0, APROP_Health) < GetUserVariable(0, "user_max_health")) {
            Thing_ChangeTID(ttid, 0);
            SetResultValue(0);
            terminate;
        }
        else {
            SetActivator(ttid);
            SetPointer(AAPTR_TARGET, 0, AAPTR_NULL);
            Thing_ChangeTID(ttid, 0);
            SetResultValue(1);
            terminate;
        }
    }
    // Si no tiene target, no es valido tampoco
    Thing_ChangeTID(ttid, 0);
    SetResultValue(1);
}

script "healTarget" (int amount) {
    if (SetActivator(0, AAPTR_TARGET)) {
        int htid = UniqueTID();
        int mtid = UniqueTID();
        Spawn("HealEffect", GetActorX(0)+5, GetActorY(0), GetActorZ(0), htid);
        Thing_ChangeTID(0, mtid);
        SetActivator(htid);
        SetPointer(AAPTR_TRACER, mtid);
        Delay(15);
        Thing_Damage2(mtid, amount, "Healing");
        Thing_ChangeTID(mtid, 0);
    }
}

script "holdActorVelocityFor" (int tics) {
    int currVelX = GetActorVelX(0);
    int currVelY = GetActorVelY(0);
    int currVelZ = GetActorVelZ(0);
    SetActorVelocity(0, 0, 0, 0, false, false);
    Delay(tics);
    SetActorVelocity(0, currVelX, currVelY, currVelZ, false, false);
}

script "setTIDTracerPointerToSelf" (int tid) {
    int mtid = UniqueTID();
    Thing_ChangeTID(0, mtid);
    SetActivator(tid);
    SetPointer(AAPTR_TRACER, mtid);
    if (CheckActorClass(0, "IceOrbitEffect")) {
        SetUserVariable(0, "user_se_monster_height", GetActorProperty(mtid, APROP_Height) >> 16);
        SetUserVariable(0, "user_se_monster_radius", GetActorProperty(mtid, APROP_Radius) >> 16);
    }
    Thing_ChangeTID(mtid, 0);
}

script "setTIDTargetPointerToSelf" (int tid) {
    int mtid = UniqueTID();
    Thing_ChangeTID(0, mtid);
    SetActivator(tid);
    SetPointer(AAPTR_TARGET, mtid);
    Thing_ChangeTID(mtid, 0);
}

script "showShellsInShotgun" (void) CLIENTSIDE { //deprecated
    int shells = ACS_NamedExecuteWithResult("AggeSGShellsLeft", 0);
    SetFont("BIGFONT");
    SetHudSize(320, 200, 0);
    HudMessage(i:shells; HUDMSG_PLAIN|HUDMSG_NOTWITHFULLMAP|HUDMSG_FADEINOUT, 1, CR_ORANGE, 100.0, 193.0, 3.0, 0.5, 1.0);
}

script "setTIDTracerPointerToSelfTracer" (int tid) {
    int mtid = UniqueTID();
    Thing_ChangeTID(0, mtid);
    SetActivator(tid);
    SetPointer(AAPTR_TRACER, mtid, AAPTR_TRACER);
    Thing_ChangeTID(mtid, 0);
}

script "applySlowEffect" (int duration) {
    int ttid = UniqueTID();
    Thing_ChangeTID(0, ttid);
    if (SetActivator(0, AAPTR_TRACER)) {
        if (PlayerNumber() >= 0) { //Es un jugador
            SetActivator(ttid);
            Thing_ChangeTID(ttid, 0);
            ACS_NamedExecuteAlways("applyEffectOnPlayer", 0, 2, duration);
        }
        else { //Es chobi
            SetActivator(ttid);
            Thing_ChangeTID(ttid, 0);
            ACS_NamedExecuteAlways("applyEffect", 0, 9, duration);
        }
    }
}

script "setTracerToPlayerFromTarget" (void) {
    int ttid = UniqueTID();
    Thing_ChangeTID(0, ttid);
    if (SetActivator(0, AAPTR_TARGET)) {
        SetActivator(0, AAPTR_TARGET);
        int ptid = ActivatorTID();
        if (!ptid) {
            ptid = UniqueTID();
            Thing_ChangeTID(0, ptid);
        }
        SetActivator(ttid);
        SetPointer(AAPTR_TRACER, ptid);
    }
}

script "setActorSpeed" (int value, int tid) {
    value = value << 16;
    if (CheckInventory("SlowCounter") == 1) SetActorProperty(tid, APROP_Speed, FixedDiv(value, 2.0));
    else SetActorProperty(tid, APROP_Speed, value);

    if (CheckInventory("ReverseToken") == 1) SetActorProperty(tid, APROP_Speed, -GetActorProperty(tid, APROP_Speed));
}

script "transferHealthTo" (int tid) {
    int currHealth = GetActorProperty(0, APROP_Health);
    SetActorProperty(tid, APROP_Health, currHealth);
}

script "setInvokedTo" (int tid) {
    SetUserVariable(tid, "user_invoked", 1);
}

script "setAlphaTo" (int amount) {
    amount = amount << 16;
    SetActorProperty(0, APROP_Alpha, amount);
}

script "homingAtNegativeZVelocity" (void) {
    int velz;
    while (velz >= 0) {
        velz = GetActorVelZ(0);
        Delay(1);
    }

    int ttid = UniqueTID();
    int ttid2 = UniqueTID();
    Thing_ChangeTID(0, ttid);
    if (SetActivator(0, AAPTR_TARGET)) {
        Thing_ChangeTID(0, ttid2);
        SetActivator(ttid);
        if (SetPointer(AAPTR_TRACER, ttid2, AAPTR_TARGET)) {
            SetUserVariable(0, "user_homing_flag", 1);
        }
        Thing_ChangeTID(ttid, 0);
        Thing_ChangeTID(ttid2, 0);
    }
}

script "subtractMasterDiabloIllusionVar" (int qty) {
    if (SetActivator(0, AAPTR_MASTER)) {
        SetUserVariable(0, "user_illusions_on", GetUserVariable(0, "user_illusions_on")-qty);
    }
}

script "removeForceShieldFromTracer" (void) {
    if (SetActivator(0, AAPTR_TRACER)) {
        SetUserVariable(0, "user_shield_on", 0);
        ACS_NamedExecuteAlways("setInvulnerable", 0, 0);
    }
}

script "showCautionAnimation" (void) CLIENTSIDE {
    SetHudSize(640, 480, 0);
    SetFont("BIGFONT");
    HudMessage(s:"'Subterranean Sun'"; HUDMSG_PLAIN|HUDMSG_NOTWITHFULLMAP|HUDMSG_FADEINOUT, 34, CR_ORANGE, 505.0, 390.0, 2.5, 1.5, 0.5);
    SetFont("SCARD000");
    HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEINOUT, 33, CR_ORANGE, 600.0, 435.0, 2.5, 1.5, 0.5);
    for (int i=0; i<6; i++) {
        SetFont("SPELL000");
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 8, CR_ORANGE, -254.0+(20.0*i), 0.0+(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 9, CR_ORANGE, 0.0+(20.0*i), 0.0+(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 10, CR_ORANGE, 254.0+(20.0*i), 0.0+(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 11, CR_ORANGE, 508.0+(20.0*i), 0.0+(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 12, CR_ORANGE, 762.0+(20.0*i), 0.0+(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 13, CR_ORANGE, -254.0+(20.0*i), 127.0+(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 14, CR_ORANGE, 0.0+(20.0*i), 127.0+(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 15, CR_ORANGE, 254.0+(20.0*i), 127.0+(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 16, CR_ORANGE, 508.0+(20.0*i), 127.0+(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 17, CR_ORANGE, 762.0+(20.0*i), 127.0+(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 18, CR_ORANGE, -254.0+(20.0*i), 254.0+(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 19, CR_ORANGE, 0.0+(20.0*i), 254.0+(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 20, CR_ORANGE, 254.0+(20.0*i), 254.0+(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 21, CR_ORANGE, 508.0+(20.0*i), 254.0+(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 22, CR_ORANGE, 762.0+(20.0*i), 254.0+(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 23, CR_ORANGE, -254.0+(20.0*i), 381.0-(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 24, CR_ORANGE, 0.0+(20.0*i), 381.0-(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 25, CR_ORANGE, 254.0+(20.0*i), 381.0-(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 26, CR_ORANGE, 508.0+(20.0*i), 381.0-(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 27, CR_ORANGE, 762.0+(20.0*i), 381.0-(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 28, CR_ORANGE, -254.0+(20.0*i), 508.0-(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 29, CR_ORANGE, 0.0+(20.0*i), 508.0-(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 30, CR_ORANGE, 254.0+(20.0*i), 508.0-(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 31, CR_ORANGE, 508.0+(20.0*i), 508.0-(20.0*i), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 32, CR_ORANGE, 762.0+(20.0*i), 508.0-(20.0*i), 0, 2.0, 0.25);
        SetFont("CAUTIO1");
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 1, CR_ORANGE, 70.0+(5.0+(20.0*i)), 32.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 2, CR_ORANGE, 580.0+(5.0+(20.0*i)), 32.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 3, CR_ORANGE, 70.0+(5.0+(20.0*i)), 447.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 4, CR_ORANGE, 580.0+(5.0+(20.0*i)), 447.0, 0.35);
        SetFont("WARN1");
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 5, CR_ORANGE, 130.0-(5.0+(20.0*i)), 240.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 6, CR_ORANGE, 530.0-(5.0+(20.0*i)), 240.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 7, CR_ORANGE, 930.0-(5.0+(20.0*i)), 240.0, 0.35);
        Delay(3);
        SetFont("SPELL000");
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 8, CR_ORANGE, -254.0+(5.0+(20.0*i)), 0.0+(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 9, CR_ORANGE, 0.0+(5.0+(20.0*i)), 0.0+(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 10, CR_ORANGE, 254.0+(5.0+(20.0*i)), 0.0+(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 11, CR_ORANGE, 508.0+(5.0+(20.0*i)), 0.0+(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 12, CR_ORANGE, 762.0+(5.0+(20.0*i)), 0.0+(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 13, CR_ORANGE, -254.0+(5.0+(20.0*i)), 127.0+(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 14, CR_ORANGE, 0.0+(5.0+(20.0*i)), 127.0+(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 15, CR_ORANGE, 254.0+(5.0+(20.0*i)), 127.0+(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 16, CR_ORANGE, 508.0+(5.0+(20.0*i)), 127.0+(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 17, CR_ORANGE, 762.0+(5.0+(20.0*i)), 127.0+(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 18, CR_ORANGE, -254.0+(5.0+(20.0*i)), 254.0+(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 19, CR_ORANGE, 0.0+(5.0+(20.0*i)), 254.0+(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 20, CR_ORANGE, 254.0+(5.0+(20.0*i)), 254.0+(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 21, CR_ORANGE, 508.0+(5.0+(20.0*i)), 254.0+(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 22, CR_ORANGE, 762.0+(5.0+(20.0*i)), 254.0+(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 23, CR_ORANGE, -254.0+(5.0+(20.0*i)), 381.0-(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 24, CR_ORANGE, 0.0+(5.0+(20.0*i)), 381.0-(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 25, CR_ORANGE, 254.0+(5.0+(20.0*i)), 381.0-(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 26, CR_ORANGE, 508.0+(5.0+(20.0*i)), 381.0-(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 27, CR_ORANGE, 762.0+(5.0+(20.0*i)), 381.0-(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 28, CR_ORANGE, -254.0+(5.0+(20.0*i)), 508.0-(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 29, CR_ORANGE, 0.0+(5.0+(20.0*i)), 508.0-(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 30, CR_ORANGE, 254.0+(5.0+(20.0*i)), 508.0-(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 31, CR_ORANGE, 508.0+(5.0+(20.0*i)), 508.0-(5.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 32, CR_ORANGE, 762.0+(5.0+(20.0*i)), 508.0-(5.0+(20.0*i)), 0, 2.0, 0.25);
        SetFont("CAUTIO1");
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 1, CR_ORANGE, 70.0+(10.0+(20.0*i)), 32.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 2, CR_ORANGE, 580.0+(10.0+(20.0*i)), 32.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 3, CR_ORANGE, 70.0+(10.0+(20.0*i)), 447.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 4, CR_ORANGE, 580.0+(10.0+(20.0*i)), 447.0, 0.35);
        SetFont("WARN1");
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 5, CR_ORANGE, 130.0-(10.0+(20.0*i)), 240.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 6, CR_ORANGE, 530.0-(10.0+(20.0*i)), 240.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 7, CR_ORANGE, 930.0-(10.0+(20.0*i)), 240.0, 0.35);
        Delay(2);
        SetFont("SPELL000");
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 8, CR_ORANGE, -254.0+(10.0+(20.0*i)), 0.0+(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 9, CR_ORANGE, 0.0+(10.0+(20.0*i)), 0.0+(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 10, CR_ORANGE, 254.0+(10.0+(20.0*i)), 0.0+(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 11, CR_ORANGE, 508.0+(10.0+(20.0*i)), 0.0+(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 12, CR_ORANGE, 762.0+(10.0+(20.0*i)), 0.0+(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 13, CR_ORANGE, -254.0+(10.0+(20.0*i)), 127.0+(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 14, CR_ORANGE, 0.0+(10.0+(20.0*i)), 127.0+(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 15, CR_ORANGE, 254.0+(10.0+(20.0*i)), 127.0+(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 16, CR_ORANGE, 508.0+(10.0+(20.0*i)), 127.0+(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 17, CR_ORANGE, 762.0+(10.0+(20.0*i)), 127.0+(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 18, CR_ORANGE, -254.0+(10.0+(20.0*i)), 254.0+(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 19, CR_ORANGE, 0.0+(10.0+(20.0*i)), 254.0+(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 20, CR_ORANGE, 254.0+(10.0+(20.0*i)), 254.0+(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 21, CR_ORANGE, 508.0+(10.0+(20.0*i)), 254.0+(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 22, CR_ORANGE, 762.0+(10.0+(20.0*i)), 254.0+(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 23, CR_ORANGE, -254.0+(10.0+(20.0*i)), 381.0-(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 24, CR_ORANGE, 0.0+(10.0+(20.0*i)), 381.0-(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 25, CR_ORANGE, 254.0+(10.0+(20.0*i)), 381.0-(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 26, CR_ORANGE, 508.0+(10.0+(20.0*i)), 381.0-(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 27, CR_ORANGE, 762.0+(10.0+(20.0*i)), 381.0-(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 28, CR_ORANGE, -254.0+(10.0+(20.0*i)), 508.0-(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 29, CR_ORANGE, 0.0+(10.0+(20.0*i)), 508.0-(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 30, CR_ORANGE, 254.0+(10.0+(20.0*i)), 508.0-(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 31, CR_ORANGE, 508.0+(10.0+(20.0*i)), 508.0-(10.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 32, CR_ORANGE, 762.0+(10.0+(20.0*i)), 508.0-(10.0+(20.0*i)), 0, 2.0, 0.25);
        SetFont("CAUTIO2");
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 1, CR_ORANGE, 70.0+(15.0+(20.0*i)), 32.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 2, CR_ORANGE, 580.0+(15.0+(20.0*i)), 32.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 3, CR_ORANGE, 70.0+(15.0+(20.0*i)), 447.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 4, CR_ORANGE, 580.0+(15.0+(20.0*i)), 447.0, 0.35);
        SetFont("WARN2");
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 5, CR_ORANGE, 130.0-(15.0+(20.0*i)), 240.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 6, CR_ORANGE, 530.0-(15.0+(20.0*i)), 240.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 7, CR_ORANGE, 930.0-(15.0+(20.0*i)), 240.0, 0.35);
        Delay(3);
        SetFont("SPELL000");
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 8, CR_ORANGE, -254.0+(15.0+(20.0*i)), 0.0+(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 9, CR_ORANGE, 0.0+(15.0+(20.0*i)), 0.0+(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 10, CR_ORANGE, 254.0+(15.0+(20.0*i)), 0.0+(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 11, CR_ORANGE, 508.0+(15.0+(20.0*i)), 0.0+(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 12, CR_ORANGE, 762.0+(15.0+(20.0*i)), 0.0+(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 13, CR_ORANGE, -254.0+(15.0+(20.0*i)), 127.0+(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 14, CR_ORANGE, 0.0+(15.0+(20.0*i)), 127.0+(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 15, CR_ORANGE, 254.0+(15.0+(20.0*i)), 127.0+(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 16, CR_ORANGE, 508.0+(15.0+(20.0*i)), 127.0+(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 17, CR_ORANGE, 762.0+(15.0+(20.0*i)), 127.0+(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 18, CR_ORANGE, -254.0+(15.0+(20.0*i)), 254.0+(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 19, CR_ORANGE, 0.0+(15.0+(20.0*i)), 254.0+(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 20, CR_ORANGE, 254.0+(15.0+(20.0*i)), 254.0+(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 21, CR_ORANGE, 508.0+(15.0+(20.0*i)), 254.0+(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 22, CR_ORANGE, 762.0+(15.0+(20.0*i)), 254.0+(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 23, CR_ORANGE, -254.0+(15.0+(20.0*i)), 381.0-(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 24, CR_ORANGE, 0.0+(15.0+(20.0*i)), 381.0-(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 25, CR_ORANGE, 254.0+(15.0+(20.0*i)), 381.0-(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 26, CR_ORANGE, 508.0+(15.0+(20.0*i)), 381.0-(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 27, CR_ORANGE, 762.0+(15.0+(20.0*i)), 381.0-(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 28, CR_ORANGE, -254.0+(15.0+(20.0*i)), 508.0-(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 29, CR_ORANGE, 0.0+(15.0+(20.0*i)), 508.0-(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 30, CR_ORANGE, 254.0+(15.0+(20.0*i)), 508.0-(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 31, CR_ORANGE, 508.0+(15.0+(20.0*i)), 508.0-(15.0+(20.0*i)), 0, 2.0, 0.25);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA|HUDMSG_FADEOUT, 32, CR_ORANGE, 762.0+(15.0+(20.0*i)), 508.0-(15.0+(20.0*i)), 0, 2.0, 0.25);
        SetFont("CAUTIO2");
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 1, CR_ORANGE, 70.0+(20.0+(20.0*i)), 32.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 2, CR_ORANGE, 580.0+(20.0+(20.0*i)), 32.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 3, CR_ORANGE, 70.0+(20.0+(20.0*i)), 447.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 4, CR_ORANGE, 580.0+(20.0+(20.0*i)), 447.0, 0.35);
        SetFont("WARN2");
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 5, CR_ORANGE, 130.0-(20.0+(20.0*i)), 240.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 6, CR_ORANGE, 530.0-(20.0+(20.0*i)), 240.0, 0.35);
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEOUT, 7, CR_ORANGE, 930.0-(20.0+(20.0*i)), 240.0, 0.35);
        Delay(2);
    }
}

script "setInvulnerable" (int isOn) {
    SetActorProperty(0, APROP_Invulnerable, isOn);
}

script "setSubSunTid" (int tid) {
    Thing_ChangeTID(0, tid);
    SUB_SUN_TID = tid;
}

script "thrustTowardTheSun" (void) {
    int subSunTid = SUB_SUN_TID;
    //int actorX = GetActorX(0);
    //int actorY = GetActorY(0);
    int actorZ = GetActorZ(0);
    //int sunX = GetActorX(subSunTid);
    //int sunY = GetActorY(subSunTid);
    int sunZ = GetActorZ(subSunTid);
    int currMass = GetActorProperty(0, APROP_Mass);
    if (currMass > 800) SetActorProperty(0, APROP_Mass, 500);
    //if (CheckActorClass(0, "JetpackZombie")) SetActorProperty(0, APROP_Mass, 100);
    while(CheckFlag(subSunTid, "MISSILE")) {
        if (GetActorProperty(0, APROP_Health) <= 0) terminate;
        if (actorZ < sunZ) ThrustThingZ(0, 10, 0, 0);
        else ThrustThingZ(0, 10, 1, 0);

        //TODO: Hacer el ThrustThing de XY quizás (funciona bien el a_radius_thrust)
        /*int sunAngle;
        if (actorX < sunX) {
            if (actorY < sunY)
                sunAngle = VectorAngle(sunX - actorX, sunY - actorY) >> 8;
            else
                sunAngle = VectorAngle(sunX - actorX, actorY - sunY) >> 8;
        }
        else {
            if (actorY < sunY)
                sunAngle = VectorAngle(actorX - sunX, sunY - actorY) >> 8;
            else
                sunAngle = VectorAngle(actorX - sunX, actorY - sunY) >> 8;
        }
        ThrustThing(sunAngle, 15, 0, 0);*/
        Delay(3);
    }
    //if (CheckActorClass(0, "JetpackZombie")) SetActorProperty(0, APROP_Mass, 0x7fffffff);
    SetActorProperty(0, APROP_Mass, currMass);
}

script "meter" (void) {
    SetResultValue(CheckInventory("Meter"));
}

script "playerAcquiresTarget" (void) {
    int ttid = UniqueTID();
    int ptid = GetActorProperty(0, APROP_TargetTID);
    SetActivator(0, AAPTR_TRACER);
    Thing_ChangeTID(0, ttid);
    SetActivator(ptid);
    if (GetActorProperty(0, APROP_TargetTID) == 0) {
        PlaySound(0, "agge/target", CHAN_AUTO);
        int etid = UniqueTID();
        SetPointer(AAPTR_TARGET, ttid);
        Spawn("TargetSpot", GetActorX(0), GetActorY(0), GetActorZ(0), etid);
        SetActivator(etid);
        SetPointer(AAPTR_TRACER, ttid);
        SetPointer(AAPTR_TARGET, ptid);
        SetUserVariable(0, "user_se_monster_height", GetActorProperty(ttid, APROP_Height) >> 16);
    }
    else {
        PlaySound(0, "agge/powerupfail", CHAN_AUTO);
        Thing_ChangeTID(GetActorProperty(0, APROP_TargetTID), 0);
        SetPointer(AAPTR_TARGET, 0, AAPTR_NULL);
        terminate;
    }
}

script "targetPlayerHasTarget" (void) {
    SetActivator(GetActorProperty(0, APROP_TargetTID));
    SetResultValue(GetActorProperty(0, APROP_TargetTID) != 0);
}

script "setTargetToNull" (int tid) {
    SetPointer(AAPTR_TARGET, tid, AAPTR_NULL);
}

script "setTracerToNull" (int tid) {
    SetPointer(AAPTR_TRACER, tid, AAPTR_NULL);
}

script "isTargetDead" (void) {
    if (SetActivator(0, AAPTR_TARGET)) {
        SetResultValue(ClassifyActor(0) & ACTOR_DEAD);
        terminate;
    }
    SetResultValue(1);
}

script "isTracerDead" (void) {
    if (SetActivator(0, AAPTR_TRACER)) {
        SetResultValue(ClassifyActor(0) & ACTOR_DEAD);
        terminate;
    }
    SetResultValue(1);
}

script "cells" (void) {
    SetResultValue(CheckInventory("Cell"));
}

script "isTIDBeingUsed" (int tid) {
    if (tid == 0) {
        SetResultValue(0);
        terminate;
    }
    SetResultValue(IsTIDUsed(tid));
}

script "damageActorBy" (int tid, int value) {
    Thing_Damage2(tid, value, "Friendly");
}

script "showXAuraFor" (int opt, int tics) {
    int ttid = UniqueTID();
    int ptid;
    if (PlayerNumber() >= 0) ptid = ActivatorTID();
    else {
        ptid = UniqueTID();
        Thing_ChangeTID(0, ptid);
    }

    switch(opt) {
        case 1: { Spawn("FirePowerSmokeSpawner", GetActorX(0), GetActorY(0), GetActorZ(0), ttid); }; break;
        case 2: { Spawn("RedPowerSmokeSpawner", GetActorX(0), GetActorY(0), GetActorZ(0), ttid); }; break;
    }

    SetActivator(ttid);
    SetPointer(AAPTR_TRACER, ptid);
    SetActivator(ptid);
    if (PlayerNumber() == -1) Thing_ChangeTID(ptid, 0);
    Delay(tics);
    SetActorState(ttid, "Death");
}

script "SpawnEDFloorExplosionOnTarget" (void) {
    SetActivator(0, AAPTR_TARGET);
    int ttid = UniqueTID();
    Spawn("EDFloorExplosion", GetActorX(0), GetActorY(0), GetActorFloorZ(0), ttid);
    SetActivator(ttid);
    SetUserVariable(0, "user_monster_target", ACS_NamedExecuteWithResult("isTargetAMonster"));
    Thing_ChangeTID(ttid, 0);
}

script "SpawnEDFloorExplosionOnPlayers" (void) {
    for (int i=PLAYER_TID_START; i<PLAYER_TID_START+PLAYER_MAX; i++) {
        Spawn("EDStrongFloorExplosion", GetActorX(i), GetActorY(i), GetActorFloorZ(i));
    }
}

script "reversePlayerControls" (int tics) {
    SetActivator(0, AAPTR_TARGET);
    if (ActivatorTID()>=PLAYER_TID_START && ActivatorTID()<PLAYER_TID_START+PLAYER_MAX) {
        SetHudSize(640, 480, 0);
        SetFont("DFACEE");
        HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_FADEINOUT|HUDMSG_ALPHA, 0, CR_ORANGE, 320.0, 240.0, 0.8, 0.3, 0.3, 0.8);
        int currSpeed = GetActorProperty(0, APROP_Speed);
        SetActorProperty(0, APROP_Speed, -currSpeed);
        while (tics > 0) {
            Delay(1);
            if (ClassifyActor(0) & ACTOR_DEAD) {
                TakeInventory("ReverseToken", 1);
            }
            tics--;
        }
        SetActorProperty(0, APROP_Speed, currSpeed);
        TakeInventory("ReverseToken", 1);
    }
}

script "curseDelayCooldown" (int tics) {
    while(tics > 0) {
        Delay(1);
        SetUserVariable(0, "user_curse_delay", GetUserVariable(0, "user_curse_delay") - 1);
        tics--;
    }
}

script "skyRocketDelayCooldown" (int tics) {
    while(tics > 0) {
        Delay(1);
        SetUserVariable(0, "user_sky_rocket_delay", GetUserVariable(0, "user_sky_rocket_delay") - 1);
        tics--;
    }
}

script "isTargetAMonster" (void) {
    int tid = GetActorProperty(0, APROP_TargetTID);
    SetResultValue(!(tid>=PLAYER_TID_START && tid<PLAYER_TID_START+PLAYER_MAX));
}

script "enableSuperArmor" (void) {
    SetActorProperty(0, APROP_DamageFactor, 0.5);
    int ttid = UniqueTID();
    int mtid = UniqueTID();
    Spawn("EDEyeEffect", GetActorX(0), GetActorY(0), GetActorZ(0), ttid);
    Thing_ChangeTID(0, mtid);
    SetActivator(ttid);
    SetPointer(AAPTR_TRACER, mtid);
    SetUserVariable(0, "user_se_monster_height", GetActorProperty(mtid, APROP_Height) >> 16);
    SetActivator(mtid);
    Thing_ChangeTID(mtid, 0);
}

script "enableRegen" (int threshold, int minHeal, int maxHeal, int tics) {
    int ttid = UniqueTID();
    int mtid = UniqueTID();
    Spawn("RegenEffect", GetActorX(0), GetActorY(0), GetActorZ(0), ttid);
    Thing_ChangeTID(0, mtid);
    SetActivator(ttid);
    SetPointer(AAPTR_TRACER, mtid);
    SetUserVariable(0, "user_se_monster_height", GetActorProperty(mtid, APROP_Height) >> 16);
    SetUserVariable(0, "user_delay_max", tics);
    SetActivator(mtid);
    Thing_ChangeTID(mtid, 0);

    while ((ClassifyActor(0) & ACTOR_ALIVE) && GetActorProperty(0, APROP_Health) < threshold) {
        Thing_Damage2(0, Random(-maxHeal, -minHeal), "Healing");
        Delay(tics);
    }
    SetUserVariable(0, "user_regen", 0);
    SetActorState(ttid, "Death");
}

script "launchMissileToTarget" (int tics, int offx, int offy) {
    SetActivator(0, AAPTR_TARGET);
    int targetX = GetActorX(0) + (offx << 16);
    int targetY = GetActorY(0) + (offy << 16);
    int targetZ = GetActorZ(0);
    int ceilingZ = GetActorCeilingZ(0);
    int floorZ = GetActorFloorZ(0);
    Spawn("MissileTargeter", targetX, targetY, floorZ);
    Delay(tics);
    if (ceilingZ-floorZ < 128) terminate;
    Spawn("FallingEDRocket", targetX, targetY, ceilingZ-27.0);
    Spawn("FallingEDRocket", targetX+Random(45.0, 90.0), targetY+Random(45.0, 90.0), ceilingZ-27.0);
    Spawn("FallingEDRocket", targetX-Random(45.0, 90.0), targetY-Random(45.0, 90.0), ceilingZ-27.0);
}

script "launchGiantMissileToTarget" (int tics) {
    SetActivator(0, AAPTR_TARGET);
    int targetX = GetActorX(0);
    int targetY = GetActorY(0);
    int targetZ = GetActorZ(0);
    int ceilingZ = GetActorCeilingZ(0);
    Spawn("GiantMissileTargeter", targetX, targetY, targetZ);
    Delay(tics);
    if (targetZ+56 > ceilingZ) terminate;
    Spawn("FallingGiantEDRocket", targetX, targetY, ceilingZ-27.0);
}

script "subtractMasterEDAlliesCount" (int qty) {
    if (SetActivator(0, AAPTR_MASTER)) {
        SetUserVariable(0, "user_allies_count", GetUserVariable(0, "user_allies_count")-qty);
    }
}

script "setEDSky" (void) {
    str textureName;
    int i;
    while(!CheckFlag(0, "CORPSE")) {
        for (i=0; i<100; i++) {
            if (i<10) textureName = StrParam(s:"RNGSKY0", d:i);
            else textureName = StrParam(s:"RNGSKY", d:i);

            ChangeSky(textureName, textureName);
            Delay(2);
        }
    }
    ChangeSky("COMSKY00","COMSKY00");
}

script "ALookFromMaster" (void) {
    if (SetActivator(0, AAPTR_TARGET)) SetActorState(0, "See");
    else terminate;

    while (ClassifyActor(0) & ACTOR_ALIVE) {
        Delay(1);
    }

    SetActorState(0, "SpawnLoop");
}

script "dogs_switchCooldown" (int tics) {
    int fullyCharged = tics;
    int progressFactor = 4.0 / fullyCharged;
    int playFeedbackSound = 1;
    int chargedTime, progressC;
    SetUserVariable(0, "user_dogs_switch_cooldown", tics);

    while(tics > 0) {
        progressC = (progressFactor * chargedTime) >> 16;
        ACS_NamedExecuteWithResult("dogs_showIconProgress", progressC);

        Delay(1);
        SetUserVariable(0, "user_dogs_switch_cooldown", GetUserVariable(0, "user_dogs_switch_cooldown") - 1);
        tics--;
        if (chargedTime < fullyCharged) chargedTime++;
    }

    ACS_NamedExecuteWithResult("dogs_showIconProgress", 4);
    PlaySound(0, "terro/buffready");
}

script "dogs_showIconProgress" (int entry) CLIENTSIDE {
    SetHudSize(10000, 7500, 0);
    if (CheckInventory("SwitchToken")) SetFont(SV_DRAWABLE_DOGS_ICON_B[entry]);
    else SetFont(SV_DRAWABLE_DOGS_ICON_A[entry]);
    HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_NOTWITHFULLMAP, 100, 0, 1000.0, 7000.0, 0);
}

script "dogs_buffDuration" (int tics) {
    SetUserVariable(0, "user_dogs_buff_duration", tics);
    while(tics > 0) {
        Delay(1);
        SetUserVariable(0, "user_dogs_buff_duration", GetUserVariable(0, "user_dogs_buff_duration") - 1);
        tics--;
    }
}

script "dogs_currentSwitchCooldownValue" (void) {
    SetResultValue(GetUserVariable(0, "user_dogs_switch_cooldown"));
}

script "dogs_currentBuffDurationValue" (void) {
    SetResultValue(GetUserVariable(0, "user_dogs_buff_duration"));
}

script "cvar_MoveBob" (void) {
    SetResultValue(GetCVar("movebob"));
}

script "movePlayerCameraTo" (int height) {
    SetActorProperty(0, APROP_ViewHeight, height << 16);
}

script "setPitchToTarget" (int offz, int targetOffZ) {
    int ttid = UniqueTID();
    Thing_ChangeTID(0, ttid);
    if (SetActivator(0, AAPTR_TARGET)) {
        int targetX = GetActorX(0);
        int targetY = GetActorY(0);
        int targetZ = GetActorZ(0) + (targetOffZ << 16);
    }
    else terminate;

    SetActivator(ttid);
    Thing_ChangeTID(ttid, 0);
    int length;
    int y = GetActorY(0) - targetY;
    int x = GetActorX(0) - targetX;
    int z = (GetActorZ(0) + (offz << 16)) - targetZ;

    int angle = VectorAngle(x, y);
    if (((angle + 0.125) % 0.5) > 0.25) length = FixedDiv(y, Sin(angle));
    else length = FixedDiv(x, Cos(angle));

    SetActorPitch(0, GetActorPitch(0)+VectorAngle(length, z));
}



























// HPBAR START
// Codigo de HPBAR.acs sacado de hpbar-v22.pk3, amoldado para el mod (solo mostrar la HP de un boss especifico)
script "HPBar_Enter" ENTER {
    Delay(1);
    ACS_NamedExecuteAlways("HPBar_HitTarget", 0);
}

script "HPBar_Respawn" RESPAWN {
    Delay(1);
    ACS_NamedExecuteAlways("HPBar_HitTarget",0);
}

script "HPBar_HitTarget" (void) {
    int pn = PlayerNumber();
    while(ClassifyActor(0) & ACTOR_ALIVE) {
        if (ClassifyActor(0) & ACTOR_ALIVE) LineAttack(0, GetActorAngle(0), GetActorPitch(0), 0, StrParam(s:"HPBarPuff", i:pn), "None", 8192.0, FHF_NORANDOMPUFFZ|FHF_NOIMPACTDECAL, 0);
        Delay(1);
        if (ClassifyActor(0) & ACTOR_ALIVE) LineAttack(0, GetActorAngle(0), GetActorPitch(0), 0, StrParam(s:"HPBarThruPuff", i:pn), "None", 8192.0, FHF_NORANDOMPUFFZ|FHF_NOIMPACTDECAL, 0);
        Delay(1);
    }
}

script "HPBar_GetTarget" (int pn) {
    if ((pn >= 0) && (SetActivator(0,AAPTR_TRACER) == 1)) {
        // Esto cambiaria a una lista si en el futuro agregamos más
        if (CheckActorClass(0, "ElderDemon"))
            startTrackingHP(pn);
    }
}

script "HPBar_LinebarView" (int player, int currHP, int maxHP) CLIENTSIDE {
    if (ConsolePlayerNumber() == player) {
        int hpPercentage = (currHP * 100) / maxHP;
        str monsterName = GetActorClass(0);
        bool invul = getActorInvul();
        showFullHPBar(currHP, maxHP, hpPercentage, monsterName, invul);
    }
}

function void startTrackingHP(int pn) {
    int currHP = GetActorProperty(0, APROP_Health);
    int maxHP = GetUserVariable(0, "user_max_health");
    if (ClassifyActor(0) & ACTOR_ALIVE)
        ACS_NamedExecuteAlways("HPBar_LinebarView", 0, pn, currHP, maxHP);
}

function bool getActorInvul(void) {
    return (GetActorProperty(0, APROP_Invulnerable) | !CheckFlag(0, "SHOOTABLE") | CheckFlag(0, "NODAMAGE"));
}

function void showFullHPBar(int currHP, int maxHP, int hpPercentage, str monsterName, bool invul) {
    SetHudSize(FULL_HUD_WIDTH, FULL_HUD_HEIGHT, true);
    SetFont("SMALLFONT");
    HudMessageBold(d: currHP, s: " / ", d: maxHP, s: " (", d: hpPercentage, s: "%)"; HUDMSG_FADEOUT, 6000, CR_WHITE, FULL_HPBAR_X, FULL_HPBAR_Y, FULL_HOLDTIME);
    
    if (hpPercentage < 100) {
        int viewHPPercentage = (hpPercentage * 0.15) >> 16;
        SetFont(StrParam(s:"FILL", i:viewHPPercentage));
    }
    else SetFont("FILL14");

    for (int i = 0; i <= 100 && i <= hpPercentage; ++i) {
        HudMessageBold(s: "A"; HUDMSG_FADEOUT, 6001 + i, CR_UNTRANSLATED, FULL_HPBAR_X - 97.0 + ((i * 2) << 16), FULL_HPBAR_Y, FULL_HOLDTIME);
    }

    SetFont("HPBAR");
    HudMessageBold(s: "A"; HUDMSG_FADEOUT, 6102, CR_UNTRANSLATED, FULL_HPBAR_X, FULL_HPBAR_Y, FULL_HOLDTIME);
    SetFont("SMALLFONT");
    HudMessageBold(s: monsterName; HUDMSG_FADEOUT, 6103, CR_WHITE, FULL_HPBAR_X, FULL_NAME_Y, FULL_HOLDTIME);

    int ln;
    if (invul) {
        HudMessageBold(s: "\c[i8]Invulnerable\c-"; HUDMSG_FADEOUT, 6104 + ln, CR_WHITE, FULL_HPBAR_X, FULL_INFO_Y + ((ln * 10) << 16), FULL_HOLDTIME);
        ++ln;
    }
}
// HPBAR END























/************************
*   ARMA 4 CLASE AGGE   
************************/

/*
** FUNCIONES DE BASE DE DATOS
*/

// La intención es usar la DB para mantener el progreso de los stats IN-GAME. Por ahora, no
// hay planes de dejar persistida la data una vez se cierra el server.
str TEARS_STATS_TABLE[TEAR_STATS] = {
    "0Radius", "1Height", "2Damage", "3Range", "4Speed Boost", "5Scale", "6Fire Rate Boost"
};

script "saveTearStatsData" (void) {
    int i, j;
    str pName;
    BeginDBTransaction();
    for (i=0; i<PLAYER_MAX; i++) {
        if (TEARS_STATS_BY_PLAYER[i][0] == 0) { continue; }
        pName = StrParam(s:"Player", d:i);
        for (j=0; j<TEAR_STATS; j++) {
            SetDBEntry(pName, TEARS_STATS_TABLE[j], TEARS_STATS_BY_PLAYER[i][j]);
        }
    }
    EndDBTransaction();
}

script "loadTearStatsData" (int pnum) {
    int i;

    str pName = StrParam(s:"Player", d:pnum);
    int tearStats = GetDBEntries(pName);

    if (CountDBResults(tearStats) == 0) {
        ACS_NamedExecuteAlways("resetTearStats", 0);
        FreeDBResults(tearStats);
        terminate;
    }

    for (i=0; i<TEAR_STATS; i++) {
        TEARS_STATS_BY_PLAYER[pnum][i] = GetDBResultValue(tearStats, i);
        if (IsNetworkGame()) ACS_NamedExecuteAlways("sendToClient", 0, pnum, i, TEARS_STATS_BY_PLAYER[pnum][i]);
    }

    FreeDBResults(tearStats);
}









/*
** COMPORTAMIENTO Y STATS DEL ARMA
*/

// Tear Stats Information Script
script 334 (void) NET CLIENTSIDE {
    int pnum = PlayerNumber();
    Log(s:"Radius: ", i:TEARS_STATS_BY_PLAYER[pnum][0]);
    Log(s:"Height: ", i:TEARS_STATS_BY_PLAYER[pnum][1]);
    Log(s:"Damage: ", i:TEARS_STATS_BY_PLAYER[pnum][2]);
    Log(s:"Range: ", i:TEARS_STATS_BY_PLAYER[pnum][3]);
    Log(s:"Speed Boost: ", i:TEARS_STATS_BY_PLAYER[pnum][4]);
    Log(s:"Scale: ", i:TEARS_STATS_BY_PLAYER[pnum][5]);
    Log(s:"Fire Rate Boost: ", i:TEARS_STATS_BY_PLAYER[pnum][6]);
}

script 330 UNLOADING {
    ACS_NamedExecuteAlways("saveTearStatsData", 0);
}

script "resetTearStats" (void) {
    int pnum = PlayerNumber();

    // Radius y height no se pueden cambiar en zandro 3.0
    TEARS_STATS_BY_PLAYER[pnum][0] = 2;  //Radius
    TEARS_STATS_BY_PLAYER[pnum][1] = 3;  //Height
    TEARS_STATS_BY_PLAYER[pnum][2] = 2;  //Damage
    TEARS_STATS_BY_PLAYER[pnum][3] = 35; //Range (en tics)
    TEARS_STATS_BY_PLAYER[pnum][4] = 0;  //Speed Boost
    TEARS_STATS_BY_PLAYER[pnum][5] = 1;  //Scale
    TEARS_STATS_BY_PLAYER[pnum][6] = 1;  //Fire Rate Boost

    if (IsNetworkGame()) {
        for (int i=0; i<TEAR_STATS; i++) {
            ACS_NamedExecuteAlways("sendToClient", 0, pnum, i, TEARS_STATS_BY_PLAYER[pnum][i]);
        }
    }
}

script "sendToClient" (int pnumber, int entry, int val) CLIENTSIDE {
    TEARS_STATS_BY_PLAYER[pnumber][entry] = val;
}

script "addTearBoost" (int boostType, int bonus) {
    int pnum = PlayerNumber();
    TEARS_STATS_BY_PLAYER[pnum][boostType] = TEARS_STATS_BY_PLAYER[pnum][boostType] + bonus;
    if (boostType == 6 && TEARS_STATS_BY_PLAYER[pnum][boostType] < 1) { TEARS_STATS_BY_PLAYER[pnum][boostType] = 1; }
    if (IsNetworkGame()) {
        ACS_NamedExecuteAlways("sendToClient", 0, pnum, boostType, TEARS_STATS_BY_PLAYER[pnum][boostType]);
    }

    if (GetUserVariable(0, "user_isaac_hands_is_active")) ACS_NamedExecuteAlways("showStats", 0, 1);
}

script "setTearStats" (void) {
    int ptid = GetActorProperty(0, APROP_TargetTID);
    int pnum = ACS_NamedExecuteWithResult("getPlayerNumberFromTID", ptid); 
    ACS_NamedExecuteAlways("setRadius", 0, TEARS_STATS_BY_PLAYER[pnum][0]);
    ACS_NamedExecuteAlways("setHeight", 0, TEARS_STATS_BY_PLAYER[pnum][1]);
    ACS_NamedExecuteAlways("setDamage", 0, TEARS_STATS_BY_PLAYER[pnum][2], ptid);
    ACS_NamedExecuteAlways("setRange", 0, TEARS_STATS_BY_PLAYER[pnum][3], ptid);
    ACS_NamedExecuteAlways("setSpeed", 0, TEARS_STATS_BY_PLAYER[pnum][4], ptid);
    ACS_NamedExecuteAlways("setScale", 0, TEARS_STATS_BY_PLAYER[pnum][5]);

    if (CheckActorInventory(ptid, "ALumpOfCoalGiver")) {
        Thing_SetTranslation(0, 2);
        ACS_NamedExecuteAlways("setDamageBoostPerTime", 0, 1, 35, 10);
    }

    if (CheckActorClass(0, "NormalTearProjectile") ||
        CheckActorClass(0, "MonstroTearProjectile") ||
        CheckActorClass(0, "BouncingTearProjectile") ||
        CheckActorClass(0, "MonstroBouncingTearProjectile")) {
        if (CheckActorInventory(ptid, "BloodOfTheMartyrGiver")) Thing_SetTranslation(0, 3);
        if (CheckActorInventory(ptid, "LostContactGiver")) Thing_SetTranslation(0, 10);
        if (CheckActorInventory(ptid, "SacredHeartGiver")) Thing_SetTranslation(0, 8);
        if (CheckActorInventory(ptid, "SpoonBenderGiver")) Thing_SetTranslation(0, 9);
        if (CheckActorInventory(ptid, "TheParasiteGiver")) Thing_SetTranslation(0, 13);
    }
}

script "setRadius" (int radius) {
    SetActorProperty(0, APROP_Radius, radius << 16);
}

script "setHeight" (int hgt) {
    SetActorProperty(0, APROP_Height, hgt << 16);
}

script "setDamage" (int damage, int ptid) {
    damage = applyGiversToDamage(damage, ptid);
    SetActorProperty(0, APROP_Damage, damage);
}

function int applyGiversToDamage(int damage, int ptid) {
    if (CheckActorInventory(ptid, "SacredHeartGiver")) {
        damage = multiplyValue(damage, 2.3);
    }

    if (CheckActorInventory(ptid, "AdrenalineGiver") && GetActorProperty(ptid, APROP_Health) <= 100) {
        damage = damage + (2 * ((GetActorProperty(ptid, APROP_Health) - 100) / -10)); // +2 damage cada -10 de HP, máximo +18 con menos de 10 HP
    }

    if (CheckActorInventory(ptid, "TwentyTwentyGiver")) {
        damage = multiplyValue(damage, 0.75);
    }

    // Flat damage siempre se aplica al final luego de todos los multipliers
    if (CheckActorInventory(ptid, "IpecacGiver")) {
        if (CheckActorInventory(ptid, "BrimstoneGiver")) damage = damage + 2;
        else damage = damage + IPECAC_FLAT_BONUS_DMG;
    }

    return damage;
}

function int multiplyValue(int value, int multiplier) {
    // Multiplica (o divide) a value.
    // value es un integer y multiplier es un fixed number.
    // Se randomiza el redondeo de float->integer en base a la parte decimal del float.
    // Ej: 0.5 tiene 50% chance de redondear a 1 y 50% chance de redondear a 0.
    int dmgCutDec = (multiplier * value) & 0x0000FFFF;
    int rand = Random(0x00000000, 0x0000FFFF);
    if (dmgCutDec <= rand) value = ((multiplier * value) >> 16);
    else value = ((multiplier * value) >> 16) + 1;

    return value;
}

script "setRange" (int range, int ptid) {
    if (CheckActorInventory(ptid, "MyReflectionGiver") == 1) {
        range = multiplyValue(range, 1.6);
    }

    while (range > 0) {
        if (GetUserVariable(0, "user_deadflag")) terminate;

        range--;
        SetUserVariable(0, "user_remaining_range", range);
        Delay(1);
    }

    ThrustThingZ(0, 15, 1, 0);
    SetUserVariable(0, "user_deadflag", 1);
    SetActorState(0, "DeathEnd");
}

script "setSpeed" (int boost, int ptid) {
    if (CheckActorInventory(ptid, "MyReflectionGiver") == 1) boost = multiplyValue(boost, 1.6);

    if (boost != 0) {
        int newVelX = ((GetActorVelX(0) * boost) / 100) + GetActorVelX(0);
        int newVelY = ((GetActorVelY(0) * boost) / 100) + GetActorVelY(0);
        int newVelZ = ((GetActorVelZ(0) * boost) / 100) + GetActorVelZ(0);
        SetActorVelocity(0, newVelX, newVelY, newVelZ, false, false);
    }
}

// Al ser algo puramente visual queda del lado del cliente
script "setScale" (int scale) CLIENTSIDE {
    if (scale < 1) scale = 1;
    switch(scale) {
        case 1: {
            SetActorProperty(0, APROP_ScaleX, 0.125);
            SetActorProperty(0, APROP_ScaleY, 0.125);
        }; break;
        case 2: {
            SetActorProperty(0, APROP_ScaleX, 0.25);
            SetActorProperty(0, APROP_ScaleY, 0.25);
        }; break;
        case 3: {
            SetActorProperty(0, APROP_ScaleX, 0.375);
            SetActorProperty(0, APROP_ScaleY, 0.375);
        }; break;
        case 4: {
            SetActorProperty(0, APROP_ScaleX, 0.5);
            SetActorProperty(0, APROP_ScaleY, 0.5);
        }; break;
        case 5: {
            SetActorProperty(0, APROP_ScaleX, 0.625);
            SetActorProperty(0, APROP_ScaleY, 0.625);
        }; break;
        case 6: {
            SetActorProperty(0, APROP_ScaleX, 0.75);
            SetActorProperty(0, APROP_ScaleY, 0.75);
        }; break;
        case 7: {
            SetActorProperty(0, APROP_ScaleX, 0.875);
            SetActorProperty(0, APROP_ScaleY, 0.875);
        }; break;
        case 8: {
            SetActorProperty(0, APROP_ScaleX, 1.0);
            SetActorProperty(0, APROP_ScaleY, 1.0);
        }; break;
    }
}

script "getFireRateBoost" (void) {
    int boost = TEARS_STATS_BY_PLAYER[PlayerNumber()][6];
    if (CheckInventory("IpecacGiver") && CheckInventory("BrimstoneGiver")) {
        boost = boost+3;
    }
    SetResultValue(boost);
}

script "showPowerups" (int displayFlag) CLIENTSIDE {
    for (int i=1; i<=DISPLAYABLE_POWERUP_MAX; i++) {
        if (displayFlag) {
            SetFont(StrRight(SV_DISPLAYABLE_POWERUPS[i-1], 6));
            if (CheckInventory(StrLeft(SV_DISPLAYABLE_POWERUPS[i-1], StrLen(SV_DISPLAYABLE_POWERUPS[i-1])-6)) == 1)
                HudMessage(s:"A"; HUDMSG_PLAIN, i, 0, FixedDiv(i << 16, 65.0), 0.05, 0);
            else HudMessage(s:"A"; HUDMSG_PLAIN|HUDMSG_ALPHA, i, 0, FixedDiv(i << 16, 65.0), 0.05, 0, 0.25);
        }
        else HudMessage(s:""; HUDMSG_PLAIN, i, 0, 0, 0, 1);
    }

    ACS_NamedExecuteAlways("showStats", 0, displayFlag);
}

script "showStats" (int displayFlag) CLIENTSIDE {
    int pnum = PlayerNumber();
    int idFrom = DISPLAYABLE_POWERUP_MAX+1;
    for (int i=0; i<TEAR_STATS; i++) {
        if (displayFlag) {
            str stat = StrRight(TEARS_STATS_TABLE[i], StrLen(TEARS_STATS_TABLE[i])-1);
            if (i == 4 || i == 6) { stat = StrLeft(stat, StrLen(stat)-6); }
            SetFont("BIGFONT");
            HudMessage(s:stat, s:": ", i:TEARS_STATS_BY_PLAYER[pnum][i]; HUDMSG_PLAIN|HUDMSG_NOTWITHFULLMAP, idFrom+i, CR_ORANGE, 0.007, FixedDiv((i << 16) + 1.0 , 60.0) + 0.74, 0);
        }
        else HudMessage(s:""; HUDMSG_PLAIN, idFrom+i, 0, 0, 0, 1);
    }
}

script "checkForNoGiverInInventory" (int option) {
    SetResultValue(CheckInventory(StrLeft(SV_DISPLAYABLE_POWERUPS[option], StrLen(SV_DISPLAYABLE_POWERUPS[option])-6)) == 0);
}

script "checkForNoGiverInTargetInventory" (int option) {
    int ptid = GetActorProperty(0, APROP_TargetTID);
    SetResultValue(CheckActorInventory(ptid, StrLeft(SV_DISPLAYABLE_POWERUPS[option], StrLen(SV_DISPLAYABLE_POWERUPS[option])-6)) == 0);
}

script "checkForNoGiverInTracerInventory" (int option) {
    int ptid = GetActorProperty(0, APROP_TracerTID);
    SetResultValue(CheckActorInventory(ptid, StrLeft(SV_DISPLAYABLE_POWERUPS[option], StrLen(SV_DISPLAYABLE_POWERUPS[option])-6)) == 0);
}

// Por defecto duran 2:30 minutos, se puede agregar un parametro más adelante para duraciones especificas
script "activateTemporalPowerup" (int option) {
    str powerup = StrLeft(SV_DISPLAYABLE_POWERUPS[option], StrLen(SV_DISPLAYABLE_POWERUPS[option])-6);
    GiveInventory(powerup, 1);

    switch(option) {
        case 26: {
            ACS_NamedExecuteAlways("addTearBoost", 0, 2, 1);
            ACS_NamedExecuteAlways("addTearBoost", 0, 6, -2);
            ACS_NamedExecuteAlways("addTearBoost", 0, 4, -90);
            //ACS_NamedExecuteAlways("addTearBoost", 0, 1, 2); //Más height (para cuando zandro lo soporte)
        }; break;
        case 33: {
            ACS_NamedExecuteWithResult("addTearBoost", 0, 3, 5);
            //ACS_NamedExecuteWithResult("addTearBoost", 0, 1, 1);
            //ACS_NamedExecuteWithResult("addTearBoost", 0, 0, 1);
            //ACS_NamedExecuteWithResult("addTearBoost", 0, 5, 1);
        }; break;
        case 38: {
            if (CheckInventory("IsaacHands")) TakeInventory("IsaacHands", 1);
            if (CheckInventory("BlackIsaacHands")) TakeInventory("BlackIsaacHands", 1);
            if (CheckInventory("OrangeIsaacHands")) TakeInventory("OrangeIsaacHands", 1);
            if (CheckInventory("GreenIsaacHands")) TakeInventory("GreenIsaacHands", 1);

            GiveInventory("BrimstoneHands", 1);
            Delay(10);
            SetWeapon("BrimstoneHands");
            ACS_NamedExecuteAlways("setClientBrimstoneHands", 0);
            Delay(35);
            PlaySound(0, "agge/brimgrowl", CHAN_AUTO);
        }; break;
        case 39: {
            if (CheckInventory("BrimstoneHands")) break;
            if (CheckInventory("GreenIsaacHands")) break;
            if (CheckInventory("IsaacHands")) TakeInventory("IsaacHands", 1);
            if (CheckInventory("BlackIsaacHands")) TakeInventory("BlackIsaacHands", 1);

            GiveInventory("OrangeIsaacHands", 1);
            Delay(10);
            SetWeapon("OrangeIsaacHands");
            ACS_NamedExecuteAlways("setClientOrangeIsaacHands", 0);
        }; break;
        case 40: {
            //ACS_NamedExecuteAlways("addTearBoost", 0, 1, -3);
            //ACS_NamedExecuteAlways("addTearBoost", 0, 0, -3);
            ACS_NamedExecuteAlways("addTearBoost", 0, 5, -3);
            ACS_NamedExecuteAlways("addTearBoost", 0, 3, -10);
            ACS_NamedExecuteAlways("addTearBoost", 0, 4, -75);
            ACS_NamedExecuteAlways("addTearBoost", 0, 6, -3);
            if (CheckInventory("BrimstoneHands")) break;
            if (CheckInventory("IsaacHands")) TakeInventory("IsaacHands", 1);
            if (CheckInventory("BlackIsaacHands")) TakeInventory("BlackIsaacHands", 1);
            if (CheckInventory("OrangeIsaacHands")) TakeInventory("OrangeIsaacHands", 1);

            GiveInventory("GreenIsaacHands", 1);
            Delay(10);
            SetWeapon("GreenIsaacHands");
            ACS_NamedExecuteAlways("setClientGreenIsaacHands", 0);
        }; break;
    }

    Delay(5250);

    switch(option) {
        case 26: {
            ACS_NamedExecuteAlways("addTearBoost", 0, 2, -1);
            ACS_NamedExecuteAlways("addTearBoost", 0, 6, 2);
            ACS_NamedExecuteAlways("addTearBoost", 0, 4, 90);
            //ACS_NamedExecuteAlways("addTearBoost", 0, 1, -2); //Más height (para cuando zandro lo soporte)
        }; break;
        case 33: {
            ACS_NamedExecuteWithResult("addTearBoost", 0, 3, -5);
            //ACS_NamedExecuteWithResult("addTearBoost", 0, 1, -1);
            //ACS_NamedExecuteWithResult("addTearBoost", 0, 0, -1);
            //ACS_NamedExecuteWithResult("addTearBoost", 0, 5, -1);
        }; break;
        case 38: {
            TakeInventory("BrimstoneHands", 1);
            if (CheckInventory("IpecacGiver")) {
                GiveInventory("GreenIsaacHands", 1);
                Delay(10);
                SetWeapon("GreenIsaacHands");
                ACS_NamedExecuteAlways("setClientGreenIsaacHands", 0);
            }
            else {
                if (CheckInventory("FireMindGiver")) {
                    GiveInventory("OrangeIsaacHands", 1);
                    Delay(10);
                    SetWeapon("OrangeIsaacHands");
                    ACS_NamedExecuteAlways("setClientOrangeIsaacHands", 0);
                }
                else {
                    if (CheckInventory("DarkMatterGiver")) {
                        GiveInventory("BlackIsaacHands", 1);
                        Delay(10);
                        SetWeapon("BlackIsaacHands");
                        ACS_NamedExecuteAlways("setClientBlackIsaacHands", 0);
                    }
                    else {
                        GiveInventory("IsaacHands", 1);
                        Delay(10);
                        SetWeapon("IsaacHands");
                        ACS_NamedExecuteAlways("setClientIsaacHands", 0);
                    }
                }
            }
        }; break;
        case 39: {
            TakeInventory("OrangeIsaacHands", 1);
            if (CheckInventory("DarkMatterGiver")) {
                GiveInventory("BlackIsaacHands", 1);
                Delay(10);
                SetWeapon("BlackIsaacHands");
                ACS_NamedExecuteAlways("setClientBlackIsaacHands", 0);
            }
            else {
                GiveInventory("IsaacHands", 1);
                Delay(10);
                SetWeapon("IsaacHands");
                ACS_NamedExecuteAlways("setClientIsaacHands", 0);
            }
        }; break;
        case 40: {
            ACS_NamedExecuteAlways("addTearBoost", 0, 6, 3);
            //ACS_NamedExecuteAlways("addTearBoost", 0, 1, 3);
            //ACS_NamedExecuteAlways("addTearBoost", 0, 0, 3);
            ACS_NamedExecuteAlways("addTearBoost", 0, 5, 3);
            ACS_NamedExecuteAlways("addTearBoost", 0, 3, 10);
            ACS_NamedExecuteAlways("addTearBoost", 0, 4, 75);
            TakeInventory("GreenIsaacHands", 1);
            if (CheckInventory("FireMindGiver")) {
                GiveInventory("OrangeIsaacHands", 1);
                Delay(10);
                SetWeapon("OrangeIsaacHands");
                ACS_NamedExecuteAlways("setClientOrangeIsaacHands", 0);
            }
            else {
                if (CheckInventory("DarkMatterGiver")) {
                    GiveInventory("BlackIsaacHands", 1);
                    Delay(10);
                    SetWeapon("BlackIsaacHands");
                    ACS_NamedExecuteAlways("setClientBlackIsaacHands", 0);
                }
                else {
                    GiveInventory("IsaacHands", 1);
                    Delay(10);
                    SetWeapon("IsaacHands");
                    ACS_NamedExecuteAlways("setClientIsaacHands", 0);
                }
            }
        }; break;
    }

    TakeInventory(powerup, 1);
}

script "setClientBrimstoneHands" (void) CLIENTSIDE {
    SetWeapon("BrimstoneHands");
}

script "setClientGreenIsaacHands" (void) CLIENTSIDE {
    SetWeapon("GreenIsaacHands");
}

script "setClientOrangeIsaacHands" (void) CLIENTSIDE {
    SetWeapon("OrangeIsaacHands");
}

script "setClientBlackIsaacHands" (void) CLIENTSIDE {
    SetWeapon("BlackIsaacHands");
}

script "setClientIsaacHands" (void) CLIENTSIDE {
    SetWeapon("IsaacHands");
}

script "setParasiteChildrenStats" (int ctid) {
    int masterDmg = GetActorProperty(0, APROP_Damage);
    int masterScaleX = GetActorProperty(0, APROP_ScaleX);
    int masterScaleY = GetActorProperty(0, APROP_ScaleY);
    int masterRange = GetUserVariable(0, "user_remaining_range");
    int masterCount = GetUserVariable(0, "user_parasite_count");

    int masterExplosionFlag;
    if (CheckActorClass(0, "FireTearProjectile") && GetUserVariable(0, "user_explosion_flag") ||
        CheckActorClass(0, "BouncingFireTearProjectile") && GetUserVariable(0, "user_explosion_flag") ||
        CheckActorClass(0, "MonstroBouncingFireTearProjectile") && GetUserVariable(0, "user_explosion_flag")) {
        masterExplosionFlag = 1;
    }

    int masterGravity;
    if (CheckActorClass(0, "ExplosiveTearProjectile") ||
        CheckActorClass(0, "BouncingExplosiveTearProjectile")) {
        masterGravity = GetActorProperty(0, APROP_Gravity);
        int masterExplosionRange = GetUserVariable(0, "user_explosion_range");
    }

    SetActivator(ctid);

    SetActorProperty(0, APROP_Damage, masterDmg / 2);

    //Lo mismo sería para height y radius
    SetActorProperty(0, APROP_ScaleX, masterScaleX / 2);
    SetActorProperty(0, APROP_ScaleY, masterScaleY / 2);

    if (masterGravity != 0) {
        SetActorProperty(0, APROP_Gravity, masterGravity);
        SetUserVariable(0, "user_explosion_range", masterExplosionRange);
        Thing_ChangeTID(ctid, 0);
        terminate;
    }

    if (masterRange < 5) masterRange = 5;
    ACS_NamedExecuteAlways("setRange", 0, masterRange);

    if (masterExplosionFlag) SetUserVariable(0, "user_explosion_flag", 1);
    Thing_ChangeTID(ctid, 0);

    SetUserVariable(0, "user_parasite_count", masterCount+1);
}

script "isDamageTooLow" (int threshold) {
    SetResultValue(GetActorProperty(0, APROP_Damage) < threshold);
}

script "checkForBackstabDebuff" (void) {
    int projectileAngle = GetActorAngle(0) >> 8;
    int projectileDamage = GetActorProperty(0, APROP_Damage);

    if (SetActivator(0, AAPTR_TRACER)) {
        int monsterAngle = GetActorAngle(0) >> 8;
        int minThreshold, maxThreshold;

        if (monsterAngle >= 32 && monsterAngle < 224) {
            minThreshold = monsterAngle - 32;
            maxThreshold = monsterAngle + 32;
            if (projectileAngle >= minThreshold && projectileAngle <= maxThreshold) {
                ACS_NamedExecuteAlways("dealBackstabDamage", 0, projectileDamage);
            }
        }
        else {
            if (monsterAngle < 32) {
                minThreshold = monsterAngle + 224;
                maxThreshold = monsterAngle + 32;
                if (projectileAngle >= minThreshold || projectileAngle <= maxThreshold) {
                    ACS_NamedExecuteAlways("dealBackstabDamage", 0, projectileDamage);
                }
            }
            else {
                minThreshold = monsterAngle - 224;
                maxThreshold = monsterAngle - 32;
                if (projectileAngle <= minThreshold || projectileAngle >= maxThreshold) {
                    ACS_NamedExecuteAlways("dealBackstabDamage", 0, projectileDamage);
                }
            }
        }
    }

}

script "dealBackstabDamage" (int damage) {
    // Segundo golpe y efecto bleeding
    Thing_Damage2(0, damage, "None");
    ACS_NamedExecuteAlways("applyEffect", 0, 6);
}

script "setIsaacHandsSelected" (int isOn) {
    SetUserVariable(0, "user_isaac_hands_is_active", isOn);
}

script "isParasiteProjectile" (void) {
    SetResultValue(CheckActorClass(0, "ParasiteTearProjectile")
                || CheckActorClass(0, "ParasiteFearTearProjectile")
                || CheckActorClass(0, "ParasiteFireTearProjectile")
                || CheckActorClass(0, "ParasiteExplosiveTearProjectile")
                || CheckActorClass(0, "ParasiteBouncingTearProjectile")
                || CheckActorClass(0, "ParasiteBouncingFearTearProjectile")
                || CheckActorClass(0, "ParasiteBouncingFireTearProjectile"));
}

script "setDamageToChildren" (int tid) {
    int masterDmg = GetActorProperty(0, APROP_Damage);
    int masterPetrFlag = GetUserVariable(0, "user_petrifying_flag");
    int masterPoisonFlag = GetUserVariable(0, "user_poison_flag");
    SetActivator(tid);
    SetUserVariable(0, "user_master_damage", masterDmg);
    SetUserVariable(0, "user_petrifying_flag", masterPetrFlag);
    SetUserVariable(0, "user_poison_flag", masterPoisonFlag);
    Thing_ChangeTID(tid, 0);
}

script "dealPiercingDamage" (void) {
    int damageToDeal = GetUserVariable(0, "user_master_damage");
    int hasUranus = CheckActorInventory(GetActorProperty(0, APROP_TargetTID), "UranusGiver");
    if (SetActivator(0, AAPTR_TRACER)) {
        if (!CheckInventory("PiercingDamageFlag")) {
            GiveInventory("PiercingDamageFlag", 1);
            if (hasUranus)
                Thing_Damage2(0, damageToDeal*3, "IsaacIce");
            else
                Thing_Damage2(0, damageToDeal*3, "Piercing");
            PlaySound(0, "agge/scythehit", CHAN_AUTO);
            Delay(1);
            TakeInventory("PiercingDamageFlag", 1);
        }
    }
}

script "setTracerToPlayer" (int rotationTics) {
    // La cantidad de tics completa de la rotación del sprite tiene que ser idealmente de rotationTics+1
    int ptid = GetActorProperty(0, APROP_TargetTID);
    int currVelX = GetActorVelX(0);
    int currVelY = GetActorVelY(0);
    int currVelZ = GetActorVelZ(0);
    SetActorVelocity(0, 0, 0, 0, false, false);
    int currAngle = GetActorAngle(0);
    int newAngle;
    if (currAngle > 0.5) newAngle = currAngle - 0.5;
    else newAngle = currAngle + 0.5;

    SetActorAngle(0, newAngle);
    PlaySound(0, "agge/reflactivated", CHAN_AUTO);
    Delay(rotationTics);
    SetActorVelocity(0, currVelX, currVelY, currVelZ, false, false);
    SetPointer(AAPTR_TRACER, ptid);
    TakeActorInventory(ptid, "WeaponSpecialAction", 1);
}

script "getChargedAttackTime" (void) {
    int frboost = ACS_NamedExecuteWithResult("getFireRateBoost");
    
    if (CheckInventory("BrimstoneGiver")) frboost = 0.33 * frboost;
    else if (CheckInventory("MonstrosLungGiver")) frboost = 0.23 * frboost;

    int factor = FixedMul(frboost, 1.5);
    int maxTime = 87.5;
    int result = FixedDiv(maxTime, factor) >> 16;
    SetResultValue(result);
}

script "getBrimstoneDamage" (void) {
    if (CheckActorClass(0, "PlayerDoppelganger") || CheckActorClass(0, "HomingPlayerDoppelganger")) SetActivator(GetActorProperty(0, APROP_MasterTID));
    int pnum = PlayerNumber();
    int damage = TEARS_STATS_BY_PLAYER[pnum][2];
    damage = applyGiversToDamage(damage, SV_PLAYERS_NUMBERS_2_TIDS[pnum]);
    SetResultValue(damage);
}

script "setBrimstoneColor" (void) CLIENTSIDE {
    BRIMSTONE_COLOR = 0;
    if (CheckInventory("ALumpOfCoalGiver")) BRIMSTONE_COLOR = 14;
    if (CheckInventory("LostContactGiver")) BRIMSTONE_COLOR = 15;
    if (CheckInventory("TheCommonColdGiver")) BRIMSTONE_COLOR = 16;
    if (CheckInventory("SacredHeartGiver")) BRIMSTONE_COLOR = 17;
    if (CheckInventory("SpoonBenderGiver")) BRIMSTONE_COLOR = 18;
    if (CheckInventory("TheParasiteGiver")) BRIMSTONE_COLOR = 19;
}

script "showBrimstoneTranslation" (void) CLIENTSIDE {
    Thing_SetTranslation(0, BRIMSTONE_COLOR);
    if (BRIMSTONE_COLOR == 16 || BRIMSTONE_COLOR == 17 || BRIMSTONE_COLOR == 18) {
        if (CheckActorClass(0, "BrimstoneBeam")) SetActorProperty(0, APROP_Alpha, 0.2);
    }
    if (BRIMSTONE_COLOR == 15) SetActorProperty(0, APROP_Alpha, 0.2);
    if (BRIMSTONE_COLOR == 14) SetActorProperty(0, APROP_RenderStyle, STYLE_Translucent);
}

script "setBrimstoneDeathRender" (void) CLIENTSIDE {
    if (!BRIMSTONE_COLOR || BRIMSTONE_COLOR == 16 || 
        BRIMSTONE_COLOR == 15 || BRIMSTONE_COLOR == 17 ||
        BRIMSTONE_COLOR == 18 || BRIMSTONE_COLOR == 19) {
        SetActorProperty(0, APROP_RenderStyle, STYLE_Translucent);
    }
    else SetActorProperty(0, APROP_RenderStyle, STYLE_Add);
}

script "setBrimstoneDamage" (void) {
    int tempTid = UniqueTID();
    Thing_ChangeTID(0, tempTid);
    SetActivator(0, AAPTR_TARGET);
    int damage = ACS_NamedExecuteWithResult("getBrimstoneDamage");
    SetActorProperty(tempTid, APROP_Damage, damage);
    Thing_ChangeTID(tempTid, 0);
}

script "userIsaacHandsLaserFromBehingIsActive" (void) {
    SetResultValue(GetUserVariable(0, "user_isaac_hands_laser_from_behind"));
}

script "transferMasterEffectsInventory" (void) {
    int ptid = GetActorProperty(0, APROP_MasterTID);

    if (!ptid) {
        int utid = UniqueTID();
        Thing_ChangeTID(0, utid);
        SetActivator(0, AAPTR_MASTER);
        ptid = GetActorProperty(0, APROP_TargetTID);
        SetActivator(utid);
        SetPointer(AAPTR_MASTER, ptid);
        Thing_ChangeTID(utid, 0);
    }
    
    int effectsPowerups[14] = {"LostContactGiver", "TheCommonColdGiver", "DarkMatterGiver",
                                "MomsContactsGiver", "ALumpOfCoalGiver", "Infestation2Giver",
                                "PiscesGiver", "8InchNailsGiver", "TheParasiteGiver", "RubberCementGiver",
                                "FireMindGiver", "IpecacGiver", "UranusGiver", "OcularRiftGiver"};

    for (int i=0; i<14; i++) {
        if (CheckActorInventory(ptid, effectsPowerups[i])) GiveInventory(effectsPowerups[i], 1);
    }
}

script "transferMasterPitch" (void) {
    SetActorPitch(0, GetActorPitch(GetActorProperty(0, APROP_MasterTID)));
}

script "setIpecacStats" (void) {
    Thing_SetTranslation(0, 21);
    int ptid = GetActorProperty(0, APROP_TargetTID);
    int pnum = ACS_NamedExecuteWithResult("getPlayerNumberFromTID", ptid);
    ACS_NamedExecuteAlways("setRadius", 0, TEARS_STATS_BY_PLAYER[pnum][0]);
    ACS_NamedExecuteAlways("setHeight", 0, TEARS_STATS_BY_PLAYER[pnum][1]);
    ACS_NamedExecuteAlways("setDamage", 0, TEARS_STATS_BY_PLAYER[pnum][2], ptid);
    ACS_NamedExecuteAlways("setGravity", 0, TEARS_STATS_BY_PLAYER[pnum][3], ptid);
    ACS_NamedExecuteAlways("setSpeed", 0, TEARS_STATS_BY_PLAYER[pnum][4], ptid);
    ACS_NamedExecuteAlways("setScale", 0, TEARS_STATS_BY_PLAYER[pnum][5]);
    ACS_NamedExecuteAlways("setExplosionRange", 0, TEARS_STATS_BY_PLAYER[pnum][5]);

    if (CheckActorInventory(ptid, "ALumpOfCoalGiver")) {
        Thing_SetTranslation(0, 2);
        ACS_NamedExecuteAlways("setDamageBoostPerTime", 0, 1, 35, 10);
    }

    if (CheckActorInventory(ptid, "BloodOfTheMartyrGiver")) Thing_SetTranslation(0, 3);
    if (CheckActorInventory(ptid, "LostContactGiver")) Thing_SetTranslation(0, 10);
    if (CheckActorInventory(ptid, "SacredHeartGiver")) Thing_SetTranslation(0, 8);
    if (CheckActorInventory(ptid, "SpoonBenderGiver")) Thing_SetTranslation(0, 9);
    if (CheckActorInventory(ptid, "TheParasiteGiver")) Thing_SetTranslation(0, 13);
}

script "setGravity" (int gravity) {
    gravity = 0.5 - (FixedMul(0.1, FixedDiv(gravity << 16, 35.0)));
    SetActorProperty(0, APROP_Gravity, gravity);
}

script "setExplosionRange" (int range) {
    if (range < 1) range = 1;
    range = range * 32;
    SetUserVariable(0, "user_explosion_range", range);
}

script "hasHitAnEnemy" (void) {
    SetResultValue(SetActivator(0, AAPTR_TRACER));
}

script "brimstoneFreezeCheck" (void) {
    SetActivator(0, AAPTR_TRACER);
    Delay(1);
    if (GetActorProperty(0, APROP_Health) <= 0) SetActorState(0, "Death.IsaacIce");
}

script "startIsaacMusic" (int number) CLIENTSIDE {
    LocalSetMusic(StrParam(s:"ISAAC", i:number));
}

script "stopMusic" (void) CLIENTSIDE {
    LocalSetMusic("*");
}