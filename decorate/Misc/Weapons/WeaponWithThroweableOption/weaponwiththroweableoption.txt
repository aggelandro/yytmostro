ACTOR WeaponWithGrenadeOption : DoomWeapon {
	Weapon.AmmoType "HandGrenadeAmmo"

	States {
		Ready:
			TNT1 A -1 A_Jump(256, "Ready")
			Wait
			
		Fire:
			TNT1 A -1 A_Jump(256, "Fire")
			Wait
			
		Select:
			TNT1 A -1 A_Jump(256, "Select")
			Wait
			
		Deselect:
			TNT1 A -1 A_Jump(256, "Deselect")
			Wait
			
		GrenadeCheck:
			"####" "#" 0 A_JumpIfInventory("HandGrenadeAmmo", 1, "GrenadeToss")
			"####" "#" 0 A_TakeInventory("ClassThroweableAction", 1)
			"####" "#" -1 A_Jump(256, "Ready")
			Wait
			
		GrenadeToss:
			"####" "#" 0 A_PlaySound("weapons/weapondown", CHAN_WEAPON)
			"####" "########" 1 A_Lower
			TNT1 A 4
			HGRN ABC 1
			HGRN D 2
			HGRN EFG 1
			HGRN HI 2
			TNT1 A 15 A_PlaySound("weapons/hgrenadepin", CHAN_WEAPON)
			TNT1 A 0 A_PlaySound("weapons/hgrenadethrow", CHAN_AUTO)
			TNT1 A 0 A_TakeInventory("HandGrenadeAmmo", 1)
			HGNE A 1 A_FireCustomMissile("ThrownGrenade", 0, 0, 0, 0)
			HGRN J 1
			HGRN KLM 2
			HGRN NO 1
			TNT1 A 6
			HGRN O -1 A_Jump(256, "Select")
			Wait
	}
}

ACTOR ThrownGrenade {  
	Radius 2
	Height 2
	Speed 30
	Damage 8
	Scale 0.4
	SeeSound "weapons/hgrenadebounce"
	+MTHRUSPECIES
	+MISSILE
	+DONTFALL
	+CANBOUNCEWATER
	+DOOMBOUNCE
	+NOTELEPORT
	WallBounceFactor 0.16
	BounceFactor 0.36
	Gravity 0.6
	Mass 400
	Decal "Scorch"
	Obituary "%o was fragged by %k's hand grenade."
	
	States {
		Spawn:
			HGN1 A 3 NoDelay
			HGN1 BCDEFGHABCDEFGHABCDEFGH 3
			Goto Death
			
		Death: 
			TNT1 A 0
			TNT1 A 0 A_Explode  
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_PlaySound("weapons/hgrenadeex")
			TNT1 A 0 A_SpawnItemEx("OrangeParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128) 
			TNT1 AAA 0 A_CustomMissile("SmallRedExplosion", 0, 0, random(0,360), 2, random(0,360))
			TNT1 AAA 0 A_CustomMissile("SmokeSmallExplosion", 0, 0, random(0,360), 2, random(0,360))
			TNT1 AAAA 0 A_SpawnItemEx("Debris",0,0,0,Random(4,-4),Random(4,-4),Random(4,8),0,128)
			TNT1 A 1 A_SpawnItem("RocketEx")
			Stop
	}
}

ACTOR HandGrenadeAmmo : Ammo {
	Scale 0.3
	Inventory.Amount 1
	Inventory.MaxAmount 4
	Ammo.BackpackAmount 2
	Ammo.BackpackMaxAmount 8
	Inventory.RestrictedTo "Agge", "Thanuris", "Ron"
	VisibleToPlayerClass "Agge", "Thanuris", "Ron"
	Inventory.Pickupmessage "Hand Grenade"
	Inventory.PickupSound "weapons/hgrenadepickup"
	Inventory.Icon "GRNPA0"
	
	States {
		Spawn:
			GRNP A -1
			Stop
	}
}

ACTOR ClassThroweableAction : Inventory {
  +INVENTORY.AUTOACTIVATE
  Inventory.Amount 1
  Inventory.MaxAmount 1
}