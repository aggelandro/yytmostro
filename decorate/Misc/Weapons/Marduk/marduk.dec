ACTOR Marduk : YYTWeapon {
	Tag "Marduk"
	Inventory.PickupSound "weapons/mardukpickup"
	Inventory.PickupMessage "You got a Living Weapon, Marduk!"
	Weapon.AmmoType1 "DemonAmmo"
	Weapon.AmmoGive 8
	+NOALERT
	
	States {
		Spawn:
			DGAZ XY 6
			Loop
		
		Ready:
			TNT1 A 0 A_JumpIf(CallACS("demonAmmo") == 0, "ReadyClose")
			DGAZ ABCDDDCB 4 A_WeaponReady
			TNT1 A 0 A_Jump(180, "ReadyBlink")
			Loop
			
		ReadyBlink:
			DGAZ E 2 A_WeaponReady
			DGAZ A 4 A_WeaponReady
			TNT1 A 0 A_Jump(128,"Ready")
			DGAZ E 2 A_WeaponReady
			Goto Ready
			
		ReadyClose:
			TNT1 A 0 A_JumpIf(CallACS("demonAmmo") > 0, "Ready")
			DGAZ E 1
			Loop
			
		Select:
			DGAZ E 1 A_Raise
			Loop
	
		Deselect:
			DGAZ E 1 A_Lower
			Loop
			
		Fire:
			TNT1 A 0 A_JumpIf(CallACS("demonAmmo") < 1, "Ready")
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_PlaySound("weapons/marduk", CHAN_WEAPON, 1, 0, 0.7)
			DGAZ F Random(1, 2) Bright A_GunFlash
			TNT1 A 0 A_FireCustomMissile("DemonTracer", frandom(-0.05, 0.05), 0, 0, 10, 0, frandom(-0.15, 0.15))
			TNT1 A 0 A_TakeInventory("DemonAmmo", 1)
			DGAZ G Random(1, 2) Bright
			DGAZ HIJ Random(1, 2)
			Goto Ready
			
		Flash:
			TNT1 A 2 A_Light1
			TNT1 A 2 A_Light2
			TNT1 A 0 A_Light0
			Goto LightDone
	}
}

ACTOR MardukParasite : StatusEffect {
	var int user_death_time;
	var int user_damage;
	
	States {
		Spawn:
			TNT1 A 0 NoDelay A_SetUserVar("user_death_time", 70)
			TNT1 A 0 A_SetUserVar("user_damage", 1)
			TNT1 A 0 ACS_NamedExecuteWithResult("marduk_startDeathTimer")
			TNT1 A 0 ACS_NamedExecuteWithResult("marduk_parasiteDamage", 35)
			TNT1 A 0 A_JumpIfInventory("MardukParasiteCount", 2, "Spawn2", AAPTR_TRACER)
			TNT1 A 0 A_JumpIf(CallACS("isTracerDead"), "Death")
			MPBR AAA 1 A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height/2, 0, WARPF_NOCHECKPOSITION)
			TNT1 A 0 A_JumpIfInventory("MardukParasiteCount", 2, "Spawn2", AAPTR_TRACER)
			MPBR AAA 1 BRIGHT A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height/2, 0, WARPF_NOCHECKPOSITION)
			Goto Spawn+4
			
		Spawn2:
			TNT1 A 0 A_SetUserVar("user_death_time", 70)
			TNT1 A 0 A_SetUserVar("user_damage", 2)
			TNT1 A 0 A_JumpIfInventory("MardukParasiteCount", 3, "Spawn3", AAPTR_TRACER)
			TNT1 A 0 A_JumpIf(CallACS("isTracerDead"), "Death")
			MPBR BBB 1 A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height/2, 0, WARPF_NOCHECKPOSITION)
			TNT1 A 0 A_JumpIfInventory("MardukParasiteCount", 3, "Spawn3", AAPTR_TRACER)
			MPBR BBB 1 BRIGHT A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height/2, 0, WARPF_NOCHECKPOSITION)
			Goto Spawn2+2
			
		Spawn3:
			TNT1 A 0 A_SetUserVar("user_death_time", 70)
			TNT1 A 0 A_SetUserVar("user_damage", 3)
			TNT1 A 0 A_JumpIfInventory("MardukParasiteCount", 4, "Spawn4", AAPTR_TRACER)
			TNT1 A 0 A_JumpIf(CallACS("isTracerDead"), "Death")
			MPBR CCC 1 A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height/2, 0, WARPF_NOCHECKPOSITION)
			TNT1 A 0 A_JumpIfInventory("MardukParasiteCount", 4, "Spawn4", AAPTR_TRACER)
			MPBR CCC 1 BRIGHT A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height/2, 0, WARPF_NOCHECKPOSITION)
			Goto Spawn3+2
			
		Spawn4:
			TNT1 A 0 A_SetUserVar("user_death_time", 70)
			TNT1 A 0 A_SetUserVar("user_damage", 4)
			TNT1 A 0 A_JumpIfInventory("MardukParasiteCount", 5, "Spawn5", AAPTR_TRACER)
			TNT1 A 0 A_JumpIf(CallACS("isTracerDead"), "Death")
			MPBR DDD 1 A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height/2, 0, WARPF_NOCHECKPOSITION)
			TNT1 A 0 A_JumpIfInventory("MardukParasiteCount", 5, "Spawn5", AAPTR_TRACER)
			MPBR DDD 1 BRIGHT A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height/2, 0, WARPF_NOCHECKPOSITION)
			Goto Spawn4+2
			
		Spawn5:
			TNT1 A 0 A_SetUserVar("user_death_time", 70)
			TNT1 A 0 A_SetUserVar("user_damage", 5)
			TNT1 A 0 A_JumpIfInventory("MardukParasiteCount", 6, "Spawn6", AAPTR_TRACER)
			TNT1 A 0 A_JumpIf(CallACS("isTracerDead"), "Death")
			MPBR EEE 1 A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height/2, 0, WARPF_NOCHECKPOSITION)
			TNT1 A 0 A_JumpIfInventory("MardukParasiteCount", 6, "Spawn6", AAPTR_TRACER)
			MPBR EEE 1 BRIGHT A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height/2, 0, WARPF_NOCHECKPOSITION)
			Goto Spawn5+2
			
		Spawn6:
			TNT1 A 0 A_SetUserVar("user_death_time", 70)
			TNT1 A 0 A_SetUserVar("user_damage", 6)
			TNT1 A 0 A_JumpIf(CallACS("isTracerDead"), "Death")
			MPBR FFF 1 A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height/2, 0, WARPF_NOCHECKPOSITION)
			MPBR FFF 1 BRIGHT A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height/2, 0, WARPF_NOCHECKPOSITION)
			Goto Spawn6+2
			
		Death:
			"####" "#" 0 A_JumpIfInventory("MardukParasiteCount", 6, "ExplosionDeath", AAPTR_TRACER)
			"####" "#" 0 A_TakeInventory("MardukParasiteCount", 0, 0, AAPTR_TRACER)
			"####" "#" 0 A_ChangeFlag("NOGRAVITY", FALSE)
			"####" "#" 0 A_ChangeFlag("NOINTERACTION", FALSE)
			"####" "#" 1 A_FadeOut(0.05)
			Goto Death+4
			
		ExplosionDeath:
			TNT1 A 0 A_SpawnItemEx("RedParticleSpawner", 0, 0, user_se_monster_height/4, 0, 0, 0, 0, 128)
			TNT1 A 0 A_Explode(5*Random(6, 10), 128, 0, 1, 64)
			TNT1 AAA 0 A_SpawnItemEx("BigRedExplosion2", 0, 0, user_se_monster_height/4, 0, 0, 0, random(0,360), 128)
			TNT1 AAA 0 A_SpawnItemEx("SmokeSmallExplosion", 0, 0, user_se_monster_height/4, 0, 0, 0, random(0,360), 128)
			TNT1 A 3
			TNT1 AAAAA 0 A_SpawnItemEx("BloodExplosion", 0, 0, user_se_monster_height/2, 0, 0, 0, random(0,360), 128)
			TNT1 A 0 A_TakeInventory("MardukParasiteCount", 0, 0, AAPTR_TRACER)
			Stop
	}
}

ACTOR MardukBloodSplash {
	Scale 0.75
	Mass 5
	+NOBLOCKMAP
	+FORCEXYBILLBOARD
	+RANDOMIZE
	+DONTSPLASH
	+NOTELEPORT
	+NOINTERACTION
	+NOGRAVITY
	+ALLOWPARTICLES
	
	States {
		Spawn:
			TNT1 A 0 NoDelay A_PlaySound("weapons/mardukhitflesh")
			BLIM ABCDE 3
			Stop
	}
}

ACTOR BloodExplosion {
	Mass 5
	+NOBLOCKMAP
	+PUFFGETSOWNER
	+FORCEXYBILLBOARD
	+DONTSPLASH
	+NOTELEPORT
	+NOGRAVITY
	+ALLOWPARTICLES

	States {
		Spawn:
			TNT1 A 0 NoDelay A_SpawnItemEx("MardukBloodSplash", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_NOCHECKPOSITION)
			TNT1 AAAAAAAAAA 0 A_SpawnItemEx("BloodParticle1", frandom(-8, 8), frandom(-8, 8), frandom(-8, 8), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(0.0, 4.0), frandom(0, 360), SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_NOCHECKPOSITION | SXF_TRANSFERPOINTERS)
			TNT1 AAAAAAAAAA 1 A_SpawnItemEx("BloodParticle2", frandom(-8, 8), frandom(-8, 8), frandom(-8, 8), frandom(-2.5, 2.5), frandom(-2.5, 2.5), frandom(1.0, 2.0), frandom(0, 360), SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_NOCHECKPOSITION)
			TNT1 AAAAAAAAAA 1 A_SpawnItemEx("BloodFloorSplashSpawner", 0, 0, 0, frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(1.0, 4.0), frandom(0, 360), SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_NOCHECKPOSITION)
			Stop
	}
}

ACTOR BloodParticle1 {
	Scale 0.4
	Gravity 0.3
	Mass 5
	+NOBLOCKMAP
	+PUFFGETSOWNER
	+FORCEXYBILLBOARD
	+RANDOMIZE
	+DONTSPLASH
	+NOTELEPORT
	+ALLOWPARTICLES
	+NOCLIP
	+CORPSE
	
	States {
		Spawn:
			TNT1 A 1 NoDelay A_Jump(256, "Blood1", "Blood2", "Blood3", "Blood4", "Blood5", "Blood6")
			Stop
			
		Blood1:
			NBL1 A -1
			Stop
			
		Blood2:
			NBL1 B -1
			Stop
			
		Blood3:
			NBL1 C -1
			Stop
			
		Blood4:
			NBL1 D -1
			Stop
			
		Blood5:
			NBL1 E -1
			Stop
			
		Blood6:
			NBL1 F -1
			Stop
			
		Crash:
			TNT1 A 1 A_SpawnItemEx("BloodSpotSmall", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
			Stop
	}
}

ACTOR BloodParticle2 {
	Radius 1
	Height 2
	Gravity 0.18
	Mass 5
	+NOBLOCKMAP
	+PUFFGETSOWNER
	+FORCEXYBILLBOARD
	+RANDOMIZE
	+DONTSPLASH
	+NOTELEPORT
	+ALLOWPARTICLES
	+NOCLIP
	+CORPSE

	States {
		Spawn:
			NBL2 ABCD 4
			Stop
			
		Crash:
			TNT1 A 1
			Stop
	}
}

ACTOR BloodFloorSplashSpawner {
	Radius 1
	Height 1
	Gravity 0.5
	+CORPSE
	+DONTSPLASH
	+NOTELEPORT
	+NOBLOCKMONST
	+NOBLOCKMAP

	States {
		Spawn:
			TNT1 A -1
			Stop
			
		Crash:
			TNT1 A 0 A_SpawnItemEx("BloodFloorSplash", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY | SXF_NOCHECKPOSITION)
			Stop
	}
}

ACTOR BloodFloorSplash {
	Radius 1
	Height 2
	Scale 0.5
	Mass 5
	+PUFFGETSOWNER
	+FORCEXYBILLBOARD
	+DONTSPLASH
	+NOTELEPORT
	+ALLOWPARTICLES
	
	States {
		Spawn:
			BDRP BCDEF 4
			Stop
	}
}

ACTOR BloodSpotSmall {
	Radius 8
	Height 2
	+NOGRAVITY
	
	States {
		Spawn:
			TNT1 A 1 NoDelay A_PlaySound("weapons/mardukbloodsplash")
			TNT1 A 0 A_Jump(256, "BloodSpotSprite1", "BloodSpotSprite2", "BloodSpotSprite3", "BloodSpotSprite4")
			Stop
			
		BloodSpotSprite1:
			NSPT A 1 A_FadeOut(0.08)
			Loop
			
		BloodSpotSprite2:
			NSPT B 1 A_FadeOut(0.08)
			Loop
			
		BloodSpotSprite3:
			NSPT C 1 A_FadeOut(0.08)
			Loop
			
		BloodSpotSprite4:
			NSPT D 1 A_FadeOut(0.08)
			Loop
	}
}

ACTOR MardukParasiteCount : Inventory { Inventory.Amount 1 Inventory.MaxAmount 6 }