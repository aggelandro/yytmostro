ACTOR Falneitz : YYTWeapon {
	Tag "Falneitz"
	Inventory.PickupSound "misc/hellwp"
	Inventory.PickupMessage "You got the Star Weapon, Falneitz!"
	Weapon.AmmoType1 "DemonAmmo"
	Weapon.AmmoType2 "DemonAmmo"
	Weapon.AmmoGive 30
	Weapon.UpSound "weapons/wepsel"

	States {
		Spawn:
			WFAL A -1
			Stop
			
		Ready:
			TNT1 A 0 A_JumpIfInventory("SlideKickAction", 1, "SlideKick")
			FALN A 1 A_WeaponReady
			Loop
			
		Deselect:
			FALN A 1 A_Lower
			Loop
			
		Select:
			TNT1 A 0 A_TakeInventory("SlideKickAction", 1)
			FALN A 1 A_Raise
			Goto Select+1
			
		Fire:
			TNT1 A 0 A_JumpIf(CallACS("demonAmmo") < 3, "Empty")
			FALN N 1 Bright A_GunFlash("Flash2")
			FALN O 1 Bright A_PlayWeaponSound("weapons/falnfi")
			TNT1 A 0 A_FireCustomMissile("CometNail", frandom(-3, 3), 0, -4, 10, 0, frandom(-1, 1))
			TNT1 A 0 A_FireCustomMissile("CometNail", frandom(-3, 3), 0, 4, 10, 0, frandom(-1, 1))
			TNT1 A 0 A_TakeInventory("DemonAmmo", 3)
			FALN P 1 Bright
			TNT1 A 0 A_Jump(CallACS("player_userJamChance"), "Jam")
			TNT1 A 0 ACS_NamedExecuteWithResult("player_addToUserJamChance", 1)
			FALN A 1 A_Refire
			FALN B 2
			FALN C 3
			FALN A 4
			FALN B 5
			FALN C 6
			Goto Ready
			
		AltFire:
			TNT1 A 0 A_JumpIf(CallACS("demonAmmo") < 15, "Empty")
			FALN E 3 Bright A_GunFlash("Flash1")
			FALN F 0 Bright A_PlayWeaponSound("weapons/falnf2")
			FALN FFFFFFFFFFFFFFFFFFFF 0 Bright A_FireCustomMissile("FalneitzShrapnel", frandom(-11, 11), 0, 0, 10, 0, frandom(-7, 7))
			TNT1 A 0 A_TakeInventory("DemonAmmo", 15)
			FALN F 7 Bright
			FALN GH 4 Bright
			TNT1 A 0 A_Jump(CallACS("player_userOverloadChance"), "Overload")
			TNT1 A 0 ACS_NamedExecuteWithResult("player_addToUserOverloadChance", 10)
			FALN IJKLM 2
			FALN BCABCABCABCA 2 A_PlaySound("weapons/nailin")
			FALN A 2 A_Refire
			Goto Ready
			
		Jam:
			FALN BCABCABCABCABCABCABCA 2 A_PlaySound("weapons/nailin")
			TNT1 A 0 ACS_NamedExecuteWithResult("player_resetUserJamChance")
			Goto Ready
			
		Overload:
			TNT1 A 0 A_JumpIf(CallACS("demonAmmo") < 15, "Empty")
			TNT1 A 0 A_JumpIf(CallACS("demonAmmo") < 15, "OverloadEnd")
			FALN E Random(1, 3) Bright A_GunFlash("Flash1")
			FALN F 0 Bright A_PlayWeaponSound("weapons/falnf2")
			FALN FFFFFFFFFFFFFFFFFFFF 0 Bright A_FireCustomMissile("FalneitzShrapnel", frandom(-11, 11), 0, 0, 10, 0, frandom(-11, 11))
			TNT1 A 0 A_TakeInventory("DemonAmmo", 15)
			FALN F Random(4, 7) Bright
			FALN GH Random(2, 4) Bright
			TNT1 A 0 A_Jump(Random(85, 128), "OverloadEnd")
			Goto Overload+1
			
		OverloadEnd:
			FALN IJKLM 6
			FALN BCABCABCABCA 4 A_PlaySound("weapons/nailin")
			FALN A 4
			TNT1 A 0 ACS_NamedExecuteWithResult("player_resetUserOverloadChance")
			Goto Ready
			
		Empty:
			FALN BCABCABCABCA 2 A_PlaySound("weapons/nailin")
			TNT1 A 0 ACS_NamedExecuteWithResult("player_resetUserOverloadChance")
			Goto Ready
			
	 Flash1:
			TNT1 A 5 Bright A_Light1
			TNT1 A 5 Bright A_Light2
			TNT1 A 3 Bright A_Light1
			TNT1 A 1 Bright A_Light0
			Goto Flash2
			
	 Flash2:
			TNT1 A 1 Bright A_Light1
			TNT1 A 1 Bright A_Light2
			TNT1 A 1 Bright A_Light1
			TNT1 A 1 Bright A_Light0
			Stop
	}
}

ACTOR FalneitzShrapnel {  
	Height 8
	Radius 4
	Bouncetype "Hexen"
	Bouncecount 3
	Speed 128
	Obituary "%o was perforated by %k's shrapnel."
	DeathSound "weapons/tink"
	BounceSound "weapons/trace"
	Projectile
	+THRUGHOST
	+THRUACTORS
	+NOBOSSRIP
	
	States {
		Spawn:
			MFRG BCDEBCDEBCDEFGHIJKLMNOPQ 1 BRIGHT A_SpawnItemEx("FalneitzDamager2", 0, 0, 0, velx, vely, velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			Goto SpawnLoop
			
		SpawnLoop:
			MFRG RST 1 Bright A_SpawnItemEx("FalneitzDamager2", 0, 0, 0, velx, vely, velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			Loop
			
		XDeath:
			TNT1 A 0
			Stop
			
		Crash:
		Death:
			NAIL D 2
			TNT1 A 0 A_SetTranslucent(1.0, 1)
			NAIL EFGHI 2 Bright A_FadeOut(0.1)
			Stop
	}
}

ACTOR FalneitzDamager {
	Projectile
	Height 8
	Radius 4
	Damage 2
	Speed 0
	+PAINLESS
	+BLOODSPLATTER
	+HITTRACER
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	Obituary "%k killed %o with fragmenting nails."
	
	States {
		Spawn:
			TNT1 A 1
			Stop
			
		Death:
			TNT1 A 0
			Stop
	}
}

ACTOR FalneitzDamager2 : FalneitzDamager { Damage 3 }
ACTOR FalneitzDamager3 : FalneitzDamager { Damage 4 }

ACTOR CometNail : FastProjectile {
	Radius 3
	Height 6
	Speed 128
	Obituary "%k killed %o with fragmenting nails."
	DeathSound "weapons/nailhi"
	+THRUGHOST
	+THRUACTORS
	+NOBOSSRIP
	+NODAMAGETHRUST
	+HITTRACER
	+FORCEXYBILLBOARD
	
	States {
		Spawn:
			NAIL B 1 A_SpawnItemEx("FalneitzDamager3", 0, 0, 0, velx, vely, velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			Loop
			
		Death:
			IMFX Q 2 Bright A_SetTranslucent(1.0, 1)	  
			IMFX R 2 Bright A_SpawnItemEx("CometNail2", 0, 0, 1, 70, 0, 0, 45, 0)
			TNT1 A 0 A_SpawnItemEx("CometNail2", 0, 0, 1, 70, 0, 0, 135, 0)
			TNT1 A 0 A_SpawnItemEx("CometNail2", 0, 0, 1, 70, 0, 0, -45, 0)
			TNT1 A 0 A_SpawnItemEx("CometNail2", 0, 0, 1, 70, 0, 0, -135, 0)
			IMFX STUVW 2 Bright A_Fadeout(0.05)
			Stop
	}
}

ACTOR CometNail2 : FastProjectile {
	Damage 2
	Radius 2
	Height 4
	Speed 128
	Obituary "%o got nailed by %k"
	+THRUGHOST
	+THRUACTORS
	+NOBOSSRIP
	+NODAMAGETHRUST
	+FORCEXYBILLBOARD
	
	States {
		Spawn:
			NAIL C 1 Bright A_SpawnItemEx("FalneitzDamager", 0, 0, 0, velx, vely, velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			Loop
			
		Crash:
		Death:
			NAIL D 2 Bright A_ChangeFlag("FORCEXYBILLBOARD", TRUE)
			TNT1 A 0 A_SetTranslucent(1.0, 1)
			TNT1 A 0 A_Playsound("weapons/trace")
			NAIL EFGHI 2 Bright A_Fadeout(0.125)
			Stop
			
		XDeath: 
			TNT1 A 0 A_PlaySound("weapons/trace")
			Stop
	}
}