Actor AbstractTracer: FastProjectile
{
 damage 0
 radius 2
 height 2
 speed 140
 renderstyle ADD
 alpha 0.9
 scale .15
 PROJECTILE
 +RANDOMIZE
 +FORCEXYBILLBOARD
 +DONTSPLASH
 +NOEXTREMEDEATH
 states
 {
  Spawn:
   TRAC A 1 BRIGHT
   Loop
  Death:
   Stop
  XDeath:
   TNT1 A 0
   Stop
 }
}

Actor BulletTracer : AbstractTracer
{
 speed 80
 damage (random(3,4))
 damagetype "Bullet"
 Decal BulletChip
 +BLOODSPLATTER 
 +THRUGHOST
 -DONTSPLASH
 +MISSILE
 States
 {
  Spawn:
   TRAC A 1 NoDelay BRIGHT
   TNT1 A 0 ThrustThingZ(0,random(-8, 8),0,1)
   TRAC A 1 BRIGHT
   Goto Idle	
  Idle:
   TRAC A 1 BRIGHT
   Loop	
  Death:
   TNT1 A 1 A_SpawnItem("BulletPuff")
   Stop
 }
}

Actor ZombieTracer : BulletTracer { damage (random(1,5)*3) }
Actor ShotgunnerTracer : BulletTracer { damage (random(1,5)*3) }
Actor ChaingunnerTracer : BulletTracer { damage (random(1,5)*3) }
Actor JZombieTracer : BulletTracer { damage 4 }
Actor AggeSSGTracer : BulletTracer {}
Actor TerrorPistolTracer : BulletTracer { damage (random(5,7)) }

Actor TerrorWeakShotgunTracer : BulletTracer {
	Damage (random(4,5))
	
	States {
		Death:
		 TNT1 A 1 A_SpawnItem("SSTWeakShotgunPuff")
		 Stop
	}
}

Actor TerrorShotgunTracer : BulletTracer {
	damage (random(3,6))
	
	States {
		Death:
		 TNT1 A 1 A_SpawnItem("SSTShotgunPuff")
		 Stop
	}
}

Actor TerrorSSGTracer : BulletTracer { damage 2 }
ACTOR EterealRifleTracer : BulletTracer {
	DamageType "Lightning"
	PoisonDamage 2,3,0
	PoisonDamageType Lightning
	Species "Player"
	-THRUGHOST
	+THRUSPECIES
	
	States {
		Spawn:
			TRAB A 1 NoDelay BRIGHT
			TNT1 A 0 ThrustThingZ(0,random(-8, 8),0,1)
			TRAB A 1 BRIGHT
			Goto Idle
			
		Idle:
			TRAB A 1 BRIGHT
			Loop
			
		XDeath:
			TNT1 A 0 A_Jump(192, 2)
			TNT1 A 0 A_SpawnItemEx("DecorativeSparks",random(-4,4),random(-4,4),1,-2,random(-1,3),-1)
			TNT1 A 0 A_SetScale(0.05)
			ZA99 ABCDEFG 3
			Stop
			
		Death:
			TNT1 A 1 A_SpawnItem("BulletPuff")
			Stop
 }
}