//inventory actors for weapons
ACTOR CargaUnica : Ammo
{
  Inventory.PickupMessage "Got an unique weapon charge!"
  Inventory.Amount 1
  Inventory.MaxAmount 1
  Ammo.BackpackAmount 0
  Ammo.BackpackMaxAmount 1
  Inventory.Icon "CLIPA0"
}
ACTOR CargaAgge : CargaUnica
{
	Inventory.Amount 10
	Inventory.MaxAmount 10
	Ammo.BackpackMaxAmount 10
}

//projectiles and effects for monsters and weapons
ACTOR Hellfire : ArchvileFire
{
  Translation "208:231=192:207","248:249=192:207","160:167=192:207"
  DamageType "AfireF"
  +PAINLESS
  +MOVEWITHSECTOR
  -SOLID
  +FORCERADIUSDMG
  -NOGRAVITY
  +NODAMAGETHRUST
  Gravity 1
  States
  {
  // fire is active for 492 frames
  Spawn:
    FIRE A 2 bright 
    FIRE BAB 2 bright 
    FIRE C 2 bright 
    FIRE BCBCDCDCDEDED 6 bright A_Explode(6,20,false,false)
	FIRE BCBCDCDCDEDED 6 bright A_Explode(6,20,false,false)
	FIRE BCBCDCDCDEDED 6 bright A_Explode(6,20,false,false)
    FIRE E 2 bright
    FIRE FGH 4 bright
    Stop
  }
}
ACTOR AggeNewFire {
	Radius 8
	Height 40
	RenderStyle "Add"
	DamageType "FireBF"
	Alpha 0.90
	Projectile
	+PAINLESS
	-NOGRAVITY
	+LOWGRAVITY
	+NOEXPLODEFLOOR
	+NODAMAGETHRUST
	
	States {
		Spawn:
			TNT1 A 0 NoDelay A_PlaySound("agge/flame", CHAN_5, 1.0, TRUE)
			FLME A 2 bright A_Explode(5, 32, 0)
			FLME BCDEFGHIJKLMN 2 bright A_Explode(5, 16, 0)
			FLME A 2 bright A_Explode(5, 32, 0)
			FLME BCDEFGHIJKLMN 2 bright A_Explode(5, 16, 0)
			FLME A 2 bright A_Explode(5, 32, 0)
			FLME BCDEFGHIJKLMN 2 bright A_Explode(5, 16, 0)
			FLME A 2 bright A_Explode(5, 32, 0)
			FLME BCDEFGHIJKLMN 2 bright A_Explode(5, 16, 0)
			FLME A 2 bright A_Explode(5, 32, 0)
			FLME BCDEFGHIJKLMN 2 bright A_Explode(5, 16, 0)
			TNT1 A 0 A_Jump(192, "Death")
			Loop
		
		Death:
			TNT1 A 0 A_StopSound(CHAN_5)
			TNT1 A 1
			Stop
	}
}
ACTOR HellfireM : Hellfire
{
	-PAINLESS
	-FORCERADIUSDMG
	DamageType "AFire"
	Alpha 0.5
	Obituary "%o was burned alive"
	States
	{
	Spawn:
    FIRE A 2 bright 
    FIRE BAB 2 bright 
    FIRE C 2 bright 
    FIRE BCBCDCDCDEDED 4 bright A_Explode(4,20,false,false)
	FIRE BCBCDCDCDEDED 4 bright A_Explode(4,20,false,false)
	FIRE BCBCDCDCDEDED 4 bright A_Explode(4,20,false,false)
    FIRE E 2 bright
    FIRE FGH 4 bright
    Stop
	}
}
ACTOR FlameThrowerFire : PlasmaBall
{
	Translation "208:231=192:207","248:249=192:207","160:167=192:207"
	+NODAMAGETHRUST
	DamageType "FireBF"
	RenderStyle "Add"
	Alpha 0.5
	Damage 1
	Speed 20
	+FORCERADIUSDMG
	+THRUACTORS
	SeeSound "agge/flamefire"
	DeathSound "agge/flamehit"
	
	States {
		Spawn:
			FRFX ABCD 2 Bright A_Explode(5*random(2,5),15)
			TNT1 A 0 A_LowGravity
			FRFX EFGHIJKLMNOP 2 Bright A_Explode(5*random(2,5),15)
			Goto Death
			
		Death:
			FRFX C 1 Bright A_SpawnItemEx("AggeNewFire",5,0,0,0,0,0,0)
			Stop
			
		XDeath:
			FIRE C 1 Bright
			Stop
	}
}

ACTOR FlameThrowerFireF : FlameThrowerFire
{
	DamageType "AFire"
	Speed 35
	States
	{
	Spawn:
		FIRE A 1 Bright
		FIRE A 0 Bright A_Explode(4*random(1,2),15)
		FIRE B 1 Bright
		FIRE A 0 Bright A_Explode(4*random(1,2),15)
		FIRE C 1 Bright
		FIRE A 0 Bright A_Explode(4*random(1,2),15)
		Loop
	Death:
		FIRE C 1 Bright A_SpawnItemEx("HellFireM",20,0,0,0,0,0,0)
		Stop
	}
}
ACTOR BulletPuffF : BulletPuff
{
	DamageType "Friendly"
}
ACTOR PlasmaBallF : PlasmaBall
{
	DamageType "Friendly"
}

ACTOR ElectricSpark1 {
	+NOGRAVITY
	+BRIGHT
	
	States {
	  Spawn:
			LEFF I 1 NoDelay A_PlaySound("elect/spark", CHAN_AUTO)
			LEFF A 1
			LEFF H 1
			LEFF B 1
			LEFF D 1
			Stop
	}
}

ACTOR ElectricSpark2 {
	+NOGRAVITY
	+BRIGHT
	
	States {
	  Spawn:
			LEFF C 1 NoDelay A_PlaySound("elect/spark", CHAN_AUTO)
			LEFF F 1
			LEFF G 1
			LEFF H 1
			LEFF E 1
			Stop
	}
}

ACTOR StatusEffect {
	+NOGRAVITY
	+NOINTERACTION
	+SEEKERMISSILE
	var int user_se_monster_height;
	
	States {
	
		Death:
			TNT1 A 0
			Stop
	}
}

ACTOR BleedingEffect : StatusEffect {
	
	States {
	  Spawn:
			BLEE ABCDEFGHI 2 A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height, 0, WARPF_NOCHECKPOSITION)
			Loop
	}
}

ACTOR PoisonEffect : StatusEffect {
	
	States {
	  Spawn:
			POIS ABCDEFG 2 A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height, 0, WARPF_NOCHECKPOSITION)
			Loop
	}
}

ACTOR FearEffect : StatusEffect {
	
	States {
	  Spawn:
			FEAR ABCB 2 A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height, 0, WARPF_NOCHECKPOSITION)
			Loop
	}
}

ACTOR PetrifyingEffect : StatusEffect {
	
	States {
	  Spawn:
			PETR ABCB 20 A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height, 0, WARPF_NOCHECKPOSITION)
			Loop
	}
}

ACTOR BurnEffect : StatusEffect {
	
	States {
	  Spawn:
			BUR2 ABCDEF 2 A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height, 0, WARPF_NOCHECKPOSITION)
			Loop
	}
}

ACTOR SlowEffect : StatusEffect {
	
	States {
	  Spawn:
			SLO2 AB 5 A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height, 0, WARPF_NOCHECKPOSITION)
			Loop
	}
}

ACTOR LightFollower : StatusEffect {

	States {
	  Spawn:
			TNT1 A 0 NoDelay A_JumpIf(ACS_NamedExecuteWithResult("checkForNoGiverInTracerInventory", 38), 3)
			TNT1 B 1 A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height, 0, WARPF_NOCHECKPOSITION)
			TNT1 A 0 A_Jump(256, 5)
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("checkForNoGiverInTracerInventory", 40), 3)
			TNT1 C 1 A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height, 0, WARPF_NOCHECKPOSITION)
			TNT1 A 0 A_Jump(256, 2)
			TNT1 A 1 A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height, 0, WARPF_NOCHECKPOSITION)
			TNT1 A 0 ACS_NamedExecuteWithResult("killIfPlayerIsDead")
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("checkForNoGiverInTracerInventory", 39), "Death")
			Loop
	}
}

ACTOR HealEffect : StatusEffect {
	Scale 0.65
	
	States {
		Spawn:
			TNT1 A 0 NoDelay A_PlaySound("misc/heal", CHAN_AUTO)
			AREH A 1 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			AREH B 2 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			AREH C 1 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			AREH D 2 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			AREH E 1 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			AREH F 2 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			AREH G 1 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			AREH H 2 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			AREH I 1 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			AREH J 2 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			AREH K 1 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			AREH L 2 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			AREH M 1 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			AREH N 2 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			AREH O 1 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			AREH P 2 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			AREH Q 1 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			AREH R 2 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			AREH S 1 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			AREH T 2 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			Stop
	}
}

ACTOR HealEffectArea : HealEffect {
	RenderStyle "Stencil"
	StencilColor "Red"
}

ACTOR DmgUpEffect : StatusEffect {
	RenderStyle Add
	Alpha 0.9
	var int user_se_monster_radius;

	States {
	  Spawn:
			TNT1 A 0 NoDelay A_Jump(256, "Sprite1", "Sprite2", "Sprite3", "Sprite4", "Sprite5", "Sprite6")
			TNT1 A 15
			Loop
			
		Sprite1:
			FURI A 2 A_Warp(AAPTR_TRACER, random(-user_se_monster_radius/2, user_se_monster_radius/2), random(-user_se_monster_radius/2, user_se_monster_radius/2), random(-(user_se_monster_height/5), user_se_monster_height/5), 0, WARPF_NOCHECKPOSITION)
			Goto Spawn+1
			
		Sprite2:
			FURI B 2 A_Warp(AAPTR_TRACER, random(-user_se_monster_radius/2, user_se_monster_radius/2), random(-user_se_monster_radius/2, user_se_monster_radius/2), random(-(user_se_monster_height/5), user_se_monster_height/5), 0, WARPF_NOCHECKPOSITION)
			Goto Spawn+1
		
		Sprite3:
			FURI C 2 A_Warp(AAPTR_TRACER, random(-user_se_monster_radius/2, user_se_monster_radius/2), random(-user_se_monster_radius/2, user_se_monster_radius/2), random(-(user_se_monster_height/5), user_se_monster_height/5), 0, WARPF_NOCHECKPOSITION)
			Goto Spawn+1
		
		Sprite4:
			FURI D 2 A_Warp(AAPTR_TRACER, random(-user_se_monster_radius/2, user_se_monster_radius/2), random(-user_se_monster_radius/2, user_se_monster_radius/2), random(-(user_se_monster_height/5), user_se_monster_height/5), 0, WARPF_NOCHECKPOSITION)
			Goto Spawn+1
			
		Sprite5:
			FURI E 2 A_Warp(AAPTR_TRACER, random(-user_se_monster_radius/2, user_se_monster_radius/2), random(-user_se_monster_radius/2, user_se_monster_radius/2), random(-(user_se_monster_height/5), user_se_monster_height/5), 0, WARPF_NOCHECKPOSITION)
			Goto Spawn+1
		
		Sprite6:
			FURI F 2 A_Warp(AAPTR_TRACER, random(-user_se_monster_radius/2, user_se_monster_radius/2), random(-user_se_monster_radius/2, user_se_monster_radius/2), random(-(user_se_monster_height/5), user_se_monster_height/5), 0, WARPF_NOCHECKPOSITION)
			Goto Spawn+1
	}
}

ACTOR IceTrail {
  Speed 0
  RenderStyle Add
  Alpha 0.75
  +RANDOMIZE
	+NOINTERACTION
	+NOGRAVITY
	
  States {
		Spawn:
			TNT1 A 5
			GG4S ABC 3 Bright
			TNT1 A 0 A_Jump(191,3)
			TNT1 A 0 A_CustomMissile("IceTrail", 0, 24, 0)
			TNT1 A 0 A_CustomMissile("IceTrail", 0, -24, 0)
			GG4S DEFGHIJKLMNOPQR 3 Bright
			Stop
	}
}

ACTOR IceOrbitEffect : StatusEffect {
	var int user_spawn_tid;
	var int user_se_monster_radius;
	
	States {
	  Spawn:
			TNT1 A 0 NoDelay A_SetUserVar("user_spawn_tid", ACS_NamedExecuteWithResult("getUniqueTID"))
			TNT1 A 0 A_SpawnItemEx("IceOrbit", random(-user_se_monster_radius/2, user_se_monster_radius/2), random(-user_se_monster_radius/2, user_se_monster_radius/2), random(-(user_se_monster_height/5), user_se_monster_height/5), 0, 0, 0, 0, 128, 0, user_spawn_tid)
			TNT1 A 0 ACS_NamedExecuteWithResult("setTIDTracerPointerToSelfTracer", user_spawn_tid)
			TNT1 A 0 A_SetUserVar("user_spawn_tid", ACS_NamedExecuteWithResult("getUniqueTID"))
			TNT1 A 0 A_SpawnItemEx("IceOrbit", random(-user_se_monster_radius/2, user_se_monster_radius/2), random(-user_se_monster_radius/2, user_se_monster_radius/2), random(-(user_se_monster_height/5), user_se_monster_height/5), 0, 0, 0, 0, 128, 0, user_spawn_tid)
			TNT1 A 0 ACS_NamedExecuteWithResult("setTIDTracerPointerToSelfTracer", user_spawn_tid)
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			TNT1 A 1 A_Warp(AAPTR_TRACER, 0, 0, user_se_monster_height/2, 0, WARPF_NOCHECKPOSITION)
			TNT1 A 0 ACS_NamedExecuteWithResult("killEffectIfMonsterIsDead")
			TNT1 A 0 A_JumpIfInventory("LoopFlag", Random(55, 137), 1)
			Goto Spawn+6
			TNT1 A 0 A_TakeInventory("LoopFlag", 137)
			Goto Spawn+3
	}
}

ACTOR IceOrbit {
	Speed 7
  RenderStyle Add
  Alpha 0.75
  +RANDOMIZE
	+NOINTERACTION
	+NOGRAVITY
	+SEEKERMISSILE
	
	States {
		Spawn:
			ICPR AAABBBCCC 1 A_SpawnItemEx("IceOrbitTrail",0,0,0,0,0,0,0,128)
			TNT1 A 0 A_SeekerMissile(32, 40, SMF_PRECISE)
			ICPR AAABBBCCC 1 A_SpawnItemEx("IceOrbitTrail",0,0,0,0,0,0,0,128)
			TNT1 A 0 A_SeekerMissile(32, 40, SMF_PRECISE)
			ICPR AAABBBCCC 1 A_SpawnItemEx("IceOrbitTrail",0,0,0,0,0,0,0,128)
			TNT1 A 0 A_SeekerMissile(32, 40, SMF_PRECISE)
			ICPR AAABBBCCC 1 A_SpawnItemEx("IceOrbitTrail",0,0,0,0,0,0,0,128)
			TNT1 A 0 A_SeekerMissile(32, 40, SMF_PRECISE)
			ICPR AAABBBCCC 1 A_SpawnItemEx("IceOrbitTrail",0,0,0,0,0,0,0,128)
			TNT1 A 0 A_SeekerMissile(32, 40, SMF_PRECISE)
			ICPR AAABBBCCC 1 A_SpawnItemEx("IceOrbitTrail",0,0,0,0,0,0,0,128)
			TNT1 A 0 A_SeekerMissile(32, 40, SMF_PRECISE)
			ICPR AAABBBCCC 1 A_SpawnItemEx("IceOrbitTrail",0,0,0,0,0,0,0,128)
			TNT1 A 0 A_SeekerMissile(32, 40, SMF_PRECISE)
			ICPR AAABBBCCC 1 A_SpawnItemEx("IceOrbitTrail",0,0,0,0,0,0,0,128)
			TNT1 A 0 A_SeekerMissile(32, 40, SMF_PRECISE)
			ICPR AAABBBCCC 1 A_SpawnItemEx("IceOrbitTrail",0,0,0,0,0,0,0,128)
			TNT1 A 0 A_SeekerMissile(32, 40, SMF_PRECISE)
			ICPR AAABBBCCC 1 A_SpawnItemEx("IceOrbitTrail",0,0,0,0,0,0,0,128)
			TNT1 A 0 A_SeekerMissile(32, 40, SMF_PRECISE)
			Goto Death
			
		Death:
			ICPR DEFGH 3
			Stop
	}
}

ACTOR IceOrbitTrail {
	Scale 0.85
	Renderstyle Add
	Alpha 0.65
	+NOGRAVITY
	+FLOAT
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+DONTSPLASH
	+NOCLIP
	+NOINTERACTION
	+THRUACTORS
	
	States {
		Spawn:
			ICPR ABC 2 BRIGHT A_FadeOut(0.25)
			Loop
	}
}

//blood effects
ACTOR BlueBlood : Blood  {
	Translation "168:191=192:207"
}
ACTOR GreenBlood : Blood {
	Translation "168:191=112:127"
}
//monsters that I use more than once
ACTOR MiniArachnotron
{
  RenderStyle Stencil
  StencilColor Red
  Species Spider
  Health 1
  Radius 6
  Height 6
  Scale 0.1
  Mass 20
  Speed 10
  PainChance 0
  Monster
  -COUNTKILL
  +FLOORCLIP
  +BOSSDEATH
  +ALWAYSFAST
  +THRUSPECIES
  -SOLID
  SeeSound "arania/sight"
  PainSound "baby/pain"
  DeathSound "arania/death"
  ActiveSound "arania/active"
  Obituary "%o 's heels were bitten off by a spider."
  States
  {
  Spawn:
    BSPI AB 10 A_Look
    Loop
  See:
    BSPI A 2 Fast A_Chase
    BSPI ABBCC 3 Fast A_Chase
    BSPI D 2 Fast A_Chase
    BSPI DEEFF 2 Fast A_Chase
    Goto See
  Melee:
    BSPI A 1 Fast Bright A_FaceTarget
    BSPI G 1 Fast Bright A_CustomMeleeAttack(1)
    BSPI H 3 Fast Bright
    BSPI H 3 Fast Bright
    Goto See
  Pain:
    BSPI I 3
    BSPI I 3 A_Pain
    Goto See+1
	Pain.Lightning:
		TNT1 A 0 A_SpawnItemEx("ElectricSpark1",random(-10,10),random(-15,-5),random(0,Height*0.5),0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("ElectricSpark2",random(-10,10),random(5,15),random(0,Height*0.5),0,0,0,0)
    Goto Pain
  Death:
    BSPI J 20 A_Scream
    BSPI K 7 A_NoBlocking
    BSPI LMNO 7
    BSPI P -1 A_BossDeath
    Stop
  Raise:
    BSPI P 5
    BSPI ONMLKJ 5
    Goto See+1
  }
}

ACTOR FriendlyMiniArachnotron : MiniArachnotron {
	+FRIENDLY
	RenderStyle Stencil
  StencilColor DarkBlue
	
	States {
	
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("deathTimer", 2100) // Viven 1 min
			Goto Super::Spawn
			
		Melee:
			BSPI A 5 Fast Bright A_FaceTarget
			BSPI G 10 Fast Bright A_CustomMeleeAttack(1)
			BSPI H 3 Fast Bright
			BSPI H 3 Fast Bright
			Goto See
	}
}