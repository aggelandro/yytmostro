ACTOR RonTurret
{
  Translation "64:79=224:231,9:12=224:231"
  Health 50
  Radius 20
  Height 24
  Mass 100000
  Speed 0
  PainChance 0
  Monster
  +FLOORCLIP
  -SOLID
  +NOBLOOD
  +FRIENDLY
  +NORADIUSDMG
  +ALWAYSFAST
  +PAINLESS
  HitObituary "$OB_IMPHIT"
  Obituary "$OB_IMP"
  var int user_DuracionDisparos;
  States
  {
  Spawn:
    TURR A 100
    Goto ActualSpawn
  ActualSpawn:
	TURR A 1 A_Look
    Loop
  See:
    TURR A 3 Fast A_Chase
    Loop
  Melee:
  Missile:
	TURR A 0 A_JumpIf(user_DuracionDisparos > 19, "Death")
    TURR A 8 A_FaceTarget
	TURR A 0 A_SetUserVar("user_DuracionDisparos", user_DuracionDisparos+1)
    TURR A 0 A_CustomMissile("MiniRocketF",16,15)
	TURR A 0 A_CustomMissile("MiniRocketF",16,-15)
	TURR BC 5 
	TURR A 0 A_SpidRefire
    Goto Missile
  Death:
    TURR D 0
    TURR D 0 A_PlaySound("baby/death")
    TURR D 0
    TURR D 0 A_NoBlocking
	TURR D 0 A_SpawnItemEX("RonTurretItem")
    Stop
  }
}
ACTOR RonTurretItem : CustomInventory
{
  +COUNTITEM
  +INVENTORY.INVBAR
  Inventory.ForbiddenTo "Agge","Terro","zLayer","Lucianeto"
  Inventory.RestrictedTo "Ron"
  Inventory.PickupMessage "You got a Turret!"
  Inventory.PickupSound "misc/p_pkup"
  Inventory.Icon "TURA0"
  Inventory.Amount 1
  Inventory.MaxAmount 1
  States
  {
  Spawn:
    TURR D -1
    Stop
  Use:
	TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEX("RonTurret")
    Stop
  }
}
ACTOR MiniRocketF : Rocket
{
	Scale 0.2
	Radius 2
	Height 2
	Speed 20
	Damage 8
	DamageType Friendly
	-RANDOMIZE
	States
	{
		Death:
			MISL B 8 Bright A_Explode(random(5,10),20)
			MISL C 6 Bright
			MISL D 4 Bright
			Stop
	}
}