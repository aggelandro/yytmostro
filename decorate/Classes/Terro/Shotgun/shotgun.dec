actor TerroShotgun : WeaponWithSubWeaponOption {
  Weapon.AmmoGive 8
  Weapon.AmmoType "Shell"
  Inventory.PickupMessage "Te pasaron la escopeta!"
  Obituary "%o was pelted by %k's shotgun."
  Decal "bulletchip"
  Attacksound "misc/null"
  +NOALERT
	var int user_fire_count;
	
	States {
		Spawn:
			HSHT Z -1
			Stop
			
		Ready:
			HSHT A 0 A_JumpIfInventory("ClassThroweableAction", 1, "SubWeaponCheck")
			TNT1 A 0 A_JumpIfInventory("SlideKickAction", 1, "SlideKick")
			HSHT A 1 A_WeaponReady
			Loop
			
		Deselect:
			HSHT A 1 A_Lower
			Loop
			
		Select:
			TNT1 A 0 A_TakeInventory("ClassThroweableAction", 1)
			TNT1 A 0 A_TakeInventory("SlideKickAction", 1)
			HSHT A 1 A_Raise
			Loop
			
		SuperSelect:
			TNT1 A 0 A_TakeInventory("ClassThroweableAction", 1)
			HSHT A 1 A_Raise
			HSHT A 0 A_Raise
			Loop
			
		DryFire:
			TNT1 A 0 A_JumpIfInventory("TerroSGFireCount", 1, "Reload")
			HSHT A 5 A_PlayWeaponSound("agge/ssgclick")
			Goto Ready
			
		Fire:
			TNT1 A 0 A_JumpIf(CallACS("shells") == 0, "DryFire")
			TNT1 A 0 A_TakeInventory("FireReleased", 1)
			TNT1 A 0 ACS_NamedExecuteWithResult("readHoldingFireButtonFor", 5)
			HSHT A 1
			TNT1 A 0 A_TakeInventory("Shell", 1)
			TNT1 A 0 A_PlaySoundEx("weapons/slamfire2", CHAN_AUTO)
			TNT1 A 0 Radius_Quake(2,4,0,1, 0)
			TNT1 A 0 A_GunFlash
			HSHT NO 2 BRIGHT
			HSHT O -1 A_Jump(256, "Fire1", "Fire2", "Fire3")
			Wait
			
		Fire1:
			TNT1 A 0 A_FireCustomMissile("TerrorShotgunTracer", frandom(-1,1), 1, 0, -2, 0, frandom(-1.25,1.25))
			TNT1 AA 0 A_FireCustomMissile("TerrorShotgunTracer", frandom(-1,1), 0, 0, -2, 0, frandom(-1.25,1.25))
			TNT1 AA 0 A_FireCustomMissile("TerrorShotgunTracer", frandom(-1.1,1.1), 0, 0, -2, 0, frandom(-2,2))
			TNT1 AA 0 A_FireCustomMissile("TerrorShotgunTracer", frandom(-1.1,1.1), 0, 0, -2, 0, frandom(-1.5,1.5))
			TNT1 AAAAA 0 A_FireCustomMissile("TerrorShotgunTracer", frandom(-1.5,1.5), 0, 0, -2, 0, frandom(-1.5,1.5))
			Goto FireEnd
			
		Fire2:
			TNT1 A 0 A_FireCustomMissile("TerrorShotgunTracer", frandom(-1.37,1.37), 1, 0, -2, 0, frandom(-1.37,1.37))
			TNT1 AAAAAAAAAAA 0 A_FireCustomMissile("TerrorShotgunTracer", frandom(-1.37,1.37), 0, 0, -2, 0, frandom(-1.37,1.37))
			Goto FireEnd
			
		Fire3:
			TNT1 A 0 A_FireCustomMissile("TerrorShotgunTracer", frandom(-0.5,0.5), 1, 0, -2, 0, frandom(-1,1))
			TNT1 AAAAA 0 A_FireCustomMissile("TerrorShotgunTracer", frandom(-0.5,0.5), 0, 0, -2, 0, frandom(-1,1))
			TNT1 AAAAAA 0 A_FireCustomMissile("TerrorShotgunTracer", frandom(-1,1), 0, 0, -2, 0, frandom(-1,1))
			Goto FireEnd
			
		FireEnd:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_GiveInventory("TerroSGFireCount", 1)
			TNT1 A 0 A_JumpIf(CallACS("terroSGFireCount")>=3 || !CallACS("fireReleased"), 3)
			HSHT P 2 A_WeaponReady(WRF_NOSWITCH)
			HSHT QRSTUVVAAA 1
			Goto Reload
			HSHT P 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
			HSHT QRSTUVVAAA 1
			Goto Reload
			
		Reload:
			HSHT BCD 1
			TNT1 A 0 A_PlaySound("terro/shotgunpump", CHAN_WEAPON)
			HSHT EEFGHIJKLM 1
			TNT1 A 0 A_SpawnItemEx("ShellCasing",10,-2,25,Random(-4,-5),Random(-4,-5),Random(4,6),270)
			HSHT MMLKJIHGFE 1
			TNT1 A 0 A_TakeInventory("TerroSGFireCount", 1)
			TNT1 A 0 A_JumpIf(CallACS("terroSGFireCount")==0, 1)
			Goto Reload+3
			HSHT DCB 1
			TNT1 A 0
			Goto Ready
			
		Flash:
			TNT1 A 2 A_Light1
			TNT1 A 2 A_Light2
			TNT1 A 0 A_Light0
			Goto LightDone
  }
}

ACTOR TerroSGFireCount : Inventory { Inventory.MaxAmount 3 Inventory.Amount 1 }
ACTOR FireReleased : Inventory { Inventory.MaxAmount 1 Inventory.Amount 1 }