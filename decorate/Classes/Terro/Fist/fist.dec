ACTOR TerroFist : Weapon
{
	+NOAUTOFIRE
	States
	{
	Ready:
		PERR A 0 A_JumpIfInventory("TerroFistFire",10,"Transform")
		PERR A 0 A_JumpIfInventory("TerroFistDuration",1,"FireReady")
		PERR A 1 A_WeaponReady
		Loop
	FireReady:
		PERF A 0 A_TakeInventory("TerroFistDuration",1)
		PERF A 0 A_JumpIfInventory("TerroFistDuration",1,1)
		Goto TransformOff
		PERF A 1 A_WeaponReady
		Loop
    Deselect:
		PERR A 0 A_JumpIfInventory("TerroFistDuration",1,2)
		PERR A 1 A_Lower
		Loop
		PERF A 1 A_Lower
		Loop
    Select:
		PERR A 0 A_JumpIfInventory("TerroFistDuration",1,2)
		PERR A 1 A_Raise
		Loop
		PERF A 1 A_Raise
		Loop
	Transform:
		TERR A 0 A_TakeInventory("TerroFistFire",10)
		TERF A 0 A_GiveInventory("TerroFistDuration",350)
		TERR ZYXW 2
		TERR V 8 A_PlaySound("terro/punio10",CHAN_AUTO)
		TERF V 8
		TERF WYXZ 2
		Goto FireReady
	TransformOff:
		TERF ZYXW 2
		TERF V 8 A_PlaySound("terro/puniooff",CHAN_AUTO)
		TERR A 0 A_TakeInventory("TerroFistFire",10)
		TERF A 0 A_TakeInventory("TerroFistDuration",350)
		TERR V 8
		TERR WYXZ 2
		Goto Ready
    Fire:
		TERR A 0 A_JumpIfInventory("PowerStrength",1,"BerserkPunch")
		TERR A 0 A_JumpIfInventory("TerroFistDuration",1,"FirePunch")
		TERR MN 1
		TERR D 0 A_PlaySound("terro/puniol",CHAN_WEAPON)
		TERR O 1 A_CustomPunch(1 * random(1, 10), TRUE,0,"TerroFistPuff")
		TERR N 1
		TERR M 1
		Goto Ready
	Altfire:
		TERR A 0 A_JumpIfInventory("PowerStrength",1,"BerserkAltPunch")
		TERR A 0 A_JumpIfInventory("TerroFistDuration",1,"FireAltPunch") 
		TERR A 1
		TERR B 1
		TERR C 1
		TERR D 0 A_PlaySound("terro/punior",CHAN_WEAPON)
		TERR D 0 A_CustomPunch(2 * random(1,8), TRUE,0,"TerroFistPuff")
		TERR D 1
		TERR EFGHI 1
		Goto Ready
	BerserkPunch:
		TERR A 0 A_JumpIfInventory("TerroFistDuration",1,"BerserkFirePunch")
		TERR MN 1
		TERR D 0 A_PlaySound("terro/puniol",CHAN_WEAPON)
		TERR O 1 A_CustomPunch(10 * random(1, 10), TRUE,0,"TerroFistPuff")
		TERR N 1
		TERR M 1 
		Goto Ready
	BerserkAltPunch:
		TERR A 0 A_JumpIfInventory("TerroFistDuration",1,"BerserkFireAltPunch")
		TERR A 1
		TERR B 1
		TERR C 1
		TERR D 0 A_PlaySound("terro/punior",CHAN_WEAPON)
		TERR D 0 A_CustomPunch(20 * random(1,8), TRUE,0,"TerroFistPuff")
		TERR D 1
		TERR EFGHI 1
		Goto Ready
	FirePunch:
		TERF MN 1
		TERR D 0 A_PlaySound("terro/puniol",CHAN_WEAPON)
		TERF O 1 A_CustomPunch(2 * random(1, 10), TRUE,0,"TerroPunchPuff")
		TERF NM 1
		TERF A 0 A_TakeInventory("TerroFistDuration",5)
		Goto FireReady
    FireAltPunch:
		TERF A 1
		TERF B 1
		TERF C 1
		TERR D 0 A_PlaySound("terro/punior",CHAN_WEAPON)
		TERF D 0 A_CustomPunch(4 * random(1,8), TRUE,0,"TerroPunchPuff")
		TERF D 1
		TERF EFGHI 1
		TERF A 0 A_TakeInventory("TerroFistDuration",9)
		Goto FireReady
	BerserkFirePunch:
		TERF MN 1
		TERR D 0 A_PlaySound("terro/puniol",CHAN_WEAPON)
		TERF O 1 A_CustomPunch(20 * random(1, 10), TRUE,0,"TerroExplosionFist")
		TERF NM 1
		TERF A 0 A_TakeInventory("TerroFistDuration",5)
		Goto FireReady
	BerserkFireAltPunch:
		TERF A 1
		TERF B 1
		TERF C 1
		TERR D 0 A_PlaySound("terro/punior",CHAN_WEAPON)
		TERF D 0 A_CustomPunch(40 * random(1,8), TRUE,0,"TerroExplosionFistBig")
		TERF D 1
		TERF EFGHI 1
		TERF A 0 A_TakeInventory("TerroFistDuration",9)
		Goto FireReady
	}
}

ACTOR TerroFistDuration : Inventory
{
	Inventory.MaxAmount 350
}

ACTOR TerroFistFire : Inventory
{
	Inventory.MaxAmount 10
}
ACTOR TerroPunchPuff
{
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	XDeath:
		TNT1 A 1 A_PlaySound("terro/puniopun",CHAN_AUTO)
		Stop
	Melee:
		TNT1 A 1 A_PlaySound("terro/puniopun",CHAN_AUTO)
		Stop
	Crash:
		TNT1 A 0
		Stop
	}
}
ACTOR TerroFistPuff
{
	+NOGRAVITY
	+NOINTERACTION
	+PUFFONACTORS
	+PUFFGETSOWNER
	States
	{
	XDeath:
		TNT1 A 0 A_PlaySound("terro/puniopun",CHAN_AUTO)
		TNT1 A 0 A_JumpIfInventory("TerroFistFire",9,"Ten",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("TerroFistFire",8,"Nine",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("TerroFistFire",7,"Eight",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("TerroFistFire",6,"Seven",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("TerroFistFire",5,"Six",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("TerroFistFire",4,"Five",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("TerroFistFire",3,"Four",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("TerroFistFire",2,"Three",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("TerroFistFire",1,"Two",AAPTR_TARGET)
		TNT1 A 0 A_PlaySound("terro/punio1",CHAN_AUTO)
		TNT1 A 1 A_GiveInventory("TerroFistFire",1,AAPTR_TARGET)
		Stop
	Melee:
		TNT1 A 0 A_PlaySound("terro/puniopun",CHAN_AUTO)
		TNT1 A 0 A_JumpIfInventory("TerroFistFire",9,"Ten",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("TerroFistFire",8,"Nine",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("TerroFistFire",7,"Eight",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("TerroFistFire",6,"Seven",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("TerroFistFire",5,"Six",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("TerroFistFire",4,"Five",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("TerroFistFire",3,"Four",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("TerroFistFire",2,"Three",AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInventory("TerroFistFire",1,"Two",AAPTR_TARGET)
		TNT1 A 0 A_PlaySound("terro/punio1",CHAN_AUTO)
		TNT1 A 1 A_GiveInventory("TerroFistFire",1,AAPTR_TARGET)
		Stop
	Crash:
		TNT1 A 1
		Stop
	Ten:
		TNT1 A 1 A_GiveInventory("TerroFistFire",1,AAPTR_TARGET)
		Stop
	Nine:
		TNT1 A 0 A_PlaySound("terro/punio9",CHAN_AUTO)
		TNT1 A 1 A_GiveInventory("TerroFistFire",1,AAPTR_TARGET)
		Stop
	Eight:
		TNT1 A 0 A_PlaySound("terro/punio8",CHAN_AUTO)
		TNT1 A 1 A_GiveInventory("TerroFistFire",1,AAPTR_TARGET)
		Stop
	Seven:
		TNT1 A 0 A_PlaySound("terro/punio7",CHAN_AUTO)
		TNT1 A 1 A_GiveInventory("TerroFistFire",1,AAPTR_TARGET)
		Stop
	Six:
		TNT1 A 0 A_PlaySound("terro/punio6",CHAN_AUTO)
		TNT1 A 1 A_GiveInventory("TerroFistFire",1,AAPTR_TARGET)
		Stop
	Five:
		TNT1 A 0 A_PlaySound("terro/punio5",CHAN_AUTO)
		TNT1 A 1 A_GiveInventory("TerroFistFire",1,AAPTR_TARGET)
		Stop
	Four:
		TNT1 A 0 A_PlaySound("terro/punio4",CHAN_AUTO)
		TNT1 A 1 A_GiveInventory("TerroFistFire",1,AAPTR_TARGET)
		Stop
	Three:
		TNT1 A 0 A_PlaySound("terro/punio3",CHAN_AUTO)
		TNT1 A 1 A_GiveInventory("TerroFistFire",1,AAPTR_TARGET)
		Stop
	Two:
		TNT1 A 0 A_PlaySound("terro/punio2",CHAN_AUTO)
		TNT1 A 1 A_GiveInventory("TerroFistFire",1,AAPTR_TARGET)
		Stop
	}
}
ACTOR TerroExplosionFist
{
	Projectile
	Speed 0
	Radius 5
	Height 6
	DamageType "Friendly"
	Scale 0.3
	+NOGRAVITY
	-SOLID
	+PUFFONACTORS
	States
	{
	Spawn:
		MISL B 0 NoDelay A_PlaySound("weapons/rocklx",CHAN_AUTO)
		MISL B 8 Bright A_Explode(32,5,0,0,5)
		MISL C 6 Bright
		MISL D 4 Bright
		Stop
	}
}
ACTOR TerroExplosionFistBig : TerroExplosionFist
{
	Radius 8
	Height 8
	Scale 0.6
	States
	{
	Spawn:
		MISL B 0 NoDelay A_PlaySound("weapons/rocklx",CHAN_AUTO)
		MISL B 8 Bright A_Explode(50,8,0,0,8)
		MISL C 6 Bright
		MISL D 4 Bright
		Stop
	}
}