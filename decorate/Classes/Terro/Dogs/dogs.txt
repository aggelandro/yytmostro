ACTOR Dogs : DoomWeapon {
	Weapon.AmmoUse 1
  Weapon.AmmoGive 40
  Weapon.AmmoType "Cell"
  Inventory.PickupMessage "$GOTPLASMA"
	+WEAPON.DONTBOB
	+NOALERT
	
	States {
		Ready:
			TNT1 A 0 A_JumpIfInventory("SwitchToken", 1, "ReadyLoop2")
			Goto ReadyLoop
			
		ReadyLoop:
			TNT1 A 0 A_JumpIf(CallACS("dogs_currentSwitchCooldownValue") > 0, 2)
			TNT1 A 1 A_WeaponReady
			Loop
			TNT1 A 1 A_WeaponReady(WRF_NOSECONDARY)
			Loop
			
		ReadyLoop2:
			TNT1 A 0 A_JumpIf(CallACS("dogs_currentSwitchCooldownValue") > 0, 3)
			PECH AB 3 A_WeaponReady
			Loop
			PECH AB 3 A_WeaponReady(WRF_NOSECONDARY)
			Loop
			
		Deselect:
			TNT1 A 0 A_JumpIfInventory("SwitchToken", 1, "Deselect2")
			TNT1 A 1 A_Lower
			Loop
			
		Deselect2:
			PECH A 1 A_Lower
			Loop
		
		Select:
			TNT1 A 0 A_JumpIfInventory("SwitchToken", 1, "Select2")
			TNT1 A 1 A_Raise
			Loop
			
		Select2:
			PECH A 1 A_Raise
			Loop
		
		Fire:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_JumpIfInventory("SwitchToken", 1, "Fire2")
			PELB B 3
			PELB C 2
			PELB D 2 A_FireCustomMissile("CacoBall", 0, 0)
			TNT1 A 0 A_FireCustomMissile("DogProjectile", 0, 1, 0, -27)
			PELB E 2
			TNT1 A 18 A_ReFire
			Goto Ready
			
		Hold:
			TNT1 A 0 A_AlertMonsters
			PELB B 3
			PELB C 2
			TNT1 A 0 A_Jump(96, 4)
			PELB D 2 A_FireCustomMissile("DogProjectile", 0, 1, 0, -27)
			PELB E 2
			TNT1 A 18 A_ReFire
			Goto Ready
			PELB D 2 A_FireCustomMissile("CacoBall", 0, 0)
			TNT1 A 0 A_FireCustomMissile("DogProjectile", 0, 1, 0, -27)
			PELB E 2
			TNT1 A 18 A_ReFire
			Goto Ready
			
		Fire2:
			PECH A 4
			PECH B 3 A_TakeInventory("Cell", 1)
			TNT1 A 0 A_FireCustomMissile("SeekerDogProjectile", 0, 1, 0, -27)
			PECH B 20 A_ReFire("Hold2")
			Goto Ready
			
		Hold2:
			TNT1 A 0 A_AlertMonsters
			PECH A 4
			PECH B 3 A_TakeInventory("Cell", 1)
			TNT1 A 0 A_FireCustomMissile("SeekerDogProjectile", 0, 1, 0, -27)
			PECH B 20 A_ReFire("Hold2")
			Goto Ready
			
		AltFire:
			TNT1 A 0 ACS_NamedExecuteWithResult("dogs_switchCooldown", 350)
			TNT1 A 0 A_JumpIfInventory("SwitchToken", 1, "AltFire2")
			TNT1 A 1 A_GiveInventory("SwitchToken", 1)
			Goto Ready
			
		AltFire2:
			TNT1 A 1 A_TakeInventory("SwitchToken", 1)
			Goto Ready
	}
}

ACTOR CacoBall {
	Height 8
	Radius 4
	BounceFactor 0.6
	Gravity 0.2
	Speed 25
	Scale 0.2
	BounceType "Doom"
	+BOUNCEONWALLS
	+BOUNCEONFLOORS
	+ALLOWBOUNCEONACTORS
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+THRUACTORS
	+MISSILE
	+NOBLOCKMAP
	+NOTELEPORT
	
	States {
		Spawn:
			CACB A 1
			Loop
			
		Death:
			CACB A 1 A_FadeOut
			Loop
	}
}

ACTOR DogProjectile {
	Height 25
	Radius 13
	BounceFactor 1.0
	Speed 20
	Damage 5
	Projectile
	Gravity 0.8
	MaxStepHeight 100
	BounceType "Hexen"
	SeeSound "terro/dogbark"
	DeathSound "terro/dogbite"
	+FLOORHUGGER
	+NOEXPLODEFLOOR
	+STEPMISSILE
	+RANDOMIZE
	+USEBOUNCESTATE
	-NOGRAVITY
	
	States {
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("deathTimerNoMonster", Random(210, 280))
			DOGW A 1 A_ChangeFlag("FLOORHUGGER", FALSE)
			DOGW A 2
			DOGW BCD 3
			Goto SpawnLoop
			
		Bounce.Floor:
			TNT1 A 0 A_ChangeFlag("FLOORHUGGER", TRUE)
			DOGW ABCD 3
			TNT1 A 0 A_ChangeFlag("FLOORHUGGER", FALSE)
			Goto SpawnLoop
			
		SpawnLoop:
			DOGW ABCD 3
			Loop
			
		XDeath:
			DOGW EFGH 6
			Stop
			
		Death:
			TNT1 A 0 A_ChangeFlag("MISSILE", FALSE)
			PERS A Random(4,6) A_FadeOut(0.08)
			Loop
	}
}

ACTOR SeekerDogProjectile : DogProjectile {
	Speed 14
	+SEEKERMISSILE
	-DROPOFF
	//var int user_floorhug_flag;
	
	States {
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("deathTimerNoMonster", Random(210, 280))
			//DOGW A 1 A_ChangeFlag("FLOORHUGGER", FALSE)
			//TNT1 A 0 ACS_NamedExecuteWithResult("dogs_enableFlyingBulletAvoider")
			DOGW A 3 A_SeekerMissile(15, 20, SMF_LOOK|SMF_CURSPEED, 256, 3)
			//TNT1 A 0 A_ChangeFlag("FLOORHUGGER", user_floorhug_flag)
			DOGW B 3 A_SeekerMissile(15, 20, SMF_LOOK|SMF_CURSPEED, 256, 3)
			//TNT1 A 0 A_ChangeFlag("FLOORHUGGER", user_floorhug_flag)
			DOGW C 3 A_SeekerMissile(15, 20, SMF_LOOK|SMF_CURSPEED, 256, 3)
			//TNT1 A 0 A_ChangeFlag("FLOORHUGGER", user_floorhug_flag)
			DOGW D 3 A_SeekerMissile(15, 20, SMF_LOOK|SMF_CURSPEED, 256, 3)
			//TNT1 A 0 A_ChangeFlag("FLOORHUGGER", user_floorhug_flag)
			Goto SpawnLoop
			
		Bounce.Floor:
			TNT1 A 0
			Goto SpawnLoop
			
		SpawnLoop:
			DOGW A 3 A_SeekerMissile(15, 20, SMF_LOOK|SMF_CURSPEED, 256, 3)
			//TNT1 A 0 A_ChangeFlag("FLOORHUGGER", user_floorhug_flag)
			DOGW B 3 A_SeekerMissile(15, 20, SMF_LOOK|SMF_CURSPEED, 256, 3)
			//TNT1 A 0 A_ChangeFlag("FLOORHUGGER", user_floorhug_flag)
			DOGW C 3 A_SeekerMissile(15, 20, SMF_LOOK|SMF_CURSPEED, 256, 3)
			//TNT1 A 0 A_ChangeFlag("FLOORHUGGER", user_floorhug_flag)
			DOGW D 3 A_SeekerMissile(15, 20, SMF_LOOK|SMF_CURSPEED, 256, 3)
			//TNT1 A 0 A_ChangeFlag("FLOORHUGGER", user_floorhug_flag)
			Loop
	}
}

ACTOR SwitchToken : Inventory {
	Inventory.Amount 1
	Inventory.MaxAmount 1
}