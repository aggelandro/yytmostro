ACTOR ContraWeapon : WeaponWithGrenadeOption {
	Weapon.SelectionOrder 700
	Weapon.Kickback 0
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "Clip"
	Inventory.PickupMessage "You got the Contra weapon!"
	Obituary "%o was blown away by %k's Super Bomb."
	Tag "Contra Weapon"
	States {
	
		Ready:
			SHTG A 0 A_JumpIfInventory("ClassThroweableAction", 1, "GrenadeCheck")
			SHTG A 1 A_WeaponReady
			Loop
			
		Deselect:
			SHTG A 1 A_Lower
			Loop
			
		Select:
			TNT1 A 0 A_TakeInventory("ClassThroweableAction", 1)
			SHTG A 1 A_Raise
			Loop
			
		Fire:
			SHTG A 0 A_JumpIfInventory("SpreadGunGiver", 2, "SuperSpreadGun")
			SHTG A 0 A_JumpIfInventory("SpreadGunGiver", 1, "SpreadGun")
			SHTG A 0 A_JumpIfInventory("HomingGunGiver", 2, "SuperHomingGun")
			SHTG A 0 A_JumpIfInventory("HomingGunGiver", 1, "HomingGun")
			SHTG A 0 A_JumpIfInventory("PrototypeGunGiver", 2, "SuperPrototypeGun")
			SHTG A 0 A_JumpIfInventory("PrototypeGunGiver", 1, "PrototypeGun")
			SHTG A 0 A_JumpIfInventory("LaserGunGiver", 2, "SuperLaserGun")
			SHTG A 0 A_JumpIfInventory("LaserGunGiver", 1, "LaserGun")
			SHTG A 0 A_JumpIfInventory("FlamethrowerGunGiver", 2, "SuperFlamethrowerGun")
			SHTG A 0 A_JumpIfInventory("FlamethrowerGunGiver", 1, "FlamethrowerGun")
			SHTG A 0 A_JumpIfInventory("CrusherGunGiver", 2, "SuperCrusherGun")
			SHTG A 0 A_JumpIfInventory("CrusherGunGiver", 1, "CrusherGun")
			Goto MachineGun
			
		MachineGun:
			SHTG A 0 A_GunFlash
			SHTG A 0 A_PlaySound("contra/gun", CHAN_WEAPON)
			SHTG A 5 A_FireCustomMissile("MachineGunBullet")
			SHTG A 5 A_ReFire
			Goto Ready
		
		SpreadGun:
			SHTG A 0 A_GunFlash
			SHTG A 0 A_PlaySound("contra/spread", CHAN_WEAPON)
			SHTG A 0 A_FireCustomMissile("SpreadGunBullet",-12,1)
			SHTG A 0 A_FireCustomMissile("SpreadGunBullet",-6,1)
			SHTG A 0 A_FireCustomMissile("SpreadGunBullet",0,0)
			SHTG A 0 A_FireCustomMissile("SpreadGunBullet",6,0)
			SHTG A 8 A_FireCustomMissile("SpreadGunBullet",12,0)
			SHTG A 5 A_ReFire
			Goto Ready
			
		SuperSpreadGun:
			SHTG A 0 A_GunFlash
			SHTG A 0 A_PlaySound("contra/spread", CHAN_WEAPON)
			SHTG A 0 A_FireCustomMissile("SpreadGunBullet",-12,1)
			SHTG A 0 A_FireCustomMissile("SpreadGunBullet",-9,1)
			SHTG A 0 A_FireCustomMissile("SpreadGunBullet",-6,1)
			SHTG A 0 A_FireCustomMissile("SpreadGunBullet",-3,1)
			SHTG A 0 A_FireCustomMissile("SpreadGunBullet",0,0)
			SHTG A 0 A_FireCustomMissile("SpreadGunBullet",3,0)
			SHTG A 0 A_FireCustomMissile("SpreadGunBullet",6,0)
			SHTG A 0 A_FireCustomMissile("SpreadGunBullet",9,0)
			SHTG A 8 A_FireCustomMissile("SpreadGunBullet",12,0)
			SHTG A 5 A_ReFire
			Goto Ready
			
		HomingGun:
			SHTG A 0 A_GunFlash
			SHTG A 0 A_PlaySound("contra/homing", CHAN_WEAPON)
			SHTG A 5 A_FireCustomMissile("HomingGunMissile")
			SHTG A 5 A_ReFire
			Goto Ready
			
		SuperHomingGun:
			SHTG A 0 A_GunFlash
			SHTG A 0 A_PlaySound("contra/homing", CHAN_WEAPON)
			SHTG A 0 A_FireCustomMissile("HomingGunMissile",-4)
			SHTG A 5 A_FireCustomMissile("HomingGunMissile",4)
			SHTG A 5 A_ReFire
			Goto Ready
			
		PrototypeGun:
			SHTG A 0 A_GunFlash
			SHTG A 0 A_PlaySound("contra/crusher", CHAN_WEAPON)
			SHTG A 0 A_FireCustomMissile("PrototypeGunMissile",-12,1)
			SHTG A 0 A_FireCustomMissile("PrototypeGunMissile",-6,1)
			SHTG A 0 A_FireCustomMissile("PrototypeGunMissile",0,0)
			SHTG A 0 A_FireCustomMissile("PrototypeGunMissile",6,0)
			SHTG A 7 A_FireCustomMissile("PrototypeGunMissile",12,0)
			SHTG A 5 A_ReFire
			Goto Ready
			
		SuperPrototypeGun:
			SHTG A 0 A_GunFlash
			SHTG A 0 A_PlaySound("contra/crusher", CHAN_WEAPON)
			SHTG A 0 A_FireCustomMissile("PrototypeGunMissile",-12,1)
			SHTG A 0 A_FireCustomMissile("PrototypeGunMissile",-9,1)
			SHTG A 0 A_FireCustomMissile("PrototypeGunMissile",-6,1)
			SHTG A 0 A_FireCustomMissile("PrototypeGunMissile",-3,1)
			SHTG A 0 A_FireCustomMissile("PrototypeGunMissile",0,0)
			SHTG A 0 A_FireCustomMissile("PrototypeGunMissile",3,0)
			SHTG A 0 A_FireCustomMissile("PrototypeGunMissile",6,0)
			SHTG A 0 A_FireCustomMissile("PrototypeGunMissile",9,0)
			SHTG A 7 A_FireCustomMissile("PrototypeGunMissile",12,0)
			SHTG A 5 A_ReFire
			Goto Ready
			
		LaserGun:
			SHTG A 0 A_GunFlash
			SHTG A 0 A_PlaySound("contra/laser", CHAN_WEAPON)
			SHTG A 10 A_FireCustomMissile("LaserShooter")
			TNT1 A 0 A_TakeInventory("Clip", 1)
			SHTG A 5 A_ReFire
			Goto Ready
			
		SuperLaserGun:
			SHTG A 0 A_GunFlash
			SHTG A 0 A_PlaySound("contra/laser", CHAN_WEAPON)
			SHTG A 10 A_FireCustomMissile("SuperLaserShooter")
			TNT1 A 0 A_TakeInventory("Clip", 2)
			SHTG A 5 A_ReFire
			Goto Ready
			
		FlamethrowerGun:
			SHTG A 0 A_GunFlash
			SHTG A 0 A_PlaySound("contra/flamethrower", CHAN_WEAPON)
			SHTG A 2 A_FireCustomMissile("FlamethrowerProjectile")
			SHTG A 5 A_ReFire
			Goto Ready
			
		SuperFlamethrowerGun:
			SHTG A 0 A_GunFlash
			SHTG A 0 A_PlaySound("contra/flamethrower", CHAN_WEAPON)
			SHTG A 2 A_FireCustomMissile("SuperFlamethrowerProjectile")
			SHTG A 5 A_ReFire
			Goto Ready
			
		CrusherGun:
			SHTG A 0 A_JumpIfInventory("CrusherCooldown",2,"NoCrusher")
			SHTG A 0 A_GiveInventory("CrusherCooldown",1)
			SHTG A 0 A_GunFlash
			SHTG A 0 A_PlaySound("contra/crusher", CHAN_WEAPON)
			SHTG A 10 A_FireCustomMissile("CrusherMissile")
			SHTG A 0 A_TakeInventory("Clip", 3)
			SHTG A 5 A_ReFire
			Goto Ready
			
		SuperCrusherGun:
			SHTG A 0 A_JumpIfInventory("CrusherCooldown",3,"NoCrusher")
			SHTG A 0 A_GiveInventory("CrusherCooldown",1)
			SHTG A 0 A_GunFlash
			SHTG A 0 A_PlaySound("contra/crusher", CHAN_WEAPON)
			SHTG A 10 A_FireCustomMissile("SuperCrusherMissile")
			SHTG A 0 A_TakeInventory("Clip", 3)
			SHTG A 5 A_ReFire
			Goto Ready
			
		NoCrusher:
			SHTG A 1
			SHTG A 5 A_ReFire
			Goto Ready
			
		Flash:
			SHTF A 2 Bright A_Light1
			SHTF B 2 Bright A_Light2
			Goto LightDone
	}
}

ACTOR MachineGunBullet : FastProjectile {
	Radius 4
	Height 8
	Speed 60
	Damage (Random(4, 5))
	DamageType "Bullet"
	Projectile
	+RANDOMIZE
	+BLOODSPLATTER
	+SPECTRAL
	Obituary "%o was shot by %k's Machine Gun."
	
	States {
	
		Spawn:
			CPRJ A 1 NoDelay BRIGHT
			Loop
			
		Death:
			TNT1 A 0 A_SpawnItemEx("ContraSparkEffect",0,0,0,Random(-1,1),Random(-1,1),Random(-1,1),0,SXF_NOCHECKPOSITION)
			Stop
			
		Crash:
		XDeath:
			TNT1 A 0 A_PlaySound("contra/ping")
			TNT1 A -1 A_Jump(256, "Death")
			Wait
	}
}

ACTOR ContraSparkEffect {
	+NOINTERACTION
	
	States {
		Spawn:
			CPRJ F 4 NoDelay BRIGHT
			CPRJ G 4 BRIGHT
			Stop
	}
}

ACTOR SpreadGunBullet : MachineGunBullet {
	Speed 40
	Scale 0.75
	-RANDOMIZE
	Obituary "%o was caught in %k's Spread Gun."
	
	States {
		Spawn:
			CPRJ D 4 NoDelay BRIGHT
			CPRJ E 1 BRIGHT
			Goto Spawn+1
	}
}

ACTOR HomingGunMissile : MachineGunBullet {
	+SEEKERMISSILE
	+SCREENSEEKER
	+THRUGHOST
	Scale 0.75
	Obituary "%o was locked on by %k's Homing Gun."
	
	States {
		Spawn:
			CPRJ IJ 1 NoDelay Bright A_SeekerMissile(28,28,SMF_LOOK|SMF_CURSPEED,256)
			Loop
	}
}

ACTOR PrototypeGunMissile : HomingGunMissile {
	Obituary "%o was seeked out by %k's Prototype Weapon."
	Speed 40
	
	States {
		Spawn:
			CPRJ KL 1 Bright A_SeekerMissile(14,14,SMF_LOOK|SMF_CURSPEED,50)
			Loop
	}
}

ACTOR LaserShooter {
	PROJECTILE
	+NOINTERACTION
	Speed 1
	Radius 4
	Height 8
	RenderStyle None
	
	States {
		Spawn:
			TNT1 A 0
			TNT1 A 1 A_SpawnItemEx("Laser",0,0,0,momx*30,momy*30,momz*30,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			TNT1 A 1 A_SpawnItemEx("Laser2",0,0,0,momx*30,momy*30,momz*30,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			TNT1 A 1 A_SpawnItemEx("Laser3",0,0,0,momx*30,momy*30,momz*30,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			TNT1 A 1 A_SpawnItemEx("Laser4",0,0,0,momx*30,momy*30,momz*30,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			Stop
	}
}

ACTOR SuperLaserShooter : LaserShooter {

	States {
		Spawn:
			TNT1 A 0
			TNT1 A 1 A_SpawnItemEx("Laser",0,0,0,momx*30,momy*30,momz*30,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			TNT1 A 1 A_SpawnItemEx("Laser2",0,0,0,momx*30,momy*30,momz*30,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			TNT1 A 1 A_SpawnItemEx("Laser3",0,0,0,momx*30,momy*30,momz*30,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			TNT1 A 1 A_SpawnItemEx("Laser4",0,0,0,momx*30,momy*30,momz*30,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			TNT1 A 1 A_SpawnItemEx("Laser",0,0,0,momx*30,momy*30,momz*30,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			TNT1 A 1 A_SpawnItemEx("Laser2",0,0,0,momx*30,momy*30,momz*30,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			Stop
	}
}

ACTOR Laser : MachineGunBullet {
	Scale 2.8
	DamageType "Laser"
	Damage (Random(6, 7))
	-RANDOMIZE
	Obituary "%o was pierced by %k's Laser."
	
	States {
		Spawn:
			CPRJ MMNN 1 Bright
			Loop
	}
}

ACTOR Laser2 : Laser {
	States {
		Spawn:
			CPRJ MNNM 1 Bright
			Loop
	}
}

ACTOR Laser3 : Laser {
	States {
		Spawn:
			CPRJ NNMM 1 Bright
			Loop
	}
}

ACTOR Laser4 : Laser {
	States {
		Spawn:
			CPRJ NMMN 1 Bright
			Loop
	}
}

ACTOR FlamethrowerProjectile {
	Radius 4
	Height 8
	Speed 30
	Projectile
	+PAINLESS
	+RIPPER
	+NOBOSSRIP
	+BLOODLESSIMPACT
	RenderStyle Add
	Scale 0.2
	Alpha 1
	
	States {
		Spawn:
			CEXP A 2 NoDelay BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP B 0 A_SetScale(scaleX + 0.1,(scaleY + 0.1))
			CEXP B 2 BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP C 0 A_SetScale(scaleX + 0.1,(scaleY + 0.1))
			CEXP C 2 BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP D 0 A_SetScale(scaleX + 0.1,(scaleY + 0.1))
			CEXP D 2 BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP E 0 A_SetScale(scaleX + 0.1,(scaleY + 0.1))
			CEXP E 2 BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP F 0 A_SetScale(scaleX + 0.1,(scaleY + 0.1))
			CEXP F 2 BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP G 0 A_SetScale(scaleX + 0.1,(scaleY + 0.1))
			CEXP G 2 BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP H 0 A_SetScale(scaleX + 0.1,(scaleY + 0.1))
			CEXP H 2 BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP I 0 A_SetScale(scaleX + 0.1,(scaleY + 0.1))
			CEXP I 2 BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			
		Death:
			CEXP J 0 A_ChangeFlag("NOINTERACTION",1)
			CEXP JKLM 2 BRIGHT
			Stop
	}
}

ACTOR FlamethrowerDamager {
	Projectile
	Radius 4
	Damage 1
	Speed 0
	DamageType "FireR"
	+BLOODSPLATTER
	+SPECTRAL
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	Obituary "%o was barbecued by %k's Flamethrower."
	
	States {
		Spawn:
			TNT1 A 1
			Stop
			
		Death:
			TNT1 A 0
			Stop
			
		Crash:
		XDeath:
			TNT1 A 0 A_PlaySound("contra/ping")
			Stop
	}
}

ACTOR SuperFlamethrowerProjectile : FlamethrowerProjectile {
	States {
		Spawn:
			CEXP A 2 NoDelay BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP B 0 A_SetScale(scaleX + 0.1,(scaleY + 0.1))
			CEXP B 2 BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP C 0 A_SetScale(scaleX + 0.1,(scaleY + 0.1))
			CEXP C 2 BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP D 0 A_SpawnItemEx("MiniFlamethrowerProjectile",0,0,0,momx+Random(-2,2),momy+Random(-2,2),momz+Random(-2,2),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP D 0 A_SetScale(scaleX + 0.1,(scaleY + 0.1))
			CEXP D 2 BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP E 0 A_SpawnItemEx("MiniFlamethrowerProjectile",0,0,0,momx+Random(-2,2),momy+Random(-2,2),momz+Random(-2,2),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP E 0 A_SetScale(scaleX + 0.1,(scaleY + 0.1))
			CEXP E 2 BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP F 0 A_SpawnItemEx("MiniFlamethrowerProjectile",0,0,0,momx+Random(-2,2),momy+Random(-2,2),momz+Random(-2,2),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP F 0 A_SetScale(scaleX + 0.1,(scaleY + 0.1))
			CEXP F 2 BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP G 0 A_SpawnItemEx("MiniFlamethrowerProjectile",0,0,0,momx+Random(-2,2),momy+Random(-2,2),momz+Random(-2,2),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP G 0 A_SetScale(scaleX + 0.1,(scaleY + 0.1))
			CEXP G 2 BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP H 0 A_SpawnItemEx("MiniFlamethrowerProjectile",0,0,0,momx+Random(-2,2),momy+Random(-2,2),momz+Random(-2,2),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP H 0 A_SetScale(scaleX + 0.1,(scaleY + 0.1))
			CEXP H 2 BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP I 0 A_SpawnItemEx("MiniFlamethrowerProjectile",0,0,0,momx+Random(-2,2),momy+Random(-2,2),momz+Random(-2,2),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP I 0 A_SetScale(scaleX + 0.1,(scaleY + 0.1))
			CEXP I 2 BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			Goto Death
	}
}

ACTOR MiniFlamethrowerProjectile : FlamethrowerProjectile {
	States {
		Spawn:
			CEXP AB 1 NoDelay BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP C 0 A_SetScale(scaleX + 0.025,(scaleY + 0.025))
			CEXP CD 1 BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP E 0 A_SetScale(scaleX + 0.025,(scaleY + 0.025))
			CEXP EF 1 BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			CEXP G 0 A_SetScale(scaleX + 0.025,(scaleY + 0.025))
			CEXP GH 1 BRIGHT A_SpawnItemEx("FlamethrowerDamager",0,0,0,momx,momy,momz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
			
		Death:
			CEXP I 0 A_ChangeFlag("NOINTERACTION",1)
			CEXP IJKLM 1 BRIGHT
			Stop
	}
}

ACTOR CrusherCooldown : Inventory {
	Inventory.Amount 1
	Inventory.MaxAmount 3
}

ACTOR CrusherMissile : MachineGunBullet {
	Damage (Random(15, 20))
	+SKYEXPLODE
	+THRUGHOST
	RenderStyle Normal
	Obituary "%o was crushed by %k's Crusher."
	ReactionTime 12
	
	States {
		Spawn:
			CPRJ KL 1 BRIGHT A_Countdown
			Loop
			
		Death:
			TNT1 A 6 A_SpawnItemEx("CrusherDamagerInitial")
			TNT1 AAAAA 6 A_SpawnItemEx("CrusherDamager")
			TNT1 A 0 A_TakeFromTarget("CrusherCooldown",1)
			Stop
	}
}

ACTOR SuperCrusherMissile : CrusherMissile {
	Speed 80
	ReactionTime 32
}

ACTOR CrusherDamager {
	Damage (Random(15, 20))
	DamageType "FireR"
	Radius 32
	Speed 0
	Projectile
	+BLOODSPLATTER
	+SPECTRAL
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	Obituary "%o was crushed by %k's Crusher."
	
	States {
		Spawn:
			TNT1 A 1 NoDelay
			
		Death:
			TNT1 A 0 A_SpawnItemEx("CrusherFX",Random(-16,16),Random(-16,16),Random(-16,16),0,0,0,0,SXF_NOCHECKPOSITION)
			Stop
		
		Crash:
		XDeath:
			TNT1 A 0 A_PlaySound("contra/ping")
			Goto Death
	}
}

ACTOR CrusherDamagerInitial : CrusherDamager {
	States {			
		Death:
			TNT1 A 0 A_SpawnItemEx("CrusherFX",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Stop
	}
}

ACTOR CrusherFX {
	+NOINTERACTION
	Scale 1.5
	
	States {
		Spawn:
			CEXP N 0
			CEXP N 2 BRIGHT A_PlaySound("contra/enemydeathbig",0,128)
			CEXP OPQR 2 BRIGHT
			Stop
	}
}