ACTOR Dynamite : YYTWeapon {
	Tag "TNT"
	Inventory.PickupSound "TNT/Pickup"
	Weapon.Kickback 80
	Weapon.AmmoGive 2
	Weapon.AmmoType "RocketAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoUse2	1
	Weapon.AmmoType2 "RocketAmmo"
	+NOAUTOAIM
	+WEAPON.EXPLOSIVE
	+WEAPON.NOALERT
	
	States {
		Deselect:
			DYND A 0 A_JumpIfnoammo("DeselectAmmoLess")
			DYND AB 3
			DYND C 1 A_PlaySound("TNT/Zip")
			DYND DEFG 1 A_Lower
			Goto DeselectLoop
			
		DeselectLoop:
			DYND H 1 A_Lower
			Loop
			
		DeselectAmmoLess:
			ZIPD ABC 2
			ZIPD C 1 A_Lower
			Goto DeselectAmmoLess+3
			
		Select:
			TNT1 A 0 A_TakeInventory("SlideKickAction", 1)
			DYNU AAA 1 A_Raise
			DYNU AB 1
			DYNU C 1 A_PlayWeaponSound("TNT/Zip")
			DYNU D 1 A_Raise
			DYNU EF 1 A_Raise
			Goto SelectLoop
			
		SelectLoop:
			DYNU F 1 A_Raise
			Loop
			
		Ready:
			TNT1 A 0 A_JumpIfInventory("SlideKickAction", 1, "SlideKick")
			DYNR A 3 A_WeaponReady
			TNT1 A 0 A_JumpIfInventory("SlideKickAction", 1, "SlideKick")
			DYNR B 3 A_WeaponReady
			TNT1 A 0 A_JumpIfInventory("SlideKickAction", 1, "SlideKick")
			DYNR C 3 A_WeaponReady
			Loop
			
		Fire:
			DYNL AB 3
			DYNL C 3 A_PlayWeaponSound("TNT/Fuse")
			DYNL DEFG 3
			TNT1 A 0 A_Refire
			DYNL G 1 BRIGHT
			TNT1 A 0 A_StopSound(CHAN_WEAPON)
			Goto Throw
			
		Hold:
			TNT1 A 0 ACS_NamedExecuteWithResult("dynamite_loadDistanceAttack", 70)
			Goto Fire2
			
		Fire2:
			DYNT AABBAABBAABBAABBAAB 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "Throw")
			DYNT BCCBBCCBBCCBBCCBBCC 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "Throw")
			DYNT DDCCDDCCDDCCDDCCDDC 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "Throw")
			DYNT CDDEEDDEEDDEEDDEEDD 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "Throw")
			TNT1 A 0 A_PlayWeaponSound("TNT/Fuse")
			DYNT EEFFEEFFEEFFEEFFEEF 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "Throw")
			DYNT FGGFFGGFFGGFFGGFFGG 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "Throw")
			DYNT HHGGHHGGHHGGHHGGHHG 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "Throw")
			DYNT GHHIIHHIIHHIIHHIIHH 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "Throw")
			TNT1 A 0 A_PlayWeaponSound("TNT/Fuse")
			DYNT IIJJIIJJIIJJIIJJIIJ 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "Throw")
			DYNT JKKJJKKJJKKJJKKJJKK 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "Throw")
			DYNT KKKKKKKKK 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "Throw")
			Goto Explode
			
		Throw:
			TNT1 A 0 A_FireCustomMissile("TNT")
			Goto ThrowFinish
			
		ThrowFinish:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_StopSound(CHAN_WEAPON)
			TNT1 A 0 A_PlayWeaponSound("TNT/Throw")
			DYNU GHIJKLMNO 2
			TNT1 A 0 A_Refire("Antibug")
			Goto Ready
			
		Antibug:
			TNT1 A 0
			Goto Ready
			
		Explode:
			TNT1 A 0 A_SpawnItem("FloorKaboom")
			TNT1 A 0 A_PlaySound("weapons/rocklx")
			TNT1 A 0 ACS_NamedExecuteWithResult("pausePlayer", 1)
			BLWS A 4 BRIGHT
			BLWS B 8 BRIGHT
			BLWS CCCCCCCCCCC 1 BRIGHT A_Lower
			TNT1 A 0 ACS_NamedExecuteWithResult("pausePlayer", 0)
			TNT1 A 0 Thing_Destroy(tid, 1)
			Goto ExplodeLoop
			
		ExplodeLoop:
			BLWS C 1 BRIGHT A_Lower
			Loop
			
		AltFire:
			DYNL AB 3
			DYNL C 3 A_PlayWeaponSound("TNT/Fuse")
			DYNL DEFG 3
			TNT1 A 0 A_Refire
			DYNL G 1 BRIGHT
			TNT1 A 0 A_StopSound(CHAN_WEAPON)
			Goto Throw
			
		AltHold:
			TNT1 A 0 ACS_NamedExecuteWithResult("dynamite_loadDistanceAttack", 70, 1)
			Goto AltFire2
			
		AltFire2:
			DYNT AABBAABBAABBAABBAAB 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "AltThrow")
			DYNT BCCBBCCBBCCBBCCBBCC 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "AltThrow")
			DYNT DDCCDDCCDDCCDDCCDDC 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "AltThrow")
			DYNT CDDEEDDEEDDEEDDEEDD 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "AltThrow")
			TNT1 A 0 A_PlayWeaponSound("TNT/Fuse")
			DYNT EEFFEEFFEEFFEEFFEEF 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "AltThrow")
			DYNT FGGFFGGFFGGFFGGFFGG 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "AltThrow")
			DYNT HHGGHHGGHHGGHHGGHHG 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "AltThrow")
			DYNT GHHIIHHIIHHIIHHIIHH 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "AltThrow")
			TNT1 A 0 A_PlayWeaponSound("TNT/Fuse")
			DYNT IIJJIIJJIIJJIIJJIIJ 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "AltThrow")
			DYNT JKKJJKKJJKKJJKKJJKK 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "AltThrow")
			DYNT KKKKKKKKK 1 BRIGHT A_JumpIf(CallACS("dynamite_getThrowPower") > 0, "AltThrow")
			Goto Explode
			
		AltThrow:
			TNT1 A 0 A_FireCustomMissile("BoingTNT")
			Goto ThrowFinish
	}
}

ACTOR TNT {
	Decal Scorch
	Radius 6
	Height 4
	Speed 15
	Scale 0.5
	Gravity 0.7
	Health 1
	GibHealth 0
	Reactiontime 49
	Damage (5 * Random(12, 24))
	DamageType "Dynamite"
	PROJECTILE
	+DONTGIB
	+DONTSPLASH
	+NOBOUNCESOUND
	+PUSHABLE
	+RANDOMIZE
	+SHOOTABLE
	+THRUGHOST
	-NOGRAVITY
	-SOLID
	DeathSound "TNT/Explode"
	ActiveSound "TNT/Fuse"
	SeeSound "TNT/Fuse"
	var int user_dynamite_throw_power;
	
	States {			
		Spawn:
			TNT1 A 0 NoDelay A_LoopActiveSound
			TNT1 A 0 A_SetUserVar("user_dynamite_throw_power", CallACS("dynamite_getThrowPowerFromPlayer"))
			TNT1 A 1 
			TNT1 A 0 A_JumpIf(user_dynamite_throw_power > 0, "Spawn2")
			Goto See
			
		Spawn2:
			PTNT E 1 ThrustThingZ(0, user_dynamite_throw_power / 2, 0, 1)
			TNT1 A 0 ThrustThing(angle * 256 / 360, user_dynamite_throw_power / 10, 0, 0)
			PTNT E 1 ThrustThingZ(0, user_dynamite_throw_power / 2, 0, 1)
			Goto Cont
			
		See:
			PTNT E 1
			TNT1 A 0 A_Countdown
			PTNT E 1 A_LoopActiveSound
			TNT1 A 0 A_CheckFloor("Cont")
			Loop
			
		Cont:
			TNT1 A 0 A_Playsound("TNT/Fuse", CHAN_7, 1.0, TRUE)
			PTNT F 1
			TNT1 A 0 A_Countdown
			PTNT F 1 A_LoopActiveSound
			PTNT G 1
			TNT1 A 0 A_Countdown
			PTNT G 1 A_LoopActiveSound
			PTNT H 1
			TNT1 A 0 A_Countdown
			PTNT H 1 A_LoopActiveSound
			PTNT A 1
			TNT1 A 0 A_Countdown
			PTNT A 1 A_LoopActiveSound
			PTNT B 1
			TNT1 A 0 A_Countdown
			PTNT B 1 A_LoopActiveSound
			PTNT C 1
			TNT1 A 0 A_Countdown
			PTNT C 1 A_LoopActiveSound
			PTNT D 1
			TNT1 A 0 A_Countdown
			PTNT D 1 A_LoopActiveSound
			Goto Cont+1
			
		Death:
			TNT1 A 0 A_StopSound(CHAN_7)
			TNT1 A 1 BRIGHT A_Explode(64, 172, XF_HURTSOURCE, 1, 86)
			TNT1 A 0 A_Playsound("TNT/Explode2", CHAN_AUTO)
			TNT1 A 0 A_SpawnItem("FloorKaboom", -16)
			TNT1 A 1 BRIGHT A_Scream
			Stop
			
		XDeath:
			TNT1 A 0 A_StopSound(CHAN_7)
			TNT1 A 1 BRIGHT A_Explode(96, 172, XF_HURTSOURCE, 1, 86)
			TNT1 A 0 A_Playsound("TNT/Explode2", CHAN_AUTO)
			TNT1 A 0 A_SpawnItem("Kaboom", -16, -15 + random(-2, 2))
			TNT1 A 1 BRIGHT A_Scream
			Stop
	}
}

ACTOR BoingTNT : TNT {
	BounceFactor 0.5
	BounceCount 2
	+DOOMBOUNCE
}

ACTOR FloorKaboom {
	Radius 0
	Height 0
	Decal Scorch
	RenderStyle "Add"
	Alpha 0.45
	+DONTSPLASH
	+NOBLOCKMAP
	+RANDOMIZE
	
	States {
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("FloorKaboomDouble", 0, 0, 0, momx, momy, momz, 0, SXF_NOCHECKPOSITION)
			Goto Death
			
		Death:
			TNT1 A 0 Radius_quake(2, 7, 0, 16, 0)
			TNT1 AAA 0 A_SpawnItemEx("NapalmDebris",0,0,0,Random(4,-4),Random(4,-4),Random(4,8),0,128)
			TNT1 AAA 0 A_SpawnItemEx("NapalmDebris2",0,0,0,Random(4,-4),Random(4,-4),Random(4,8),0,128)
			TNT1 A 0 A_Jump(128, "AltDeath")
			DEXP ABC 2 BRIGHT
			TNT1 A 0 Radius_Quake(1, 10, 0, 16, 0)
			DEXP DEFGHI 3 BRIGHT
			DEXP JKLM 2 BRIGHT
			Stop
			
		AltDeath:
			DEXP NOP 2 BRIGHT
			TNT1 A 0 Radius_Quake(1, 10, 0, 16, 0)
			TNT1 A 0 A_JumpIf(waterlevel>2, "AltDeathVapor")
			DEXP QRSTU 3 BRIGHT
			DEXP VWXYZ 2 BRIGHT
			Stop
			
		AltDeathVapor:
			DEXP DEFGHI 1
			DEXP JKLM 1
			Stop
	}
}

ACTOR FloorKaboomDouble : FloorKaboom {
	States {
		Spawn:
			TNT1 A 0 A_Jump(128, "AltDeath")
			DEXP ABC 1 BRIGHT
			TNT1 A 0 A_JumpIf(waterlevel>2, "DeathVapor")
			DEXP DEFGHI 2 BRIGHT
			DEXP JKLM 2 BRIGHT
			Stop
			
		DeathVapor:
			DEXP DEFGHI 1 BRIGHT
			DEXP JKLM 1 BRIGHT
			Stop
			
		AltDeath:
			DEXP NOP 1 BRIGHT
			TNT1 A 0 A_JumpIf(waterlevel>2, "AltDeathVapor")
			DEXP QRSTU 2 BRIGHT
			DEXP VWXYZ 2 BRIGHT
			Stop
			
		AltDeathVapor:
			DEXP DEFGHI 1 BRIGHT
			DEXP JKLM 1 BRIGHT
			Stop
	}
}

ACTOR Kaboom : BulletPuff {
	Radius 32
	Height 50
	Decal Scorch
	RenderStyle "Add"
	Alpha 0.45
	+DONTSPLASH
	+NOGRAVITY
	+RANDOMIZE
	
	States {
		Spawn:
			TNT1 A 0 Radius_quake(2, 8, 0, 16, 0)
			TNT1 A 0 A_SpawnItemEx("KaboomDouble", 0, 0, 0, momx, momy, momz, 0, SXF_NOCHECKPOSITION)
			Goto Death
			
		Death:
			TNT1 AAA 0 A_SpawnItemEx("NapalmDebris",0,0,0,Random(4,-4),Random(4,-4),Random(4,8),0,128)
			TNT1 AAA 0 A_SpawnItemEx("NapalmDebris2",0,0,0,Random(4,-4),Random(4,-4),Random(4,8),0,128)
			TNT1 A 0 A_Jump(128, "AltDeath")
			DEXP ABCD 3 BRIGHT
			TNT1 A 0 Radius_Quake(1, 10, 0, 16, 0)
			DEXP EFGHI 3 BRIGHT
			DEXP JKLMN 2 BRIGHT
			Stop
			
		AltDeath:
			DEXP OPQ 3 BRIGHT
			TNT1 A 0 Radius_Quake(1, 10, 0, 16, 0)
			DEXP RSTUV 3 BRIGHT
			DEXP WXYZ 2 BRIGHT
			Stop
	}
}

ACTOR KaboomDouble : Kaboom {
	States {
		Spawn:
			TNT1 A 0 A_Jump(128,"AltDeath")
			DEXP BCD 1 BRIGHT
			DEXP E 2 BRIGHT A_JumpIf(waterlevel>2, "DeathVapor")
			DEXP FGHIJKLMN 2 BRIGHT
			Stop
			
		DeathVapor:
			DEXP EFGHIJKLMN 1 BRIGHT
			Stop
			
		AltDeath:
			DEXP OPQ 2 BRIGHT
			DEXP R 2 BRIGHT A_JumpIf(waterlevel>2, "AltDeathVapor")
			DEXP STUVWXYZ 2 BRIGHT
			Stop
			
		AltDeathVapor:
			DEXP RSTUVWXYZ 1 BRIGHT
			Stop
	}
}