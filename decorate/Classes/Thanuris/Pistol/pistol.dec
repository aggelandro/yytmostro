ACTOR ThanuPistol : WeaponWithGrenadeOption
{
   Inventory.PickupSound "misc/w_pkup"
   Inventory.PickupMessage "You picked up the Pistol. It appears to have been amplified by unknown technology."
   Weapon.AmmoType1 "Clip"
   Weapon.AmmoType2 "Clip"
   Weapon.AmmoGive 20
   Weapon.AmmoUse1 1
   Weapon.AmmoUse2 1
   States
   {
   Spawn:
      SPPP A -1
      Loop
   Ready:
			SPRP A 0 A_JumpIfInventory("ClassThroweableAction", 1, "GrenadeCheck")
      SPRP A 1 A_WeaponReady
      Loop
   Deselect:
      SPRP A 1 A_Lower
      Loop  
   Select:
			TNT1 A 0 A_TakeInventory("ClassThroweableAction", 1)
      SPRP A 1 A_Raise
      Loop 
  Fire:
      TNT1 A 0
	  TNT1 A 0 A_PlaySound("SPRPSHOT")
	  SPRP E 1 Bright 
      TNT1 A 0 A_FireCustomMissile("ScatterPShot", 0, 1, 0, 0, random(-3,3), 0)
	  TNT1 A 0 A_GunFlash
	  SPRP F 1 Bright
	  SPRP G 1
	  TNT1 A 0 A_CheckReload
	  TNT1 A 0 A_PlaySound("SPRPSHOT")
	  SPRP E 1 Bright 
	  TNT1 A 0 A_FireCustomMissile("ScatterPShot", random(-3,3), 1, 0, 0, random(-3,3), 0)
	  TNT1 A 0 A_GunFlash
	  SPRP F 1 Bright
	  SPRP G 1
	  TNT1 A 0 A_CheckReload
	  TNT1 A 0 A_PlaySound("SPRPSHOT")
	  SPRP E 1 Bright 
	  TNT1 A 0 A_FireCustomMissile("ScatterPShot", random(-2,4), 1, 0, 0, random(-3,3), 0)
	  TNT1 A 0 A_GunFlash
	  SPRP F 1 Bright
	  SPRP G 1	  
	  SPRP D 2
	  SPRP G 1
	  SPRP CBA 2
	  SPRP A 2
	  SPRP A 1 A_ReFire
      Goto Ready  	  
  AltFire:
	  TNT1 A 0
	  TNT1 A 0 A_PlaySound("SPRPSHOT")
	  SPRP E 1 Bright 
	  TNT1 A 0 A_FireCustomMissile("ScatterPShot", 0, 1, 0, 0, 0, 0)
	  TNT1 A 0 A_GunFlash
	  SPRP F 1 Bright
	  SPRP G 1
	  SPRP D 2
	  SPRP G 2
	  SPRP CBA 1
	  SPRP A 2
	  SPRP A 1 A_ReFire
      Goto Ready  
   Flash:
      TNT1 A 1 Bright A_Light1
	  TNT1 A 1 Bright A_Light0
      Stop
   }
}

ACTOR ScatterPShot : FastProjectile
{
   Speed 50
   Radius 4
   Height 4
   Damage (3*random(2,3))
   Scale 0.25
   PROJECTILE
   +BRIGHT
   RenderStyle Add
   MissileHeight 8
   MissileType "ScatterPShotTrail"
   SeeSound "NDLSHOT"
   States
   {
   Spawn:
      SPPJ A 1
	  TNT1 A 0 A_SpawnItemEx("ScatterShotGlow", 0, 0, 0, 0, 0, 0, 0, 32)
      Loop
   Death:
      TNT1 A 0
	  TNT1 A 0 A_PlaySound("SPRPIMPT")
	  TNT1 A 0 A_SetScale(0.20)
	  TNT1 AAAA 0 A_CustomMissile("ScatterSpark",0,0,random(0,360),CMF_AIMDIRECTION,random(0,12))
      SPRI ABCDE 1
      Stop
   }
}

ACTOR ScatterSpark
{
   Speed 15
   Radius 4
   Height 4
   Scale 0.15
   Gravity 0.3
   PROJECTILE
   +BRIGHT
   -NOGRAVITY
   +DROPOFF 
   +THRUACTORS
   RenderStyle Add
   States
   {
   Spawn:
      SPPJ A 1
	  TNT1 A 0 A_SpawnItemEx("ScatterShotGlowSmall", 0, 0, 0, 0, 0, 0, 0, 32)
	  TNT1 A 0 A_SpawnItemEx("ScatterShotXDMG", 0, 0, 0, 0, 0, 0, 0, 32)
      Loop
   Death:
      TNT1 A 0
	  TNT1 A 0 A_PlaySound("SPRPIMPT",5,0.12,0)
	  TNT1 A 0 A_SetScale(0.08)
      SPRI ABCDE 1
      Stop
   }
}

ACTOR ScatterShotXDMG
{                 
   Speed 2
   Radius 6
   Height 7
   Damage 0
//   Damage (1*random(1,3))
   PROJECTILE
   +NODAMAGETHRUST
   States
   {
   Spawn:
      TNT1 A 1
      Stop
   Death:
      TNT1 A 0
      Stop
   }
}

//AESTHETIC EFFECTS

ACTOR ScatterShotGlow
{
   +NOINTERACTION
   +THRUACTORS
   RenderStyle Add
   Alpha 0.4
   Scale 0.25
   States
   {
   Spawn:
      TNT1 A 0
	  SPGL A 1 Bright
      Stop
   }
}

ACTOR ScatterShotGlowSmall : ScatterShotGlow
{
   Scale 0.1
}

ACTOR ScatterPShotTrail
{
    Radius 6
    Height 8
    Scale 0.06
    RenderStyle Add
    Alpha 0.4
	+NOINTERACTION
	+THRUACTORS
    States
    {
    Spawn:
	NDTL A 1 A_FadeOut (0.1)
	Loop
    }
}