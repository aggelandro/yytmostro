ACTOR EterealShocker : YYTWeapon {
	Weapon.AmmoType "Cell"
	Weapon.AmmoUse 40
	Weapon.AmmoGive 40
	Inventory.PickupMessage "Ya tenes el poder, wachin"
	+NOALERT
	
	States {
		Select:
			TNT1 A 0 A_TakeInventory("WeaponSpecialAction", 1)
			TNT1 A 0 A_TakeInventory("SlideKickAction", 1)
			TLKS A 1 A_Raise
			Goto Select+2
			
		Deselect:
			TLKS A 1 A_Lower
			Loop
			
		Ready:
			TNT1 A 0 A_JumpIfInventory("SpellReady", 1, "Ready2")
			TNT1 A 0 A_JumpIfInventory("AltSpellReady", 1, "Ready2")
			TNT1 A 0 A_JumpIfInventory("WeaponSpecialAction", 1, "AcquireTarget")
			TNT1 A 0 A_JumpIfInventory("SlideKickAction", 1, "SlideKick")
			TNT1 A 0 A_CheckFlag("CORPSE", "RemoveTarget", AAPTR_TARGET)
			TLKS A 1 A_WeaponReady
			Loop
			
		RemoveTarget:
			TNT1 A 0 ACS_NamedExecuteWithResult("setTargetToNull", 0)
			Goto Ready+3
			
		Ready2:
			TNT1 A 0 A_JumpIfInventory("WeaponSpecialAction", 1, "AcquireTarget")
			TNT1 A 0 A_JumpIfInventory("SlideKickAction", 1, "SlideKick")
			A35T A 1 A_WeaponReady
			Loop
			
		Fire:
			TNT1 A 0 A_JumpIfInventory("SpellReady", 1, "Fire2")
			TNT1 A 0 A_JumpIfInventory("AltSpellReady", 1, "AltFire2")
			TLKS A 3
			TLKS E 3 A_SpawnItemEx("EterealRifle", 20, 320, Height/2, 0, 0, 0, -45, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TLKS E 0 A_SpawnItemEx("EterealRifle", 20, 280, Height/2, 0, 0, 0, -39, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TLKS E 0 A_SpawnItemEx("EterealRifle", 20, 240, Height/2, 0, 0, 0, -33, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TLKS E 0 A_SpawnItemEx("EterealRifle", 20, 200, Height/2, 0, 0, 0, -26, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TLKS E 0 A_SpawnItemEx("EterealRifle", 20, 160, Height/2, 0, 0, 0, -20, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TLKS F 3 A_SpawnItemEx("EterealRifle", 20, 120, Height/2, 0, 0, 0, -15, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TLKS G 3 A_SpawnItemEx("EterealRifle", 20, 80, Height/2, 0, 0, 0, -10, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TLKS H 3 A_SpawnItemEx("EterealRifle", 20, 40, Height/2, 0, 0, 0, -5, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TLKS I 3 A_PlaySound("agge/cast", CHAN_WEAPON, 2)
			TLKS I 0 A_SpawnItemEx("EterealRifle", 20, 0, Height/2, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TLKS J 3 A_SpawnItemEx("EterealRifle", 20, -40, Height/2, 0, 0, 0, 5, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TLKS J 0 A_SpawnItemEx("EterealRifle", 20, -80, Height/2, 0, 0, 0, 10, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TLKS J 0 A_SpawnItemEx("EterealRifle", 20, -120, Height/2, 0, 0, 0, 15, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TLKS J 0 A_SpawnItemEx("EterealRifle", 20, -160, Height/2, 0, 0, 0, 20, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TLKS J 0 A_SpawnItemEx("EterealRifle", 20, -200, Height/2, 0, 0, 0, 26, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TLKS J 0 A_SpawnItemEx("EterealRifle", 20, -240, Height/2, 0, 0, 0, 33, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TLKS J 0 A_SpawnItemEx("EterealRifle", 20, -280, Height/2, 0, 0, 0, 39, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TLKS J 0 A_SpawnItemEx("EterealRifle", 20, -320, Height/2, 0, 0, 0, 45, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TNT1 A 0 A_GiveInventory("SpellReady", 1)
			Goto Ready
			
		AltFire:
			TNT1 A 0 A_JumpIfInventory("AltSpellReady", 1, "AltFire2")
			TNT1 A 0 A_JumpIfInventory("SpellReady", 1, "Fire2")
			TLKS AEFGH 3
			TLKS I 3 A_PlaySound("agge/cast", CHAN_WEAPON, 2)
			TLKS J 3 A_RadiusGive("EterealRifleCurse", 2048, RGF_MONSTERS)
			TNT1 A 0 A_GiveInventory("AltSpellReady", 1)
			Goto Ready
		
		Fire2:
			TNT1 A 0 A_JumpIf(CallACS("cells") < 40, "Ready")
			A35T ABC 3
			A35T D 3 A_Playsound("agge/snap", CHAN_WEAPON, 2, false, ATTN_STATIC)
			A35T E 3
			A35T F 3 A_RadiusGive("ShottoToken", 4096, RGF_OBJECTS, 1)
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_TakeInventory("Cell", 40)
			A35T F 98
			A35T GHIJ 3
			TNT1 A 0 A_TakeInventory("SpellReady", 1)
			Goto Ready
			
		AltFire2:
			TNT1 A 0 A_JumpIf(CallACS("cells") < 40, "Ready")
			A35T ABC 3
			A35T D 3 A_Playsound("agge/snap", CHAN_WEAPON, 2, false, ATTN_STATIC)
			A35T E 3
			A35T F 3 A_RadiusGive("ShottoToken", 4096, RGF_OBJECTS, 1)
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_TakeInventory("Cell", 40)
			A35T GHIJ 3
			TNT1 A 0 A_TakeInventory("AltSpellReady", 1)
			Goto Ready
			
		AcquireTarget:
			TNT1 A 0 A_TakeInventory("WeaponSpecialAction", 1)
			CH0K R 2
			CH0K S 2 A_RailAttack(0, 0, 0, None, None, RGF_SILENT|RGF_NOPIERCING, 0, "TargetPuff")
			CH0K TUSTUSTU 2
			Goto Ready
	}
}

ACTOR TargetPuff {
	SeeSound "misc/nothing"
	Species "Player"
	+PUFFONACTORS
	+PUFFGETSOWNER
	+HITTRACER
	+MTHRUSPECIES
	+ALWAYSPUFF
	+NODECAL
	+BLOODLESSIMPACT
	
	States {
		XDeath:
			TNT1 A 1 ACS_NamedExecuteWithResult("playerAcquiresTarget")
			Stop
	}
}

ACTOR EterealRifle {
	Health 9999
	Scale 0.15
	Alpha 0.05
	ReactionTime 160
	Mass 1000
	//FloatBobStrength 0.5
	+NOGRAVITY
	+FLOATBOB
	+VULNERABLE // Necesario porque el radiusgive de zandromierda es limitado
	+BRIGHT
	
	States {
		Spawn:
			TNT1 A 0 NoDelay A_GiveInventory("LoopFlag", 1)
			ERAA A 1 A_FadeIn(0.05)
			TNT1 A 0 A_CountDown
			TNT1 A 0 A_JumpIfInventory("ShottoToken", 1, "Shoot")
			TNT1 A 0 A_JumpIfInventory("LoopFlag", 8, 1)
			Loop
			TNT1 A 0 A_SpawnItemEx("BlueBodyParticle2Spawner", Random(-10, 10), 0, 0, 0, 0, 0, 0, 128)
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			ERAA A 1 A_FadeIn(0.05)
			TNT1 A 0 A_JumpIfInventory("ShottoToken", 1, "Shoot")
			TNT1 A 0 A_JumpIfInventory("LoopFlag", 16, 1)
			Goto Spawn+6
			TNT1 A 0 A_SpawnItemEx("BlueBodyParticle2Spawner", Random(-10, 10), 0, 0, 0, 0, 0, 0, 128)
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			ERAA A 1 A_FadeIn(0.05)
			TNT1 A 0 A_JumpIfInventory("ShottoToken", 1, "Shoot")
			TNT1 A 0 A_TakeInventory("LoopFlag", 17)
			Goto FadeOut
			
		FadeOut:
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			ERAA A 1 A_FadeOut(0.05, 0)
			TNT1 A 0 A_JumpIfInventory("ShottoToken", 1, "Shoot")
			TNT1 A 0 A_JumpIfInventory("LoopFlag", 7, 1)
			Loop
			TNT1 A 0 A_SpawnItemEx("BlueBodyParticle2Spawner", Random(-10, 10), 0, 0, 0, 0, 0, 0, 128)
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			ERAA A 1 A_FadeOut(0.05, 0)
			TNT1 A 0 A_JumpIfInventory("ShottoToken", 1, "Shoot")
			TNT1 A 0 A_JumpIfInventory("LoopFlag", 15, 1)
			Goto FadeOut+5
			TNT1 A 0 A_SpawnItemEx("BlueBodyParticle2Spawner", Random(-10, 10), 0, 0, 0, 0, 0, 0, 128)
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			ERAA A 1 A_FadeOut(0.05, 0)
			TNT1 A 0 A_JumpIfInventory("ShottoToken", 1, "Shoot")
			TNT1 A 0 A_JumpIfInventory("LoopFlag", 17, 1)
			Goto FadeOut+10
			TNT1 A 0 A_TakeInventory("LoopFlag", 17)
			Goto Spawn
			
		Shoot:
			TNT1 A 0 A_TakeInventory("LoopFlag", 17)
			TNT1 A 0 A_ChangeFlag("VULNERABLE", FALSE)
			ERAA A 5 A_FaceTarget
			TNT1 A 0 A_ChangeFlag("FLOATBOB", FALSE)
			TNT1 A 0 ACS_NamedExecuteWithResult("setAlphaTo", 1)
			TNT1 A 0 A_SpawnItemEx("LaserCharging", 45, 0, 8, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
			ERAA A 93 A_FaceTarget
			TNT1 A 0 A_PlaySound("agge/laserfire", CHAN_AUTO, 2)
			Goto ShootLoop
			
		ShootLoop:
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			ERAA A 2 A_FaceTarget
			TNT1 A 0 A_JumpIf(CallACS("targetPlayerHasTarget"), 3)
			TNT1 A 0 A_CustomMissile("ElectricMiniLaser", 10, 0, 0, CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH)
			TNT1 A 0 A_Jump(256, 2)
			TNT1 A 0 A_CustomMissile("ElectricMiniLaser", 10)
			TNT1 A 0 A_JumpIfInventory("LoopFlag", 18, 1)
			Loop
			TNT1 A 0 A_TakeInventory("LoopFlag", 18)
			TNT1 A 0 A_SpawnItemEx("BlueBodyParticle2Spawner", Random(-10, 10), 0, 0, 0, 0, 0, 0, 128)
			Goto Death
			
		Death:
			ERAA A 1 A_FadeOut(0.05, 0)
			Loop
	}
}

ACTOR CursedEterealRifle : EterealRifle {
	States {
		Shoot:
			TNT1 A 0 A_TakeInventory("LoopFlag", 17)
			ERAA A 5 A_FaceTarget
			TNT1 A 0 A_ChangeFlag("FLOATBOB", FALSE)
			TNT1 A 0 ACS_NamedExecuteWithResult("setAlphaTo", 1)
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			TNT1 A 0 A_CustomMissile("EterealRifleTracer", 0, 0, frandom(-5.1, 5.1), 0, frandom(-3.55, 3.55))
			TNT1 A 0 A_JumpIfInventory("LoopFlag", 33, 1)
			Goto Shoot+4
			TNT1 A 0 A_TakeInventory("LoopFlag", 33)
			TNT1 A 0 A_PlaySound("agge/w7fire", CHAN_AUTO)
			TNT1 A 0 A_SpawnItemEx("BlueBodyParticle2Spawner", Random(-10, 10), 0, 0, 0, 0, 0, 0, 128)
			Goto Death
	}
}

ACTOR LaserCharging {
	Scale 0.15
	+NOGRAVITY
	+BRIGHT
	
	States {
		Spawn:
			TNT1 A 0 NoDelay A_PlaySound("agge/lasercharge", CHAN_AUTO)
			ZA99 GFEDCB 15
			ZA99 A 3
			Stop
	}
}

ACTOR SpellReady : Inventory { Inventory.MaxAmount 1 Inventory.Amount 1 }
ACTOR AltSpellReady : SpellReady {}
ACTOR ShottoToken : Inventory { Inventory.MaxAmount 1 Inventory.Amount 1 }

Actor ElectricMiniLaser : FastProjectile {
	Height 3
	Radius 3
	Speed 70
	Renderstyle Add
	Alpha 0.8
	Scale 0.03
	Damage 1
	Missiletype "ElectricMiniLaserTrail"
	MissileHeight 8
	Damagetype "Lightning"
	Species "Player"
	+THRUSPECIES
	+RANDOMIZE
	
	States {
		Spawn:
			YAE1 A 1 NoDelay Bright A_ChangeVelocity(1,0.5*random(-3,3),0.5*random(-2,2),CVF_RELATIVE)
			Loop
			
		Death:
			TNT1 A 0 A_SpawnItemEx("DecorativeSparks",random(-4,4),random(-4,4),1,-2,random(-1,3),-1)
			TNT1 AAA 0 A_CustomMissile("SmallBlueExplosion", 0, 0, random(0,360), 2, random(0,360))
			TNT1 AAA 0 A_CustomMissile("SmokeSmallExplosion", 0, 0, random(0,360), 2, random(0,360))  
			TNT1 A 10
			Stop
  }
}

ACTOR ElectricMiniLaserTrail {
  RenderStyle Add
  Alpha 0.75
  Scale 0.03
  +NOGRAVITY
  +CLIENTSIDEONLY
  +NOINTERACTION
  +NOBLOCKMAP
	
  States {
		Spawn:
		YAE1 A 2 NoDelay bright A_FadeOut(0.04)
		Loop
  }
}

ACTOR EterealRifleCurse : CustomInventory {
	Inventory.MaxAmount 1
	Inventory.Amount 1
	
	States {
		Pickup:
			TNT1 A 0 A_SetUserVar("user_spawn_tid", ACS_NamedExecuteWithResult("getUniqueTID"))
			TNT1 A 0 A_SpawnItemEx("CursedEterealRifle", Random(-10, 10), Random(-10, 10), Random(Height+5, Height+20), 0, 0, 0, 0, 0, 0, "user_spawn_tid")
			TNT1 A 0 ACS_NamedExecuteWithResult("setTIDTargetPointerToSelf", "user_spawn_tid")
			Stop
	}
}