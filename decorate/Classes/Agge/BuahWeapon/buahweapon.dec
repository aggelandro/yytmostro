ACTOR BuahWeapon : Weapon {	
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You got the buah weapon!"
	Obituary "%o was PUIJed by %k's buah weapon."
	Weapon.AmmoGive 100
	Weapon.AmmoUse 1
	Weapon.AmmoType "Clip"

	States {
	 
		Spawn:
		  MGUN A -1
		  Stop

		Ready:
		  TNT1 A 0 ACS_NamedExecuteWithResult("showPowerups", 1)
		  Goto ReadyLoop

		ReadyLoop:
		  CFLM AAAABBBBCCCC 1 A_WeaponReady
		  Loop

		Deselect:
		  TNT1 A 0 ACS_NamedExecuteWithResult("showPowerups", 0)
		  Goto DeselectLoop

		DeselectLoop:
		  CFLM A 1 A_Lower
		  Loop

		Select:
		  CFLM A 1 A_Raise
		  Loop

		Fire:
			CFLM A 1 Offset (0, 40)
			CFLM D 2 Offset (0, 50)
			CFLM D 2 Offset (0, 36)
			CFLM E 4 Bright
			TNT1 A 0 A_JumpIfInventory("TwentyTwentyGiver", 1, "FireDoubleShot")
			Goto FireNormalShot

		FireNormalShot:
			// No se pueden usar variables de usuario en armas, gracias Mauricio
			TNT1 A 0 A_JumpIfInventory("DarkMatterGiver", 1, "FireFearShot")
			CFLM E 0 A_PlaySound("agge/buah", CHAN_WEAPON)
			CFLM F 2 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			CFLM E 0 A_FireCustomMissile("NormalTearProjectile")
			CFLM E 2 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			CFLM E 0 A_FireCustomMissile("NormalTearProjectile")
			CFLM F 0 A_PlaySound("agge/buah", CHAN_WEAPON)
			CFLM F 2 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			CFLM F 0 A_FireCustomMissile("NormalTearProjectile")
			CFLM E 2 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			CFLM E 0 A_FireCustomMissile("NormalTearProjectile")
			Goto FireEnd
		 
		FireFearShot:
			CFLM E 0 A_PlaySound("agge/buah", CHAN_WEAPON)
			CFLM F 2 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			CFLM E 0 A_FireCustomMissile("FearTearProjectile")
			CFLM E 2 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			CFLM E 0 A_FireCustomMissile("FearTearProjectile")
			CFLM F 0 A_PlaySound("agge/buah", CHAN_WEAPON)
			CFLM F 2 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			CFLM F 0 A_FireCustomMissile("FearTearProjectile")
			CFLM E 2 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			CFLM E 0 A_FireCustomMissile("FearTearProjectile")
			Goto FireEnd
		 

		FireDoubleShot:
			TNT1 A 0 A_JumpIfInventory("DarkMatterGiver", 1, "FireDoubleFearShot")
			CFLM E 0 A_PlaySound("agge/buah", CHAN_WEAPON)
			CFLM F 2 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			CFLM E 0 A_FireCustomMissile("NormalTearProjectile", 0, TRUE, -10)
			CFLM E 0 A_FireCustomMissile("NormalTearProjectile", 0, TRUE, 10)
			CFLM E 2 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			CFLM E 0 A_FireCustomMissile("NormalTearProjectile", 0, TRUE, -10)
			CFLM E 0 A_FireCustomMissile("NormalTearProjectile", 0, TRUE, 10)
			CFLM F 0 A_PlaySound("agge/buah", CHAN_WEAPON)
			CFLM F 2 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			CFLM E 0 A_FireCustomMissile("NormalTearProjectile", 0, TRUE, -10)
			CFLM E 0 A_FireCustomMissile("NormalTearProjectile", 0, TRUE, 10)
			CFLM E 2 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			CFLM E 0 A_FireCustomMissile("NormalTearProjectile", 0, TRUE, -10)
			CFLM E 0 A_FireCustomMissile("NormalTearProjectile", 0, TRUE, 10)
			Goto FireEnd
			
		FireDoubleFearShot:
			CFLM E 0 A_PlaySound("agge/buah", CHAN_WEAPON)
			CFLM F 2 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			CFLM E 0 A_FireCustomMissile("FearTearProjectile", 0, TRUE, -10)
			CFLM E 0 A_FireCustomMissile("FearTearProjectile", 0, TRUE, 10)
			CFLM E 2 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			CFLM E 0 A_FireCustomMissile("FearTearProjectile", 0, TRUE, -10)
			CFLM E 0 A_FireCustomMissile("FearTearProjectile", 0, TRUE, 10)
			CFLM F 0 A_PlaySound("agge/buah", CHAN_WEAPON)
			CFLM F 2 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			CFLM E 0 A_FireCustomMissile("FearTearProjectile", 0, TRUE, -10)
			CFLM E 0 A_FireCustomMissile("FearTearProjectile", 0, TRUE, 10)
			CFLM E 2 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			CFLM E 0 A_FireCustomMissile("FearTearProjectile", 0, TRUE, -10)
			CFLM E 0 A_FireCustomMissile("FearTearProjectile", 0, TRUE, 10)
			Goto FireEnd
		 
		FireEnd:
			CFLM F 4 Bright
			CFLM E 4 Bright
			CFLM G 1
			CFLM G 1 Offset (0, 40)
			CFLM G 1 A_Refire
			Goto Ready

		Flash:
			TNT1 A 2 A_Light1
			TNT1 A 2 A_Light2
			TNT1 A 1 A_Light1
			TNT1 A 1 A_Light0
			Stop
  }
}

ACTOR NormalTearProjectile {	
  Radius 3
  Height 5
  Damage 2
	Gravity 1.0
	Speed 3.0
	Projectile
	var int user_isdead;

	+BRIGHT
	+HITTRACER
	
  States {
	
	  Spawn:
			// Esperar a que exista A_SetSize en zandromierda para height y radius
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("setTearStats")
			TNT1 A 0 A_SetUserVar("user_isdead", 0)
			TNT1 A 0 A_Jump(256, "SpawnLoop") //La forma que tiene decorate de resolver por herencia
			Wait
			
		SpawnLoop:
			TBFI M 1
			Loop
			
		Death:
			TNT1 A 0 A_SetUserVar("user_isdead", 1)
			TDFI A 0 A_PlaySound("agge/lagrima",CHAN_AUTO)
			TDFI A 1
			TDFI ABC 2
			TDFI D 2
			TDFI E 1
			TDFI F 2
			TDFI G 1
			TDFI H 2
			TDFI I 1
			TDFI J 2
			TDFI K 1
			TDFI L 2
			TDFI M 1
			TDFI N 2
			TNT1 A 0 A_JumpIfInventory("Infestation2Giver", 1, "InfestationDeath", AAPTR_TARGET)
			Stop
			
		InfestationDeath:
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("isEffectChanceSuccessful", 20) == 0, 1)
			TNT1 A 0 A_GiveInventory("InfestationDebuff", 1, AAPTR_TRACER)
			Stop
  }
}

ACTOR FearTearProjectile : NormalTearProjectile {
	
  States {
			
		SpawnLoop:
			DMFI A 2
			DMFI B 2
			DMFI C 2
			DMFI D 2
			Loop
			
		XDeath:
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("isEffectChanceSuccessful", 8) == 0, "Death")
			TNT1 A 0 A_GiveInventory("FearDebuff", 1, AAPTR_TRACER)
			Goto Death
			
		Death:
			TNT1 A 0 ACS_NamedExecuteWithResult("showShotDeathTranslation", 1)
			TNT1 A 0 A_Jump(256, "Super::Death")
			Wait
  }
}