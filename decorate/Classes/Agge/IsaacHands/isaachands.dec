ACTOR IsaacHands : Weapon {	
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You got the isaac hands!"
	Obituary "%o was PUIJed by %k's isaac hands."
	Weapon.AmmoGive 100
	Weapon.AmmoUse 1
	Weapon.AmmoType "Clip"

	States {
	 
		Spawn:
		  MGUN A -1
		  Stop

		Ready:
		  TNT1 A 0 ACS_NamedExecuteWithResult("showPowerups", 1)
			TNT1 A 0 A_Jump(256, "ReadyLoop")
		  Wait

		ReadyLoop:
		  CFLM AAAABBBBCCCC 1 A_WeaponReady
		  Loop

		Deselect:
		  TNT1 A 0 ACS_NamedExecuteWithResult("showPowerups", 0)
			TNT1 A 0 A_Jump(256, "DeselectLoop")
		  Wait

		DeselectLoop:
		  CFLM A 1 A_Lower
		  Loop

		Select:
		  CFLM A 1 A_Raise
		  Loop

		Fire:
			CFLM A 1 Offset (0, 40)
			CFLM D 2 Offset (0, 50)
			CFLM D 2 Offset (0, 36)
			CFLM E 4 Bright
			Goto FireNormalShot

		FireNormalShot:
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			CFLM E 0 A_PlaySound("agge/buah", CHAN_WEAPON)
			CFLM E 1 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			CFLM F 1 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("checkForNoGiverInInventory", 22), 3) //OFFSET
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("isEffectChanceSuccessful", 20) == 0, 2)
			CFLM E 0 A_FireCustomMissile("NormalTearProjectile", 180, FALSE, 0, 20)
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("checkForNoGiverInInventory", 15), 4) //OFFSET
			CFLM E 0 A_FireCustomMissile("NormalTearProjectile", 0, TRUE, -10)
			CFLM E 0 A_FireCustomMissile("NormalTearProjectile", 0, FALSE, 10)
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("checkForNoGiverInInventory", 23), 9) //OFFSET
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("checkForNoGiverInInventory", 23), 9) //OFFSET
			TNT1 A 0 A_JumpIfInventory("TwentyTwentyGiver", 1, 3) //OFFSET
			CFLM E 0 A_FireCustomMissile("NormalTearProjectile", 0, TRUE, -10)
			CFLM E 0 A_FireCustomMissile("NormalTearProjectile", 0, FALSE, 10)
			CFLM E 1 Bright
			CFLM F 1 Bright
			CFLM E 0 A_FireCustomMissile("NormalTearProjectile", 0, FALSE, -20)
			CFLM E 0 A_FireCustomMissile("NormalTearProjectile", 0, FALSE, 20)
			TNT1 A 0 A_JumpIfInventory("LoopFlag", 4, "FireEnd")
			Loop
			CFLM E 0 A_FireCustomMissile("NormalTearProjectile")
			TNT1 A 0 A_JumpIfInventory("LoopFlag", 4, "FireEnd")
			Loop
		 
		FireEnd:
			TNT1 A 0 A_TakeInventory("LoopFlag", 4)
			CFLM F 4 Bright
			CFLM E 4 Bright
			CFLM G 1
			CFLM G 1 Offset (0, 40)
			CFLM G 1 A_Refire
			Goto Ready

		Flash:
			TNT1 A 2 A_Light1
			TNT1 A 2 A_Light2
			TNT1 A 1 A_Light1
			TNT1 A 1 A_Light0
			Stop
  }
}

ACTOR BlackIsaacHands : IsaacHands {
	
	States {
		ReadyLoop:
		  CFLB AAAABBBBCCCC 1 A_WeaponReady
		  Loop
			
		DeselectLoop:
		  CFLB A 1 A_Lower
		  Loop

		Select:
		  CFLB A 1 A_Raise
		  Loop
			
		Fire:
			CFLB A 1 Offset (0, 40)
			CFLB D 2 Offset (0, 50)
			CFLB D 2 Offset (0, 36)
			CFLB E 4 Bright
			Goto FireNormalShot
		 
		FireNormalShot:
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			CFLB E 0 A_PlaySound("agge/buah", CHAN_WEAPON)
			CFLB E 1 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			CFLB F 1 Bright A_SetTics(10 / ACS_NamedExecuteWithResult("getFireRateBoost"))
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("checkForNoGiverInInventory", 22), 3) //OFFSET
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("isEffectChanceSuccessful", 20) == 0, 2)
			CFLB E 0 A_FireCustomMissile("FearTearProjectile", 180, FALSE)
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("checkForNoGiverInInventory", 15), 4) //OFFSET
			CFLB E 0 A_FireCustomMissile("FearTearProjectile", 0, TRUE, -10)
			CFLB E 0 A_FireCustomMissile("FearTearProjectile", 0, FALSE, 10)
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("checkForNoGiverInInventory", 23), 9) //OFFSET
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("checkForNoGiverInInventory", 23), 9) //OFFSET
			TNT1 A 0 A_JumpIfInventory("TwentyTwentyGiver", 1, 3) //OFFSET
			CFLB E 0 A_FireCustomMissile("FearTearProjectile", 0, TRUE, -10)
			CFLB E 0 A_FireCustomMissile("FearTearProjectile", 0, FALSE, 10)
			CFLB E 1 Bright
			CFLB F 1 Bright
			CFLB E 0 A_FireCustomMissile("FearTearProjectile", 0, FALSE, -20)
			CFLB E 0 A_FireCustomMissile("FearTearProjectile", 0, FALSE, 20)
			TNT1 A 0 A_JumpIfInventory("LoopFlag", 4, "FireEnd")
			Loop
			CFLB E 0 A_FireCustomMissile("FearTearProjectile")
			TNT1 A 0 A_JumpIfInventory("LoopFlag", 4, "FireEnd")
			Loop
		 
		FireEnd:
			TNT1 A 0 A_TakeInventory("LoopFlag", 4)
			CFLB F 4 Bright
			CFLB E 4 Bright
			CFLB G 1
			CFLB G 1 Offset (0, 40)
			CFLB G 1 A_Refire
			Goto Ready
	}
}

ACTOR NormalTearProjectile {	
  Radius 3
  Height 5
  Damage 2
	Gravity 1.0
	Speed 3.0
	Health 1
	Projectile
	Species "Player" //Para que no se choquen las tears entre ellas cuando son shielded
	var int user_isdead;
	var int user_petrifying_flag;

	+BRIGHT
	+HITTRACER
	+MTHRUSPECIES
	
  States {
	
	  Spawn:
			// Esperar a que exista A_SetSize en zandromierda para height y radius
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("setTearStats")
			TNT1 A 0 A_SetUserVar("user_isdead", 0)
			TNT1 A 0 A_SetUserVar("user_petrifying_flag", 0)
			TNT1 A 0 A_JumpIfInventory("LostContactGiver", 1, "SpawnShieldedTear", AAPTR_TARGET)
			TNT1 A 0 A_JumpIfInventory("MomsContactsGiver", 1, "SpawnPetrifyingTear", AAPTR_TARGET)
			TNT1 A 0 A_Jump(256, "SpawnLoop") //La forma que tiene decorate de resolver por herencia
			Wait
			
		SpawnShieldedTear:
			TNT1 A 0 A_SetTranslucent(0.75, 1)
			TNT1 A 0 A_ChangeFlag("SHOOTABLE", TRUE)
			TNT1 A 0 A_ChangeFlag("NOBLOCKMAP", FALSE)
			TNT1 A 0 A_JumpIfInventory("MomsContactsGiver", 1, "SpawnPetrifyingTear", AAPTR_TARGET)
			TNT1 A 0 A_Jump(256, "SpawnLoop")
			Wait
			
		SpawnPetrifyingTear:
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("isEffectChanceSuccessful", 15) == 0, "SpawnLoop")
			TNT1 A 0 ACS_NamedExecuteWithResult("showTranslation", 6)
			TNT1 A 0 A_SetUserVar("user_petrifying_flag", 1)
			TNT1 A 0 A_Jump(256, "SpawnLoop")
			Wait
			
		SpawnLoop:
			TBFI M 1
			Loop
			
		XDeath:
			TNT1 A 0 A_JumpIf(user_petrifying_flag==0, "Death")
			TNT1 A 0 A_GiveInventory("PetrifyingDebuff", 1, AAPTR_TRACER)
			TNT1 A 0 A_Jump(256, "Death")
			Wait
			
		Death:
			TNT1 A 0 A_SetUserVar("user_isdead", 1)
			TDFI A 0 A_PlaySound("agge/lagrima",CHAN_AUTO)
			TDFI A 1
			TDFI ABC 2
			TDFI D 2
			TDFI E 1
			TDFI F 2
			TDFI G 1
			TDFI H 2
			TDFI I 1
			TDFI J 2
			TDFI K 1
			TDFI L 2
			TDFI M 1
			TDFI N 2
			TNT1 A 0 A_JumpIfInventory("Infestation2Giver", 1, "InfestationDeath", AAPTR_TARGET)
			Stop
			
		InfestationDeath:
			TNT1 A 0 A_GiveInventory("InfestationDebuff", 1, AAPTR_TRACER)
			Stop
  }
}

ACTOR FearTearProjectile : NormalTearProjectile {
	
  States {
			
		SpawnLoop:
			DMFI A 2
			DMFI B 2
			DMFI C 2
			DMFI D 2
			Loop
			
		XDeath:
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("isEffectChanceSuccessful", 8) == 0, "Death")
			TNT1 A 0 A_GiveInventory("FearDebuff", 1, AAPTR_TRACER)
			Goto Super::XDeath
			
		Death:
			TNT1 A 0 ACS_NamedExecuteWithResult("showTranslation", 4)
			TNT1 A 0 A_Jump(256, "Super::Death")
			Wait
  }
}

ACTOR LoopFlag : Inventory {
	-INVBAR
  +UNDROPPABLE
  Inventory.MaxAmount 4
  Inventory.Icon TNT1A0
}