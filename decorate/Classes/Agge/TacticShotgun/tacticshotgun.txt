ACTOR TacticShotgun : WeaponWithGrenadeOption {
	Weapon.SelectionOrder 1300
  Weapon.AmmoUse 0
  Weapon.AmmoGive 8
  Weapon.AmmoType "Shell"
  Inventory.PickupMessage "$GOTSHOTGUN"
  Obituary "$OB_MPSHOTGUN"
  Tag "$TAG_SHOTGUN"
	+NOALERT
	
	States {
		Spawn:
			SHAT A -1
			Stop
	
		Select:
			TNT1 A 0 A_TakeInventory("ClassThroweableAction", 1)
			RSHT A 1 A_Raise
			Loop
			
		Deselect:
			RSHT A 1 A_Lower
			Loop
			
		Ready:
			RSHT A 0 A_JumpIfInventory("ClassThroweableAction", 1, "GrenadeCheck")
			RSHT A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
	
		Fire:
			TNT1 A 0 A_JumpIf(CallACS("aggeSGShellsLeft") == 0, "Empty")
			TNT1 A 0 A_PlaySound("agge/ssgclick", CHAN_WEAPON)
			TNT1 A 0 A_JumpIfInventory("ZoomedFlag",1,"ZFire")
			RSHT B 2
			TNT1 A 0 A_PlaySound("agge/sgfire", CHAN_WEAPON)
			TNT1 A 0 A_ZoomFactor(0.65)
			TNT1 A 0 Radius_Quake(1, 5, 0, 1, 0)
			TNT1 A 0 A_AlertMonsters
			SHTH A 1 
			TNT1 A 0 A_FireCustomMissile("WeaponFireLight")
			SHTH A -1 A_Jump(256, "Fire1", "Fire2", "Fire3")
			Wait
			
		Fire1:
			TNT1 AAAA 0 A_FireCustomMissile("TerrorShotgunTracer", random(-1,1), 0, 0, -2, 0, random(-1.25,1.25))
			TNT1 AAA 0 A_FireCustomMissile("TerrorShotgunTracer", random(-1.1,1.1), 0, 0, -2, 0, random(-2,2))
			TNT1 AAA 0 A_FireCustomMissile("TerrorShotgunTracer", random(-1.1,1.1), 0, 0, -2, 0, random(-1.5,1.5))
			TNT1 AAAAAA 0 A_FireCustomMissile("TerrorShotgunTracer", random(-1.5,1.5), 0, 0, -2, 0, random(-1.5,1.5))
			Goto FireEnd
			
		Fire2:
			TNT1 AAAAAAAAAAAAA 0 A_FireCustomMissile("TerrorShotgunTracer", random(-1.37,1.37), 0, 0, -2, 0, random(-1.37,1.37))
			Goto FireEnd
			
		Fire3:
			TNT1 AAAAAAA 0 A_FireCustomMissile("TerrorShotgunTracer", random(-0.5,0.5), 0, 0, -2, 0, random(-1,1))
			TNT1 AAAAAAA 0 A_FireCustomMissile("TerrorShotgunTracer", random(-1,1), 0, 0, -2, 0, random(-1,1))
			Goto FireEnd
			
		FireEnd:
			TNT1 A 0 A_TakeInventory("AggeSGShellsLeft", 1)
			TNT1 A 0 ACS_NamedExecuteWithResult("showShellsInShotgun")
			TNT1 A 0 A_JumpIfInventory("ZoomedFlag",1,"ZFireEnd")
			TNT1 A 0 A_ZoomFactor(1.0)
			SHTH B 2 A_GunFlash
			SHTH C 5
			SHTH D 2
			SHTH E 1
			RSHT B 1
			TNT1 A 0 A_PlaySound("agge/sgpump2", CHAN_AUTO)
			TNT1 A 0 A_ZoomFactor(0.99)
			SHTP A 1 A_SetPitch(Pitch+0.416)
			SHTP B 1 A_SetPitch(Pitch+0.416)
			SHTP CC 1 A_SetPitch(Pitch+0.416)
			SHTP DD 1 A_SetPitch(Pitch+0.416)
			TNT1 A 0 A_SpawnItemEx("ShellCasing",10,-2,25,Random(-4,-5),Random(-4,-5),Random(4,6),270)
			TNT1 A 0 A_PlaySound("agge/sgpump", CHAN_AUTO)
			TNT1 A 0 A_ZoomFactor(1.0)
			SHTP E 1 A_SetPitch(Pitch-0.625)
			SHTP F 1 A_SetPitch(Pitch-0.625)
			SHTP G 1 A_SetPitch(Pitch-0.625)
			SHTP H 1 A_SetPitch(Pitch-0.625)
			Goto Ready
			
		Empty:
			RSHT A 5 A_PlayWeaponSound("agge/ssgclick")
			TNT1 A 0 ACS_NamedExecuteWithResult("showShellsInShotgun")
			TNT1 A 0 A_JumpIfInventory("ZoomedFlag",1,"ZOut")
			Goto Ready
			
		Reload:
			TNT1 A 0 A_JumpIfInventory("ZoomedFlag",1,"ZOut")
			TNT1 A 0 A_JumpIfInventory("AggeSGShellsLeft", 7, "Ready")
			TNT1 A 0 A_JumpIf(CallACS("shells") == 0, "Ready")
			RSHT CDE 1
			TNT1 A 0 A_PlaySound("agge/sgrmove", CHAN_WEAPON)
			RSHT F 8
			Goto ReloadLoop
			
		ReloadLoop:
			RSHT F 2 A_WeaponReady(WRF_NOBOB)
			TNT1 A 0 A_PlayWeaponSound("agge/ssgclick")
			RSHT GHI 2
			TNT1 A 0 A_PlaySound("agge/sginsert", CHAN_AUTO)
			TNT1 A 0 A_PlaySound("agge/sgpump2", CHAN_AUTO)
			TNT1 A 0 A_TakeInventory("Shell", 1)
			TNT1 A 0 A_GiveInventory("AggeSGShellsLeft", 1)
			TNT1 A 0 ACS_NamedExecuteWithResult("showShellsInShotgun")
			RSHT JK 3
			RSHT LMN 1
			TNT1 A 0 A_JumpIfInventory("AggeSGShellsLeft", 7, "ReloadEnd")
			TNT1 A 0 A_JumpIf(CallACS("shells") == 0, "ReloadEnd")
			Loop
			
		ReloadEnd:
			RSHT EDC 1
			Goto Ready
			
		Flash:
			TNT1 A 0 A_SetAngle(angle+random(-2,2))
			TNT1 A 1 A_SetPitch(pitch-2)
			TNT1 A 1 A_Light2
			TNT1 A 1 A_Light1
			TNT1 A 1 A_SetPitch(pitch-random(80,85)*0.1)
			TNT1 A 1 A_SetPitch(pitch+4)
			TNT1 A 1 A_SetPitch(pitch+2)
			TNT1 A 1 A_SetPitch(pitch+1)
			TNT1 A 2 A_SetPitch(pitch+0.5)
			TNT1 AA 3 A_SetPitch(pitch+0.25)
			TNT1 AAA 2 A_SetPitch(pitch+0.13)
			TNT1 A 2 A_Light0
			Goto LightDone
			
		AltFire:
			TNT1 A 0 A_JumpIfInventory("ZoomedFlag",1,"ZOut")
			TNT1 A 0 A_GiveInventory("ZoomedFlag",1)
			TNT1 A 0 A_PlaySound("agge/sgrmove", CHAN_WEAPON)
			ZSHT AABC 1 A_ZoomFactor(1.2)
			ZSHT D 4
			ZSHT DDDD 3 A_WeaponReady(WRF_ALLOWRELOAD|WRF_NOSECONDARY)
			Goto ZReady
			
		ZReady:
			ZSHT D 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
			
		ZOut:
			TNT1 A 0 A_PlaySound("agge/sgrmove", CHAN_WEAPON)
			ZSHT CBAA 1
			TNT1 A 0 A_ZoomFactor(1.0)
			TNT1 A 0 A_TakeInventory("ZoomedFlag",1)
			Goto Ready
			
		ZFire:
			ZSHT E 1
			TNT1 A 0 A_PlaySound("agge/sgfire", CHAN_WEAPON)
			ZSHT E 1
			TNT1 A 0 A_ZoomFactor(1.0)
			TNT1 A 0 Radius_Quake(1, 5, 0, 1, 0)
			TNT1 A 0 A_AlertMonsters
			ZSHT F 1
			TNT1 A 0 A_FireCustomMissile("WeaponFireLight")
			ZSHT F -1 A_Jump(256, "Fire1", "Fire2", "Fire3")
			Wait
			
		ZFireEnd:
			TNT1 A 0 A_SetPitch(Pitch-3.45)
			ZSHT G 2
			TNT1 A 0 A_ZoomFactor(1.2)
			ZSHT H 5
			ZSHT IJ 3
			TNT1 A 0 A_SpawnItemEx("ShellCasing",10,-2,25,Random(-4,-5),Random(-4,-5),Random(4,6),270)
			TNT1 A 0 A_PlaySound("agge/sgpump", CHAN_AUTO)
			TNT1 A 0 A_ZoomFactor(1.0)
			ZSHT KL 3
			Goto ZReady
	}
}

ACTOR AggeSGShellsLeft : Ammo { Inventory.MaxAmount 7 +IGNORESKILL }

ACTOR WeaponFireLight {
	PROJECTILE
	+NOGRAVITY
	+NOCLIP
	+NOBLOCKMAP
	+DONTSPLASH
	+THRUACTORS
	+DOOMBOUNCE
	Renderstyle None
	Radius 1.3
	Height 1
	
	States {
		Spawn:
			TNT1 AAAAA 1
			Stop
	}
}

ACTOR ZoomedFlag : Inventory { Inventory.MaxAmount 1 Inventory.Amount 1 }