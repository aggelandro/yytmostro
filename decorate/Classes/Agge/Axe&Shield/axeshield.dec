ACTOR AxeShield : WeaponWithCryoGrenadeOption {
  Weapon.AmmoType2 "EnergyShieldAmmo"
  Weapon.Kickback 100
  Obituary "$OB_MPFIST"
  Tag "Fist"
  +WEAPON.WIMPY_WEAPON
  +WEAPON.MELEEWEAPON
	+WEAPON.NOALERT
	
  States {
		Ready:
			TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, 5) //OFFSET
			CONE B 0 A_JumpIfInventory("ClassThroweableAction", 1, "SWGrenadeCheck")
			CONE B 0 A_JumpIfInventory("SlideKickAction", 1, "SlideKick")
			CONE AB 4 A_WeaponReady
			Loop
			CONB B 0 A_JumpIfInventory("ClassThroweableAction", 1, "SWGrenadeCheck")
			CONB B 0 A_JumpIfInventory("SlideKickAction", 1, "SlideKick")
			CONB AB 4 A_WeaponReady
			Loop
			
		Deselect:
			TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, 3) //OFFSET
			CONE AB 1 A_Lower
			Loop
			CONB AB 1 A_Lower
			Loop
			
		Select:
			TNT1 A 0 A_TakeInventory("ClassThroweableAction", 1)
			TNT1 A 0 A_TakeInventory("SlideKickAction", 1)
			TNT1 A 0 A_PlaySound("agge/summon", CHAN_WEAPON)
			TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, 11)
			CONE TUVWXYZ 3 A_Raise
			CONF ABC 2
			Goto Ready
			CONB TUVWXYZ 3 A_Raise
			CONC ABC 2
			Goto Ready
			
		Fire:
			TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, "Berserked")
			TNT1 A -1 A_Jump(256, "VSwing", "HSwing")
			Wait
			
		VSwing:
			TNT1 A 0 A_PlaySound("agge/swing", CHAN_WEAPON)
			CONE GH 3
			TNT1 A 0 A_FireCustomMissile("AxeHit", 0, 0, 0, 15)
			CONE I 2 A_ZoomFactor(0.90)
			TNT1 A 0 A_Quake(1, 6, 0, 100)
			CONE I 1
			TNT1 A 0 A_JumpIfInventory("HammerHasHit", 0, "Hit")
			TNT1 A 0 A_FireCustomMissile("AxeHit", 0, 0, 0, 15)
			CONF D 1
			TNT1 A 0 A_ZoomFactor(0.97)
			TNT1 A 0 A_JumpIfInventory("HammerHasHit", 0, "Hit")
			CONF D 1
			TNT1 A 0 A_JumpIfInventory("HammerHasHit", 0, "Hit")
			CONF E 1
			TNT1 A 0 A_ZoomFactor(1)
			TNT1 A 0 A_JumpIfInventory("HammerHasHit", 0, "Hit")
			CONF E 1
			CONE J 4
			TNT1 A 0 A_TakeInventory("HammerHasHit", 1)
			CONE K 3 A_Refire("HSwing")
			Goto Ready
			
		HSwing:
			TNT1 A 0 A_PlaySound("agge/swing", CHAN_WEAPON)
			CONE LM 3
			TNT1 A 0 A_FireCustomMissile("AxeHit", 0, 0, 0, 15)
			CONE N 2 A_ZoomFactor(0.90)
			TNT1 A 0 A_Quake(1, 6, 0, 100)
			CONE N 1
			TNT1 A 0 A_JumpIfInventory("HammerHasHit", 0, "Hit2")
			TNT1 A 0 A_FireCustomMissile("AxeHit", 0, 0, 0, 15)
			CONF F 1
			TNT1 A 0 A_ZoomFactor(0.97)
			TNT1 A 0 A_JumpIfInventory("HammerHasHit", 0, "Hit2")
			CONF F 1
			TNT1 A 0 A_JumpIfInventory("HammerHasHit", 0, "Hit2")
			CONF G 1
			TNT1 A 0 A_ZoomFactor(1)
			TNT1 A 0 A_JumpIfInventory("HammerHasHit", 0, "Hit2")
			CONF G 1
			CONE O 3
			CONE P 1
			CONE K 3 A_Refire("VSwing")
			Goto Ready
			
		Hit:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_ZoomFactor(0.80)
			CONF D 1 Offset(2, 30)
			TNT1 A 0 A_Quake(1, 12, 0, 100)
			CONF D 1 Offset(3, 29)
			TNT1 A 0 A_ZoomFactor(0.84)
			CONF D 1 Offset(2, 31)
			CONF D 1 Offset(0, 32)
			TNT1 A 0 A_ZoomFactor(0.97)
			CONE H 3 Offset(0, 31)
			TNT1 A 0 A_ZoomFactor(1)
			CONE G 3 Offset(0, 32)
			TNT1 A 0 A_TakeInventory("HammerHasHit", 1)
			CONE K 3 A_Refire("HSwing")
			Goto Ready
			
		Hit2:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_ZoomFactor(0.80)
			CONF F 1 Offset(2, 32)
			TNT1 A 0 A_Quake(1, 12, 0, 100)
			CONF F 1 Offset(3, 32)
			TNT1 A 0 A_ZoomFactor(0.84)
			CONF F 1 Offset(2, 32)
			CONF F 1 Offset(0, 32)
			TNT1 A 0 A_ZoomFactor(0.97)
			CONE M 3 Offset(0, 32)
			TNT1 A 0 A_ZoomFactor(1)
			CONE L 3 Offset(0, 32)
			TNT1 A 0 A_TakeInventory("HammerHasHit", 1)
			TNT1 A 0 Offset(0, 0)
			CONE K 3 A_Refire("VSwing")
			Goto Ready
			
		Berserked:
			TNT1 A 0 A_Refire("BerserkedHold")
			Goto BerserkedVSwing
			
		BerserkedVSwing:
			TNT1 A 0 A_PlaySound("agge/swing", CHAN_WEAPON)
			CONB GH 3
			TNT1 A 0 A_FireCustomMissile("RedAxeHit", 0, 0, 0, 15)
			CONB I 1 A_ZoomFactor(0.90)
			TNT1 A 0 A_Quake(1, 6, 0, 100)
			TNT1 A 0 A_JumpIfInventory("HammerHasHit", 0, "BerserkedHit")
			CONC D 1
			TNT1 A 0 A_ZoomFactor(0.97)
			TNT1 A 0 A_FireCustomMissile("RedAxeHit", 0, 0, 0, 15)
			TNT1 A 0 A_JumpIfInventory("HammerHasHit", 0, "BerserkedHit")
			CONC D 1
			TNT1 A 0 A_JumpIfInventory("HammerHasHit", 0, "BerserkedHit")
			CONC E 1
			TNT1 A 0 A_ZoomFactor(1)
			TNT1 A 0 A_JumpIfInventory("HammerHasHit", 0, "BerserkedHit")
			CONC E 1
			CONB J 2
			TNT1 A 0 A_TakeInventory("HammerHasHit", 1)
			CONB K 2 A_Refire("BerserkedHSwing")
			Goto Ready
			
		BerserkedHit:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_ZoomFactor(0.80)
			CONC D 1 Offset(2, 30)
			TNT1 A 0 A_Quake(1, 12, 0, 100)
			CONC D 1 Offset(3, 29)
			TNT1 A 0 A_ZoomFactor(0.84)
			CONC D 1 Offset(2, 31)
			CONC D 1 Offset(0, 32)
			TNT1 A 0 A_ZoomFactor(0.97)
			CONB H 2 Offset(0, 31)
			TNT1 A 0 A_ZoomFactor(1)
			CONB G 2 Offset(0, 32)
			TNT1 A 0 A_TakeInventory("HammerHasHit", 1)
			TNT1 A 0 ACS_NamedExecuteWithResult("pausePlayer", 0)
			CONB K 2 A_Refire("BerserkedHSwing")
			Goto Ready
			
		BerserkedHSwing:
			TNT1 A 0 A_PlaySound("agge/swing", CHAN_WEAPON)
			CONE L 3
			CONB M 3
			TNT1 A 0 A_FireCustomMissile("RedAxeHit", 0, 0, 0, 15)
			CONB N 1 A_ZoomFactor(0.90)
			TNT1 A 0 A_Quake(1, 6, 0, 100)
			TNT1 A 0 A_JumpIfInventory("HammerHasHit", 0, "BerserkedHit2")
			TNT1 A 0 A_FireCustomMissile("RedAxeHit", 0, 0, 0, 15)
			CONC F 1
			TNT1 A 0 A_ZoomFactor(0.97)
			TNT1 A 0 A_JumpIfInventory("HammerHasHit", 0, "BerserkedHit2")
			CONC F 1
			TNT1 A 0 A_JumpIfInventory("HammerHasHit", 0, "BerserkedHit2")
			CONC G 1
			TNT1 A 0 A_ZoomFactor(1)
			TNT1 A 0 A_JumpIfInventory("HammerHasHit", 0, "BerserkedHit2")
			CONC G 1
			CONB O 2
			CONB P 1
			CONB K 2 A_Refire("BerserkedVSwing")
			Goto Ready
			
		BerserkedHit2:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_ZoomFactor(0.80)
			CONC F 1 Offset(2, 32)
			TNT1 A 0 A_Quake(1, 12, 0, 100)
			CONC F 1 Offset(3, 32)
			TNT1 A 0 A_ZoomFactor(0.84)
			CONC F 1 Offset(2, 32)
			CONC F 1 Offset(0, 32)
			TNT1 A 0 A_ZoomFactor(0.97)
			CONB M 2 Offset(0, 32)
			TNT1 A 0 A_ZoomFactor(1)
			CONE L 2 Offset(0, 32)
			TNT1 A 0 A_TakeInventory("HammerHasHit", 1)
			TNT1 A 0 Offset(0, 0)
			CONB K 2 A_Refire("BerserkedVSwing")
			Goto Ready
			
		BerserkedHold:
			TNT1 A 0 ACS_NamedExecuteWithResult("loadChargedAttack", 35, 0, 1)
			Goto BerserkedLoop
			
		BerserkedLoop:
			CONB G 1 
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("getChargeState") == 1, "FireCharged")
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("getChargeState") == 2, "Berserked")
			Loop
			
		FireCharged:
			TNT1 A 0 ACS_NamedExecuteWithResult("stopAndPositionPlayer")
			TNT1 A 0 A_PlaySound("agge/swing", CHAN_WEAPON)
			CONB GH 2
			TNT1 A 0 A_FireCustomMissile("RedAxeHitNoDamage", 0, 0, 0, 15)
			CONB I 1 A_ZoomFactor(0.90)
			TNT1 A 0 A_Quake(1, 6, 0, 100)
			CONB I 1
			TNT1 A 0 A_JumpIfInventory("HammerHasHit", 0, "BerserkedHit")
			CONC D 1
			TNT1 A 0 A_JumpIfInventory("HammerHasHit", 0, "BerserkedHit")
			CONC D 1
			CONC E 2
			TNT1 A 0 A_AlertMonsters
			CONC E 10 A_SpawnItemEx("AxeFloorFire", 40)
			CONC D 2
			CONB I 2
			CONB IH 2
			CONB G 3
			TNT1 A 0 ACS_NamedExecuteWithResult("pausePlayer", 0)
			TNT1 A 0 A_TakeInventory("HammerHasHit", 1)
			TNT1 A 0 A_Refire("BerserkedHold")
			Goto Ready
		
		AltFire:
			TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, "AltBerserked")
			CONE KCDE 3
			TNT1 A 0 ACS_NamedExecuteWithResult("setEnergyShieldReload", 0)
			CONE F 1 A_SpawnItemEx("EnergyShield", 20, 0, 0)
			Goto AltHold
			
		AltHold:
			CONE F 1 A_TakeInventory("EnergyShieldAmmo", 1)
			CONE C 0 A_JumpIf(ACS_NamedExecuteWithResult("checkForNoAmmoInInventory", 1), "ShieldEnd")
			CONE C 0 A_Refire("AltHold")
			CONE EDCK 3
			TNT1 A 0 ACS_NamedExecuteWithResult("setEnergyShieldReload", 1)
			Goto Ready
			
		AltBerserked:
			CONB K 2
			CONE C 2
			CONB DE 2
			TNT1 A 0 ACS_NamedExecuteWithResult("setEnergyShieldReload", 0)
			CONB F 1 A_SpawnItemEx("EnergyShield", 20, 0, 0)
			Goto AltBerserkedHold
			
		AltBerserkedHold:
			CONB F 1 A_TakeInventory("EnergyShieldAmmo", 1)
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("checkForNoAmmoInInventory", 1), "BerserkedShieldEnd")
			TNT1 A 0 A_Refire("AltBerserkedHold")
			CONB EDCK 2
			TNT1 A 0 ACS_NamedExecuteWithResult("setEnergyShieldReload", 1)
			Goto Ready
			
		ShieldEnd:
			TNT1 A 0 A_PlaySound("agge/shildbrk",CHAN_WEAPON)
			CONE EDCK 3
			TNT1 A 0 ACS_NamedExecuteWithResult("setEnergyShieldReload", 1)
			Goto Ready
			
		BerserkedShieldEnd:
			TNT1 A 0 A_PlaySound("agge/shildbrk", CHAN_WEAPON)
			CONB EDCK 2
			TNT1 A 0 ACS_NamedExecuteWithResult("setEnergyShieldReload", 1)
			Goto Ready	 
  }
}

ACTOR EnergyShield {
  Health 999999
  Radius 16
  Height 56
  DamageFactor "Friendly", 0
  PainChance 255
	Projectile
  +SHOOTABLE
	-NOBLOCKMAP
  +NODAMAGE
  +DONTRIP
  +NOBLOOD
  +REFLECTIVE
	+SEEKERMISSILE
	var int user_spawn_tid;
	
  States {
		Spawn:
			// Swapea el target con el master para poder guardar al jugador actual en el master.
			// Nota: El puntero a target cambia internamente cuando la bala es reflejada (pain state).
			TNT1 A 0 NoDelay A_RearrangePointers(AAPTR_MASTER, AAPTR_TARGET, AAPTR_TRACER)
			Goto SpawnLoop
	 
		SpawnLoop:
			MISL A 1 A_Warp(AAPTR_MASTER, 20, 0, 0, 0, WARPF_NOCHECKPOSITION)
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("checkForNoAmmoInPointerInventory", 1), "Death")
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("isMasterPressingAltFire") == 0, "Death")
			Loop
	 
		Pain:
			TNT1 A 0 A_JumpIf(CallACS("isTIDBeingUsed", user_spawn_tid), "SpawnLoop")
			TNT1 A 0 A_SetUserVar("user_spawn_tid", ACS_NamedExecuteWithResult("getUniqueTID"))
			TNT1 A 3 A_SpawnItemEx("ShieldHit", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS, 0, user_spawn_tid)
			Goto SpawnLoop
	 
		Death:
			TNT1 A 2
			Stop
  }
}

ACTOR ShieldHit {
  Scale 0.2
  Renderstyle Add
  +NOGRAVITY
  +NOINTERACTION
  +SEEKERMISSILE
	
  States {
		Spawn:
			TNT1 A 0 NoDelay A_PlaySound("agge/visor", CHAN_AUTO)
			SHIE ABCDEFGHIJKLMNOPQRST 2 BRIGHT A_Warp(AAPTR_MASTER, 10, 0, 38)
			Stop
  }
}

ACTOR EnergyShieldAmmo : Ammo {
    Inventory.MaxAmount 200
	  Inventory.Icon "SHICOA0"
}

ACTOR AxeFloorFire {
  Radius 1
  Height 1
  Speed 0
  Damage 8
  Projectile
  RenderStyle Add
	Scale 0.5
  +NOCLIP
  +FLOORHUGGER
  +FORCERADIUSDMG
	DamageType "FireRF"
	
  States {
		Spawn:
			TNT1 A 0 NoDelay A_PlaySound("weapons/rocklx", CHAN_AUTO)
			TNT1 A 0 A_Quake(4,12,0,400)
			TNT1 A 0 A_Quake(2,12,0,800)
			FFED AB 2 Bright
			TNT1 A 0 A_SpawnItemEx("OrangeParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
			FFED C 4 Bright A_Explode(100, 110, 0)
			FFED DEF 4 Bright
			Stop       
   }
}

ACTOR AxeHit {
	Radius 3
	Height 6
	Damage (3 * random(1, 10))
	Speed 30
	DamageType "Lightning"
	Decal "CacoScorch"
	Scale 0.7
	Projectile
	+NODECAL
	+FORCEXYBILLBOARD
	+BLOODSPLATTER
	
	States {
		Spawn:
			TNT1 A 1 BRIGHT
			TNT1 A 1
			Stop
			
		XDeath:
			TNT1 A 0 A_PlaySound("agge/scythehit", CHAN_WEAPON)
			Goto Death+1
		
		Death:
			TNT1 A 0 A_PlaySound("agge/pared", CHAN_WEAPON)
			TNT1 A 0 A_GiveInventory("HammerHasHit", 1, AAPTR_TARGET)
			TNT1 A 0 A_SpawnItemEx("BlueParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
			FAXE RSTUVWX 2
			Stop
		}
}

ACTOR RedAxeHit : AxeHit {
	Translation "192:207=176:191","240:247=40:47"
	Damage (20 * random(1, 10))
	
	States {
		Death:
			TNT1 A 0 A_PlaySound("agge/pared",CHAN_WEAPON)
			TNT1 A 0 A_GiveInventory("HammerHasHit", 1, AAPTR_TARGET)
			TNT1 A 0 A_SpawnItemEx("RedParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
			FAXE RSTUVWX 2
			Stop
	}
}

ACTOR RedAxeHitNoDamage : RedAxeHit {
	Damage 0
	
	States {
		Spawn:
			TNT1 A 1 BRIGHT
			Stop
			
		XDeath:
			TNT1 A 0
			Stop
			
		Death:
			TNT1 A 0 A_JumpIf(Z <= FloorZ+5 || Z >= CeilingZ-5, "Death2")
			TNT1 A 0 A_PlaySound("agge/pared",CHAN_WEAPON)
			TNT1 A 0 A_GiveInventory("HammerHasHit", 1, AAPTR_TARGET)
			TNT1 A 0 A_SpawnItemEx("RedParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
			FAXE RSTUVWX 2
			Stop
			
		Death2:
			TNT1 A 0
			Stop
	}
}

ACTOR HammerHasHit : Inventory { Inventory.MaxAmount 1 Inventory.Amount 1 }