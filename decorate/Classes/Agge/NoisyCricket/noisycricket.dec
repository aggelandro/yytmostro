ACTOR NoisyCricket : WeaponWithCryoGrenadeOption {
	Inventory.PickupMessage "You got the Noisy Cricket!"
	Obituary "%o was fried by %k's Noisy Cricket."
	Weapon.AmmoType1 "Clip"
	Weapon.AmmoType2 "RocketAmmo"
	Weapon.AmmoGive 20
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Scale 0.6
	
	States {
		Spawn:
			PLBT A -1
			Loop
			
		Ready:
			PLBG A 0 A_JumpIfInventory("ClassThroweableAction", 1, "SWGrenadeCheck")
			PLBG A 0 A_JumpIfInventory("SlideKickAction", 1, "SlideKick")
			PLBG A 1 A_WeaponReady
			Loop
			
		Deselect:
			PLBG A 1 A_Lower
			Loop
			
		Select:
			TNT1 A 0 A_TakeInventory("ClassThroweableAction", 1)
			PLBG A 0 A_TakeInventory("SlideKickAction", 1)
			PLBG A 1 A_Raise
			Loop
			
		Fire:
			PLBG A 3
			PLBG E 0 A_Light1
			PLBG E 0 A_PlayWeaponSound("agge/pistolzap")
			PLBG E 0 A_Recoil(0.5)
			PLBG E 4 Bright A_FireCustomMissile("BlueLightningBolt",0,1,9,13)
			PLBG E 0 A_Recoil(-0.5)
			PLBG C 3 A_Light0
			PLBG D 2
			PLBG A 0 A_ReFire
			Goto Ready
			
		Altfire:
			PLBG A 0 A_JumpIfNoAmmo("Ready")
			TNT1 A 0 A_ChangeVelocity(0, 0, 0, CVF_REPLACE)
			TNT1 A 0 ACS_NamedExecuteWithResult("setActorSpeed", 0)
			PLBG A 3
			PLBG B 0 A_Light1
			PLBG B 0 ThrustThing(angle * 256 / 360 + 128, 18, 0, 0)
			PLBG B 0 A_PlaySound("agge/pedo",CHAN_AUTO)
			PLBG B 0 A_TakeInventory("RocketAmmo",1)
			PLBG B 2 Bright A_SpawnItemEx("NoisyCricketExplosion",40,20,0)
			PLBG C 1
			PLBG A 2 offset(0,42)
			PLBG A 2 offset(0,52)
			PLBG A 2 offset(0,62)
			PLBG A 12 offset(0,72)
			PLBG A 2 offset(0,62)
			PLBG A 2 offset(0,52)
			PLBG A 2 offset(0,42)
			TNT1 A 0 ACS_NamedExecuteWithResult("setActorSpeed", 1)
			PLBG C 1 A_Light0
			PLBG D 1
			Goto Ready
	}
}

ACTOR BlueLightningBolt {
	Radius 3
	Height 6
	Speed 30
	Damage 1
	PoisonDamage 2,5,0
	PoisonDamageType Lightning
	Scale 0.75
	PROJECTILE
	+ADDITIVEPOISONDAMAGE
	+ADDITIVEPOISONDURATION
	+STRIFEDAMAGE
	DamageType Lightning
	DeathSound "agge/pistolded"
	RenderStyle Add
	
	States {
		Spawn:
			BFX2 K 0 A_PlaySound("agge/pistoltzz",CHAN_AUTO)
			BFX2 KLMNOKLMNOKLMNOKLMNOKLMNO 2 Bright A_CustomMissile("BlueLightningBoltTrail",0,0,0,0)
			Goto Spawn
			
		Death:
			TNT1 A 0 A_SpawnItemEx("BlueParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
			WPGZ FEDCBA 4 bright
			Stop
	}
}

ACTOR BlueLightningBoltTrail {
	Radius 0
	Height 1
	Damage 0
	Speed 0
	Scale 0.5
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.50
	
	States {
		Spawn:
			BFX2 KLMNO 2 Bright
			Stop 
	}
}

ACTOR NoisyCricketExplosion
{
   Radius 128
   Height 64
   Speed 0
   Scale 0.5
   DamageType "Friendly"
   +NOBLOCKMAP
   +NOGRAVITY
   RenderStyle Normal
   States
   {
   Spawn:
	  FB1S O 0 NoDelay A_Explode(64,128,0,1,128)
	  FB1S ABCDEFGHIJKLMNOPQRSTUVWXYZ 1
	  FB2S ABCDEFGHIJ 1
      Stop
   }
}