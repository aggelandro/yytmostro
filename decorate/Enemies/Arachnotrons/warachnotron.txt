ACTOR Warachnotron : Arachnotron2 {
	Obituary "%o was vaporized by a Warachnotron."
	Scale 0.75
	Radius 52
	SeeSound "warach/see"
  PainSound "warach/pain"
  DeathSound "warach/death"
	+MISSILEMORE
	var int user_shield_on;
	
	States {
		Spawn:
			CSPI B 10 A_Look
			Loop
			
		See:
			CSPI A 2 A_PlaySound("warach/walk", CHAN_BODY)
			CSPI AABBB 2 A_Chase
			CSPI C 2 A_PlaySound("warach/walk", CHAN_BODY)
			CSPI CCDDD 2 A_Chase
			Loop
			
		Missile:
			CSPI B 15 A_FaceTarget
			TNT1 A 0 A_CustomMissile("BlueBlasterShot",30,-12,0,CMF_AIMOFFSET)
			CSPI E 3 Bright A_CustomMissile("BlueBlasterShot",30,12,0,CMF_AIMOFFSET)
			CSPI M 3 A_FaceTarget
			TNT1 A 0 A_CustomMissile("BlueBlasterShot",30,-12,0,CMF_AIMOFFSET)
			CSPI E 3 Bright A_CustomMissile("BlueBlasterShot",30,12,0,CMF_AIMOFFSET)
			TNT1 A 0 A_SpidRefire
			CSPI M 3 A_FaceTarget
			TNT1 A 0 A_CustomMissile("BlueBlasterShot",30,-12,0,CMF_AIMOFFSET)
			CSPI E 3 Bright A_CustomMissile("BlueBlasterShot",30,12,0,CMF_AIMOFFSET)
			CSPI M 3 A_FaceTarget
			TNT1 A 0 A_CustomMissile("BlueBlasterShot",30,-12,0,CMF_AIMOFFSET)
			CSPI E 3 Bright A_CustomMissile("BlueBlasterShot",30,12,0,CMF_AIMOFFSET)
			CSPI B 15 A_FaceTarget
			TNT1 A 0 A_SpidRefire
			Goto Missile+1
			
		Pain:
			CSPI F 12 A_Pain
			TNT1 A 0 A_Jump(64, "Shield")
			Goto See+1
			
		Shield:
			TNT1 A 0 A_JumpIf(user_shield_on, "See")
			CSPI ME 3 A_FaceTarget
			TNT1 A 0 A_SetUserVar("user_spawn_tid", ACS_NamedExecuteWithResult("getUniqueTID"))
			TNT1 A 0 A_SpawnItemEx("ForceShield", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER, 0, user_spawn_tid)
			TNT1 A 0 ACS_NamedExecuteWithResult("setTIDTracerPointerToSelf", user_spawn_tid)
			TNT1 A 0 A_PlaySound("warach/shielded", CHAN_BODY)
			TNT1 A 0 A_SetUserVar("user_shield_on", 1)
			TNT1 A 0 A_SetInvulnerable
			Goto See+1
			
		Death:
			CSPI F 20 A_Scream
			CSPI G 6
			CSP2 GGGGG 2 Bright A_CustomMissile("BigBlueExplosion", random(20,40), random(10,-10), random(0,360), 2, random(0,360))
			TNT1 AAAA 0 A_CustomMissile("SmokeSmallExplosion", random(20,40), 0, random(0,360), 2, random(0,360))
			CSPI H 6 A_NoBlocking
			CSPI IJK 6
			CSPI L -1 A_KillChildren
			Stop
		
		Raise:
			CSPI KJIHGF 5
			Goto See+1
	}
}

ACTOR ForceShield {
	Health 300
	Radius 55
	Height 64
	Alpha 0.05
	Scale 0.17
	PainChance 255
	Species "WarachnoTech"
	+NOGRAVITY
	+SEEKERMISSILE
	+SHOOTABLE
	+NODAMAGE
	-NOBLOCKMAP
	+NOBLOOD
	var int user_hits;
	
	States {
		Spawn:
			TNT1 A 0 NoDelay A_SetUserVar("user_hits", 10)
			TNT1 A 0 ACS_NamedExecuteWithResult("deathTimer", 280)
			FSHD A 1 A_FadeIn(0.3)
			TNT1 A 0 A_Warp(AAPTR_TRACER, 0, 0, 0, 0, WARPF_NOCHECKPOSITION)
			FSHD A 1 A_FadeIn(0.3)
			TNT1 A 0 A_Warp(AAPTR_TRACER, 0, 0, 0, 0, WARPF_NOCHECKPOSITION)
			FSHD A 1 A_FadeIn(0.3)
			TNT1 A 0 A_Warp(AAPTR_TRACER, 0, 0, 0, 0, WARPF_NOCHECKPOSITION)
			FSHD A 1 A_FadeIn(0.3)
			TNT1 A 0 A_Warp(AAPTR_TRACER, 0, 0, 0, 0, WARPF_NOCHECKPOSITION)
			Goto SpawnLoop
			
		SpawnLoop:
			FSHD A 1
			TNT1 A 0 A_Warp(AAPTR_TRACER, 0, 0, 0, 0, WARPF_NOCHECKPOSITION)
			Loop
			
		Pain:
			TNT1 A 0 A_SetUserVar("user_hits", user_hits-1)
			TNT1 A 0 A_PlaySound("LHK/block", CHAN_AUTO)
			TNT1 A 0 A_JumpIf(user_hits == 0, "Death")
			Goto SpawnLoop
			
		Death:
			TNT1 A 0 ACS_NamedExecuteWithResult("removeForceShieldFromTracer")
			TNT1 A 0 A_PlaySound("agge/shildbrk",CHAN_WEAPON)
			Stop
	}
}