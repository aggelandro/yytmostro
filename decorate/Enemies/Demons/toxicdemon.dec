ACTOR Toxigi : Pinky2 {
	DamageFactor "Poison", 0
	DamageFactor "PPoison", 0
	Obituary "%o was poisoned to death by a Toxiiiigiiii."
	DropItem "IsaacItemsTier1LowChanceSpawner"
	DropItem "IsaacItemsTier1MidChanceSpawner"
	DropItem "IsaacItemsTier2LowChanceSpawner"
	DropItem "ContraItemPinkySpawner"
	
	States {
		SpawnLoop:
			SAR6 AB 10 A_Look
			Loop
			
		See:
			SAR6 AABBCCDD 2 Fast A_Chase
			SAR6 A 0 A_Jump(51, 4)
			SAR6 A 0 A_Jump(51, 2)
			SAR6 AABBCCDD 1 A_Chase
			SAR6 AABBCCDD 1 A_Chase
			SAR6 AABBCCDD 1 A_Chase
			SAR6 AABBCCDD 2 Fast A_Chase
			Loop
			
		Melee:
			SAR6 EF 8 Fast A_FaceTarget
			SAR6 G 8 Fast A_SargAttack
			Goto See
			
		Missile:
			TNT1 A 0 A_JumpIfCloser(192, 2)
			Goto See
			SAR6 EF 8 Fast A_FaceTarget
			TNT1 A 0 A_PlaySound("toxigi/breath", CHAN_AUTO)
			SAR6 F 3 Fast Bright A_CustomMissile("ToxicBreath", 28, 0, 0)
			SAR6 E 2 FAST Bright A_FaceTarget
			SAR6 F 3 FAST Bright A_CustomMissile("ToxicBreath", 28, 0, 2)
			SAR6 E 2 FAST Bright A_FaceTarget
			SAR6 F 3 FAST Bright A_CustomMissile("ToxicBreath", 28, 0, 4)
			SAR6 E 2 FAST Bright A_FaceTarget
			SAR6 F 3 FAST Bright A_CustomMissile("ToxicBreath", 28, 0, 6)
			SAR6 E 2 FAST Bright A_FaceTarget
			SAR6 F 3 FAST Bright A_CustomMissile("ToxicBreath", 28, 0, 4)
			SAR6 E 2 FAST Bright A_FaceTarget
			SAR6 F 3 FAST Bright A_CustomMissile("ToxicBreath", 28, 0, 2)
			SAR6 E 2 FAST Bright A_FaceTarget
			SAR6 F 3 FAST Bright A_CustomMissile("ToxicBreath", 28, 0, 0)
			SAR6 E 2 FAST Bright A_FaceTarget
			TNT1 A 0 A_PlaySound("toxigi/breath", CHAN_AUTO)
			SAR6 F 3 FAST Bright A_CustomMissile("ToxicBreath", 28, 0, -2)
			SAR6 E 2 FAST Bright A_FaceTarget
			SAR6 F 3 FAST Bright A_CustomMissile("ToxicBreath", 28, 0, -4)
			SAR6 E 2 FAST Bright A_FaceTarget
			SAR6 F 3 FAST Bright A_CustomMissile("ToxicBreath", 28, 0, -6)
			SAR6 E 2 FAST Bright A_FaceTarget
			SAR6 F 3 FAST Bright A_CustomMissile("ToxicBreath", 28, 0, -4)
			SAR6 E 2 FAST Bright A_FaceTarget
			SAR6 F 3 FAST Bright A_CustomMissile("ToxicBreath", 28, 0, -2)
			SAR6 E 2 FAST Bright A_FaceTarget
			SAR6 F 3 FAST Bright A_CustomMissile("ToxicBreath", 28, 0, 0)
			SAR6 G 2 FAST
			Goto See
			
		Pain:
			SAR6 H 2 Fast
			SAR6 H 2 Fast A_Pain
			Goto See
			
		Death:
			TNT1 A 0 A_SpawnItemEx("BoneGib", 0, 0, 0, random(-5, 5), random(-5, 5), 3, random(0, 359))
			SAR6 I 8
			SAR6 J 8 A_Scream
			SAR6 K 4
			SAR6 L 4 A_NoBlocking
			SAR6 M 4
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			TNT1 A 0 A_SpawnItemEx("PoisonCloud2",Random(-8,8),Random(-8,8),Random(5,16),Random(2,4),Random(2,4),(0.1)*Random(-10,10),Random(-20,20),0)
			TNT1 A 0 A_SpawnItemEx("PoisonCloud2",Random(-8,8),Random(-8,8),Random(5,16),Random(2,4),Random(-2,-4),(0.1)*Random(-10,10),Random(-20,20),0)
			TNT1 A 0 A_SpawnItemEx("PoisonCloud2",Random(-8,8),Random(-8,8),Random(5,16),Random(-2,-4),Random(-2,-4),(0.1)*Random(-10,10),Random(-20,20),0)
			TNT1 A 0 A_SpawnItemEx("PoisonCloud2",Random(-8,8),Random(-8,8),Random(5,16),Random(-2,-4),Random(2,4),(0.1)*Random(-10,10),Random(-20,20),0)
			SAR6 NN 2 A_SpawnItemEx("PoisonGasSpawner", 0, 0, Random(5,16))
			TNT1 A 0 A_JumpIfInventory("LoopFlag", 10, 1)
			Goto Death+6
			TNT1 A 0 A_TakeInventory("LoopFlag", 10)
			SAR6 N -1
			Stop
			
		Death.FireBF:
		Death.AfireF:
		Death.Afire:		
		Death.FireRF:
		Death.FireR:
			TNT1 A 0
			Goto Death
			
		Raise:
			SAR6 N 5
			SAR6 MLKJI 5
			Goto See
	}
}

ACTOR SpectreToxigi : Toxigi {
	+SHADOW
	RenderStyle OptFuzzy
	Alpha 0.5
	SeeSound "spectre/sight"
	AttackSound "spectre/melee"
	PainSound "spectre/pain"
	DeathSound "spectre/death"
	ActiveSound "spectre/active"
}

ACTOR ToxicBreath {
	Radius 4
	Height 6
	Speed 8
	Damage 1
	DamageType "Poison"
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.8
	
	+HITTRACER
	
	States {
		Spawn:
			DTBA ABCDEFG 2 A_SpawnItemEx("ToxicBreathTrail",0,0,0,0,0,0,0,128)
			Goto Death
			
		XDeath:
			TNT1 A 0 A_JumpIfInventory("PoisonCounter", 0, "Death", AAPTR_TRACER)
			TNT1 A 0 A_GiveInventory("PoisonCounter", 1, AAPTR_TRACER)
			TNT1 A 0 ACS_NamedExecuteWithResult("applyEffectOnPlayer", 1, 1050, 6)
			Goto Death
			
		Death:
			DTBA HIJKLMOP 1
			Stop
	}
}

ACTOR ToxicBreathTrail {
	Scale 0.85
	Renderstyle Add
	Alpha 0.55
	
	+NOGRAVITY
	+FLOAT
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+DONTSPLASH
	+NOCLIP
	+NOINTERACTION
	+THRUACTORS
	
	States {
		Spawn:
			DTBA ABCDEFGHIJKLMOP 1 BRIGHT A_FadeOut(0.25)
			Loop
	}
}

ACTOR PoisonCloud2 {
	Radius 2
	Height 2
	RenderStyle Add
	Scale 0.5
	DamageType "Poison"
	Species "Demon"
	+THRUSPECIES
	+FORCERADIUSDMG
	+BLOODLESSIMPACT
	+RIPPER
	+THRUACTORS
	+DONTSPLASH
	+NOGRAVITY
	+NODAMAGETHRUST
	+NOTELEPORT
	+SKYEXPLODE
	+FORCEXYBILLBOARD
	
	States { 
		Spawn:
			TRPS ABCDEFGDEFGDEFGDEFG 3 A_Explode(2,38)
			Goto Death
			
		Death:
			TRPS CCBBAA 1 A_FadeOut(0.15)
			Stop
	}
}

ACTOR PoisonGas {
	+MISSILE
	+DONTSPLASH
	+CLIENTSIDEONLY
	+NOGRAVITY
	+FORCEXYBILLBOARD
	+THRUACTORS
	+NOTELEPORT
	+NOBLOCKMAP
	+NOINTERACTION
	RenderStyle Add
	Scale 0.5
	
	States {
		Spawn:
			FIG1 ABCDEFGHIJKL 2 NoDelay Bright A_FadeOut
			Stop
	}
}

ACTOR PoisonGasSpawner : GreenParticleSpawner {
  States {
		Spawn:
			TNT1 A 0 NoDelay A_SpawnItemEx("PoisonGas",3,random(-2,2),random(12,16),1,0,random(1,2),random(90,270),128,0)
			TNT1 A 1
			Stop
	}
}

ACTOR PoisonCounter : Inventory {
	-INVBAR
  +UNDROPPABLE
  Inventory.MaxAmount 10
	Inventory.Amount 1
}