ACTOR MirrorKnight : HellKnight2
{
  Translation "0:255=0:15"//, "40:63=5:8", "56:79=5:8", "112:123=5:8", "124:127=5:8", "144:255=5:8"
	DropItem "IsaacItemsTier1HighChanceSpawner"
	DropItem "IsaacItemsTier1LowChanceSpawner"
	DropItem "IsaacItemsTier2MidChanceSpawner"
	DropItem "DemonAmmo", 48
	DropItem "HomingGunToken", 48
	var int user_has_invoked_already;
	
  States {
		See:
			TNT1 A 0 A_JumpIf(user_has_invoked_already, 4)
			BOS2 A 0 A_SpawnItemEx("KnightIllusion",10,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION,0)
			BOS2 A 0 A_SpawnItemEx("KnightIllusion",-10,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION,0)
			TNT1 A 0 A_SetUserVar("user_has_invoked_already", 1)
			TNT1 A 0
			Goto Super::See
			
		Death:
			BOS2 I 8 
			BOS2 J 8 A_Scream
			BOS2 K 8 A_KillChildren
			BOS2 L 8 A_NoBlocking
			BOS2 MN 8
			BOS2 O -1
			Stop
			
		Death.FireBF:
		Death.AfireF:
		Death.Afire:		
		Death.FireRF:
		Death.FireR:
			TNT1 A 0 A_SetUserVar("user_burning_loop", Random(3, 6))
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			8BAR ABC 4 BRIGHT
			TNT1 A 0 A_PlaySound("knight/pain", CHAN_AUTO)
			8BAR DEF 4 BRIGHT
			TNT1 A 0 A_PlaySound("knight/pain", CHAN_AUTO)
			TNT1 A 0 A_JumpIfInventory("LoopFlag", user_burning_loop, 1)
			Goto Death.FireR+1
			TNT1 A 0 A_PlaySound("knight/death", CHAN_AUTO)
			TNT1 A 0 A_KillChildren
			8BAR GHIJK 5 BRIGHT
			8BAR L 5 BRIGHT A_NoBlocking
			8BAR MN 5 BRIGHT
			8BAR O -1
			Stop
			
		Death.Ice:
			"####" "#" 0 A_KillChildren
			"####" "#" 0 A_GenericFreezeDeath
			"####" "#" 175
			"####" "#" 0 A_IceGuyDie
			Stop
			
		Death.IsaacIce:
			"####" "#" 0 A_KillChildren
			Goto Super::Death.IsaacIce
  }
}
ACTOR KnightIllusion : HellKnight2
{
	Translation "0:255=0:15"
	Radius 4
	Height 4
	ProjectilePassHeight 1
	MissileType "GhostBall"
	RenderStyle Translucent
	Alpha 0.4
	Painchance 0
	+NORADIUSDMG
	+NOBLOOD
	+THRUACTORS
	+NOTARGET
	-SOLID
	States
	{
	See:
		TNT1 A 0 A_JumpIfTargetInLOS("TPMaster", 0, JLOSF_CHECKMASTER|JLOSF_CLOSENOJUMP, 0, 1280)
		BOS2 AA 3 A_Chase
		BOS2 BB 3 A_Chase
		BOS2 CC 3 A_Chase
		BOS2 DD 3 A_Chase
		Loop
	Melee:
	Missile:
		BOS2 EF 8 A_FaceTarget
		BOS2 G 8 A_CustomMissile("GhostBall")
		Goto See
	TPMaster:
		BOS2 AAAAAAAA 5 A_FadeOut(0.05)
		BOS2 A 1 A_Warp(AAPTR_MASTER, 0, 0, 0, 0, WARPF_NOCHECKPOSITION, "FadeIn")
		BOS2 AAAAAAAA 1 A_FadeIn(0.05)
		Goto See
	FadeIn:
		BOS2 AAAAAAAA 1 A_FadeIn(0.05)
		Goto See+1
	Death:
		B0S2 A 0
		BOS2 AAAAAAA 5 A_FadeOut(0.05)
		Stop
	}
}
ACTOR GhostBall : BaronBall
{
	Damage 2
	+MTHRUSPECIES
}