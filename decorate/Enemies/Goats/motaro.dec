ACTOR Motaro : HellKnight2 {
	Speed 14
	Height 88
	Radius 32
  GibHealth 25
	Obituary "Motaro wins. Flawless Victory."
	MaxStepHeight 24
	MaxDropOffHeight 32
	SeeSound "belph/see"
  PainSound "belph/pain"
  DeathSound "belph/death"
	DropItem "IsaacItemsTier1HighChanceSpawner"
	DropItem "IsaacItemsTier2LowChanceSpawner"
	DropItem "IsaacItemsTier2LowChanceSpawner"
	DropItem "IsaacItemsTier2LowChanceSpawner"
	DropItem "CrusherGunToken", 55
	+DONTHURTSPECIES
	+MISSILEMORE
	
	States {
		SpawnLoop:
			BOS3 BD 10 A_Look
			Loop
			
		See:
			TNT1 A 0 A_CheckRange(256, "TeleportChance")
			BOS3 AAAA 2 A_Chase
			BOS3 A 0 A_CheckSight("See2")
			BOS3 BBBB 2 A_Chase
			BOS3 CCCC 2 A_Chase
			BOS3 BBBB 2 A_Chase
			Loop
			
		See2:
			BOS3 AABBCCBB 1 A_Chase
			Goto See
			
		TeleportChance:
			TNT1 A 0 A_Jump(128, 1)
			Goto See+1
			BOS3 F 8 A_FaceTarget
			BOS3 S 4
			BOS3 T 4
			BOS3 U 4 A_Warp(AAPTR_TARGET, (ACS_NamedExecuteWithResult("getTargetRadius")*-1)-64, 0, 0, 0, 0, "TeleportSuccess")
			Goto See+1
			
		TeleportSuccess:
			BOS3 U 4
			TNT1 A 0 A_PlaySound("belph/teleport")
			BOS3 T 4
			BOS3 S 4
			Goto See+1
			
		Melee:
			BOS3 F 8
			BOS3 G 8 A_FaceTarget
			TNT1 A 0 A_CustomComboAttack("BelphegorGreenPlasmaBall", 40, 15, "belph/melee")
			BOS3 H 8 A_FaceTarget
			Goto See
			
		Missile:
			BOS3 F 8 A_FaceTarget
			BOS3 G 8 A_FaceTarget
			TNT1 A 0 A_JumpIfCloser(150, "Melee2")
			BOS3 H 8 A_CustomMissile("BelphegorGreenPlasmaBall", 40, 0, 0, 1) 
			Goto See
			
		Melee2:
			TNT1 A 0 A_CustomComboAttack("BelphegorGreenPlasmaBall", 40, 15, "belph/melee")
			BOS3 H 8 A_FaceTarget
			Goto See
			
		Pain:
			BOS3 E 3
			BOS3 E 3 A_Pain
			Goto See
			
		Death:
			TNT1 A 0 A_Jump(128, "Death2")
	    BOS3 I 8 A_Scream
			BOS3 J 8
			BOS3 K 8 A_NoBlocking
			BOS3 L 8			
			BOS3 L -1
	    Stop
			
		Death2:
			BOS3 M 8 A_Scream
			BOS3 N 8
			BOS3 O 8
			BOS3 P 8
			BOS3 Q 8 A_NoBlocking
			BOS3 R 8
			BOS3 R -1
	    Stop
			
		Raise:
			BOS3 O 8
			BOS3 NMLKJI  8
			Goto See
	}
}

ACTOR BelphegorGreenPlasmaBall : BaronBall {
	Radius 10
	Height 16
	Speed 10
	FastSpeed 30
	Projectile 
  Damage 7
	RenderStyle Add
	Alpha 0.9
	SeeSound "belph/fire"
	DamageType "BaronComet"
	+RANDOMIZE
	+FORCEXYBILLBOARD
  +THRUGHOST
	+BRIGHT

	States {
		Spawn:
			BPBA A 5
			Goto SpawnLoop
			
		SpawnLoop:
			BPBA B 1
			Loop
		
		Death:
			BPEA AABBCC 1
			TNT1 A 0 A_SpawnItemEx("OrangeParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
			TNT1 AAA 0 A_CustomMissile("SmokeBigExplosion", 0, 0, random(0,360), 2, random(0,360))
			TNT1 A 0 Radius_Quake (2, 35, 0, 15, 0)
			TNT1 A 0 A_Explode(Random(1, 15), 170, 1)
			BPEA DEFGHIJKLMNO 2
			Stop
	}
}