ACTOR AngryBaron : BaronOfHell2 {
  Health 1200
  Obituary "%o got a baron mad."
  HitObituary "%o pissed off the wrong baron."
	var int user_angry;
  States {
		See:
			BOSS A 0 A_JumpIf(health < 600,"Transform")
			BOSS AA 3 A_Chase
			BOSS A 0 A_JumpIf(health < 600,"Transform")
			BOSS BB 3 A_Chase
			BOSS A 0 A_JumpIf(health < 600,"Transform")
			BOSS CC 3 A_Chase
			BOSS A 0 A_JumpIf(health < 600,"Transform")
			BOSS DD 3 A_Chase
			Loop
			
		Transform:
			BOS4 QQQQQQ 2 Bright A_CustomMissile("Kaboom2", random(20,80), 0, random(0,360), 2, random(0,360))
			TNT1 AAAA 0 A_CustomMissile("SmokeFX", Random(40,80), 0, random(0,360), 2, random(0,360))
			TNT1 A 0 A_SetUserVar("user_angry", 1)
			Goto AngrySee
			
		AngrySee:
			BOS4 AABBCCDD 2 Fast A_Chase("AngryMissile", "AngryMissile")
			TNT1 A 0 Fast A_PlaySound("baron/sight")
			Loop
			
		AngryMissile:
			BOS4 EF 5 Fast A_FaceTarget
			BOS4 G 10 Fast A_BruisAttack
			BOS4 EF 3 Fast A_FaceTarget
			BOS4 G 6 Fast A_BruisAttack
			BOS4 EF 2 Fast A_FaceTarget
			BOS4 G 4 Fast A_BruisAttack
			TNT1 A 0 A_Jump(87, "AngryMissile2", "BurstComet")
			Goto AngrySee
			
		AngryMissile2:
			BOS4 IJ 5 Fast A_FaceTarget
			BOS4 K 10 Fast A_BruisAttack
			BOS4 IJ 3 Fast A_FaceTarget
			BOS4 K 6 Fast A_BruisAttack
			BOS4 IJ 2 Fast A_FaceTarget
			BOS4 K 4 Fast A_BruisAttack
			TNT1 A 0 A_Jump(87, "BurstComet")
			Goto AngrySee
			
		BurstComet:
			BOS4 MN 8 Bright Fast A_FaceTarget
			BOS4 O 2 Bright Fast A_Custommissile("Comet", 32, 0, 0)
			TNT1 A 0 A_Custommissile("Comet", 32, 0, -10)
			BOS4 P 5 Bright Fast A_Custommissile("Comet", 32, 0, 10)
			Goto AngrySee
			
		Pain:
			TNT1 A 0 A_JumpIf(user_angry, "AngryPain")
			Goto Super::Pain
			
		AngryPain:
			BOS4 Q 2
			BOS4 Q 2 A_Pain
			Goto AngrySee
			
		Death:
			TNT1 A 0 A_JumpIf(user_angry, "AngryDeath")
			Goto Super::Death
			
		AngryDeath:
			BO4D A 8 BRIGHT
			BO4D B 7 BRIGHT A_PlaySound("angrybaron/death", CHAN_BODY)
			BO4D C 5 BRIGHT A_Fall
			BO4D DE 5 BRIGHT
			TNT1 AAAAA 0 A_CustomMissile("SmokeFX", Random(30,60), 0, random(0,360), 2, random(0,360))	
			BO4D FGHIJ 4 BRIGHT
			TNT1 A -1
			Stop
			
		Raise:
			BOSS O 8
			BOSS NMLKJI 8
			TNT1 A 0 A_SetUserVar("user_angry", 0)
			Goto See
  }
}

ACTOR Kaboom2 {
	Speed 3
	Damage 0
	Renderstyle Add
	Alpha 0.75
	PROJECTILE
	+NOBLOCKMAP
	+NOTELEPORT
	+DONTSPLASH
	+NOGRAVITY
	+MISSILE
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOCLIP
	Scale 1.3
	
	States {
		Spawn:
		FXPG A 1 NoDelay Bright A_PlaySound("weapons/rocklx")
		FXPG ABBCDEFGHIJKLM 1 Bright A_FadeOut(0.05)
		Stop
	}
}

ACTOR SmokeFX : Kaboom2 {
	Speed 2
	Scale 1.15
	Renderstyle Translucent
	Alpha 0.75
	
	States {
		Spawn:
		SMK3 ABCDEFGHIJKLM 5 NoDelay A_FadeOut(0.025)
		Stop
	}
}

ACTOR Comet {
	Radius 10
	Height 10
	Speed 20
	FastSpeed 28
	Damage 8
	Scale 0.85
	PROJECTILE
	DamageType "BaronComet"
	SeeSound "commander/grenadefire"
	DeathSound "commander/explosion"
	
	States {
		Spawn:
			HCMT AAAABBBBCCCC 1 Bright A_SpawnItemEx("CometTrail",0,0,0,0,0,0,0,128)
			Loop
			
		Death:
			TNT1 A 0 Bright A_Explode(64, 102)
			TNT1 A 0 A_SpawnItemEx("GreenParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
			HCMT DE 3 Bright
			TNT1 A 0 A_SpawnItemEx("CometDeath",0,0,0,0,0,0,0,128,0)
			Stop
	}
}

ACTOR CometTrail {
	RenderStyle Add
	Alpha 0.9
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+DONTSPLASH
	+NOCLIP
	+NOINTERACTION
	+THRUACTORS
	
	States {
		Spawn:
			TNT1 A 2
			HCMT DEFGHI 2 Bright
			Stop
	}
}

ACTOR CometDeath {
	Projectile
	RenderStyle Add
	Alpha 0.7
	+NOCLIP
	
	States {
		Spawn:
			TNT1 AAA 0 NoDelay A_CustomMissile("Kaboom2", 0, 0, random(0,360), 2, random(0,360))	
			TNT1 AAA 0 A_CustomMissile("SmokeFX", 0, 0, random(0,360), 2, random(0,360))
			HCMT JKLMN 3 Bright A_FadeOut(0.1)
			Stop
	}
}