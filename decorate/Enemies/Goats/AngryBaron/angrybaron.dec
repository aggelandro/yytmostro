ACTOR AngryBaron : BaronOfHell2 {
  Health 1200
  Obituary "%o got a baron mad."
  HitObituary "%o pissed off the wrong baron."
	DropItem "IsaacItemsTier1HighChanceSpawner"
	DropItem "IsaacItemsTier1MidChanceSpawner"
	DropItem "IsaacItemsTier2HighChanceSpawner"
	DropItem "IsaacItemsTier2MidChanceSpawner"
	DropItem "CrusherGunToken", 64
	DropItem "DemonAmmo", 16
	var int user_angry;
  States {
		See:
			TNT1 A 0 A_JumpIf(user_angry, "AngrySee")
			BOSS A 0 A_JumpIf(health < 600,"Transform")
			BOSS AA 3 A_Chase
			BOSS A 0 A_JumpIf(health < 600,"Transform")
			BOSS BB 3 A_Chase
			BOSS A 0 A_JumpIf(health < 600,"Transform")
			BOSS CC 3 A_Chase
			BOSS A 0 A_JumpIf(health < 600,"Transform")
			BOSS DD 3 A_Chase
			Loop
			
		Transform:
			BOS4 QQQQQQ 2 Bright A_CustomMissile("BigGreenExplosion", random(20,80), 0, random(0,360), 2, random(0,360))
			TNT1 AAAA 0 A_CustomMissile("SmokeBigExplosion", Random(40,80), 0, random(0,360), 2, random(0,360))
			TNT1 A 0 A_SetUserVar("user_angry", 1)
			Goto AngrySee
			
		AngrySee:
			BOS4 AABBCCDD 2 Fast A_Chase("AngryMissile", "AngryMissile")
			TNT1 A 0 Fast A_PlaySound("baron/sight")
			Loop
			
		AngryMissile:
			BOS4 EF 5 Fast A_FaceTarget
			BOS4 G 10 Fast A_CustomMissile("AngryBall", 32, 0, 0)
			BOS4 EF 3 Fast A_FaceTarget
			BOS4 G 6 Fast A_CustomMissile("AngryBall", 32, 0, -5)
			BOS4 EF 2 Fast A_FaceTarget
			BOS4 G 4 Fast A_CustomMissile("AngryBall", 32, 0, 5)
			TNT1 A 0 A_Jump(128, "AngryMissile2", "BurstComet")
			Goto AngrySee
			
		AngryMissile2:
			BOS4 IJ 5 Fast A_FaceTarget
			BOS4 K 10 Fast A_CustomMissile("AngryBall", 32, 0, 0)
			BOS4 IJ 3 Fast A_FaceTarget
			BOS4 K 6 Fast A_CustomMissile("AngryBall", 32, 0, -5)
			BOS4 IJ 2 Fast A_FaceTarget
			BOS4 K 4 Fast A_CustomMissile("AngryBall", 32, 0, 5)
			TNT1 A 0 A_Jump(128, "BurstComet")
			Goto AngrySee
			
		BurstComet:
			BOS4 MN 8 Bright Fast A_FaceTarget
			BOS4 O 2 Bright Fast A_Custommissile("Comet", 32, 0, 0)
			TNT1 A 0 A_Custommissile("Comet", 32, 0, -10)
			BOS4 P 5 Bright Fast A_Custommissile("Comet", 32, 0, 10)
			Goto AngrySee
			
		Pain:
			TNT1 A 0 A_JumpIf(user_angry, "AngryPain")
			Goto Super::Pain
			
		AngryPain:
			BOS4 Q 2
			TNT1 A 0 A_Jump(104, "AngryPainMissile", "AngryPainRage")
			BOS4 Q 2 A_Pain
			Goto AngrySee
			
		AngryPainMissile:
			BOS4 Q 10 Bright A_Pain
			Goto AngryMissile
			
		AngryPainRage:
			BOS4 Q 10 Bright A_Pain
			TNT1 A 0 A_JumpIfCloser(400,1)
			Goto AngrySee
			TNT1 A 0 Bright A_PlaySound("brain/spit")
			BOS4 ON 5 Bright
			BOS4 MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM 1 Bright A_SpawnItemEx("GreenSpike",Random(400,-400),Random(400,-400),0,0,0,0,SXF_NOCHECKPOSITION)
			BOS4 N 5 Bright
			Goto AngrySee
			
		Death:
			TNT1 A 0 A_JumpIf(user_angry, "AngryDeath")
			Goto Super::Death
			
		AngryDeath:
			BO4D A 8 BRIGHT
			BO4D B 7 BRIGHT A_PlaySound("angrybaron/death", CHAN_BODY)
			BO4D C 5 BRIGHT A_Fall
			BO4D DE 5 BRIGHT
			TNT1 AAAAA 0 A_CustomMissile("SmokeBigExplosion", Random(30,60), 0, random(0,360), 2, random(0,360))
			BO4D FGHIJ 4 BRIGHT
			TNT1 A -1
			Stop
			
		Raise:
			BOSS O 8
			BOSS NMLKJI 8
			TNT1 A 0 A_SetUserVar("user_angry", 0)
			Goto See
  }
}

ACTOR Comet {
	Radius 10
	Height 10
	Speed 20
	FastSpeed 28
	Damage 8
	Scale 0.85
	PROJECTILE
	DamageType "BaronComet"
	SeeSound "commander/grenadefire"
	DeathSound "commander/explosion"
	
	States {
		Spawn:
			HCMT AAAABBBBCCCC 1 Bright A_SpawnItemEx("CometTrail",0,0,0,0,0,0,0,128)
			Loop
			
		Death:
			TNT1 A 0 Bright A_Explode(64, 102)
			TNT1 A 0 A_SpawnItemEx("GreenParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
			HCMT DE 3 Bright
			TNT1 A 0 A_SpawnItemEx("CometDeath",0,0,0,0,0,0,0,128,0)
			Stop
	}
}

ACTOR CometTrail {
	RenderStyle Add
	Alpha 0.9
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+DONTSPLASH
	+NOCLIP
	+NOINTERACTION
	+THRUACTORS
	
	States {
		Spawn:
			TNT1 A 2
			HCMT DEFGHI 2 Bright
			Stop
	}
}

ACTOR CometDeath {
	Projectile
	RenderStyle Add
	Alpha 0.7
	+NOCLIP
	
	States {
		Spawn:
			TNT1 AAA 0 NoDelay A_CustomMissile("BigGreenExplosion", 0, 0, random(0,360), 2, random(0,360))	
			TNT1 AAA 0 A_CustomMissile("SmokeBigExplosion", 0, 0, random(0,360), 2, random(0,360))
			HCMT JKLMN 3 Bright A_FadeOut(0.1)
			Stop
	}
}

ACTOR AngryBall : BaronBall {
	States {
		Spawn:
			BAL7 AB 1 Bright A_SpawnItemEx("AngryBallTrail",0,0,0,0,0,0,0,128,0)
			Loop
			
		Death:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("GreenParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
			TNT1 AAA 0 A_CustomMissile("SmallGreenExplosion", 0, 0, random(0,360), 2, random(0,360))
			TNT1 AAA 0 A_CustomMissile("SmokeSmallExplosion", 0, 0, random(0,360), 2, random(0,360))	
			BAL7 CDE 3 Bright
			Stop
	}
}

ACTOR AngryBallTrail {
	RenderStyle Add
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+DONTSPLASH
	+NOCLIP
	+NOINTERACTION
	+THRUACTORS
	Scale 0.85
	
	States {
		Spawn:
			TNT1 A 1
			BAL7 AB 1 A_FadeOut(0.25)
			Goto Spawn+1
  }
}

ACTOR GreenSpike {
	Radius 1
	Height 1
	Speed 0
	Scale 2
	Projectile
	DamageType "BaronComet"
	RenderStyle Add
	Alpha 0.95
	+FLOORHUGGER
	+NODAMAGETHRUST
	+FORCERADIUSDMG
	Obituary "%o no terminÃ³ el mod."
	
	States {
		Spawn:
			TNT1 A 0 NoDelay A_PlaySound("AM/step")
			FTRD AB 2 Bright
			FTRD C 10 Bright A_Explode(38,92,0)
			FTRD DEFGHIJ 1 Bright 
			Stop
	}
}