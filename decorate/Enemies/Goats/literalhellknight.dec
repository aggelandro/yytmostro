ACTOR LiteralHellKnight : HellKnight2 {
	Speed 14
	MeleeRange 64
	Species Goat
	DamageFactor "Poison", 0
	Obituary "%o was literally diced to bits by a Literal Knight."
	SeeSound "LHK/sight"
  PainSound "demon/pain"
  DeathSound "LHK/death"
  ActiveSound "LHK/act"
	DropItem "IsaacItemsTier1MidChanceSpawner"
	DropItem "IsaacItemsTier2LowChanceSpawner"
	DropItem "IsaacItemsTier2LowChanceSpawner"
	DropItem "CrusherGunToken", 45
	
	+FLOORCLIP
	
	States {
		
		SpawnLoop:
			SLKN AB 10 A_Look 
      Loop
			
		See:
			SLKN AABBCCDD 2 FAST A_Chase("Melee", "Missile", CHF_NIGHTMAREFAST)
			TNT1 A 0 A_Jump(128, "See")
			TNT1 A 0 A_JumpIfCloser(256, "PlaceShield")
			Loop
			
		PlaceShield:
			SLKN C 1 A_FaceTarget
			TNT1 A 0 A_SpawnItemEx("LHKEnergyShield", 15, 0, 32)
			SLKN C 34 A_FaceTarget
			Goto See
			
		Melee:
		Missile:
			SLKN EF 6 A_FaceTarget
			SLKN G 6 A_CustomComboAttack("LHKFlame", 32, 10 * random(1, 8), "agge/scythehit")
			Goto See
			
		Pain: 
			SLKN H 5
			SLKN H 5 A_Pain
			Goto See
				
    Death: 
			SLKN I 6 
			SLKN J 6 A_Scream 
			SLKN K 6 A_Fall 
			SLKN LMNO 6 
			SLKN P -1 
			Stop 
				
    Raise: 
			SLKN PONMLKJI 5 
			Goto See 
	}
}

ACTOR LHKFlame : BaronBall {
	Speed 10
	Damage 3
	SeeSound "LHK/flame"
	
	States {
		Spawn:
			SKFB ABABABABAB 4 BRIGHT
			SKFB A 0 A_SpawnItemEx("LHKFlame2",0,0,0,velx*1.5,vely*1.5,velz*1.5,0,SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPOINTERS)
			Stop
			
		Death:
			TNT1 A 0 A_StopSound(6)
			SKFB CDE 3 BRIGHT
			Stop
	}
}

ACTOR LHKFlame2 : BaronBall {
	Speed 15
	Damage 6
	
	States {
		Spawn:
			SKFB AAAABBBB 1 BRIGHT A_SpawnItemEx("LHKFlameTrail",0,0,0,0,0,0,0,128)
			Loop
			
		Death:
			TNT1 A 0 A_StopSound(6)
			SKFB CDE 3 BRIGHT
			Stop
	}
}

ACTOR LHKFlameTrail {
	Scale 0.85
	Renderstyle Add
	Alpha 0.75
	
	+NOGRAVITY
	+FLOAT
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+DONTSPLASH
	+NOCLIP
	+NOINTERACTION
	+THRUACTORS
	
	States {
		Spawn:
			SKFB AB 2 BRIGHT A_FadeOut(0.25)
			Loop
	}
}

ACTOR LHKEnergyShield : EnergyShield {
	Radius 24
  Height 64
	
	States {
		SpawnLoop:
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			TNT1 A 1
			TNT1 A 0 A_JumpIfInventory("LoopFlag", 35, 1)
			Loop
			TNT1 A 0
			Stop
		
		Pain:
			TNT1 A 3 A_SpawnItemEx("LHKShieldHit", 5, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
			Goto SpawnLoop
	}
}

ACTOR LHKShieldHit : ShieldHit {
	Scale 0.65
	
	States {
		Spawn:
			TNT1 A 0 NoDelay A_PlaySound("LHK/block", CHAN_AUTO)
			RES0 ABCDEFGHIJKLMNOPQRST 2 BRIGHT A_Warp(AAPTR_MASTER, 15, 0, 32)
			Stop
	}
}