ACTOR DoubleChaingunGuy : ChaingunGuyMod {
	Health 80
	
	States {
		SpawnLoop:
			CPOX AB 10 A_Look
			Loop
			
		See:
			CPOX AABBCCDD 3 A_Chase
			Loop
			
		Missile:
			CPOX E 10 A_FaceTarget
			CPOX F 0 A_PlaySound("chainguy/attack",CHAN_AUTO)
			CPOX F 4 Bright A_CustomMissile("ChaingunnerTracer", 32, 12, frandom(-10, 10), 1)
			CPOX E 0 Bright A_CustomMissile("ChaingunnerTracer", 32, -12, frandom(-10, 10), 1)
			CPOX E 0 A_PlaySound("chainguy/attack",CHAN_AUTO)
			CPOX E 4 Bright A_CustomMissile("ChaingunnerTracer", 32, 12, frandom(-10, 10), 1)
			CPOX E 0 Bright A_CustomMissile("ChaingunnerTracer", 32, -12, frandom(-10, 10), 1)
			CPOX F 1 A_CPOSRefire
			Goto Missile+1
			
		Pain:
			CPOX G 3
			CPOX G 3 A_Pain
			Goto See
			
		Pain.FireBF:
		Pain.AfireF:
		Pain.Afire:
		Pain.FireRF:
		Pain.FireR:
			TNT1 A 0
			Goto Pain
			
		Death:
			CPOX H 5 
			CPOX I 5 A_Scream
			CPOX J 5 A_NoBlocking
			CPOX KLM 5
			CPOX N -1
			Stop
			
		Death.FireBF:
		Death.AfireF:
		Death.Afire:		
		Death.FireRF:
		Death.FireR:
			TNT1 A 0 A_Jump(128, 1)
			Goto Super::Death.FireBF
			8CHN A 5 BRIGHT A_Scream
			8CHN BC 5 BRIGHT
			8CHN DEFGH 4 BRIGHT
			8CHN I 4 BRIGHT A_NoBlocking
			8CHN J -1
			Stop
			
		XDeath:
			CPOX O 5 
			CPOX P 5 A_XScream
			CPOX Q 5 A_NoBlocking
			CPOX RS 5
			CPOX T -1
			Stop
			
		Raise:
			CPOX N 5
			CPOX MLKJIH 5
			Goto See
	}
}