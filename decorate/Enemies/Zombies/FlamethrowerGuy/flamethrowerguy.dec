ACTOR FlamethrowerGuy : AbstractYYTZombie {
	Health 100
	Radius 20
  Mass 100
  PainChance 170
	MeleeRange 384
	Obituary "%o was burned alive by a flamethrower guy"
	SeeSound "grunt/sight"
	AttackSound "agge/flamet"
	PainSound "grunt/pain"
	DeathSound "grunt/death"
	ActiveSound "grunt/active"
	Dropitem "Chaingun"
	Dropitem "Clip"
	DropItem "IsaacItemsTier1HighChanceSpawner"
	DropItem "HomingGunToken", 15
	DropItem "HandGrenadeAmmo", 10
	DropItem "ChainsawSpawner", 32
	
	States {
		SpawnLoop:
			FUEG E 10 A_Look
			Loop
			
		See:
			FUEG A 4 A_Chase("", "")
			FUEG ABBCCDD 4 A_Chase
			Loop
		
		Melee:
			FUEG E 10 A_FaceTarget
			FUEG F 0 Bright A_PlaySound("agge/flamet", CHAN_WEAPON)
			FUEG F 10 Bright A_CustomMissile("FlameThrowerFireF", 16, 0, random(-4, 4))
			FUEG E 1 A_FaceTarget
			FUEG E 0 A_PlaySound("agge/flamet", CHAN_WEAPON)
			FUEG F 10 Bright A_CustomMissile("FlameThrowerFireF", 16, 0, random(-4, 4))
			FUEG F 1 A_CPosRefire
			Goto Melee+1
		
		Pain:
			FUEG G 3
			FUEG G 3 A_Pain
			Goto See
			
		Death:
			FUEG H 5
			FUEG I 5 A_Scream
			FUEG J 5 A_Fall
			FUEG KLMNOPQ 5
			FUEG R -1
			Stop
			
		Raise:
			FUEG QPONMLKJIH 5
			Goto See
	}
}