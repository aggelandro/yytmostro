ACTOR SpookyVileSpawner
{
	Height 0
	Radius 0
	-SOLID
	+NOGRAVITY
	RenderStyle None
	States
	{
		Spawn:
			MISL A 1
			MISL A 0 A_SpawnItemEx("SpookyVile",-10,0,0)
			MISL A 0 A_SpawnItemEx("SpookyVile",10,0,0)
			Stop
	}
}

ACTOR SpookyVile : Archvile2
{
	RenderStyle Stencil
	StencilColor Grey
	Alpha 0.3
	Health 400
	Mass 100
	-SOLID
	Obituary "%o was killed by a spooky archvile."
	DropItem "DemonAmmo", 80
	DropItem "Falneitz", 12
	DropItem "IsaacItemsTier1HighChanceSpawner"
	DropItem "IsaacItemsTier2MidChanceSpawner"
	DropItem "IsaacItemsTier3LowChanceSpawner"
	States
	{
		Missile:
			VILE G 0 Bright A_VileStart
			VILE G 10 Bright A_FaceTarget
			VILE H 8 Bright A_VileTarget("BlindFire")
			 VILE IJKLMN 8 Bright A_FaceTarget
			 VILE O 8 Bright A_VileAttack("vile/stop",10,5,70,1.0)
			VILE P 20 Bright
			Goto See
	}
}
ACTOR BlindFire : ArchvileFire
{
	RenderStyle Stencil
	StencilColor Grey
	Scale 2.5
	Damage 0
	+SEEKERMISSILE
	var int user_duracion;
	States
	{
	Spawn:
		FIRE A 0 Bright A_SeekerMissile(15,15,SMF_LOOK,256,15)
		FIRE A 1 Bright 
		FIRE BAB 1 Bright A_Warp(AAPTR_TRACER, 0, 0, 0, 0, WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		FIRE C 1 Bright 
		Goto Blinding
	Blinding:
		FIRE CCCCCCCCC 1 Bright A_Warp(AAPTR_TRACER, 24, 0, 0, 0, WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		FIRE D 0 A_SetUserVar("user_duracion", user_duracion+1)
		FIRE D 0 A_JumpIf(user_duracion >= 5, "Fizzling")
		Loop
	Fizzling:
		FIRE E 1 Bright
		FIRE FEFEFGHGHGH 1 Bright A_Fire
		Stop
  }		
}