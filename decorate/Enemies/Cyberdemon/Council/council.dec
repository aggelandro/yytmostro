ACTOR CouncilOfTheSeven : YYTMonster
{
	Health 2000
	Radius 16
	Height 56
	Mass 100
	Speed 25
	Monster
	+FLOORCLIP
	+THRUSPECIES
	+MISSILEMORE
	+NOINFIGHTING
	Species Council
	DamageFactor "AFireF", 0.1
	DamageFactor "Friendly", 0.5
	DamageFactor "Council",0
	Painchance 0
	DeathSound "*death"
	States
	{
	SpawnLoop:
		PLAY A 1 A_Look
		Loop
	See:
		PLAY ABCD 4 A_Chase 
		Loop
	Pain:
		PLAY G 4 
		PLAY G 4 A_Pain
		Goto See
	Death:
		PLAY H 10
		PLAY I 10 A_Scream
		PLAY J 10 A_NoBlocking
		PLAY KLM 10
		PLAY N -1
		Stop
	 }
}
ACTOR CouncilOfTheSevenBFG : CouncilOfTheSeven
{
	Scale 1.5
	Radius 24
	Height 84
	PainChance 10
	Health 4000
	+BOSS
	DropItem "UniqueWeaponSpawner"
	States
	{
	Missile:
		PLAY E 0 A_PlaySound("weapons/bfgf")
		PLAY EEEE 6 A_FaceTarget
		PLAY F 12 Bright A_CustomMissile("CouncilBfgBall")
		Goto See
	}
}
ACTOR CouncilOfTheSevenSSG : CouncilOfTheSeven
{
	Translation "112:127=64:79"
	States
	{
	Missile:
		PLAY E 8 A_FaceTarget
		PLAY E 0 A_PlaySound("weapons/sshotf")
		PLAY F 12 Bright A_CustomBulletAttack(11.2,7.1,20,5,"CouncilPuff")
		Goto See
	}
}
ACTOR CouncilOfTheSevenRL : CouncilOfTheSeven
{
	Translation "112:127=208:223"
	States
	{
	Missile:
		PLAY E 8 A_FaceTarget
		PLAY F 12 Bright A_CustomMissile("Rocket")
		Goto See
	}
}
ACTOR CouncilOfTheSevenPL : CouncilOfTheSeven
{
	Translation "112:127=192:207"
	Speed 40
	+MISSILEMORE
	+MISSILEEVENMORE
	States
	{
	Missile:
		PLAY E 4 A_FaceTarget
		PLAY FFFFF 2 Bright A_CustomMissile("PlasmaBall")
		Goto See
	}
}
ACTOR CouncilOfTheSevenCG : CouncilOfTheSeven
{
	Translation "112:127=96:111"
	PainChance 20
	States
	{
	Missile:
		PLAY E 4 A_FaceTarget
		PLAY F 0 bright A_PlaySound("chainguy/attack", CHAN_WEAPON)
		PLAY F 2 bright A_CustomBulletAttack(22.5, 0, 3, 3, "CouncilPuff", 0,CBAF_NORANDOM)
		PLAY E 2 A_SpidRefire
		Goto Missile+1
	}
}
ACTOR CouncilOfTheSevenSN : CouncilOfTheSeven
{
	Translation "112:127=32:47"
	States
	{
	Missile:
		PLAY EEE 8 A_FaceTarget
		PLAY F 0 bright A_PlaySound("zlayer/sniper",CHAN_AUTO)
		PLAY F 8 bright A_CustomBulletAttack(0, 0, 4, 50, "CouncilPuff", 0,CBAF_NORANDOM)
		PLAY E 8 
		Goto See
	}
}
ACTOR CouncilOfTheSevenFist : CouncilOfTheSeven
{
	Translation "112:127=16:31"
	PainChance 255
	Speed 70
	-MISSILEMORE
	+ALWAYSFAST
	States
	{
	Melee:
		PLAY E 4 A_FaceTarget
		PLAY E 1 A_CustomMeleeAttack(random(50,100),"skeleton/melee","skeleton/swing","Council")
		Goto See
	}
}
ACTOR CouncilSpawner
{
	States
	{
	Spawn:
		TNT1 A 40
		TNT1 A 0 A_SpawnItemEx("CouncilOfTheSevenBFG",0,0,0)
		TNT1 A 0 A_SpawnItemEx("CouncilOfTheSevenCG",-40,-40,0)
		TNT1 A 0 A_SpawnItemEx("CouncilOfTheSevenSSG",40,40,0)
		TNT1 A 0 A_SpawnItemEx("CouncilOfTheSevenRL",40,-40,0)
		TNT1 A 0 A_SpawnItemEx("CouncilOfTheSevenSN",-40,0,0)
		TNT1 A 0 A_SpawnItemEx("CouncilOfTheSevenPL",-40,40,0)
		TNT1 A 0 A_SpawnItemEx("CouncilOfTheSevenFist",40,0,0)
		Stop
	}
}
ACTOR CouncilBfgBall : BfgBall
{
	Species Council
	+MTHRUSPECIES
	DamageType "Council"
	States
	{
	Death:
		BFE1 AB 8 Bright
		BFE1 C 8 Bright A_BFGSpray("BFGExtraC")
		BFE1 DEF 8 Bright
		Stop
	}
}
ACTOR BfgExtraC : BfgExtra
{
	DamageType "Council"
}
ACTOR CouncilPuff : BulletPuff
{
	DamageType "Council"
}