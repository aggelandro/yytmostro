ACTOR HellCommander : Cyberdemon2 {
	Health 4500
  MeleeRange 96
  Tag "HellCommander"
  SeeSound "commander/sight"
  DeathSound "commander/death"
  Obituary "%o was blasted away by the Ancient Hell Commander."
	DropItem "IsaacItemsTier1Spawner"
	DropItem "IsaacItemsTier2Spawner"
	DropItem "IsaacItemsTier2MidChanceSpawner"
	DropItem "IsaacItemsTier3HighChanceSpawner"
	DropItem "IsaacItemsTier3MidChanceSpawner"
	DropItem "IsaacItemsTier3MidChanceSpawner"
	DropItem "IsaacItemsTier3LowChanceSpawner"
	DropItem "IsaacItemsTier3LowChanceSpawner"
	DropItem "PrototypeGunToken", 192
	DropItem "UniqueWeaponSpawner", 48
	var int user_launch_angle;
	
  States {
		SpawnLoop:
			COMM AB 10 A_Look
			Loop
			
		See:
			TNT1 A 0 A_JumpIf(user_spawn_tid != 0, "SeeLoop")
			TNT1 A 0 A_ChangeFlag(NOPAIN, TRUE)
			TNT1 A 0 ACS_NamedExecuteWithResult("showXAuraFor", 2, 87)
			TNT1 A 0 A_SetUserVar("user_spawn_tid", ACS_NamedExecuteWithResult("getUniqueTID"))
			TNT1 A 0 Radius_Quake(7, 70, 0, 15, 0)
			COMM S 87
			TNT1 A 0 A_SpawnItemEx("StrengthAura", 0, 0, 0, 0, 0, 0, 0, 0, 0, user_spawn_tid)
			TNT1 A 0 ACS_NamedExecuteWithResult("setTIDTracerPointerToSelf", user_spawn_tid)
			TNT1 A 0 A_ChangeFlag(NOPAIN, FALSE)
			Goto SeeLoop
			
		SeeLoop:
			COMM A 3 A_Chase
			TNT1 A 0 A_PlaySound("commander/metal", CHAN_BODY)
			COMM ABB 3 A_Chase
			COMM C 3 A_Hoof
			COMM CDD 3 A_Chase
			Loop
			
		Missile:
			COMM E 0 A_Jump(128,"Missile2","Missile3")
			TNT1 A 0 A_PlaySound("commander/attack", CHAN_BODY)
			COMM E 6 A_FaceTarget
			COMM F 12 Bright A_CustomMissile("HellRocket", 43, -10, frandom(-5, 5))
			COMM E 12 A_FaceTarget
			COMM F 12 Bright A_CustomMissile("HellRocket", 43, -10, frandom(-5, 5))
			COMM E 12 A_FaceTarget
			COMM F 12 Bright A_CustomMissile("HellRocket", 43, -10, frandom(-5, 5))
			COMM F 0 A_Jump(87, "Missile2", "Missile3")
			Goto SeeLoop
			
		Missile2:    
			COMM Q 0 A_JumpIfCloser(368,"Missile")
			TNT1 A 0 A_PlaySound("commander/attack", CHAN_BODY)
			COMM Q 0 A_SetUserVar("user_launch_angle", frandom(-35,10))
			COMM Q 6 A_FaceTarget
			COMM R 12 Bright A_CustomMissile("HellGrenade", 78, -27, 0, CMF_ABSOLUTEPITCH, user_launch_angle)
			COMM Q 12 A_FaceTarget
			COMM R 12 Bright A_CustomMissile("HellGrenade", 78, -27, 0, CMF_ABSOLUTEPITCH, user_launch_angle)
			COMM Q 12 A_FaceTarget
			COMM R 12 Bright A_CustomMissile("HellGrenade", 78, -27, 0, CMF_ABSOLUTEPITCH, user_launch_angle)
			COMM R 0 A_Jump(87, "Missile3")
			Goto SeeLoop
			
		Missile3:
			TNT1 A 0 A_JumpIf(Z+Height+18+15>CeilingZ, "Missile") // En caso que no le de la altura
			TNT1 A 0 A_CustomMissile("CommanderHellBallWallCheck", 150, 45)
			COMM S 2
			TNT1 A 0 A_JumpIfInventory("WallCheckDone", 1, 1)
			Goto SeeLoop
			TNT1 A 0 A_TakeInventory("WallCheckDone", 1)
			TNT1 A 0 A_PlaySound("commander/laugh", CHAN_BODY)
			TNT1 A 0 A_ChangeFlag(NOPAIN, TRUE)
			TNT1 A 0 ACS_NamedExecuteWithResult("showXAuraFor", 2, 115)
			COMM S 1 A_FaceTarget
			TNT1 A 0 A_CustomMissile("CommanderHellBall", 150, 45)
			COMM S 104 Bright A_FaceTarget
			COMM TU 5 Bright A_FaceTarget
			TNT1 A 0 A_ChangeFlag(NOPAIN, FALSE)
			Goto SeeLoop
			
		Melee:
			COMM ST 8 A_FaceTarget
			COMM U 8 A_CustomComboAttack("HCFlame", 60, 20 * random(1,10), "baron/melee")
			Goto SeeLoop
			
		Pain:
			COMM G 10 A_Pain
			Goto SeeLoop
			
		Death:
			TNT1 A 0 A_JumpIf(user_spawn_tid == 0, 2)
			TNT1 A 0 ACS_NamedExecuteWithResult("killActorWithTID", user_spawn_tid)
			COMM H 10 Bright
			COMM I 10 Bright A_Scream
			COMM JKL 10 Bright
			COMM M 10 Bright A_NoBlocking
			COMM NO 10 Bright
			COMM P 30 
			COMM P -1 A_BossDeath
			Stop
  }
}

Actor HCFlame : LHKFlame { Scale 2.0 ProjectileKickBack 500 }

Actor HellRocket : Rocket {
	Speed 25
	Scale 0.8
  Renderstyle Normal
	Decal BaronScorch
	-ROCKETTRAIL
	-RANDOMIZE
	-DEHEXPLOSION
	
  States {
		Spawn:
			TNT1 A 0 NoDelay Bright A_SpawnItemEx("HellRocketTrail")
      MELF AB 2 Bright
      Loop
			
    Death:
			TNT1 A 0 A_SpawnItemEx("OrangeParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
			TNT1 AAA 0 A_CustomMissile("NormalRedExplosion", 0, 0, random(0,360), 2, random(0,360))	
			TNT1 AAA 0 A_CustomMissile("SmokeBigExplosion", 0, 0, random(0,360), 2, random(0,360))
      TNT1 A 0 A_Explode
			TNT1 AAAA 0 A_SpawnItemEx("Debris",0,0,0,Random(4,-4),Random(4,-4),Random(4,8),0,128)
			TNT1 A 1 A_SpawnItem("MediumRocketEx")
      Stop
	}
}

Actor HellRocketTrail {
	Speed 0
	Scale 0.8
	PROJECTILE
	RenderStyle ADD
	Alpha 0.5
	+NOCLIP
	+NOINTERACTION
	
	States {
		Spawn:
			TNT1 A 0 NoDelay A_SetTranslucent(0.5)
			MELF BBBBB 2 A_Fadeout
			Stop
	}
}

Actor HellGrenade : Rocket {
	RENDERSTYLE ADD
  Alpha 1.0
	Speed 20
	BounceCount 3
	ReactionTime 35
	Gravity 0.5
	BounceType "Hexen"
	BounceSound "commander/grenadebnc"
  SeeSound "commander/grenadefire"
	Obituary "%o couldn't dodge %k's hell-bomb."
	+THRUGHOST
	+FORCEXYBILLBOARD
  +EXPLODEONWATER
  -NOGRAVITY
	-DEHEXPLOSION
  -ROCKETTRAIL
	
	States {
		Spawn: 
			HBA3 ABC 3 Bright A_SpawnItemEx("RedPuff",0,0,0,0,0,0,0,8)
			HBA3 A 0 Bright A_CountDown
			Loop
			
		Death:
			HBA3 D 0 A_NoGravity
			HBA3 D 3 Bright
			HBA3 E 3 Bright A_Explode(Random(60, 120),144,1,0,32)
			HBA3 FGHIJKLM 3 Bright
			Stop
   }
}

ACTOR RedPuff {
	Radius 0
	Height 1
	RENDERSTYLE ADD
	Alpha 0.85
	+NOGRAVITY
	+NOBLOCKMAP
	+FORCEXYBILLBOARD   
	
	States {
		Spawn:
			NULL A 3 Bright
			RPUF ABCDE 3 Bright
			Stop 
	}
}

ACTOR CommanderHellBall : BelphegorGreenPlasmaBall {
	Translation "112:127=176:191"
	Alpha 1.0
	Scale 2.0
	Height 18
	Radius 11
	Damage (Random(50, 80))
	DeathSound "commander/explosion"
	+SEEKERMISSILE
	
	States {
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("holdActorVelocityFor", 104)
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			BPBA A 1 A_JumpIf(CallACS("isTargetDead"), "Death")
			TNT1 A 0 A_JumpIfInventory("LoopFlag", 52, 1)
			Goto Spawn+1
			TNT1 A 0 A_TakeInventory("LoopFlag", 52)
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			BPBA B 1 A_JumpIf(CallACS("isTargetDead"), "Death")
			TNT1 A 0 A_JumpIfInventory("LoopFlag", 52, 1)
			Goto Spawn+5
			TNT1 A 0 A_PlaySound("commander/grenadefire", CHAN_BODY)
			Goto SpawnLoop
	
		SpawnLoop:
			BPBA B 1 A_SeekerMissile(10, 15, SMF_LOOK|SMF_CURSPEED, 256, 3)
			TNT1 A 0 Bright A_SpawnItemEx("HellBallRedPuff",0,0,0,0,0,0,0,8)
			Loop
			
		Death:
			TNT1 A 0 A_SetScale(0.5)
			CHBL AABBCC 1
			TNT1 A 0 A_SpawnItemEx("OrangeParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
			TNT1 AAA 0 A_CustomMissile("BigRedExplosion", 0, 0, random(0,360), 2, random(0,360))	
			TNT1 AAA 0 A_CustomMissile("SmokeBigExplosion", 0, 0, random(0,360), 2, random(0,360))
			TNT1 A 0 Radius_Quake (3, 35, 0, 15, 0)
			TNT1 A 0 A_Explode(Random(60, 90), 224, 1)
			CHBL DEFGHIJKLMNOPQRST 2
			Stop
	}
}

ACTOR CommanderHellBallWallCheck {
	Radius 18
	Height 11
	Speed 1
	Projectile
	+NOGRAVITY
	-NOBLOCKMAP
	
	States {
		Spawn:
			TNT1 A 1 NoDelay
			TNT1 A 0 A_GiveInventory("WallCheckDone", 1, AAPTR_TARGET)
			Stop
			
		Crash:
		XDeath:
			TNT1 A 0 A_GiveInventory("WallCheckDone", 1, AAPTR_TARGET)
			Stop
			
		Death:
			TNT1 A 0
			Stop
	}
}

ACTOR HellBallRedPuff : RedPuff { Scale 4.0 }

ACTOR StrengthAura {
	Scale 0.65
	RenderStyle Add
	Alpha 0.9
	+NOGRAVITY
	+NOINTERACTION
	+SEEKERMISSILE
	
	States {
		Spawn:
			AURA A 2 NoDelay A_RadiusGive("BuffSTRSpell", 1024, RGF_MONSTERS|RGF_NOTRACER)
			Goto SpawnLoop
			
		SpawnLoop:
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			AURA BCDA 2 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			TNT1 A 0 A_JumpIfInventory("LoopFlag", 22, 1)
			Loop
			TNT1 A 0 A_TakeInventory("LoopFlag", 22)
			AURA A 1 A_RadiusGive("BuffSTRSpell", 1024, RGF_MONSTERS|RGF_NOTRACER)
			TNT1 A 0 A_Warp(AAPTR_TRACER, 5, 0, 0, 0, WARPF_NOCHECKPOSITION)
			Loop
			
		Death:
			TNT1 A 1
			Stop
	}
}

ACTOR BuffSTRSpell : CustomInventory {
	+INVENTORY.AUTOACTIVATE
  Inventory.Amount 1
  Inventory.MaxAmount 1
	
	States {
		Pickup:
			TNT1 A 0 A_JumpIfInventory("BuffSTRGiver", 1, 3)
			TNT1 A 0 A_GiveInventory("BuffSTRGiver", 1)
			TNT1 A 0 ACS_NamedExecuteWithResult("applyEffect", 11, 350)
			TNT1 A 0
			Stop
	}
}

ACTOR BuffSTRGiver : Inventory { Inventory.MaxAmount 1 Inventory.Amount 1 }