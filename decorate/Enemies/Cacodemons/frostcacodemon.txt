ACTOR Frostodemon : Cacodemon2 {
	Obituary "%o was frozen by a Frostodemon."
  HitObituary "%o got chomped by a Frostodemon."
	Health 1200
	Mass 800
	Speed 15
	PainChance 32
	Scale 0.9
	DamageFactor "Ice", 0
	DamageFactor "IsaacIce", 0
	BloodColor "Blue"
	SeeSound "frostcaco/helsit"
	PainSound "frostcaco/helpai"
	DeathSound "frostcaco/heldth"
	ActiveSound "frostcaco/helact"
	DropItem "DemonAmmoBox", 48
	DropItem "ContraSpawner3"
	DropItem "IsaacItemsTier2HighChanceSpawner"
	DropItem "IsaacItemsTier2LowChanceSpawner"
	DropItem "IsaacItemsTier3LowChanceSpawner"
	+MISSILEMORE
	+BOSS
	var int user_is_dead;
	
	States {
		SpawnLoop:
			HELE A 10 A_Look
			Loop
			
		See:
			TNT1 A 0 A_JumpIf(user_spawn_tid != 0, "SeeLoop")
			TNT1 A 0 A_SetUserVar("user_spawn_tid", ACS_NamedExecuteWithResult("getUniqueTID"))
			TNT1 A 0 A_SpawnItemEx("IceOrbitEffect", 0, 0, 0, 0, 0, 0, 0, 128, 0, user_spawn_tid)
			TNT1 A 0 ACS_NamedExecuteWithResult("setTIDTracerPointerToSelf", user_spawn_tid)
			Goto SeeLoop
			
		SeeLoop:
			TNT1 A 0 A_SpawnItemEx("IceTrail",0,0,0,0,0,0,0,128)
			TNT1 A 0 A_SpawnItemEx("BlueBodyParticleSpawner", 0, 0, 32, 0, 0, 0, 0, 128)
			HELE A 3 A_Chase
			TNT1 A 0 A_SpawnItemEx("BlueBodyParticleSpawner", 0, 0, 32, 0, 0, 0, 0, 128)
			HELE A 3 A_Chase
			TNT1 A 0 A_SpawnItemEx("BlueBodyParticleSpawner", 0, 0, 32, 0, 0, 0, 0, 128)
			HELE A 3 A_Chase
			Loop
			
		Melee:
			HELE ABD 5 Bright A_Facetarget
			HELE C 5 Bright A_CustomMeleeAttack(10*random(1,8), "frostcaco/melee")
			Goto SeeLoop
			
		Missile:
			TNT1 A -1 A_Jump(256, "Missile1", "Missile2", "Missile3", "Missile4", "Missile5")
			Wait
			
		Missile1:
			HELE EF 5 Bright A_Facetarget
			HELE G 5 Bright
			TNT1 A 0 A_CustomMissile("FrostCacoBall", 24, 0, -10)
			TNT1 A 0 A_CustomMissile("FrostCacoBall", 24, 0, -5)
			TNT1 A 0 A_CustomMissile("FrostCacoBall", 24, 0,  0)
			TNT1 A 0 A_CustomMissile("FrostCacoBall", 24, 0,  5)
			TNT1 A 0 A_CustomMissile("FrostCacoBall", 24, 0, 10)
			Goto SeeLoop
			
		Missile2:
			HELE EF 5 Bright A_Facetarget
			HELE G 5 Bright
			TNT1 A 0 A_CustomMissile("FrostTPCacoBall", 24, 0, -10)
			TNT1 A 0 A_CustomMissile("FrostTPCacoBall", 24, 0, -5)
			TNT1 A 0 A_CustomMissile("FrostTPCacoBall", 24, 0,  0)
			TNT1 A 0 A_CustomMissile("FrostTPCacoBall", 24, 0,  5)
			TNT1 A 0 A_CustomMissile("FrostTPCacoBall", 24, 0, 10)
			Goto SeeLoop
			
		Missile3:
			HELE EF 5 Bright A_Facetarget
			HELE G 5 Bright
			TNT1 A 0 A_CustomMissile("FrostCacoBall", 24, 0, -10)
			TNT1 A 0 A_CustomMissile("FrostCacoBall", 24, 0, -5)
			TNT1 A 0 A_CustomMissile("FrostCacoBall", 24, 0,  0)
			HELE EF 5 Bright A_Facetarget
			HELE G 5 Bright
			TNT1 A 0 A_CustomMissile("FrostTPCacoBall", 24, 0, 0)
			TNT1 A 0 A_CustomMissile("FrostTPCacoBall", 24, 0, 5)
			TNT1 A 0 A_CustomMissile("FrostTPCacoBall", 24, 0, 10)
			Goto SeeLoop
			
		Missile4:
			HELE EF 5 Bright A_Facetarget
			HELE G 5 Bright
			TNT1 A 0 A_CustomMissile("FrostCacoBall", 24, 0, -20)
			TNT1 A 0 A_CustomMissile("FrostCacoBall", 24, 0, 20)
			HELE EF 5 Bright A_Facetarget
			HELE G 5 Bright
			TNT1 A 0 A_CustomMissile("FrostTPCacoBall", 24, 0, -15)
			TNT1 A 0 A_CustomMissile("FrostTPCacoBall", 24, 0,  15)
			HELE EF 5 Bright A_Facetarget
			HELE G 5 Bright
			TNT1 A 0 A_CustomMissile("FrostCacoBall", 24, 0, -10)
			TNT1 A 0 A_CustomMissile("FrostCacoBall", 24, 0, 10)
			HELE EF 5 Bright A_Facetarget
			HELE G 5 Bright
			TNT1 A 0 A_CustomMissile("FrostTPCacoBall", 24, 0, -5)
			TNT1 A 0 A_CustomMissile("FrostTPCacoBall", 24, 0, 5)
			Goto SeeLoop
			
		Missile5:
			HELE EF 5 Bright A_Facetarget
			HELE G 5 Bright
			TNT1 A 0 A_CustomMissile("FrostCacoBall", 24, 0, -5)
			TNT1 A 0 A_CustomMissile("FrostCacoBall", 24, 0, 5)
			HELE EF 5 Bright A_Facetarget
			HELE G 5 Bright
			TNT1 A 0 A_CustomMissile("FrostTPCacoBall", 24, 0, -10)
			TNT1 A 0 A_CustomMissile("FrostTPCacoBall", 24, 0,  10)
			HELE EF 5 Bright A_Facetarget
			HELE G 5 Bright
			TNT1 A 0 A_CustomMissile("FrostCacoBall", 24, 0, -15)
			TNT1 A 0 A_CustomMissile("FrostCacoBall", 24, 0, 15)
			HELE EF 5 Bright A_Facetarget
			HELE G 5 Bright
			TNT1 A 0 A_CustomMissile("FrostTPCacoBall", 24, 0, -20)
			TNT1 A 0 A_CustomMissile("FrostTPCacoBall", 24, 0, 20)
			Goto SeeLoop
			
		Pain:
			HELE H 3
			HELE H 9 A_Pain
			Goto SeeLoop
			
		Death:
			TNT1 A 0 A_SetUserVar("user_is_dead", 1)
			HELE I 5 Bright A_Scream
			HELE JKL 5 Bright
			HELE M 5 Bright A_NoBlocking
			HELE NOP 5 Bright
			HELE QRSTU 5
			HELE V -1 A_SetFloorClip
			Stop
			
		Death.FireBF:
		Death.AfireF:
		Death.Afire:
		Death.FireRF:
		Death.FireR:
			TNT1 A 0
			Goto Death
			
		Raise:
			HELE U 8 A_UnSetFloorClip
			HELE TSRQPONMLKJI 8
			TNT1 A 0 A_SetUserVar("user_spawn_tid", 0)
			TNT1 A 0 A_SetUserVar("user_is_dead", 0)
			Goto See
  }
}

ACTOR FrostCacoBall : CacodemonBall {
  Height 9
	DamageType "Ice"
	SeeSound "frostcaco/iceattack"
 	DeathSound "frostcaco/icedeath"
	+HITTRACER
	
	States {
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("YYT_projectileCheck")
			FROM AB 4 Bright
			Goto SpawnLoop
			
		SpawnLoop:
			FROM CD 2 BRIGHT A_SpawnItemEx("IceBallTrail",0,0,-11,0,0,0,0,128)
			Loop
			
		XDeath:
			TNT1 A 0 A_JumpIfInventory("SlowCounter", 0, "Death", AAPTR_TRACER)
			TNT1 A 0 A_GiveInventory("SlowCounter", 1, AAPTR_TRACER)
			TNT1 A 0 ACS_NamedExecuteWithResult("applySlowEffect", 350)
			Goto Death
			
		Death:
			TNT1 A 0 A_SpawnItemEx("BlueParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
			TNT1 AAA 0 A_CustomMissile("SmallBlueExplosion", 0, 0, random(0,360), 2, random(0,360))	
			TNT1 AAA 0 A_CustomMissile("SmokeSmallExplosion", 0, 0, random(0,360), 2, random(0,360))
			FROM EFGHI 6 Bright
			Stop
  }
}

ACTOR FrostTPCacoBall : FrostCacoBall {
	Speed 8
	Damage 4
	+SEEKERMISSILE

	States {
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("setTracerToPlayerFromTarget")
			TNT1 A 0 ACS_NamedExecuteWithResult("YYT_projectileCheck")
			FROM AB 4 Bright
			Goto SpawnLoop
		
		SpawnLoop:
			TNT1 A 0 A_GiveInventory("LoopFlag", Random(1, 3))
			FROM CD 2 BRIGHT A_SpawnItemEx("IceBallTrail",0,0,-11,0,0,0,0,128)
			TNT1 A 0 A_SeekerMissile(1, 2, SMF_LOOK|SMF_CURSPEED)
			TNT1 A 0 A_JumpIfInventory("LoopFlag", 30, 1)
			Loop
			TNT1 A 0 A_TakeInventory("LoopFlag", 35)
			TNT1 A -1 A_Jump(256, "PerformTP1", "PerformTP2", "PerformTP3", "PerformTP4")
			Wait
			
		PerformTP1:
			TNT1 A 0 A_Warp(AAPTR_TRACER, Random(116, 128), Random(116, 128), Random(24, 64), 0, 0, "TPSuccess")
			Goto SpawnLoop
			
		PerformTP2:
			TNT1 A 0 A_Warp(AAPTR_TRACER, Random(-116, -128), Random(116, 128), Random(24, 64), 0, 0, "TPSuccess")
			Goto SpawnLoop
			
		PerformTP3:
			TNT1 A 0 A_Warp(AAPTR_TRACER, Random(116, 128), Random(-116, -128), Random(24, 64), 0, 0, "TPSuccess")
			Goto SpawnLoop
			
		PerformTP4:
			TNT1 A 0 A_Warp(AAPTR_TRACER, Random(-116, -128), Random(-116, -128), Random(24, 64), 0, 0, "TPSuccess")
			Goto SpawnLoop
			
		TPSuccess:
			TNT1 A 0 A_PlaySound("frostcaco/heltel", CHAN_AUTO)
			TNT1 A 0 ACS_NamedExecuteWithResult("setAlphaTo", 0)
			FROM C 1 BRIGHT A_FaceTracer
			TNT1 A 0 A_SeekerMissile(8, 10, SMF_LOOK|SMF_CURSPEED)
			FROM CD 2 BRIGHT A_SpawnItemEx("SpawningIceBallTrail",0,0,-11,0,0,0,0,128)
			TNT1 A 0 A_FadeIn
			Goto TPSuccess+2
  }
}

ACTOR IceBallTrail {
  Speed 0
  RenderStyle Add
  Alpha 0.75
	Scale 0.2
  +RANDOMIZE
	+NOINTERACTION
	+NOGRAVITY
	
  States {
		Spawn:
			TNT1 A 2
			GG4S ABCDEFGHI 1 Bright
			Stop
	}
}

ACTOR SpawningIceBallTrail : IceBallTrail {
	Alpha 0
	
	States {
		Spawn:
			TNT1 A 2
			GG4S ABCDEFGHI 1 Bright A_FadeIn
			Stop
	}
}

ACTOR SlowCounter : Inventory {
	-INVBAR
  +UNDROPPABLE
  Inventory.MaxAmount 1
	Inventory.Amount 1
}