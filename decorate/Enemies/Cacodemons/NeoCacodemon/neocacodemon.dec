ACTOR NeoCacodemon 
{
  Health 400
  Radius 31
  Height 56
  Mass 400
  Speed 8
  PainChance 128
  Monster
  +FLOAT
  +NOGRAVITY
  SeeSound "caco/sight"
  PainSound "caco/pain"
  DeathSound "caco/death"
  ActiveSound "caco/active"
  Obituary "$OB_CACO"
  HitObituary "$OB_CACOHIT"
  States
  {
  Spawn:
    HEAD A 10 A_Look
    Loop
  See:
    HEAD A 3 A_Chase
    Loop
  Missile:
    HEAD BC 5 A_FaceTarget
    HEAD D 5 Bright A_HeadAttack
    Goto See
  Pain:
    HEAD E 3
    HEAD E 3 A_Pain
    HEAD F 6
    Goto See
	Pain.Lightning:
		TNT1 A 0 A_SpawnItemEx("ElectricSpark1",random(-10,10),random(-15,-5),random(0,Height*0.5),0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("ElectricSpark2",random(-10,10),random(5,15),random(0,Height*0.5),0,0,0,0)
    Goto Pain
  Death:
    HEAD G 8
    HEAD H 8 A_Scream
    HEAD IJ 8
    HEAD K 8 A_NoBlocking
	HEAD K 0 A_SpawnItemEx("GhostCacodemon")
    HEAD L -1 A_SetFloorClip
    Stop
  Raise:
    HEAD L 8 A_UnSetFloorClip
    HEAD KJIHG 8
    Goto See
  }
}
ACTOR GhostCacodemon : NeoCacodemon
{
	RenderStyle Translucent
	Alpha 0.5
	-SOLID
	-SHOOTABLE
	var int user_duracioncaco;
	States
	{
	Spawn:
		HEAD L 0
		HEAD L 8 ThrustThingZ(0,5,0,0)
		HEAD KJIHG 8
		HEAD A 0 A_Stop
		HEAD A 10 A_Look
		Goto Spawn+4
	See:
		HEAD A 3 A_Chase
		HEAD A 0 A_SetUserVar("user_duracioncaco",user_duracioncaco+1)
		HEAD A 0 A_JumpIf(user_duracioncaco > 200,"Death")
		Loop
	Missile:
		HEAD BC 5 A_FaceTarget
		HEAD D 5 Bright A_HeadAttack
		HEAD D 0 A_SetUserVar("user_duracioncaco",user_duracioncaco+3)
		Goto See
	Death:
		HEAD AAAAAAAAAAAAAAA 2 A_FadeOut(0.02)
		Stop
	}
}