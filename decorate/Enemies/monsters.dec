ACTOR YYTMonster
/*
// Clase abstracta para todos los monstruos del juego.
// IMPORTANTE: Spawn states pasan a ser SpawnLoop para cada monstruo especÃ­fico.
*/
{
	Monster
	var int user_max_health; // Guarda el total de HP de un monstruo
	var int user_spawn_tid; // Se usa para guardar el TID de cualquier spawn que haga el monstruo (de ser necesario)
	var int user_burning_loop; // Para animaciones de muerte ardiendo
	
	States 
	{
		Spawn:
			TNT1 A 0 NoDelay A_JumpIf(user_max_health>0, 2)
			TNT1 A 0 A_SetUserVar("user_max_health", Health)
			TNT1 A -1 A_Jump (256, "SpawnLoop")
			Wait
			
		Petrify:
			"####" "#" 0 A_ChangeFlag("NOPAIN", TRUE)
			"####" "#" 0 A_ChangeVelocity(0, 0, 0, CVF_REPLACE)
			"####" "#" 1
			Loop
			
		Depetrify:
			"####" "#" 0 A_ChangeFlag("NOPAIN", FALSE)
			"####" "#" -1 A_Jump (256, "See")
			Wait
			
		Pain.Lightning:
			TNT1 A 0 A_SpawnItemEx("ElectricSpark1",random(-10,10),random(-15,-5),random(0,Height*0.5),0,0,0,0)
			TNT1 A 0 A_SpawnItemEx("ElectricSpark2",random(-10,10),random(5,15),random(0,Height*0.5),0,0,0,0)
			TNT1 A -1 A_Jump (256, "Pain")
			Wait
			
		Pain.SubSun:
			TNT1 A 0 A_PlaySound("agge/subsunhit", CHAN_AUTO)
			TNT1 A 0 A_SpawnItemEx("OrangeParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
			TNT1 AAA 0 A_CustomMissile("SmallRedExplosion", 0, 0, random(0,360), 2, random(0,360))
			TNT1 AAA 0 A_CustomMissile("SmokeSmallExplosion", 0, 0, random(0,360), 2, random(0,360))	
			TNT1 A -1 A_Jump (256, "Pain")
			Wait
			
		Death.IsaacIce:
			"####" "#" 0 A_CheckFlag("BOSS", "Death") // No afecta a los bosses
			"####" "#" 0 A_CheckFlag("NOICEDEATH", "Death")
			"####" "#" 0 A_GenericFreezeDeath
			"####" "#" 175
			"####" "#" 0 A_SpawnItemEx("IsaacIceShard", 40, 0, Height/2, 1, 1, 0, 0, SXF_MULTIPLYSPEED|SXF_ABSOLUTEANGLE)
			"####" "#" 0 A_SpawnItemEx("IsaacIceShard", 40, 0, Height/2, 1, 1, 0, 36, SXF_MULTIPLYSPEED|SXF_ABSOLUTEANGLE)
			"####" "#" 0 A_SpawnItemEx("IsaacIceShard", 40, 0, Height/2, 1, 1, 0, 72, SXF_MULTIPLYSPEED|SXF_ABSOLUTEANGLE)
			"####" "#" 0 A_SpawnItemEx("IsaacIceShard", 40, 0, Height/2, 1, 1, 0, 108, SXF_MULTIPLYSPEED|SXF_ABSOLUTEANGLE)
			"####" "#" 0 A_SpawnItemEx("IsaacIceShard", 40, 0, Height/2, 1, 1, 0, 144, SXF_MULTIPLYSPEED|SXF_ABSOLUTEANGLE)
			"####" "#" 0 A_SpawnItemEx("IsaacIceShard", 40, 0, Height/2, 1, 1, 0, 180, SXF_MULTIPLYSPEED|SXF_ABSOLUTEANGLE)
			"####" "#" 0 A_SpawnItemEx("IsaacIceShard", 40, 0, Height/2, 1, 1, 0, 216, SXF_MULTIPLYSPEED|SXF_ABSOLUTEANGLE)
			"####" "#" 0 A_SpawnItemEx("IsaacIceShard", 40, 0, Height/2, 1, 1, 0, 252, SXF_MULTIPLYSPEED|SXF_ABSOLUTEANGLE)
			"####" "#" 0 A_SpawnItemEx("IsaacIceShard", 40, 0, Height/2, 1, 1, 0, 288, SXF_MULTIPLYSPEED|SXF_ABSOLUTEANGLE)
			"####" "#" 0 A_SpawnItemEx("IsaacIceShard", 40, 0, Height/2, 1, 1, 0, 324, SXF_MULTIPLYSPEED|SXF_ABSOLUTEANGLE)
			"####" "#" 0 A_IceGuyDie
			Stop
  }
}
ACTOR AbstractYYTZombie : YYTMonster {
	Height 56
	Speed 8
	Species "Zombie"
	+DOHARMSPECIES
  +FLOORCLIP
	
	States 
	{
		Death.FireBF:
		Death.AfireF:
		Death.Afire:
			REEB A 3 Bright A_PlaySound("grunt/pain")
			REEB B 3 Bright
			REEB C 3 Bright A_NoBlocking
			REEB DEFGH 3 Bright
			REEB I 3 Bright A_PlaySound("grunt/pain")
			REEB JKLMN 3 Bright
			REEB O 3 Bright A_PlaySound("grunt/pain")
			REEB PQR 3 Bright
			REEB S 3 Bright A_PlaySound("grunt/death")
			REEB TW 3 Bright
			REEB V -1 
			Stop
			
		Death.FireRF:
		Death.FireR:
			TNT1 A 0 ACS_NamedExecuteWithResult("showTranslation", 20)
			TNT1 A -1 A_Jump(256, "Death.Afire")
			Wait
	}
}

ACTOR ShotgunGuyMod : AbstractYYTZombie {
  Health 30
  Radius 20
  Mass 100
  PainChance 170
  SeeSound "shotguy/sight"
  AttackSound "zombie/shotgun"
  PainSound "shotguy/pain"
  DeathSound "shotguy/death"
  ActiveSound "shotguy/active"
  Obituary "$OB_SHOTGUY"
  DropItem "Shotgun"
	DropItem "Shell"
	DropItem "IsaacItemsTier1MidChanceSpawner"
	DropItem "SpreadGunToken", 10
	DropItem "HandGrenadeAmmo", 20
	
  States {
		SpawnLoop:
			SPOS AB 10 A_Look
			Loop
			
		See:
			SPOS AABBCCDD 3 A_Chase
			Loop
			
		Missile:
			SPOS E 10 A_FaceTarget
			SPOS F 0 A_CustomMissile("ShotgunnerTracer", 32, 0, frandom(-16, 16), 1)
			SPOS F 0 A_CustomMissile("ShotgunnerTracer", 32, 0, frandom(-16, 16), 1)
			SPOS F 0 A_CustomMissile("ShotgunnerTracer", 32, 0, frandom(-16, 16), 1)
			SPOS F 10 Bright A_PlaySound("zombie/shotgun",CHAN_AUTO)
			SPOS E 10
			Goto See
			
		Pain:
			SPOS G 3
			SPOS G 3 A_Pain
			Goto See
			
		Death:
			SPOS H 5
			SPOS I 5 A_Scream
			SPOS J 5 A_NoBlocking
			SPOS K 5
			SPOS L -1
			Stop
			
		Death.FireBF:
		Death.AfireF:
		Death.Afire:
		Death.FireRF:
		Death.FireR:
			TNT1 A 0 A_Jump(128, 1)
			Goto Super::Death.FireBF
			8SAR A 5 A_Scream
			8SAR B 5
			8SAR C 5 A_NoBlocking
			8SAR D 5
			8SAR E -1
			Stop
			
		XDeath:
			SPOS M 5
			SPOS N 5 A_XScream
			SPOS O 5 A_NoBlocking
			SPOS PQRST 5
			SPOS U -1
			Stop
			
		Raise:
			SPOS L 5
			SPOS KJIH 5
			Goto See
  }
}
ACTOR ChaingunGuyMod : AbstractYYTZombie {
  Health 70
  Radius 20
  Mass 100
  PainChance 170
  SeeSound "chainguy/sight"
  PainSound "chainguy/pain"
  DeathSound "chainguy/death"
  ActiveSound "chainguy/active"
  AttackSound "chainguy/attack"
  Obituary "$OB_CHAINGUY"
  Dropitem "Chaingun"
	Dropitem "Clip"
	DropItem "IsaacItemsTier1HighChanceSpawner"
	DropItem "HomingGunToken", 10
	DropItem "HandGrenadeAmmo", 20
	
  States {
		SpawnLoop:
			CPOS AB 10 A_Look
			Loop
			
		See:
			CPOS AABBCCDD 3 A_Chase
			Loop
			
		Missile:
			CPOS E 10 A_FaceTarget
			CPOS F 0 A_PlaySound("chainguy/attack",CHAN_AUTO)
			CPOS F 4 Bright A_CustomMissile("ChaingunnerTracer", 32, 0, frandom(-16, 16), 1)
			CPOS E 0 A_PlaySound("chainguy/attack",CHAN_AUTO)
			CPOS E 4 Bright A_CustomMissile("ChaingunnerTracer", 32, 0, frandom(-16, 16), 1)
			CPOS F 1 A_CPosRefire
			Goto Missile+1
			
		Pain:
			CPOS G 3
			CPOS G 3 A_Pain
			Goto See
			
		Pain.FireBF:
		Pain.AfireF:
		Pain.Afire:		
		Pain.FireRF:
		Pain.FireR:
			8CHN A 3
			8CHN A 3 A_Pain
			Goto See
			
		Death:
			CPOS H 5 
			CPOS I 5 A_Scream
			CPOS J 5 A_NoBlocking
			CPOS KLM 5
			CPOS N -1
			Stop
			
		Death.FireBF:
		Death.AfireF:
		Death.Afire:		
		Death.FireRF:
		Death.FireR:
			TNT1 A 0 A_Jump(128, 1)
			Goto Super::Death.FireBF
			8CHN A 5 BRIGHT A_Scream
			8CHN BC 5 BRIGHT
			8CHN DEFGH 4 BRIGHT
			8CHN I 4 BRIGHT A_NoBlocking
			8CHN J -1
			Stop
			
		XDeath:
			CPOS O 5 
			CPOS P 5 A_XScream
			CPOS Q 5 A_NoBlocking
			CPOS RS 5
			CPOS T -1
			Stop
			
		Raise:
			CPOS N 5
			CPOS MLKJIH 5
			Goto See
  }
}
ACTOR ZombieMan2 : AbstractYYTZombie {
  Health 20
  Radius 20
  PainChance 200
  SeeSound "grunt/sight"
  AttackSound "zombie/pistol"
  PainSound "grunt/pain"
  DeathSound "grunt/death"
  ActiveSound "grunt/active"
  Obituary "$OB_ZOMBIE"
  DropItem "Clip"
	DropItem "IsaacItemsTier1LowChanceSpawner"
	DropItem "CrusherGunToken", 1
	DropItem "HandGrenadeAmmo", 20
	
  States {
		SpawnLoop:
			POSS AB 10 A_Look
			Loop
			
		See:
			POSS AABBCCDD 4 A_Chase
			Loop
			
		Missile:
			POSS E 10 A_FaceTarget
			TNT1 A 0 A_PlaySound("zombie/pistol",CHAN_AUTO)
			POSS F 3 Bright A_CustomMissile("ZombieTracer", 32, 0, frandom(-16, 16), 1)
			POSS E 2 A_FaceTarget
			TNT1 A 0 A_PlaySound("zombie/pistol",CHAN_AUTO)
			POSS F 3 Bright A_CustomMissile("ZombieTracer", 32, 0, frandom(-16, 16), 1)
			POSS E 8
			Goto See
			
		Pain:
			POSS G 3
			POSS G 3 A_Pain
			Goto See
			
		Death:
			POSS H 5
			POSS I 5 A_Scream
			POSS J 5 A_NoBlocking
			POSS K 5
			POSS L -1
			Stop
			
		Death.FireBF:
		Death.AfireF:
		Death.Afire:		
		Death.FireRF:
		Death.FireR:
			TNT1 A 0 A_Jump(128, 1)
			Goto Super::Death.FireBF
			TNT1 A 0 A_SetUserVar("user_burning_loop", Random(3, 6))
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			8POS A 5 BRIGHT
			8POS B 5 BRIGHT
			TNT1 A 0 A_Pain
			TNT1 A 0 A_JumpIfInventory("LoopFlag", user_burning_loop, 1)
			Goto Death.FireR+2
			TNT1 A 0 A_Scream
			8POS CDEFGH 4 BRIGHT
			8POS I 4 BRIGHT A_NoBlocking
			8POS J -1
			Stop
			
		XDeath:
			POSS M 5
			POSS N 5 A_XScream
			POSS O 5 A_NoBlocking
			POSS PQRST 5
			POSS U -1
			Stop
			
		Raise:
			POSS K 5
			POSS JIH 5
			Goto See
	}
}
ACTOR DoomImp2 : YYTMonster {
  Health 60
  Radius 20
  Height 56
  Mass 100
  Speed 8
  PainChance 200
	Species "Imp"
  +FLOORCLIP
  SeeSound "imp/sight"
  PainSound "imp/pain"
  DeathSound "imp/death"
  ActiveSound "imp/active"
  HitObituary "$OB_IMPHIT"
  Obituary "$OB_IMP"
	DropItem "IsaacItemsTier1MidChanceSpawner"
	DropItem "FlamethrowerGunToken", 10
	
  States {
		SpawnLoop:
			TROO AB 10 A_Look
			Loop
			
		See:
			TNT1 A 0 A_Jump(128, "SeeFast")
			TROO AABBCCDD 3 A_Chase
			Loop
			
		SeeFast:
			TROO AABBCCDD 3 Fast A_Chase
			Loop
			
		Melee:
			TNT1 A 0 A_Jump(128, "MeleeFast")
			TROO EF 8 A_FaceTarget
			TROO G 6 A_CustomMeleeAttack(3 * random(1, 8), "imp/melee")
			Goto See
			
		MeleeFast:
			TROO EF 8 Fast A_FaceTarget
			TROO G 6 Fast A_CustomMeleeAttack(3 * random(1, 8), "imp/melee")
			Goto See
			
		Missile:
			TNT1 A -1 A_Jump(256, "Missile1", "Missile2")
			Wait
			
		Missile1:
			TNT1 A 0 A_Jump(128, "Missile1Fast")
			TROO EF 8 A_FaceTarget
			TROO G 6 A_CustomMissile("DoomImpBall", 32, 0, frandom(-10, 10))
			Goto See
			
		Missile1Fast:
			TROO EF 8 Fast A_FaceTarget
			TROO G 6 Fast A_CustomMissile("DoomImpBall", 32, 0, frandom(-10, 10))
			Goto See
			
		Missile2:
			1MP5 AB 8 A_FaceTarget
			1MP5 CC 6 A_CustomMissile("DoomImpBall", 32, 0, frandom(-10, 10))
			Goto See
			
		Pain:
			TROO H 2
			TROO H 2 A_Pain
			Goto See
			
		Death:
			TROO I 8
			TROO J 8 A_Scream
			TROO K 6
			TROO L 6 A_NoBlocking
			TROO M -1
			Stop
			
		Death.FireBF:
		Death.AfireF:
		Death.Afire:		
		Death.FireRF:
		Death.FireR:
			8IMP A 6 BRIGHT A_PlaySound("imp/pain", CHAN_AUTO)
			8IMP B 6 BRIGHT
			8IMP C 6 BRIGHT A_Scream
			8IMP DEFGHI 4 BRIGHT
			8IMP J 4 BRIGHT A_NoBlocking
			8IMP K -1
			Stop
			
		XDeath:
			TROO N 5
			TROO O 5 A_XScream
			TROO P 5
			TROO Q 5 A_NoBlocking
			TROO RST 5
			TROO U -1
			Stop
			
		Raise:
			TROO ML 8
			TROO KJI 6
			Goto See
  }
}

ACTOR Pinky2 : YYTMonster {
  Health 150
  PainChance 180
  Speed 10
  Radius 30
  Height 56
  Mass 400
  +FLOORCLIP
	Species "Demon"
  SeeSound "demon/sight"
  AttackSound "demon/melee"
  PainSound "demon/pain"
  DeathSound "demon/death"
  ActiveSound "demon/active"
  Obituary "$OB_DEMONHIT" // "%o was bit by a demon."
	DropItem "IsaacItemsTier1MidChanceSpawner"
	DropItem "ContraItemPinkySpawner"
	
  States {
		SpawnLoop:
			SARG AB 10 A_Look
			Loop
			
		See:
			SARG AABBCCDD 2 Fast A_Chase
			Loop
			
		Melee:
			SARG EF 8 Fast A_FaceTarget
			SARG G 8 Fast A_SargAttack
			Goto See
			
		Pain:
			SARG H 2 Fast
			SARG H 2 Fast A_Pain
			Goto See
			
		Death:
			SARG I 8
			SARG J 8 A_Scream
			SARG K 4
			SARG L 4 A_NoBlocking
			SARG M 4
			SARG N -1
			Stop
			
		Death.FireBF:
		Death.AfireF:
		Death.Afire:		
		Death.FireRF:
		Death.FireR:
			8PIK A 6 BRIGHT A_PlaySound("demon/pain", CHAN_AUTO)
			8PIK B 6 BRIGHT
			8PIK C 6 BRIGHT A_Scream
			8PIK DEFGH 4 BRIGHT
			8PIK I 4 BRIGHT A_NoBlocking
			8PIK J -1
			Stop
			
		Raise:
			SARG N 5
			SARG MLKJI 5
			Goto See
  }
}
ACTOR Spectre2 : Pinky2
{
  +SHADOW
  RenderStyle OptFuzzy
  Alpha 0.5
  SeeSound "spectre/sight"
  AttackSound "spectre/melee"
  PainSound "spectre/pain"
  DeathSound "spectre/death"
  ActiveSound "spectre/active"
  HitObituary "$OB_SPECTREHIT" // "%o was eaten by a spectre."
}
ACTOR Cacodemon2 : YYTMonster {
  Health 400
  Radius 31
  Height 56
  Mass 400
  Speed 8
  PainChance 128
	Species "Cacodemon"
	DamageFactor "BrimgoyleBeam", 0
  +FLOAT
  +NOGRAVITY
  SeeSound "caco/sight"
  PainSound "caco/pain"
  DeathSound "caco/death"
  ActiveSound "caco/active"
  Obituary "$OB_CACO"
  HitObituary "$OB_CACOHIT"
  BloodColor "00 00 CD"
	DropItem "IsaacItemsTier1HighChanceSpawner"
	DropItem "IsaacItemsTier2LowChanceSpawner"
	
  States {
		SpawnLoop:
			HEAD A 10 A_Look
			Loop
			
		See:
			HEAD A 3 A_Chase
			Loop
			
		Melee:
			HEAD BC 5 A_FaceTarget
			HEAD D 5 Bright A_CustomMeleeAttack(10 * random(1, 6))
			Goto See
			
		Missile:
			HEAD BC 5 A_FaceTarget
			HEAD D 5 Bright A_CustomMissile("CacodemonBall", 32, 0, frandom(-10, 10))
			Goto See
			
		Pain:
			HEAD E 3
			HEAD E 3 A_Pain
			HEAD F 6
			Goto See
			
		Death:
			HEAD G 8
			HEAD H 8 A_Scream
			HEAD IJ 8
			HEAD K 8 A_NoBlocking
			HEAD L -1 A_SetFloorClip
			Stop
			
		Death.FireBF:
		Death.AfireF:
		Death.Afire:
		Death.FireRF:
		Death.FireR:
			8CAC A 8 BRIGHT A_Pain
			8CAC B 5 BRIGHT
			8CAC C 5 BRIGHT A_Scream
			8CAC DEFGH 4 BRIGHT
			8CAC I 4 BRIGHT A_NoBlocking
			8CAC J -1 A_SetFloorClip
			Stop
			
		Raise:
			HEAD L 8 A_UnSetFloorClip
			HEAD KJIHG 8
			Goto See
		}
}
ACTOR Revenant2 : YYTMonster {
  Health 300
  Radius 20
  Height 60
  Mass 500
  Speed 10
  PainChance 100
  MeleeThreshold 196
	DamageFactor "Poison", 0
	DamageFactor "PPoison", 0
	Species "Revenant"
  +MISSILEMORE 
  +FLOORCLIP
  SeeSound "skeleton/sight"
  PainSound "skeleton/pain"
  DeathSound "skeleton/death"
  ActiveSound "skeleton/active"
  MeleeSound "skeleton/melee"
  HitObituary "$OB_UNDEADHIT" // "%o was punched by a revenant."
  Obituary "$OB_UNDEAD" // "%o couldn't evade a revenant's fireball."
  BloodColor "80 14 0A"
	DropItem "IsaacItemsTier1MidChanceSpawner"
	DropItem "IsaacItemsTier1LowChanceSpawner"
	DropItem "IsaacItemsTier2MidChanceSpawner"
	DropItem "HomingGunToken", 64
	
  States {
		SpawnLoop:
			SKEL AB 10 A_Look
			Loop
			
		See:
			SKEL AABBCCDDEEFF 2 A_Chase
			Loop
			
		Melee:
			SKEL G 0 A_FaceTarget
			SKEL G 6 A_SkelWhoosh
			SKEL H 6 A_FaceTarget
			SKEL I 6 A_SkelFist
			Goto See
			
		Missile:
			SKEL J 0 Bright A_FaceTarget
			SKEL J 10 Bright A_FaceTarget
			SKEL K 10 A_SkelMissile
			SKEL K 10 A_FaceTarget
			Goto See
			
		Pain:
			SKEL L 5
			SKEL L 5 A_Pain
			Goto See
			
		Death:
			SKEL LM 7
			SKEL N 7 A_Scream
			SKEL O 7 A_NoBlocking
			SKEL P 7
			SKEL Q -1
			Stop
			
		Death.FireBF:
		Death.AfireF:
		Death.Afire:		
		Death.FireRF:
		Death.FireR:
			8REV A 5 BRIGHT A_PlaySound("demon/pain", CHAN_AUTO)
			8REV B 5 BRIGHT
			8REV C 5 BRIGHT A_Scream
			8REV DEFGHI 4 BRIGHT
			8REV J 4 BRIGHT A_NoBlocking
			8REV K -1
			Stop
			
		Raise:
			SKEL Q 5
			SKEL PONML 5
			Goto See
  }
}
ACTOR Arachnotron2 : YYTMonster
{
  Health 500
  Radius 43
  Height 64
  Mass 600
  Speed 12
  PainChance 128
	Species "Arachnotron"
  +FLOORCLIP
  +BOSSDEATH
  SeeSound "baby/sight"
  PainSound "baby/pain"
  DeathSound "baby/death"
  ActiveSound "baby/active"
  Obituary "$OB_BABY"
	DropItem "IsaacItemsTier1MidChanceSpawner"
	DropItem "IsaacItemsTier1LowChanceSpawner"
	DropItem "IsaacItemsTier2MidChanceSpawner"
	DropItem "LaserGunToken", 64
  States {
		SpawnLoop:
			BSPI AB 10 A_Look
			Loop
			
		See:
			BSPI A 20
			TNT1 A 0 A_JumpIfInTargetLOS("EvadeChance", 45, JLOSF_DEADNOJUMP, 500)
			BSPI A 3 A_BabyMetal
			BSPI ABBCC 3 A_Chase
			BSPI D 3 A_BabyMetal
			BSPI DEEFF 3 A_Chase
			Goto See+1
			
		Missile:
			BSPI A 20 Bright A_FaceTarget
			BSPI G 4 Bright A_CustomMissile("ArachnotronPlasma", 28)
			BSPI H 4 Bright
			BSPI H 1 Bright A_SpidRefire
			Goto Missile+1
			
		Pain:
			BSPI I 3
			BSPI I 3 A_Pain
			Goto See+1
			
		Death:
			TNT1 A 0 A_Jump(128, "Eject")
			BSPI J 20 A_Scream
			BSPI K 7 A_NoBlocking
			BSPI LMNO 7
			BSPI P -1 A_BossDeath
			Stop
			
		Death.FireBF:
		Death.AfireF:
		Death.Afire:		
		Death.FireRF:
		Death.FireR:
			TNT1 A 0 A_Jump(128, "BurnEject")
			8SPI A 20 BRIGHT A_Scream
			8SPI B 7 BRIGHT A_NoBlocking
			8SPI CDEFGHI 4 BRIGHT 
			8SPI J 10 BRIGHT
			8SPI J -1 A_BossDeath
			Stop
			
		Eject:
			BSPI JJJJJJ 2 Bright A_CustomMissile("BigGreenExplosion", random(20,60), 0, random(0,360), 2, random(0,360))
			TNT1 AAAA 0 A_CustomMissile("SmokeBigExplosion", Random(40,60), 0, random(0,360), 2, random(0,360))
			TNT1 A 0 A_SpawnItemEx("Aracnorb", 0, 0, Height+5, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
			Goto Death+2
			
		BurnEject:
			BSPI JJJJJJ 2 Bright A_CustomMissile("BigRedExplosion", random(20,60), 0, random(0,360), 2, random(0,360))
			TNT1 AAAA 0 A_CustomMissile("SmokeBigExplosion", Random(40,60), 0, random(0,360), 2, random(0,360))
			TNT1 A 0 A_SpawnItemEx("Aracnorb", 0, 0, Height+5, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
			Goto Death.FireR+2
			
		Raise:
			BSPI P 5
			BSPI ONMLKJ 5
			Goto See+1
			
		EvadeChance:
			TNT1 A 0 A_JumpIf(CeilingZ <= 190, 2)
			TNT1 A 0 A_Jump(192, "EvadeLeft", "EvadeRight")
			TNT1 A 0
			Goto See+2
		
		EvadeLeft:
			SP1V A 2
			SP1V BB 3
			TNT1 A 0 A_ChangeFlag("NODROPOFF", TRUE)
			TNT1 A 0 ThrustThing(Angle*256/360+64, 14, 0, 0)
			TNT1 A 0 ThrustThingZ(0, 28, 0, 1)
			SP1V C 6
			SP2V A 3
			TNT1 A 0 A_ChangeFlag("NODROPOFF", FALSE)
			TNT1 A 0 A_Jump(100,"Missile")
			Goto See+1
		
		EvadeRight:
			SP2V A 2
			SP2V BB 3
			TNT1 A 0 A_ChangeFlag("NODROPOFF", TRUE)
			TNT1 A 0 ThrustThing(Angle*256/360+192, 14, 0, 0)
			TNT1 A 0 ThrustThingZ(0, 28, 0, 1)
			SP2V C 6
			SP1V A 3
			TNT1 A 0 A_ChangeFlag("NODROPOFF", FALSE)
			TNT1 A 0 A_Jump(100,"Missile")
			Goto See+1
  }
}
ACTOR Fatso2 : YYTMonster
{
  Health 600
  Radius 48
  Height 64
  Mass 1000
  Speed 8
  PainChance 80
	Species "Mancubus"
  +FLOORCLIP
  +BOSSDEATH
  SeeSound "fatso/sight"
  PainSound "fatso/pain"
  DeathSound "fatso/death"
  ActiveSound "fatso/active"
  Obituary "$OB_FATSO"
	DropItem "IsaacItemsTier1MidChanceSpawner"
	DropItem "IsaacItemsTier1LowChanceSpawner"
	DropItem "IsaacItemsTier1LowChanceSpawner"
	DropItem "IsaacItemsTier2MidChanceSpawner"
	DropItem "ContraItemMancubusSpawner"
	
  States {
		SpawnLoop:
			FATT AB 15 A_Look
			Loop
			
		See:
			FATT AABBCCDDEEFF 4 A_Chase
			Loop
			
		Missile:
			FATT G 20 A_FatRaise
			FATT H 10 Bright A_FatAttack1
			FATT IG 5 A_FaceTarget
			FATT H 10 Bright A_FatAttack2
			FATT IG 5 A_FaceTarget
			FATT H 10 Bright A_FatAttack3
			FATT IG 5 A_FaceTarget
			Goto See
			
		Pain:
			FATT J 3
			FATT J 3 A_Pain
			Goto See
			
		Death:
			FATT K 6
			FATT L 6 A_Scream
			FATT M 6 A_NoBlocking
			FATT NOPQRS 6
			FATT T -1 A_BossDeath
			Stop
			
		Death.FireBF:
		Death.AfireF:
		Death.Afire:		
		Death.FireRF:
		Death.FireR:
			TNT1 A 0 A_SetUserVar("user_burning_loop", Random(3, 6))
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			8FAT AB 5 BRIGHT
			TNT1 A 0 A_PlaySound("fatso/pain", CHAN_AUTO)
			8FAT CB 5 BRIGHT
			TNT1 A 0 A_PlaySound("fatso/pain", CHAN_AUTO)
			TNT1 A 0 A_JumpIfInventory("LoopFlag", user_burning_loop, 1)
			Goto Death.FireR+1
			TNT1 A 0 A_PlaySound("fatso/death", CHAN_AUTO)
			8FAT D 8 BRIGHT
			8FAT E 8 BRIGHT A_NoBlocking
			8FAT F 8 BRIGHT
			8FAT F -1
			Stop
			
		Raise:
			FATT R 5
			FATT QPONMLK 5
			Goto See
  }
}
ACTOR PainElemental2 : YYTMonster
{
  Health 400
  Radius 31
  Height 56
  Mass 400
  Speed 8
  PainChance 128
	Species "Elemental"
  +FLOAT
  +NOGRAVITY
  SeeSound "pain/sight"
  PainSound "pain/pain"
  DeathSound "pain/death"
  ActiveSound "pain/active"
	DropItem "IsaacItemsTier1MidChanceSpawner"
	DropItem "IsaacItemsTier2HighChanceSpawner"
  States
  {
  SpawnLoop:
    PAIN A 10 A_Look
    Loop
  See:
    PAIN AABBCC 3 A_Chase
    Loop
  Missile:
    PAIN D 5 A_FaceTarget
    PAIN E 5 A_FaceTarget
    PAIN F 5 Bright A_FaceTarget
    PAIN F 0 Bright A_PainAttack
    Goto See
  Pain:
    PAIN G 6
    PAIN G 6 A_Pain
    Goto See
  Death:
    PAIN H 8 Bright
    PAIN I 8 Bright A_Scream
    PAIN JK 8 Bright
    PAIN L 8 Bright A_PainDie
    PAIN M 8 Bright
    Stop
  Raise:
    PAIN MLKJIH 8
    Goto See
  }
}
ACTOR LostSoul2 : YYTMonster
{
  Health 100
  Radius 16
  Height 56
  Mass 50
  Speed 8
  Damage 3
  PainChance 256
  BloodType None
	Species "Soul"
  +FLOAT
  +NOGRAVITY
  +MISSILEMORE
  +DONTFALL
  +NOICEDEATH
  AttackSound "skull/melee"
  PainSound "skull/pain"
  DeathSound "skull/death"
  ActiveSound "skull/active"
  RenderStyle SoulTrans
  Obituary "$OB_SKULL" // "%o was spooked by a lost soul."
  States
  {
  SpawnLoop:
    SKUL AB 10 Bright A_Look
    Loop
  See:
    SKUL AB 6 Bright A_Chase
    Loop
  Missile:
    SKUL C 10 Bright A_FaceTarget
    SKUL D 4 Bright A_SkullAttack
    SKUL CD 4 Bright
    Goto Missile+2
  Pain:
    SKUL E 3 Bright
    SKUL E 3 Bright A_Pain
    Goto See
  Death:
    SKUL F 6 Bright
    SKUL G 6 Bright A_Scream
    SKUL H 6 Bright
    SKUL I 6 Bright A_NoBlocking
    SKUL J 6
    SKUL K 6
    Stop
  }
}
ACTOR BaronOfHell2 : YYTMonster
{
  Health 1000
  Radius 24
  Height 64
  Mass 1000
  Speed 8
	DamageFactor "BaronComet", 0
	DamageFactor "BaronFire", 0
  PainChance 50
	Species "Goat"
  +FLOORCLIP
  +BOSSDEATH
  SeeSound "baron/sight"
  PainSound "baron/pain"
  DeathSound "baron/death"
  ActiveSound "baron/active"
  Obituary "$OB_BARON"
  HitObituary "$OB_BARONHIT"
  BloodColor "00 64 00"
	DropItem "IsaacItemsTier1HighChanceSpawner"
	DropItem "IsaacItemsTier1MidChanceSpawner"
	DropItem "IsaacItemsTier2HighChanceSpawner"
	DropItem "IsaacItemsTier2MidChanceSpawner"
	DropItem "CrusherGunToken", 64
	
  States {
		SpawnLoop:
			BOSS AB 10 A_Look
			Loop
			
		See:
			BOSS AABBCCDD 3 A_Chase
			Loop
			
		Melee:
			BOSS EF 8 A_FaceTarget
			BOSS G 8 A_CustomMeleeAttack(10 * random(1, 8), "baron/melee")
			Goto See
			
		Missile:
			BOSS EF 8 A_FaceTarget
			BOSS G 8 A_CustomMissile("BaronBall", 32, 0, frandom(-10, 10))
			Goto See
			
		Pain:
			BOSS H 2
			BOSS H 2 A_Pain
			BOSS H -1 A_Jump(256, "See")
			Wait
			
		Death:
			BOSS I 8
			BOSS J 8 A_Scream
			BOSS K 8
			BOSS L 8 A_NoBlocking
			BOSS MN 8
			BOSS O -1 A_BossDeath
			Stop
			
		Death.FireBF:
		Death.AfireF:
		Death.Afire:		
		Death.FireRF:
		Death.FireR:
			TNT1 A 0 A_SetUserVar("user_burning_loop", Random(3, 6))
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			8BAR ABC 4 BRIGHT
			TNT1 A 0 A_PlaySound("baron/pain", CHAN_AUTO)
			8BAR DEF 4 BRIGHT
			TNT1 A 0 A_PlaySound("baron/pain", CHAN_AUTO)
			TNT1 A 0 A_JumpIfInventory("LoopFlag", user_burning_loop, 1)
			Goto Death.FireR+1
			TNT1 A 0 A_PlaySound("baron/death", CHAN_AUTO)
			8BAR GHIJK 5 BRIGHT
			8BAR L 5 BRIGHT A_NoBlocking
			8BAR MN 5 BRIGHT
			8BAR O -1
			Stop
		
		Raise:
			BOSS O 8
			BOSS NMLKJI 8
			Goto See
  }
}
ACTOR HellKnight2 : BaronOfHell2 {
  Health 500
  BloodColor "00 64 00"
  Species "Goat"
  -BOSSDEATH
  SeeSound "knight/sight"
  ActiveSound "knight/active"
  PainSound "knight/pain"
  DeathSound "knight/death"
  HitObituary "$OB_KNIGHTHIT"
  Obituary "$OB_KNIGHT"
	DropItem "IsaacItemsTier1HighChanceSpawner"
	DropItem "IsaacItemsTier1MidChanceSpawner"
	DropItem "IsaacItemsTier2LowChanceSpawner"
	DropItem "CrusherGunToken", 24
	
  States {
		SpawnLoop:
			BOS2 AB 10 A_Look
			Loop
			
		See:
			BOS2 AABBCCDD 3 A_Chase
			Loop
			
		Melee:
			BOS2 EF 8 A_FaceTarget
			BOS2 G 8 A_CustomMeleeAttack(10 * random(1, 8), "baron/melee")
			Goto See
			
		Missile:
			BOS2 EF 8 A_FaceTarget
			BOS2 G 8 A_CustomMissile("BaronBall", 32, 0, frandom(-10, 10))
			Goto See
			
		Pain:
			BOS2 H 2
			BOS2 H 2 A_Pain
			Goto See
			
		Death:
			BOS2 I 8
			BOS2 J 8 A_Scream
			BOS2 K 8
			BOS2 L 8 A_NoBlocking
			BOS2 MN 8
			BOS2 O -1
			Stop
			
		Death.FireBF:
		Death.AfireF:
		Death.Afire:		
		Death.FireRF:
		Death.FireR:
			TNT1 A 0 A_SetUserVar("user_burning_loop", Random(3, 6))
			TNT1 A 0 A_GiveInventory("LoopFlag", 1)
			8BAR ABC 4 BRIGHT
			TNT1 A 0 A_PlaySound("knight/pain", CHAN_AUTO)
			8BAR DEF 4 BRIGHT
			TNT1 A 0 A_PlaySound("knight/pain", CHAN_AUTO)
			TNT1 A 0 A_JumpIfInventory("LoopFlag", user_burning_loop, 1)
			Goto Death.FireR+1
			TNT1 A 0 A_PlaySound("knight/death", CHAN_AUTO)
			8BAR GHIJK 5 BRIGHT
			8BAR L 5 BRIGHT A_NoBlocking
			8BAR MN 5 BRIGHT
			8BAR O -1
			Stop
		
		Raise:
			BOS2 O 8
			BOS2 NMLKJI 8
			Goto See
  }
}
ACTOR Archvile2 : YYTMonster {
  DamageFactor "AFire",0
  DamageFactor "AFireF",0
  Health 700
  Radius 20
  Height 56
  Mass 500
  Speed 15
  PainChance 10
  MaxTargetRange 896
	Species "Archvile"
  +QUICKTORETALIATE
  +FLOORCLIP
  +NOTARGET
  SeeSound "vile/sight"
  PainSound "vile/pain"
  DeathSound "vile/death"
  ActiveSound "vile/active"
  MeleeSound "vile/stop"
  Obituary "$OB_VILE"
	DropItem "IsaacItemsTier1HighChanceSpawner"
	DropItem "IsaacItemsTier2MidChanceSpawner"
	DropItem "IsaacItemsTier3LowChanceSpawner"
	
  States {
		SpawnLoop:
			VILE AB 10 A_Look
			Loop
			
		See:
			VILE AABBCCDDEEFF 2 A_VileChase
			Loop
			
		Missile:
			VILE G 0 Bright A_VileStart
			VILE G 10 Bright A_FaceTarget
			VILE H 8 Bright A_VileTarget
			VILE IJKLMN 8 Bright A_FaceTarget
			VILE O 8 Bright A_VileAttack("vile/stop",70,20,70,1.0)
			VILE P 20 Bright
			Goto See
			
		Heal:
			VILE "[\]" 10 Bright
			Goto See
			
		Pain:
			VILE Q 5   
			VILE Q 5 A_Pain
			Goto See
			
		Pain.Lightning:
			TNT1 A 0 A_SpawnItemEx("ElectricSpark1",random(-10,10),random(-15,-5),random(0,Height*0.5),0,0,0,0)
			TNT1 A 0 A_SpawnItemEx("ElectricSpark2",random(-10,10),random(5,15),random(0,Height*0.5),0,0,0,0)
			Goto Pain
			
		Death:
			VILE Q 7
			VILE R 7 A_Scream
			VILE S 7 A_NoBlocking
			VILE TUVWXY 7
			VILE Z -1
			Stop
			
		Death.FireBF:
		Death.AfireF:
		Death.Afire:
		Death.FireRF:
		Death.FireR:
			8ARC A 7 BRIGHT A_Scream
			8ARC B 7 BRIGHT A_NoBlocking
			8ARC CDEFGHIJKLMNOP 4 BRIGHT
			8ARC Q -1
			Stop
  }
}
ACTOR Cyberdemon2 : YYTMonster
{
  Health 4000
  Radius 40
  Height 110
  Mass 1000
  Speed 16
  PainChance 20
  MinMissileChance 160
  DamageFactor "AfireF", 0.9
	DamageFactor "Bullet", 0.5
	PainChance "AfireF", 0
	PainChance "Afire", 0
	PainChance "FireR", 0
	PainChance "FireRF", 0
	PainChance "FireBF", 0
	Species "Cyberdemon"
  +BOSS
  +MISSILEMORE
  +FLOORCLIP
  +NORADIUSDMG
  +DONTMORPH
  +BOSSDEATH
  SeeSound "cyber/sight"
  PainSound "cyber/pain"
  DeathSound "cyber/death"
  ActiveSound "cyber/active"
  Obituary "$OB_CYBORG"
	DropItem "IsaacItemsTier1Spawner"
	DropItem "IsaacItemsTier1LowChanceSpawner"
	DropItem "IsaacItemsTier1LowChanceSpawner"
	DropItem "IsaacItemsTier2HighChanceSpawner"
	DropItem "IsaacItemsTier2LowChanceSpawner"
	DropItem "IsaacItemsTier3HighChanceSpawner"
	DropItem "PrototypeGunToken", 128
  States
  {
  SpawnLoop:
    CYBR AB 10 A_Look
    Loop
  See:
    CYBR A 3 A_Hoof
    CYBR ABBCC 3 A_Chase
    CYBR D 3 A_Metal
    CYBR D 3 A_Chase
    Loop
  Missile:
    CYBR E 6 A_FaceTarget
    CYBR F 12 BRIGHT A_CustomMissile("Rocket", 43, -25, frandom(-10, 10))
    CYBR E 12 A_FaceTarget
    CYBR F 12 BRIGHT A_CustomMissile("Rocket", 43, -25, frandom(-10, 10))
    CYBR E 12 A_FaceTarget
    CYBR F 12 BRIGHT A_CustomMissile("Rocket", 43, -25, frandom(-10, 10))
		Goto See
  Pain:
    CYBR G 10 A_Pain
    Goto See
  Pain.Lightning:
		TNT1 A 0 A_SpawnItemEx("ElectricSpark1",random(-10,10),random(-15,-5),random(0,Height*0.25),0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("ElectricSpark1",random(-10,10),random(-15,-5),random(Height*0.35,Height*0.6),0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("ElectricSpark2",random(-10,10),random(5,15),random(0,Height*0.25),0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("ElectricSpark2",random(-10,10),random(5,15),random(Height*0.35,Height*0.6),0,0,0,0)
		TNT1 A 0 A_Jump(256, "Pain")
		Wait
  Death:
    CYBR H 10
    CYBR I 10 A_Scream
    CYBR JKL 10
    CYBR M 10 A_NoBlocking
    CYBR NO 10
    CYBR P 30
    CYBR P -1 A_BossDeath
    Stop
  }
}
ACTOR SpiderMastermind2 : YYTMonster {
  Health 3000
  Radius 128
  Height 100
  Mass 1000
  Speed 12
  PainChance 40
  MinMissileChance 160
  DamageFactor "AFire", 0.01
	DamageFactor "AFireF", 0.9
	DamageFactor "Ripper", 0.25
	DamageFactor "Bullet", 0.5
	PainChance "Afire", 0
	PainChance "AfireF", 0
	PainChance "FireR", 0
	PainChance "FireRF", 0
	PainChance "FireBF", 0
	PainChance "Ripper", 0
	Species "Spider"
  +BOSS
  +MISSILEMORE
  +FLOORCLIP
  +NORADIUSDMG
  +DONTMORPH
  +BOSSDEATH
  SeeSound "spider/sight"
  AttackSound "spider/attack"
  PainSound "spider/pain"
  DeathSound "spider/death"
  ActiveSound "spider/active"
  Obituary "$OB_SPIDER"
	DropItem "IsaacItemsTier1Spawner"
	DropItem "IsaacItemsTier1LowChanceSpawner"
	DropItem "IsaacItemsTier1LowChanceSpawner"
	DropItem "IsaacItemsTier2HighChanceSpawner"
	DropItem "IsaacItemsTier2LowChanceSpawner"
	DropItem "IsaacItemsTier3HighChanceSpawner"
	DropItem "PrototypeGunToken", 128
	
  States {
		SpawnLoop:
			SPID AB 10 A_Look
			Loop
			
		See:
			SPID A 3 A_Metal
			SPID ABB 3 A_Chase
			SPID C 3 A_Metal
			SPID CDD 3 A_Chase
			SPID E 3 A_Metal
			SPID EFF 3 A_Chase
			Loop
			
		Missile:
			SPID A 20 Bright A_FaceTarget
			TNT1 A 0 A_PlaySound("spider/attack", CHAN_AUTO)
			SPID G 4 Bright A_CustomMissile("ShotgunnerTracer", 48, 0, frandom(-16, 16), 1)
			TNT1 A 0 Bright A_CustomMissile("ShotgunnerTracer", 48, 0, frandom(-16, 16), 1)
			TNT1 A 0 Bright A_CustomMissile("ShotgunnerTracer", 48, 0, frandom(-16, 16), 1)
			TNT1 A 0 A_PlaySound("spider/attack", CHAN_AUTO)
			SPID H 4 Bright A_CustomMissile("ShotgunnerTracer", 48, 0, frandom(-16, 16), 1)
			TNT1 A 0 Bright A_CustomMissile("ShotgunnerTracer", 48, 0, frandom(-16, 16), 1)
			TNT1 A 0 Bright A_CustomMissile("ShotgunnerTracer", 48, 0, frandom(-16, 16), 1)
			SPID H 1 Bright A_SpidRefire
			Goto Missile+1
			
		Pain:
			SPID I 3
			SPID I 3 A_Pain
			Goto See
			
		Pain.Lightning:
			TNT1 A 0 A_SpawnItemEx("ElectricSpark1",random(-10,10),random(-15,-5),random(0,Height*0.25),0,0,0,0)
			TNT1 A 0 A_SpawnItemEx("ElectricSpark1",random(-10,10),random(-15,-5),random(Height*0.35,Height*0.6),0,0,0,0)
			TNT1 A 0 A_SpawnItemEx("ElectricSpark2",random(-10,10),random(5,15),random(0,Height*0.25),0,0,0,0)
			TNT1 A 0 A_SpawnItemEx("ElectricSpark2",random(-10,10),random(5,15),random(Height*0.35,Height*0.6),0,0,0,0)
			Goto Pain
			
		Death:
			SPID J 20 A_Scream
			SPID K 10 A_NoBlocking
			SPID LMNOPQR 10
			SPID S 30
			SPID S -1 A_BossDeath
			Stop
			
		Death.FireBF:
		Death.AfireF:
		Death.Afire:		
		Death.FireRF:
		Death.FireR:
			TNT1 A 0 A_SetScale(2.0)
			8SPI A 20 BRIGHT A_Scream
			8SPI B 10 BRIGHT A_NoBlocking
			8SPI CDEFGHI 10 BRIGHT 
			8SPI J 30 BRIGHT
			8SPI J -1 A_BossDeath
			Stop
  }
}