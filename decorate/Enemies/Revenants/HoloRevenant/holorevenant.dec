ACTOR HoloRevenant
{
  Health 300
  Radius 20
  Height 56
  Mass 500
  Speed 10
  PainChance 100
  Monster
  MeleeThreshold 196
  +MISSILEMORE 
  +FLOORCLIP
  SeeSound "skeleton/sight"
  PainSound "skeleton/pain"
  DeathSound "skeleton/death"
  ActiveSound "skeleton/active"
  MeleeSound "skeleton/melee"
  HitObituary "%o was punched by a revenant."
  Obituary "%o caught some fists."
  States
  {
  Spawn:
    SKEL AB 10 A_Look
    Loop
  See:
    SKEL AABBCCDDEEFF 2 A_Chase
    Loop
  Melee:
    SKEL G 0 A_FaceTarget
    SKEL G 6 A_SkelWhoosh
    SKEL H 6 A_FaceTarget
    SKEL I 6 A_SkelFist
    Goto See
  Missile:
    SKEL G 0 Bright A_FaceTarget
    SKEL G 10 Bright A_FaceTarget
    SKEL H 10 A_CustomMissile("RevenantPunch")
    SKEL I 10 A_FaceTarget
    Goto See
  Pain:
    SKEL L 5
    SKEL L 5 A_Pain
    Goto See
	Pain.Lightning:
		TNT1 A 0 A_SpawnItemEx("ElectricSpark1",random(-10,10),random(-15,-5),random(0,Height*0.5),0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("ElectricSpark2",random(-10,10),random(5,15),random(0,Height*0.5),0,0,0,0)
    Goto Pain
  Death:
    SKEL LM 7
    SKEL N 7 A_Scream
    SKEL O 7 A_NoBlocking
    SKEL P 7
    SKEL Q -1
    Stop
  Raise:
    SKEL Q 5
    SKEL PONML 5
    Goto See
  }
}
ACTOR RevenantPunch : FastProjectile
{
	Radius 11
	Height 8
	Speed 75
	Damage 10
	Projectile
	RenderStyle Translucent
	Alpha 0.7
	States
	{
	Spawn:
		SKEL H 0
		SKEL H 3 A_SpawnItemEx("RevenantPunchTrail")
		Loop
	Death:
		SKEL I 0 A_RadiusThrust(10000,1)
		SKEL I 4 A_PlaySound("skeleton/melee")
		Stop
	}
}
ACTOR RevenantPunchTrail
{
	RenderStyle Translucent
	Alpha 0.5
	+NOINTERACTION
	States
	{
	Spawn:
		SKEL H 0
		SKEL H 1 A_FadeOut(0.02)
		Loop
	}
}