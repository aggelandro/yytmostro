ACTOR Skeleton : Revenant2 {
	Speed 5
	Health 250
	GibHealth 20
	SeeSound "skel/see"
  PainSound "skel/hit"
  DeathSound "skel/die"
  ActiveSound "skel/see"
  Obituary "It was too spooky for %o" 
  HitObituary "%o didn't expect to be punched by a skeleton." 
	DropItem "IsaacItemsTier1HighChanceSpawner"
	DropItem "IsaacItemsTier1MidChanceSpawner"
	DropItem "IsaacItemsTier2LowChanceSpawner"
	DropItem "DemonAmmo", 32
	DropItem "SpreadGunToken", 48
	+ALWAYSFAST
	-MISSILEMORE
	
	States {
		SpawnLoop:
			DRYB AB 10 A_Look
			Loop
			
		See:
			TNT1 A 0 A_ChangeFlag("FRIGHTENED", FALSE)
			TNT1 A 0 A_JumpIfCloser(256, "Retreat")
			DRYB AABBCCDDEEFF 2 A_Chase
			Goto See+1
			
		Retreat:
			TNT1 A 0 A_ChangeFlag("FRIGHTENED", TRUE)
			TNT1 A 0 A_JumpIfCloser(1536, 1)
			Goto See+1
			DRYB AABBCCDDEEFF 1 FAST A_Chase("Melee", "Missile", CHF_NIGHTMAREFAST)
			Goto Retreat+1
			
		Melee:
			DRYB G 6 A_FaceTarget
			DRYB H 6 A_FaceTarget
			DRYB I 6 A_CustomMeleeAttack(6 * random(1, 5), "skeleton/melee")
			TNT1 A 0 A_CheckFlag("FRIGHTENED", "Retreat")
			Goto See+1
			
		Missile:
			TNT1 A 0 A_JumpIfCloser(1536, "YesMissile")
			TNT1 A 0 A_CheckFlag("FRIGHTENED", "Retreat")
			Goto See+1
			
		YesMissile:
			DRYB G 6 A_FaceTarget
			Goto MissileLong
			
		MissileLow1:
			DRYB H 6 A_CustomMissile("BoneProj", 48, 0, 0, CMF_OFFSETPITCH, 4)
			Goto MissileEnd
			
		MissileLow2:
			DRYB H 6 A_CustomMissile("BoneProj", 48, 0, 0, CMF_OFFSETPITCH, 8)
			Goto MissileEnd
			
		MissileMid1:
			DRYB H 6 A_CustomMissile("BoneProj", 48, 0, 0, CMF_OFFSETPITCH, 16)
			Goto MissileEnd
			
		MissileMid2:
			DRYB H 6 A_CustomMissile("BoneProj", 48, 0, 0, CMF_OFFSETPITCH, 24)
			Goto MissileEnd
			
		MissileLong:
			TNT1 A 0 A_JumpIfCloser(128, "MissileLow1")
			TNT1 A 0 A_JumpIfCloser(256, "MissileLow2")
			TNT1 A 0 A_JumpIfCloser(512, "MissileMid1")
			TNT1 A 0 A_JumpIfCloser(1024, "MissileMid2")
			DRYB H 6 A_CustomMissile("BoneProj", 48, 0, 0, CMF_OFFSETPITCH, 45)
			Goto MissileEnd
			
		MissileEnd:
			DRYB I 6 A_FaceTarget
			TNT1 A 0 A_CheckFlag("FRIGHTENED", "Retreat")
			Goto See+1
			
		Pain:
			DRYB L 5
			DRYB L 5 A_Pain
			TNT1 A 0 A_CheckFlag("FRIGHTENED", "Retreat")
			Goto See+1
			
		Death:
			DRYB LM 7
			DRYB N 7 A_Scream
			DRYB O 7 A_NoBlocking
			DRYB P 7
			DRYB Q -1
			Stop
			
		Raise:
			DRYB Q 5
			DRYB PONML 5
			TNT1 A 0 A_CheckFlag("FRIGHTENED", "Retreat")
			Goto See+1
	}
}

ACTOR BoneProj {
	Radius 6
	Height 8
	Speed 20
	Health 1
	Damage 5
	Gravity 0.7
	Projectile
	RenderStyle Normal
	Alpha 1
	SeeSound "skel/throw"
	DeathSound "skel/bondie"
	var int user_homing_flag;
	-NOGRAVITY
	+RANDOMIZE
	-NOBLOCKMAP
	+SOLID
	+SHOOTABLE
	+NOTARGETSWITCH
	+NOBLOOD
	
	States {
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("homingAtNegativeZVelocity")
			TNT1 A 0 ACS_NamedExecuteWithResult("YYT_projectileCheck")
			Goto SpawnLoop
	
		SpawnLoop:
			TBAB ABCD 3 Bright
			TNT1 A 0 A_JumpIf(user_homing_flag, "Seek")
			Loop
			
		Seek:
			TNT1 A 0 A_ChangeFlag("SEEKERMISSILE", TRUE)
			TNT1 A 0 A_ChangeFlag("SCREENSEEKER", TRUE)
			Goto HomingLoop
			
		HomingLoop:
			TBAB ABCD 3 Bright A_SeekerMissile(3, 5, SMF_LOOK|SMF_CURSPEED, 256, 3)
			Loop
			
		Death:
			TNT1 A 0 A_NoBlocking
			Stop
  }
}