ACTOR Skeleton : Revenant2 {
	Speed 5
	Health 250
	GibHealth 20
	SeeSound "skel/see"
  PainSound "skel/hit"
  DeathSound "skel/die"
  ActiveSound "skel/see"
  Obituary "It was too spooky for %o" 
  HitObituary "%o didn't expect to be punched by a skeleton." 
	DropItem "IsaacItemsTier1HighChanceSpawner"
	DropItem "IsaacItemsTier1MidChanceSpawner"
	DropItem "IsaacItemsTier2LowChanceSpawner"
	
	+ALWAYSFAST
  +NOBLOOD
	-MISSILEMORE
	
	States {
		
		SpawnLoop:
			DRYB AB 10 A_Look
			Loop
			
		See:
			TNT1 A 0 A_ChangeFlag("FRIGHTENED", FALSE)
			TNT1 A 0 A_JumpIfCloser(256, "Retreat")
			DRYB AABBCCDDEEFF 2 A_Chase
			Goto See+1
			
		Retreat:
			TNT1 A 0 A_ChangeFlag("FRIGHTENED", TRUE)
			TNT1 A 0 A_CheckRange(1536, "See")
			DRYB AABBCCDDEEFF 1 FAST A_Chase("Melee", "Missile", CHF_NIGHTMAREFAST)
			Goto Retreat+1
			
		Melee:
			DRYB G 0 A_FaceTarget
			DRYB G 6 A_FaceTarget
			DRYB H 6 A_FaceTarget
			DRYB I 6 A_CustomMeleeAttack(6 * random(1, 5), "skeleton/melee")
			TNT1 A 0 A_CheckFlag("FRIGHTENED", "Retreat")
			Goto See+1
			
		Missile:
			TNT1 A 0 A_JumpIfCloser(1536, "YesMissile")
			TNT1 A 0 A_CheckFlag("FRIGHTENED", "Retreat")
			Goto See+1
			
		YesMissile:
			DRYB G 0 A_FaceTarget
			DRYB G 6 A_FaceTarget
			TNT1 A 0 A_CheckRange(128, "MissileLow2")
			Goto MissileLow1
			
		MissileLow1:
			DRYB H 6 A_CustomMissile("BoneProj", 48, 0, 0, CMF_OFFSETPITCH, 4)
			DRYB I 6 A_FaceTarget
			TNT1 A 0 A_CheckFlag("FRIGHTENED", "Retreat")
			Goto See+1
			
		MissileLow2:
			TNT1 A 0 A_CheckRange(256, "MissileMid1")
			DRYB H 6 A_CustomMissile("BoneProj", 48, 0, 0, CMF_OFFSETPITCH, 8)
			DRYB I 6 A_FaceTarget
			TNT1 A 0 A_CheckFlag("FRIGHTENED", "Retreat")
			Goto See+1
			
		MissileMid1:
			TNT1 A 0 A_CheckRange(512, "MissileMid2")
			DRYB H 6 A_CustomMissile("BoneProj", 48, 0, 0, CMF_OFFSETPITCH, 16)
			DRYB I 6 A_FaceTarget
			TNT1 A 0 A_CheckFlag("FRIGHTENED", "Retreat")
			Goto See+1
			
		MissileMid2:
			TNT1 A 0 A_CheckRange(1024, "MissileLong")
			DRYB H 6 A_CustomMissile("BoneProj", 48, 0, 0, CMF_OFFSETPITCH, 24)
			DRYB I 6 A_FaceTarget
			TNT1 A 0 A_CheckFlag("FRIGHTENED", "Retreat")
			Goto See+1
			
		MissileLong:
			DRYB H 6 A_CustomMissile("BoneProj", 48, 0, 0, CMF_OFFSETPITCH, 45)
			DRYB I 6 A_FaceTarget
			TNT1 A 0 A_CheckFlag("FRIGHTENED", "Retreat")
			Goto See+1
			
		Pain:
			DRYB L 5
			DRYB L 5 A_Pain
			TNT1 A 0 A_CheckFlag("FRIGHTENED", "Retreat")
			Goto See+1
			
		Death:
			DRYB LM 7
			DRYB N 7 A_Scream
			DRYB O 7 A_NoBlocking
			DRYB P 7
			DRYB Q -1
			Stop
			
		Raise:
			DRYB Q 5
			DRYB PONML 5
			TNT1 A 0 A_CheckFlag("FRIGHTENED", "Retreat")
			Goto See+1
	}
}

ACTOR BoneProj {
	Radius 6
	Height 8
	Speed 20
	Health 1
	Damage 5
	Gravity 0.7
	Projectile
	RenderStyle Normal
	Alpha 1
	SeeSound "skel/throw"
	DeathSound "skel/bondie"
	var int user_homing_flag;
	
	-NOGRAVITY
	+RANDOMIZE
	-NOBLOCKMAP
	+SOLID
	+SHOOTABLE
	+NOTARGETSWITCH
	+NOBLOOD
	
	States {
	
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("homingAtNegativeZVelocity")
			Goto SpawnLoop
	
		SpawnLoop:
			TBAB ABCD 3 bright A_SeekerMissile(7, 10, SMF_LOOK|SMF_CURSPEED, 256, 3)
			TNT1 A 0 A_JumpIf(user_homing_flag == 0, 3)
			TNT1 A 0 A_ChangeFlag("SEEKERMISSILE", TRUE)
			TNT1 A 0 A_ChangeFlag("SCREENSEEKER", TRUE)
			TNT1 A 0
			Loop
			
		Death:
			TNT1 A 0 A_NoBlocking
			Stop
  }
}