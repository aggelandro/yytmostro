ACTOR SpiderLord : SpiderMastermind2
{
	Health 5000
	RenderStyle Stencil
	Species Spider
	StencilColor Black
	+THRUSPECIES
	Painchance 20
	Obituary "%o couldn't challenge the spider lord."
	DropItem "UniqueWeaponSpawner"
	DropItem "IsaacItemsTier1Spawner"
	DropItem "IsaacItemsTier1LowChanceSpawner"
	DropItem "IsaacItemsTier1MidChanceSpawner"
	DropItem "IsaacItemsTier2HighChanceSpawner"
	DropItem "IsaacItemsTier2MidChanceSpawner"
	DropItem "IsaacItemsTier3HighChanceSpawner"
	DropItem "IsaacItemsTier3LowChanceSpawner"
	DropItem "PrototypeGunToken"
	DropItem "DemonAmmoBox", 128
	var int user_aranas;
	States
	{
	Missile:
		SPID A 20 Bright A_FaceTarget
		SPID G 6 Bright A_SpawnItemEx("SpiderBall",0,0,45,10,0,0,0)
		SPID G 0 Bright A_PlaySound("baron/attack")
		SPID H 6 Bright // A_SpawnItemEx("SpiderBall",0,0,50,10,0,0,0)
		SPID H 1 Bright A_SpidRefire
		Goto Missile+1
	Death:
		SPID J 20 A_Scream
		SPID K 10 A_NoBlocking
		SPID LMNO 10
		Goto Spiders
	Spiders:
		SPID S 0 A_SpawnItemEx("MiniArachnotron", random(-64,64),0,0,random(1,10),random(1,10),random(1,10),random(0,360))
		SPID S 0 A_SetUserVar("user_aranas", user_aranas+1)
		SPID S 0 A_JumpIf(user_aranas<50,"Spiders")
		SPID PQR 10
		SPID S 30
		SPID S -1 A_BossDeath
		Stop
	}	
}
ACTOR SpiderBall
{
	Species Spider
	RenderStyle Stencil
	StencilColor Black
	Health 10
	Radius 15
	Height 15
	Speed 15
	Scale 1.5
	Painchance 0
	Obituary "%o couldn't challenge the spider lord."
	Monster
	+FLOAT
	+NOGRAVITY
	+DONTFALL
	+THRUSPECIES
	+NOBLOOD
	+MISSILEMORE
	-COUNTKILL
	States
	{
	Spawn:
		BAL1 A 4 A_Look
		Loop
	See:
		BAL1 A 5 A_Chase
		Loop
	Missile:
		BAL1 A 1 A_CustomMissile("SpiderBallAttack",0)
		Stop
	Melee:
		BAL1 A 1 A_CustomMeleeAttack(20)
		MISL B 5
		MISL C 5
		MISL CCC 0 A_SpawnItemEx("MiniArachnotron", 0,0,0,random(1,5),1,random(1,5),random(0,360))
		MISL D 5
		Stop
	Death:
		MISL B 5 
		MISL C 5
		// MISL CCCC 0 A_SpawnItemEx("MiniArachnotron", 0,0,0,random(1,5),1,random(1,5),random(0,360))
		MISL D 5
		Stop
	}
}
ACTOR SpiderBallAttack
{
	Radius 12
	Height 8
	Damage 5
	Speed 15
	Scale 1.5
	Species Spider
	Projectile
	-RANDOMIZE
	+MTHRUSPECIES
	RenderStyle Stencil
	StencilColor Black
	Obituary "%o couldn't challenge the spider lord."
	SeeSound "imp/attackx"
	DeathSound "imp/shotx"
	States
	{
	Spawn:
		BAL1 A 4
		Loop
	Death:
		MISL B 5 
		MISL C 5
		MISL CCC 0 A_SpawnItemEx("MiniArachnotron", 0,0,0,random(1,5),1,random(1,5),random(0,360))
		MISL D 5
		Stop
	}
}