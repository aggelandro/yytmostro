ACTOR Aliencubus : Fatso2 {
	Health 800
	Obituary "%o was abducted by an Aliencubus."
	+QUICKTORETALIATE
	
	States {
		SpawnLoop:
			FATX AB 15 A_Look
			Loop
			
		See:
			FATX AABBCCDDEEFF 4 A_Chase
			Loop
			
		Missile:
			FATX G Random(15, 20) A_FatRaise
			FATX H Random(4, 10) Bright A_CustomMissile("AlienFireBall", 20, -25, Random(-2, 2))
			TNT1 A 0 A_CustomMissile("AlienFireBall", 20, 25, Random(-2, 2))
			FATX IG Random(3, 5) A_FaceTarget
			FATX H Random(4, 10) Bright A_CustomMissile("AlienFireBall", 20, -25, Random(4, 8))
			TNT1 A 0 A_CustomMissile("AlienFireBall", 20, 25, Random(4, 8))
			FATX IG Random(3, 5) A_FaceTarget
			FATX H Random(4, 10) Bright A_CustomMissile("AlienFireBall", 20, -25, Random(-8, -4))
			TNT1 A 0 A_CustomMissile("AlienFireBall", 20, 25, Random(-8, -4))
			FATX IG Random(3, 5) A_FaceTarget
			TNT1 A 0 A_ChangeFlag("NOPAIN", FALSE)
			Goto See
			
		Pain:
			FATX J 3
			FATX J 3 A_Pain
			TNT1 A 0 A_Jump(32, "PainMissile")
			Goto See
			
		PainMissile:
			TNT1 A 0 A_ChangeFlag("NOPAIN", TRUE)
			Goto Missile
			
		Death.FireBF:
		Death.AfireF:
		Death.Afire:		
		Death.FireRF:
		Death.FireR:
		Death:
			FATX K 6
			FATX L 6 A_Scream
			FATX M 6 A_NoBlocking
			FATX NOPQRS 6
			FATX T -1 A_BossDeath
			Stop
			
		Raise:
			FATX R 5
			FATX QPONMLK 5
			Goto See
  }
}

ACTOR AlienFireBall {
  Speed 20
  FastSpeed 26
  Damage 10
  Radius 13
  Height 25
  Projectile
  +Randomize
  RenderStyle Add
	DamageType "FireR"
  SeeSound "imp/attack"
  DeathSound "commander/grenadefire"
  Decal Scorch
	
  States {
		Spawn: 
			BRBA AB 2 Bright A_SpawnItemEx("GFBallTrail", 0, 0, 0, 0, 0, 0, 0, 128)
			Loop
			
		Death:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("OrangeParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
			TNT1 AAA 0 A_CustomMissile("NormalRedExplosion", 0, 0, random(0,360), 2, random(0,360))
			TNT1 AAA 0 A_CustomMissile("SmokeBigExplosion", 0, 0, random(0,360), 2, random(0,360))
			TNT1 A 0 A_Explode(10, 32)
			BRBA KLMNOPQRSTUVWX 3 Bright
			Stop
  }
}