ACTOR PlasmaBodyguard : Fatso2 {
	Health 1000
  DamageFactor "Lightning", 1.2
  SeeSound "plasmab/sight"
  PainSound "plasmab/pain"
  ActiveSound "plasmab/idle"
  DeathSound "plasmab/death"
  Obituary "%o was crushed by a plasma bodyguard."
	+NOBLOOD
	
	States {
		SpawnLoop:
			PFAT AB 15 A_Look
			Loop
			
		See:
			PFAT AABBCCDDEEFF 4 A_Chase
			Loop
			
		Missile:
			PFAT G 0 A_PlaySound("plasmab/attack")
			PFAT G Random(5, 10) A_FaceTarget
			PFAT H Random(4, 10) Bright A_CustomMissile("AssaultPlasma", 20, -25, Random(-2, 2))
			TNT1 A 0 A_CustomMissile("AssaultPlasma", 20, 25, Random(-2, 2))
			PFAT IG Random(3, 5) A_FaceTarget
			PFAT H Random(4, 10) Bright A_CustomMissile("AssaultPlasma", 20, -25, Random(4, 8))
			TNT1 A 0 A_CustomMissile("AssaultPlasma", 20, 25, Random(4, 8))
			PFAT IG Random(3, 5) A_FaceTarget
			PFAT H Random(4, 10) Bright A_CustomMissile("AssaultPlasma", 20, -25, Random(-8, -4))
			TNT1 A 0 A_CustomMissile("AssaultPlasma", 20, 25, Random(-8, -4))
			PFAT IG Random(3, 5) A_FaceTarget
			TNT1 A 0 A_ChangeFlag("NOPAIN", FALSE)
			Goto See
			
		Pain:
			PFAT J 3
			PFAT J 3 A_Pain
			TNT1 A 0 A_Jump(32, "PainMissile")
			Goto See
			
		PainMissile:
			TNT1 A 0 A_ChangeFlag("NOPAIN", TRUE)
			Goto Missile
			
		Death:
			PFAT K 1 A_SpawnItemEx("BlueParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
			TNT1 AAA 0 A_CustomMissile("BigBlueExplosion", 0, 0, random(0,360), 2, random(0,360))	
			TNT1 AAA 0 A_CustomMissile("SmokeBigExplosion", 0, 0, random(0,360), 2, random(0,360))
			PFAT KKKKK 1 A_TossGib
			PFAT L 6 A_Scream
			PFAT M 6 A_NoBlocking
			TNT1 A 0 A_SpawnItem("BSmokeSpawner")
			PFAT NNOOPPQQRRSS 3 A_TossGib
			PFAT T -1 A_BossDeath
			Stop
			
		Raise:
			PFAT R 5
			PFAT QPONMLK 5
			Goto See
  }
}

ACTOR AssaultPlasma : FastProjectile {
	Radius 13
	Height 8
	Speed 25
	Damage 10
	Projectile
	RenderStyle Add
	Alpha 0.75
	+RANDOMIZE
	+CANBLAST
	+DONTREFLECT 
	+PUSHABLE
	SeeSound "plasmab/PlasmaFire"
	DeathSound "plasmab/impact"
	Decal "PlasmaScorch"
	MissileType "SWPlasmaTrail"
	MissileHeight 8
	
	States {
		Spawn:
			PLSM ABAB 2 Bright
			Loop
			
		Death:
			PLSX A 0 A_SpawnItem("ShotSmoke")
			PLSX A 2 Bright A_Explode(10,40,0)
			PLSX BCDEF 2 Bright
			Stop
	}
}

ACTOR SWPlasmaTrail {
	+NOBLOCKMAP
	+NOGRAVITY
	+SHADOW
	+NOTELEPORT
	+CANNOTPUSH
	+NODAMAGETHRUST
	+RANDOMIZE
	RenderStyle Add
	Alpha 0.5
	
	States {
		Spawn:
			PLSM CDCD 1 Bright
			Goto Death
			
		Death:
			PLSM CD 1 Bright A_FadeOut(0.2)
			Stop
	}
}

ACTOR BSmokeSpawner {
	Radius 2
	Height 2
	RenderStyle Add
	+FLOORCLIP
	+NOINTERACTION
	+CLIENTSIDEONLY
	
	States {
		Spawn:
			TNT1 A 2 A_SpawnItem("BSmoke")
			TNT1 A 5 A_SpawnItem("BSmoke")
			TNT1 A 10 A_SpawnItem("BSmoke")
			TNT1 A 10 A_SpawnItem("BSmoke")
			TNT1 A 10 A_SpawnItem("BSmoke")
			TNT1 A 10 A_SpawnItem("BSmoke")
			TNT1 A 10 A_SpawnItem("BSmoke")
			TNT1 A 10 A_SpawnItem("BSmoke")
			TNT1 A 10 A_SpawnItem("BSmoke")
			TNT1 A 10 A_SpawnItem("BSmoke")
			TNT1 A 10 A_SpawnItem("BSmoke")
			TNT1 A 10 A_SpawnItem("BSmoke")
			TNT1 A 10 A_SpawnItem("BSmoke")
			TNT1 A 10 A_SpawnItem("BSmoke")
			TNT1 A 10 A_SpawnItem("BSmoke")
			TNT1 A 10 A_SpawnItem("BSmoke")
			TNT1 A 10 A_SpawnItem("BSmoke")
			TNT1 A 10 A_SpawnItem("BSmoke")
			TNT1 A 10 A_SpawnItem("BSmoke")
			TNT1 A 10 A_SpawnItem("BSmoke")
			TNT1 A 10 A_SpawnItem("BSmoke")
			TNT1 A 15 A_SpawnItem("BSmoke")
			TNT1 A 20 A_SpawnItem("BSmoke")
			Stop
  }
}