ACTOR Rictus : LostSoul2 {
	Speed 6
	PainChance 100
	DamageFactor "FireR", 0.0
	Scale 0.6
	SeeSound "rictus/sight"
	AttackSound "rictus/attack"
	PainSound "rictus/pain"
	DeathSound "rictus/die"
	ActiveSound "rictus/active"
	Obituary "%o was wasted by a Rictus."
	+FLOAT
	+NOGRAVITY
	+MISSILEMORE
	+DONTFALL
	+NOBLOOD
	+NOBLOODDECALS
	+MISSILEEVENMORE
	+QUICKTORETALIATE
	
	States {
		Spawn:
			TNT1 A 0 A_SpawnItem("RictFireSpawner",0,0,0,0)
			RICT A 2 Bright A_Look
			Loop
			
		See:
			TNT1 A 0 A_SpawnItem("RictFireSpawner",0,0,0,0)
			RICT A 2 Bright A_Chase
			Loop
			
		Missile:
			TNT1 A 0 A_Jump(28,"Kamikaze")
			Goto Normal
			
		Normal:
			TNT1 A 0 A_SpawnItem("RictFireSpawner",0,0,0,0)
			RICT A 2 Bright A_FaceTarget
			TNT1 A 0 A_SpawnItem("RictFireSpawner",0,0,0,0)
			RICT A 2 Bright A_FaceTarget
			TNT1 A 0 A_SpawnItem("RictFireSpawner",0,0,0,0)
			RICT B 2 Bright A_CustomMissile("RictusBall",0,0,0)
			RICT BA 2 Bright A_SpawnItem("RictFireSpawner",0,0,0,0)
			Goto See
			
		Kamikaze:
			TNT1 A 0 A_JumpIfCloser(450,1)
			Goto Normal
			TNT1 A 0
			Goto PainSuicide+3
			
		Pain:
			TNT1 A 0 A_JumpIfHealthLower(35,"PainSuicide")
			TNT1 A 0 A_SpawnItem("RictFireSpawner",0,0,0,0)
			RICT B 3 Bright A_Pain
			Goto See
			
		PainSuicide:
			TNT1 A 0 A_JumpIfCloser(450,1)
			Goto See
			TNT1 A 0 A_SpawnItem("RictFireSpawner",0,0,0,0)
			RICT B 3 Bright A_Pain
			TNT1 A 0 A_Jump(28,"See")
			TNT1 A 0 A_ChangeFlag(NOTARGET,1)
			RICT B 1 Bright A_ChangeFlag(NOPAIN,1)
			TNT1 A 0 A_SpawnItem("RictFireSpawner",0,0,0,0)
			TNT1 A 0 A_CustomMissile("RictusSuicide",0,0,0,0)
			RICT B 1 Bright A_Die("Suicide")
			Stop
			
		Death:
			TNT1 A 0
			TNT1 A 0 A_Scream
			TNT1 A 0 A_SetTranslucent(0.75,1)
			TNT1 AA 0 A_SpawnItemEx("SoulPart1",0,0,Random(10,20),Random(8,-8),Random(8,-8),Random(6,8))
			TNT1 A 0 A_SpawnItemEx("SoulPart2",0,0,Random(10,20),Random(8,-8),Random(8,-8),Random(6,8))
			TNT1 AA 0 A_SpawnItemEx("SoulPart3",0,0,Random(10,20),Random(8,-8),Random(8,-8),Random(6,8))
			TNT1 AA 0 A_SpawnItemEx("OrangeParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128) 
			TNT1 AAA 0 A_CustomMissile("SmallRedExplosion", 0, 0, random(0,360), 2, random(0,360))
			TNT1 AAAA 0 A_CustomMissile("SmokeSmallExplosion", 0, 0, random(0,360), 2, random(0,360))
			MISL B 5 Bright A_NoBlocking
			MISL CD 4 Bright
			Stop
			
		Death.Suicide:
			TNT1 A 0
			TNT1 A 100 A_NoBlocking
			Stop
	}
}

ACTOR RictusBall {
	Radius 10
	Height 10
	Speed 18
	FastSpeed 22
	Damage 4
	XScale 0.22
	YScale 0.28
	Species "Soul"
	DamageType "FireR"
	PROJECTILE
	RENDERSTYLE ADD
	+THRUSPECIES
	SeeSound "rictus/ball"
	DeathSound "rictus/ballex"
	
	States {
		Spawn:
			RCST AB 1 Bright A_SpawnItemEx("RSoulBallTrail",0,0,0,0,0,0,0,128,0)
			Loop
			
		Death:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("OrangeParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
			TNT1 AAA 0 A_CustomMissile("SmallRedExplosion", 0, 0, random(0,360), 2, random(0,360))
			TNT1 AAA 0 A_CustomMissile("SmokeSmallExplosion", 0, 0, random(0,360), 2, random(0,360))	  
			RCST CDE 3 Bright
			Stop
	}
}

ACTOR RSoulBallTrail {
	RenderStyle Add
	XScale 0.22
	YScale 0.28
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+DONTSPLASH
	+NOCLIP
	+NOINTERACTION
	+THRUACTORS
	
	States {
		Spawn:
			RCST AB 1 A_FadeOut(0.18)
			Loop
	}
}

ACTOR RictusSuicide {
	Radius 14
	Height 48
	Health 30
	Damage 6
	Speed 25
	Scale 0.6
	SeeSound "rictus/attack"
	DeathSound "rictus/die"
	DamageType "FireR"
	Species "Soul"
	ExplosionDamage 52
	ExplosionRadius 128
	Decal Scorch
	PROJECTILE
	+SOLID
	+SHOOTABLE
	+NOBLOOD
	+NOBLOODDECALS
	+SKYEXPLODE
	+NOTARGETSWITCH
	-NOBLOCKMAP
	
	States {
		Spawn:
			RICT B 1 Bright A_SpawnItem("RictFireSpawner",0,0,0,0)
			Loop
			
		Death:
			TNT1 A 0 A_Die
			
		Explode:
			TNT1 A 0 A_ChangeFlag(NOBLOCKMAP,1)
			TNT1 A 0 A_Explode
			TNT1 AA 0 A_SpawnItemEx("SoulPart1",0,0,Random(10,20),Random(8,-8),Random(8,-8),Random(6,8))
			TNT1 A 0 A_SpawnItemEx("SoulPart2",0,0,Random(10,20),Random(8,-8),Random(8,-8),Random(6,8))
			TNT1 AA 0 A_SpawnItemEx("SoulPart3",0,0,Random(10,20),Random(8,-8),Random(8,-8),Random(6,8))
			TNT1 AA 0 A_SpawnItemEx("OrangeParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
			TNT1 AAA 0 A_CustomMissile("SmallRedExplosion", 0, 0, random(0,360), 2, random(0,360))
			TNT1 AAAA 0 A_CustomMissile("SmokeBigExplosion", 0, 0, random(0,360), 2, random(0,360))
			MISL B 5 Bright A_SetTranslucent(0.75,1)
			MISL CD 4 Bright
			Stop
	}
}

ACTOR RictFireSpawner : RedParticleSpawner {
	States {
		Spawn:
			TNT1 AA 0 NoDelay A_SpawnItemEx("ForgottenFire",3,random(-2,2),random(8,12),1,0,random(1,2),random(90,270),128,0)
			TNT1 A 1
			Stop
	}
}

ACTOR ForgottenFire {
	+MISSILE
	+DONTSPLASH
	+CLIENTSIDEONLY
	+NOGRAVITY
	+FORCEXYBILLBOARD
	+THRUACTORS
	+NOTELEPORT
	+NOBLOCKMAP
	+NOINTERACTION
	RenderStyle Add
	Scale 0.18
	
	States {
		Spawn:
			FIR1 ABCDEFGHIJKL 1 Bright A_FadeOut
			Stop
	}
}

ACTOR SoulPart1 {
	Health 1
	Height 2
	Radius 2
	Scale 0.75
	BounceFactor 0.5
	WallBounceFactor 0.3
	BounceCount 4
	+CLIENTSIDEONLY
	+CORPSE
	+NOTELEPORT
	+THRUACTORS
	+MISSILE
	+DOOMBOUNCE
	+DONTSPLASH
	+DROPOFF
	
	States {
		Spawn:
			LGI1 ABCDEFGH 4
			Loop
			
		Death:
		Crash:
			LGI1 E 150
			LGI1 E 5 A_FadeOut(0.10)
			Goto Death+1
	}
}

ACTOR SoulPart2 : SoulPart1 {
	States {
		Spawn:
			LGI2 ABCDEFGH 4
			Loop
			
		Death:
		Crash:
			LGI2 D 150
			LGI2 D 5 A_FadeOut(0.10)
			Goto Death+1
	}
}

ACTOR SoulPart3 : SoulPart1 {
	States {
		Spawn:
			LGI3 ABCDEFGH 4
			Loop
			
		Death:
		Crash:
			LGI3 A 150
			LGI3 A 5 A_FadeOut(0.10)
			Goto Death+1
	}
}